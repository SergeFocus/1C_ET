// sza140612-0105
// sza131125-0342

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)

	Если ОбщийМодульПовтор.ВывестиНаименованияНаДругомЯзыке() Тогда
		ВозможноеПредставление = ОбщийМодульПовтор.получитьПредставлениеНаЯзыке(Данные.ссылка, , ИСТИНА);
		Если НЕ ВозможноеПредставление = Неопределено Тогда
			Представление = ВозможноеПредставление;
			СтандартнаяОбработка = ЛОЖЬ;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)

	Если ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ДляУскоренияРаботыСБазойДанныхНеВыводитьДополнительныхДанных") Тогда
		Если ВидФормы = "ФормаОбъекта" Тогда
			ВыбраннаяФорма = "ФормаЭлементаУпрощенная";
			СтандартнаяОбработка = ЛОЖЬ;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура Печать(ДокументДляПечати, Ссылка) Экспорт

	ДокументДляПечати.ИмяПараметровПечати  = "Договор" + СокрЛП(ИмяКомпьютера());
	ДокументДляПечати.КлючПараметровПечати = ДокументДляПечати.ИмяПараметровПечати;
	ДокументДляПечати.Очистить();
	СсылкаНаОбъектПечати = Ссылка[0];
	РазрядМетаданных = ""; ИмяМетаданных = "";
	ПроизвольнаяПечатнаяФорма = ОбщийМодульТекстСервер.ОпределитьПроизвольнуюПечатнуюФорму(, , СсылкаНаОбъектПечати, РазрядМетаданных, ИмяМетаданных);

	Если ЗначениеЗаполнено(ПроизвольнаяПечатнаяФорма) Тогда
		ДокументДляПечати = ОбщийМодульТекстСервер.СформироватьДокументПоПроизвольнойФорме(ПроизвольнаяПечатнаяФорма, , ДокументДляПечати, СсылкаНаОбъектПечати, РазрядМетаданных, ИмяМетаданных);
	Иначе
		Макет = Справочники.Договора.ПолучитьМакет("Печать");
		Макет.КодЯзыкаМакета = ОбщийМодульПовтор.ПолучитьТекущийЯзыкДокументов();

		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	Договора.ВидЦен,
		|	Договора.ДатаЗаключения,
		|	Договора.ДатаНачала,
		|	Договора.ДатаОкончания,
		|	Договора.ДатаРедакции,
		|	Договора.ДатаСоздания,
		|	Договора.КлиентПоставщик,
		|	ВЫРАЗИТЬ(Договора.Комментарий КАК СТРОКА(1000)) КАК Комментарий,
		|	Договора.Наименование,
		|	Договора.НомерДоговора,
		|	Договора.ОграничиваетКоличествоТоваров,
		|	Договора.ОграничиваетСуммуЗадолженности,
		|	Договора.ОграничиваетСуммуТоваров,
		|	Договора.Организация,
		|	Договора.Ответственный,
		|	Договора.ПредусматриваетСтрогиеЦены,
		|	Договора.УстанавливаетСрокОплатыВДнях,
		|	Договора.Товары.(
		|		Ссылка,
		|		НомерСтроки,
		|		Номенклатура,
		|		ЕдиницаИзмерения,
		|		Цена,
		|		Комментарий
		|	),
		|	Договора.ПроцентКомиссионногоВознаграждения,
		|	Договора.ОбъектКлиента
		|ИЗ Справочник.Договора КАК Договора
		|ГДЕ Договора.Ссылка В(&Ссылка)";
		Запрос.Параметры.Вставить("Ссылка", Ссылка);

		Выборка = Запрос.Выполнить().Выбрать();
		ОбластьЗаголовок 	= Макет.ПолучитьОбласть("Заголовок");
		ОбластьТоварыШапка 	= Макет.ПолучитьОбласть("ТоварыШапка");
		Шапка 			= Макет.ПолучитьОбласть("Шапка");
		ОбластьТовары 	= Макет.ПолучитьОбласть("Товары");
		Подвал 			= Макет.ПолучитьОбласть("Подвал");
		ВставлятьРазделительСтраниц = ЛОЖЬ;

		Пока Выборка.Следующий() Цикл
			Если ВставлятьРазделительСтраниц Тогда
				ДокументДляПечати.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;

			ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьЗаголовок);
			ДокументДляПечати.Вывести(ОбластьЗаголовок);
			Шапка.Параметры.Заполнить(Выборка);
			ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(Шапка);
			ДокументДляПечати.Вывести(Шапка, Выборка.Уровень());
			ВыборкаТовары = Выборка.Товары.Выбрать();

			Если НЕ ВыборкаТовары.Количество() = 0 Тогда
				ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьТоварыШапка);
				ДокументДляПечати.Вывести(ОбластьТоварыШапка);
				Пока ВыборкаТовары.Следующий() Цикл
					ОбластьТовары.Параметры.Заполнить(ВыборкаТовары);
					ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьТовары);
					ДокументДляПечати.Вывести(ОбластьТовары, ВыборкаТовары.Уровень());

				КонецЦикла;
			КонецЕсли;

			Подвал.Параметры.Заполнить(Выборка);
			ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(Подвал);
			ДокументДляПечати.Вывести(Подвал);
			ВставлятьРазделительСтраниц = ИСТИНА;

		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

#КонецЕсли
