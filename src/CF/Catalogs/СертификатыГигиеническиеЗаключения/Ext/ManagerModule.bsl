#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// sza140228-0418
Процедура Печать(ДокументДляПечати, Ссылка) Экспорт
	
	ДокументДляПечати.ИмяПараметровПечати  = "Сертификат" + СокрЛП(ИмяКомпьютера());
	ДокументДляПечати.КлючПараметровПечати = ДокументДляПечати.ИмяПараметровПечати;
	
	ДокументДляПечати.Очистить();
	
	СсылкаНаОбъектПечати = Ссылка[0];
	
	РазрядМетаданных = ""; ИмяМетаданных = "";
	ПроизвольнаяПечатнаяФорма = ОбщийМодульТекстСервер.ОпределитьПроизвольнуюПечатнуюФорму(, , СсылкаНаОбъектПечати, РазрядМетаданных, ИмяМетаданных);
	
	Если ЗначениеЗаполнено(ПроизвольнаяПечатнаяФорма) Тогда
		ДокументДляПечати = ОбщийМодульТекстСервер.СформироватьДокументПоПроизвольнойФорме(ПроизвольнаяПечатнаяФорма, , ДокументДляПечати, СсылкаНаОбъектПечати, РазрядМетаданных, ИмяМетаданных);
		
	Иначе		
		Макет = Справочники.СертификатыГигиеническиеЗаключения.ПолучитьМакет("Печать");
		Макет.КодЯзыкаМакета = ОбщийМодульПовтор.ПолучитьТекущийЯзыкДокументов();
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ СертификатыГигиеническиеЗаключения.АдресОрганизацииЭкспертизы,
		|	СертификатыГигиеническиеЗаключения.АдресХраненияНаДиске,
		|	СертификатыГигиеническиеЗаключения.Активен,
		|	СертификатыГигиеническиеЗаключения.ВидДокумента,
		|	СертификатыГигиеническиеЗаключения.ДанныеКонтрактаОПоставкеПредметаЭкспертизы,
		|	СертификатыГигиеническиеЗаключения.ДатаВыдачи,
		|	СертификатыГигиеническиеЗаключения.ДатаОкончанияДействия,
		|	СертификатыГигиеническиеЗаключения.ДатаРегистрации,
		|	СертификатыГигиеническиеЗаключения.ДатаРедакции,
		|	СертификатыГигиеническиеЗаключения.ДатаСоздания,
		|	СертификатыГигиеническиеЗаключения.ДатаЭкспертизы,
		|	СертификатыГигиеническиеЗаключения.ДействуетНаТеритории,
		|	СертификатыГигиеническиеЗаключения.ДополнительныйКод,
		|	СертификатыГигиеническиеЗаключения.Зарегистрировал,
		|	СертификатыГигиеническиеЗаключения.Код,
		|	СертификатыГигиеническиеЗаключения.КодУКТВЭД,
		|	ВЫРАЗИТЬ(СертификатыГигиеническиеЗаключения.Комментарий КАК СТРОКА(1000)) КАК Комментарий,
		|	СертификатыГигиеническиеЗаключения.КонтрагентЗаявительЭкспертизы,
		|	СертификатыГигиеническиеЗаключения.Наименование,
		|	СертификатыГигиеническиеЗаключения.НаименованиеОрганизацииЭкспертизы,
		|	СертификатыГигиеническиеЗаключения.НомерДокумента,
		|	СертификатыГигиеническиеЗаключения.НомерИДатаДоговораОпределяющегоПериодДействияЭкспертизы,
		|	СертификатыГигиеническиеЗаключения.НомерИДатаПротоколаЭкспертизы,
		|	СертификатыГигиеническиеЗаключения.Ответственный,
		|	СертификатыГигиеническиеЗаключения.ПериодДействительностиДанногоЗаключения,
		|	СертификатыГигиеническиеЗаключения.РеквизитыТорговойМаркиПредметаЭкспертизы,
		|	СертификатыГигиеническиеЗаключения.ТелефоныОрганизацииЭкспертизы,
		|	СертификатыГигиеническиеЗаключения.ТорговаяМаркаПредметаЭкспертизы,
		|	СертификатыГигиеническиеЗаключения.ФИОРуководителяЭкспертногоПодразделения,
		|	СертификатыГигиеническиеЗаключения.ФИОУтверждающегоОрганизацииЭкспертизы,
		|	СертификатыГигиеническиеЗаключения.ЧислоДополненийКЭкспертизе,
		|	СертификатыГигиеническиеЗаключения.ЧислоИзображенийСканов,
		|	СертификатыГигиеническиеЗаключения.ЧислоСтрокСостава,
		|	СертификатыГигиеническиеЗаключения.Товары.(
		|		НомерСтроки,
		|		Номенклатура,
		|		КодУКТВЭД,
		|		ПолноеНаименованиеИзДокументаЭкспертизы,
		|		Комментарий ),
		|	СертификатыГигиеническиеЗаключения.Изображения.(
		|		НомерСтроки,
		|		ИзображениеИзБазыДанных,
		|		АдресИзображения,
		|		Комментарий,
		|		ИмяФайла,
		|		РасширениеФайла,
		|		ПутьФайла,
		|		ОриентацияЛандшафт )
		|ИЗ Справочник.СертификатыГигиеническиеЗаключения КАК СертификатыГигиеническиеЗаключения
		|ГДЕ СертификатыГигиеническиеЗаключения.Ссылка В (&Ссылка)";
		
		Запрос.Параметры.Вставить("Ссылка", Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		
		ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
		Шапка 					= Макет.ПолучитьОбласть("Шапка");
		ОбластьСоставШапка 		= Макет.ПолучитьОбласть("СоставШапка");
		ОбластьСостав 			= Макет.ПолучитьОбласть("Состав");
		ОбластьИзображенияШапка = Макет.ПолучитьОбласть("ИзображенияШапка");
		ОбластьИзображения 		= Макет.ПолучитьОбласть("Изображения");
		Подвал 					= Макет.ПолучитьОбласть("Подвал");
		
		ВставлятьРазделительСтраниц = ЛОЖЬ;
		Пока Выборка.Следующий() Цикл
			Если ВставлятьРазделительСтраниц Тогда
				ДокументДляПечати.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьЗаголовок);
			ДокументДляПечати.Вывести(ОбластьЗаголовок);
			
			Шапка.Параметры.Заполнить(Выборка);
			ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(Шапка);
			ДокументДляПечати.Вывести(Шапка, Выборка.Уровень());
			
			ДокументДляПечати.Вывести(ОбластьСоставШапка);
			ВыборкаСостав = Выборка.Товары.Выбрать();
			Пока ВыборкаСостав.Следующий() Цикл
				ОбластьСостав.Параметры.Заполнить(ВыборкаСостав);
				ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьСостав);
				ДокументДляПечати.Вывести(ОбластьСостав, ВыборкаСостав.Уровень());
			КонецЦикла;
			
			ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьИзображенияШапка);
			ДокументДляПечати.Вывести(ОбластьИзображенияШапка);
			ВыборкаИзображения = Выборка.Изображения.Выбрать();
			Пока ВыборкаИзображения.Следующий() Цикл
				ОбластьИзображения.Параметры.Заполнить(ВыборкаИзображения);
				ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьИзображения);
				ДокументДляПечати.Вывести(ОбластьИзображения, ВыборкаИзображения.Уровень());
			КонецЦикла;
			
			Подвал.Параметры.Заполнить(Выборка);
			ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(Подвал);
			ДокументДляПечати.Вывести(Подвал);
			
			ВставлятьРазделительСтраниц = ИСТИНА;
		КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли