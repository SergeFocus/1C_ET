// sza131005-0202 : 
&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	МенеджерОборудованияКлиентСервер.ЗаполнитьНаименованиеРабочегоМеста(Объект, ТекущийПользователь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	Если НЕ ПроверкаУникальности()Тогда
		Ответ = Вопрос(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Указано не-уникальное наименование рабочего места!") + Символы.ПС +
		                    ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Возможно в дальнейшем это затруднит идентификацию и выбор рабочего места.") + Символы.ПС +
		                    ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Рекомендуется указывать уникальное наименование рабочих мест.") + Символы.ПС +
		                    ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Продолжить сохранение с указанным наименованием?"),
		               РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Отказ = ИСТИНА;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПолучитьКартинку(ТипОборудования, Размер)

	Попытка // Может прийти пустая ссылка или неопределено, может не быть картинки
		МетаОбъект  = ТипОборудования.Метаданные();
		Индекс      = Перечисления.ТипыПодключаемогоОборудования.Индекс(ТипОборудования);
		ИмяКартинки = МетаОбъект.ЗначенияПеречисления[Индекс].Имя;
		ИмяКартинки = "ПодключаемоеОборудование" + ИмяКартинки + Размер;
		Картинка = БиблиотекаКартинок[ИмяКартинки]
	Исключение
		Картинка = Неопределено;
	КонецПопытки;

	Возврат Картинка;

КонецФункции

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	СистемнаяИнформация = Новый СистемнаяИнформация();
	Если Объект.Код = ВРег(СистемнаяИнформация.ИдентификаторКлиента) Тогда
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;
	
	глЧислоОбъектов = глЧислоОбъектов + 1; глПроверятьСообщения = ИСТИНА;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()                                              // ПРИ ЗАКРЫТИИ
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 1);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)                                         // ПРИ ОТКРЫТИИ
	
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 0);
	
	Если ПустаяСтрока(Объект.Код) Тогда
		
		СистемнаяИнформация = Новый СистемнаяИнформация();
		Объект.Код = ВРег(СистемнаяИнформация.ИдентификаторКлиента);
		
	КонецЕсли;
	
	МенеджерОборудованияКлиентСервер.ЗаполнитьНаименованиеРабочегоМеста(Объект, ТекущийПользователь);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)          // ПРИ СОЗДАНИИ НА СЕРВЕРЕ
	
		Если ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьСобственныйПереводЭлементовИнтерфейса") Тогда
			ОбщийМодульСервер.ПеревестиРеквизитыФормы(ЭтаФорма);	
		КонецЕсли;
		
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест"
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	ТекущийПользователь = ПользователиИнформационнойБазы.ТекущийПользователь();

	#Если Не ВебКлиент Тогда
	Объект.ИмяКомпьютера = ИмяКомпьютера();
	#КонецЕсли
	Объект.СетевойПорт   = МенеджерОборудованияКлиентСервер.ПолучитьСетевойПортПоУмолчанию();

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	Место = ТекущийОбъект.Ссылка;

	СписокУстройств = МенеджерОборудованияВызовСервера.ПолучитьСписокОборудования( , , Место);
	Для Каждого Элемент Из СписокУстройств Цикл
		Если Элемент.РабочееМесто = Место Тогда
			ЛокальноеОборудование.Добавить(Элемент.Ссылка,Элемент.Наименование, ЛОЖЬ, ПолучитьКартинку(Элемент.ТипОборудования, 16));
		Иначе
			УдаленноеОборудование.Добавить(Элемент.Ссылка,Элемент.Наименование, ЛОЖЬ, ПолучитьКартинку(Элемент.ТипОборудования, 16));
		КонецЕсли;
	КонецЦикла

КонецПроцедуры

&НаСервере
Функция ПроверкаУникальности()

	Результат = ИСТИНА;

	Если НЕ ПустаяСтрока(Объект.Наименование) Тогда
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|    1
		|ИЗ
		|    Справочник.РабочиеМеста КАК РабочиеМеста
		|ГДЕ
		|    РабочиеМеста.Наименование = &Наименование
		|    И РабочиеМеста.Ссылка <> &Ссылка
		|");

		Запрос.УстановитьПараметр("Наименование", Объект.Наименование);
		Запрос.УстановитьПараметр("Ссылка"      , Объект.Ссылка);

		Результат = Запрос.Выполнить().Пустой();
	КонецЕсли;
	
	Возврат Результат;

КонецФункции



