// sza140104-0215 :
// sza131111-0049 :

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)

		Если ОбщийМодульПовтор.ВывестиНаименованияНаДругомЯзыке() Тогда
			ВозможноеПредставление = ОбщийМодульПовтор.ПолучитьПредставлениеНаЯзыке(Данные.Ссылка, , ИСТИНА);
			Если НЕ ВозможноеПредставление = Неопределено Тогда
				Представление = ВозможноеПредставление;
				СтандартнаяОбработка = ЛОЖЬ;
			КонецЕсли;
		КонецЕсли;

	КонецПроцедуры

	Процедура Печать(ДокументДляПечати, Ссылка) Экспорт

		ДокументДляПечати.ИмяПараметровПечати  = "Склад" + СокрЛП(ИмяКомпьютера());
		ДокументДляПечати.КлючПараметровПечати = ДокументДляПечати.ИмяПараметровПечати;
		ДокументДляПечати.Очистить();
		СсылкаНаОбъектПечати = Ссылка[0];
		РазрядМетаданных = ""; ИмяМетаданных = "";
		ПроизвольнаяПечатнаяФорма = ОбщийМодульТекстСервер.ОпределитьПроизвольнуюПечатнуюФорму(, , СсылкаНаОбъектПечати, РазрядМетаданных, ИмяМетаданных);

		Если ЗначениеЗаполнено(ПроизвольнаяПечатнаяФорма) Тогда
			ДокументДляПечати = ОбщийМодульТекстСервер.СформироватьДокументПоПроизвольнойФорме(ПроизвольнаяПечатнаяФорма, , ДокументДляПечати, СсылкаНаОбъектПечати, РазрядМетаданных, ИмяМетаданных);
		Иначе
			Макет = Справочники.Склады.ПолучитьМакет("Печать");
			Макет.КодЯзыкаМакета = ОбщийМодульПовтор.ПолучитьТекущийЯзыкДокументов();

			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ Склады.ВидЦенРеализацииСЭтогоСклада,
			|	Склады.Код,
			|	ВЫРАЗИТЬ(Склады.Комментарий КАК СТРОКА(1000)) КАК Комментарий,
			|	Склады.Наименование,
			|	Склады.ОбязательныйВидЦенПриПеремещенииНаЭтотСклад,
			|	Склады.ОтветственныйСотрудник,
			|	Склады.СкладПополнения,
			|	Склады.ФормулаТекстаЭтикетки,
			|	Склады.Адрес,
			|	Склады.Телефон,
			|	Склады.Регион,
			|	Склады.Локация
			|ИЗ Справочник.Склады КАК Склады
			|ГДЕ Склады.Ссылка В(&Ссылка)";
			Запрос.Параметры.Вставить("Ссылка", Ссылка);

			Выборка = Запрос.Выполнить().Выбрать();
			ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
			Шапка = Макет.ПолучитьОбласть("Шапка");
			ВставлятьРазделительСтраниц = ЛОЖЬ;

			Пока Выборка.Следующий() Цикл
				Если ВставлятьРазделительСтраниц Тогда
					ДокументДляПечати.ВывестиГоризонтальныйРазделительСтраниц();
				КонецЕсли;

				ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьЗаголовок);
				ДокументДляПечати.Вывести(ОбластьЗаголовок);
				Шапка.Параметры.Заполнить(Выборка);
				ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(Шапка);
				ДокументДляПечати.Вывести(Шапка, Выборка.Уровень());
				ВставлятьРазделительСтраниц = ИСТИНА;

			КонецЦикла;
		КонецЕсли;

	КонецПроцедуры

#КонецЕсли
