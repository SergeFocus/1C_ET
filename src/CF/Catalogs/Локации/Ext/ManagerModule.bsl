// sza140710-2218

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)

		Если ОбщийМодульПовтор.ВывестиНаименованияНаДругомЯзыке() Тогда
			ВозможноеПредставление = ОбщийМодульПовтор.ПолучитьПредставлениеНаЯзыке(Данные.Ссылка, , ИСТИНА);
			Если НЕ ВозможноеПредставление = Неопределено Тогда
				Представление = ВозможноеПредставление;
				СтандартнаяОбработка = ЛОЖЬ;
			КонецЕсли;
		КонецЕсли;

	КонецПроцедуры

	Процедура НаПечать(ТабДок, Ссылка) Экспорт

		Макет = Справочники.Локации.ПолучитьМакет("НаПечать");

		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	Локации.Важность,
		|	Локации.Высота,
		|	Локации.ДатаРедакции,
		|	Локации.ДатаСоздания,
		|	Локации.Долгота,
		|	Локации.Код,
		|	ВЫРАЗИТЬ(Локации.Комментарий КАК Строка(1000)) КАК Комментарий,
		|	Локации.Наименование,
		|	Локации.Ответственный,
		|	Локации.ПочтовыйИндекс,
		|	Локации.Размер,
		|	Локации.ЧасовойПоясОтГринвича,
		|	Локации.Широта
		|ИЗ Справочник.Локации КАК Локации
		|ГДЕ Локации.Ссылка В (&Ссылка)";
		Запрос.Параметры.Вставить("Ссылка", Ссылка);

		Выборка = Запрос.Выполнить().Выбрать();
		ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
		Шапка = Макет.ПолучитьОбласть("Шапка");
		ТабДок.Очистить();
		ВставлятьРазделительСтраниц = Ложь;

		Пока Выборка.Следующий() Цикл
			Если ВставлятьРазделительСтраниц Тогда
				ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;

			ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьЗаголовок);
			ТабДок.Вывести(ОбластьЗаголовок);
			Шапка.Параметры.Заполнить(Выборка);
			ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(Шапка);
			ТабДок.Вывести(Шапка, Выборка.Уровень());
			ВставлятьРазделительСтраниц = Истина;

		КонецЦикла;

	КонецПроцедуры

#КонецЕсли
