#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// sza151210-2139
// sza140424-1543  
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Номенклатура") Тогда
		Комментарий  = ДанныеЗаполнения.Комментарий;
		Наименование = ДанныеЗаполнения.Наименование;
		НоваяСтрока  = Товары.Добавить();
		НоваяСтрока.Номенклатура = ДанныеЗаполнения.Ссылка;
		НоваяСтрока.Количество = 1;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.НоменклатурныеГруппы") Тогда
		Комментарий  = ДанныеЗаполнения.Комментарий;
		Наименование = ДанныеЗаполнения.Наименование;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ Номенклатура.Ссылка
		|ИЗ Справочник.Номенклатура КАК Номенклатура
		|ГДЕ Номенклатура.НоменклатурнаяГруппа = &НоменклатурнаяГруппа";
		
		Запрос.УстановитьПараметр("НоменклатурнаяГруппа", ДанныеЗаполнения.ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			НоваяСтрока = Товары.Добавить();
			НоваяСтрока.Номенклатура = ВыборкаДетальныеЗаписи.Ссылка;
			НоваяСтрока.Количество = 1;
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.УстановкиЦен") Тогда
		Комментарий = ДанныеЗаполнения.Комментарий;
		Для Каждого ТекСтрокаТовара Из ДанныеЗаполнения.Товары Цикл
			НоваяСтрока = Товары.Добавить();
			НоваяСтрока.ЕдиницаИзмерения = ТекСтрокаТовара.ЕдиницаИзмерения;
			НоваяСтрока.ОСтроке = ТекСтрокаТовара.Комментарий;
			НоваяСтрока.Номенклатура = ТекСтрокаТовара.Номенклатура;
			НоваяСтрока.Количество = 1;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры
Процедура ПередЗаписью(Отказ)
	
	Если НЕ Отказ
		И НЕ ЭтоГруппа
		И НЕ ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("МассоваяЗагрузка", ИСТИНА)
		И ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ФормироватьОписаниеТаблицОбъектовДляИхСписков") Тогда
		
		ОбщийМодульСервер.ОформитьОписаниеТаблицы(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры
Процедура ПриЗаписи(Отказ)
	
	Если НЕ ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("МассоваяЗагрузка", ИСТИНА) 
		И НЕ ОбменДанными.Загрузка Тогда
		
		ОбновитьПовторноИспользуемыеЗначения();	
	КонецЕсли;
	
КонецПроцедуры
Процедура ПриКопировании(ОбъектКопирования)
	Комментарий = "";
КонецПроцедуры

#КонецЕсли