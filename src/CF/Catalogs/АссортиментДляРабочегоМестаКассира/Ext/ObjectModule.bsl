#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// sza140606-0132
Процедура ПриЗаписи(Отказ)

	Если НЕ Отказ
		И НЕ ПометкаУдаления
		И НЕ ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("МассоваяЗагрузка", ИСТИНА) Тогда

		Если НЕ ЭтоГруппа
			И Модифицированность() Тогда

			Если ЗначениеЗаполнено(Родитель) Тогда

				Если ЗначениеЗаполнено(Родитель.Подразделение)
					И (не ЗначениеЗаполнено(Подразделение) или Подразделение <> Родитель.Подразделение) Тогда

					Подразделение = Родитель.Подразделение;
				КонецЕсли;

				Если ЗначениеЗаполнено(Родитель.РеализацияОтОпределеннойОрганизации)
					И (не ЗначениеЗаполнено(РеализацияОтОпределеннойОрганизации) или РеализацияОтОпределеннойОрганизации <> Родитель.РеализацияОтОпределеннойОрганизации) Тогда

					РеализацияОтОпределеннойОрганизации = Родитель.РеализацияОтОпределеннойОрганизации;
				КонецЕсли;

				Если ЗначениеЗаполнено(Родитель.РеализацияСОпределенногоСклада)
					И (не ЗначениеЗаполнено(РеализацияСОпределенногоСклада) или РеализацияСОпределенногоСклада <> Родитель.РеализацияСОпределенногоСклада) Тогда

					РеализацияСОпределенногоСклада = Родитель.РеализацияСОпределенногоСклада;
				КонецЕсли;

				Если ЗначениеЗаполнено(Родитель.РеализоватьСИспользованиеОпределенногоВидаЦен)
					И (не ЗначениеЗаполнено(РеализоватьСИспользованиеОпределенногоВидаЦен) или РеализоватьСИспользованиеОпределенногоВидаЦен <> Родитель.РеализоватьСИспользованиеОпределенногоВидаЦен) Тогда

					РеализоватьСИспользованиеОпределенногоВидаЦен = Родитель.РеализоватьСИспользованиеОпределенногоВидаЦен;
				КонецЕсли;

				Если ЗначениеЗаполнено(Родитель.РеализоватьСПоступлениеДенегВОпределенноеХранилище)
					И (не ЗначениеЗаполнено(РеализоватьСПоступлениеДенегВОпределенноеХранилище) или РеализоватьСПоступлениеДенегВОпределенноеХранилище <> Родитель.РеализоватьСПоступлениеДенегВОпределенноеХранилище) Тогда

					РеализоватьСПоступлениеДенегВОпределенноеХранилище = Родитель.РеализоватьСПоступлениеДенегВОпределенноеХранилище;
				КонецЕсли;
			КонецЕсли;

			Отказ = ОбщийМодульСервер.ПроверитьУникальностьЗначенияРеквизита(Отказ, "АссортиментДляРабочегоМестаКассира", Наименование, Ссылка, "Подразделение", Подразделение);
		ИначеЕсли Модифицированность() Тогда

			Если НЕ Отказ
				И ЗначениеЗаполнено(Ссылка) Тогда

				Запрос = Новый Запрос;
				Запрос.Текст = "ВЫБРАТЬ
				|	АссортиментДляРабочегоМестаКассира.Ссылка
				|ИЗ Справочник.АссортиментДляРабочегоМестаКассира КАК АссортиментДляРабочегоМестаКассира
				|ГДЕ АссортиментДляРабочегоМестаКассира.Родитель = &Родитель";
				Запрос.УстановитьПараметр("Родитель", Ссылка);

				РезультатЗапроса = Запрос.Выполнить();

				Если НЕ РезультатЗапроса.Пустой() Тогда
					ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
					Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
						ОбъектАссортимента = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
						НеМенять = ИСТИНА;

						Если НЕ ОбъектАссортимента.Подразделение = Подразделение Тогда
							ОбъектАссортимента.Подразделение = Подразделение;
							НеМенять = ЛОЖЬ;
						КонецЕсли;

						Если ЗначениеЗаполнено(РеализацияОтОпределеннойОрганизации)
							И НЕ ОбъектАссортимента.РеализацияОтОпределеннойОрганизации = РеализацияОтОпределеннойОрганизации Тогда

							ОбъектАссортимента.РеализацияОтОпределеннойОрганизации = РеализацияОтОпределеннойОрганизации;
							НеМенять = ЛОЖЬ;
						КонецЕсли;

						Если ЗначениеЗаполнено(РеализацияСОпределенногоСклада)
							И НЕ ОбъектАссортимента.РеализацияСОпределенногоСклада = РеализацияСОпределенногоСклада Тогда

							ОбъектАссортимента.РеализацияСОпределенногоСклада = РеализацияСОпределенногоСклада;
							НеМенять = ЛОЖЬ;
						КонецЕсли;

						Если ЗначениеЗаполнено(РеализоватьСИспользованиеОпределенногоВидаЦен)
							И НЕ ОбъектАссортимента.РеализоватьСИспользованиеОпределенногоВидаЦен = РеализоватьСИспользованиеОпределенногоВидаЦен Тогда

							ОбъектАссортимента.РеализоватьСИспользованиеОпределенногоВидаЦен = РеализоватьСИспользованиеОпределенногоВидаЦен;
							НеМенять = ЛОЖЬ;
						КонецЕсли;

						Если ЗначениеЗаполнено(РеализоватьСПоступлениеДенегВОпределенноеХранилище)
							И НЕ ОбъектАссортимента.РеализоватьСПоступлениеДенегВОпределенноеХранилище = РеализоватьСПоступлениеДенегВОпределенноеХранилище Тогда

							ОбъектАссортимента.РеализоватьСПоступлениеДенегВОпределенноеХранилище = РеализоватьСПоступлениеДенегВОпределенноеХранилище;
							НеМенять = ЛОЖЬ;
						КонецЕсли;

						Если НЕ НеМенять Тогда
							ОбъектАссортимента.Записать();
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Комментарий = "";
	Номенклатура = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");

КонецПроцедуры

#КонецЕсли
