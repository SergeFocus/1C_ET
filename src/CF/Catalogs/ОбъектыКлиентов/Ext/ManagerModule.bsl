// sza150503-0132 
// sza140305-0303  
Процедура НаПечать(ТабДок, Ссылка) Экспорт
	
	ТабДок.ИмяПараметровПечати  = "ОбъектКиентов" + СокрЛП(ИмяКомпьютера());
	ТабДок.КлючПараметровПечати = ТабДок.ИмяПараметровПечати;
	
	ТабДок.Очистить();
	
	СсылкаНаОбъектПечати = Ссылка[0];
	
	РазрядМетаданных = ""; ИмяМетаданных = "";
	ПроизвольнаяПечатнаяФорма = ОбщийМодульТекстСервер.ОпределитьПроизвольнуюПечатнуюФорму(, , СсылкаНаОбъектПечати, РазрядМетаданных, ИмяМетаданных);
	
	Если ЗначениеЗаполнено(ПроизвольнаяПечатнаяФорма) Тогда
		ТабДок = ОбщийМодульТекстСервер.СформироватьДокументПоПроизвольнойФорме(ПроизвольнаяПечатнаяФорма, , ТабДок, СсылкаНаОбъектПечати, РазрядМетаданных, ИмяМетаданных);
		
	Иначе		
		Макет = Справочники.ОбъектыКлиентов.ПолучитьМакет("НаПечать");
		Макет.КодЯзыкаМакета = ОбщийМодульПовтор.ПолучитьТекущийЯзыкДокументов(Ссылка[0].СвязанныйКлиент);
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ОбъектыКлиентов.ДатаРедакции,
		|	ОбъектыКлиентов.ДатаСоздания,
		|	ОбъектыКлиентов.КатегорияОбъекта,
		|	ОбъектыКлиентов.Код,
		|	ОбъектыКлиентов.Комментарий,
		|	ОбъектыКлиентов.Наименование,
		|	ОбъектыКлиентов.НоменклатурнаяГруппа,
		|	ОбъектыКлиентов.Ответственный,
		|	ОбъектыКлиентов.Регион,
		|	ОбъектыКлиентов.СвязаннаяНоменклатура,
		|	ОбъектыКлиентов.СвязанныйКлиент,
		|	ОбъектыКлиентов.Стоимость,
		|	ОбъектыКлиентов.СвязанныеКлиенты.(
		|		НомерСтроки,
		|		Клиент,
		|		РольКлиента,
		|		ОСтроке ),
		|	ОбъектыКлиентов.ЗакрепленныеСотрудники.(
		|		НомерСтроки,
		|		Сотрудник,
		|		РольСотрудника,
		|		ОСтроке )
		|ИЗ Справочник.ОбъектыКлиентов КАК ОбъектыКлиентов
		|ГДЕ ОбъектыКлиентов.Ссылка В (&Ссылка)";
		
		Запрос.Параметры.Вставить("Ссылка", Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		
		ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
		Шапка = Макет.ПолучитьОбласть("Шапка");
		ОбластьСвязанныеКлиентыШапка 	= Макет.ПолучитьОбласть("СвязанныеКлиентыШапка");
		ОбластьСвязанныеКлиенты 		= Макет.ПолучитьОбласть("СвязанныеКлиенты");
		ОбластьЗакрепленныеСотрудникиШапка = Макет.ПолучитьОбласть("ЗакрепленныеСотрудникиШапка");
		ОбластьЗакрепленныеСотрудники 		= Макет.ПолучитьОбласть("ЗакрепленныеСотрудники");
		Подвал = Макет.ПолучитьОбласть("Подвал");
		
		ВставлятьРазделительСтраниц = ЛОЖЬ;
		Пока Выборка.Следующий() Цикл
			Если ВставлятьРазделительСтраниц Тогда
				ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьЗаголовок);
			ТабДок.Вывести(ОбластьЗаголовок);
			
			Шапка.Параметры.Заполнить(Выборка);
			ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(Шапка);
			ТабДок.Вывести(Шапка, Выборка.Уровень());
			
			ВыборкаСвязанныеКлиенты = Выборка.СвязанныеКлиенты.Выбрать();
			Если НЕ ВыборкаСвязанныеКлиенты.Количество() = 0 Тогда
				ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьСвязанныеКлиентыШапка);
				ТабДок.Вывести(ОбластьСвязанныеКлиентыШапка);
				
				Пока ВыборкаСвязанныеКлиенты.Следующий() Цикл
					ОбластьСвязанныеКлиенты.Параметры.Заполнить(ВыборкаСвязанныеКлиенты);
					ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьСвязанныеКлиенты);
					ТабДок.Вывести(ОбластьСвязанныеКлиенты, ВыборкаСвязанныеКлиенты.Уровень());
				КонецЦикла;	
			КонецЕсли;
			
			ВыборкаЗакрепленныеСотрудники = Выборка.ЗакрепленныеСотрудники.Выбрать();
			Если НЕ ВыборкаЗакрепленныеСотрудники.Количество() = 0 Тогда
				ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьЗакрепленныеСотрудникиШапка);
				ТабДок.Вывести(ОбластьЗакрепленныеСотрудникиШапка);
				
				Пока ВыборкаЗакрепленныеСотрудники.Следующий() Цикл
					ОбластьЗакрепленныеСотрудники.Параметры.Заполнить(ВыборкаЗакрепленныеСотрудники);
					ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьЗакрепленныеСотрудники);
					ТабДок.Вывести(ОбластьЗакрепленныеСотрудники, ВыборкаЗакрепленныеСотрудники.Уровень());
				КонецЦикла;	
			КонецЕсли;
			
			Подвал.Параметры.Заполнить(Выборка);
			ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(Подвал);
			ТабДок.Вывести(Подвал);
			
			ВставлятьРазделительСтраниц = ИСТИНА;
		КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры
