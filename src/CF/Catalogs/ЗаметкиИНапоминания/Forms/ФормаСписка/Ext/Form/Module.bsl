// sza131119-1414

&НаКлиенте
Процедура ПослеИзображенияОкнаФормы() // Когда окно уже нарисовано пользователю
	ЭтаФорма.Элементы.ФормаЗакрыть.Видимость = НЕ ЭтаФорма.Окно = Неопределено;	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 1);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 0);
	УстановитьАктуальность();
	
	ПодключитьОбработчикОжидания("ПослеИзображенияОкнаФормы", 0.3, ИСТИНА);	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьСобственныйПереводЭлементовИнтерфейса") Тогда
		ОбщийМодульСервер.ПеревестиРеквизитыФормы(ЭтаФорма);	
	КонецЕсли; 		
	
	УстановитьАктуальность();
	ОбщийМодульСервер.ОбеспечитьСписокОтборов(Список);	
	
КонецПроцедуры

&НаКлиенте
Процедура ТолькоАктуальныеПриИзменении(Элемент)
	УстановитьАктуальность();
КонецПроцедуры

&НаКлиенте
Процедура УдалитьНеактуальные(Команда)
	
	ПоказатьВопрос(Новый ОписаниеОповещения("УдалитьНеактуальныеЗавершение", ЭтаФорма), ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Вы хотите удалить все неактуальные заметки?"), РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьНеактуальныеЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		УдалитьНеактуальныеНаСервере();	
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьНеактуальныеНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ЗаметкиИНапоминания.Ссылка
	|ИЗ Справочник.ЗаметкиИНапоминания КАК ЗаметкиИНапоминания
	|ГДЕ (ЗаметкиИНапоминания.ПометкаУдаления = ИСТИНА
	|			ИЛИ ЗаметкиИНапоминания.Актуально = ЛОЖЬ
	|			ИЛИ ЗаметкиИНапоминания.ТипЗаметки <> &ТипЗаметка
	|				И ЗаметкиИНапоминания.Дата < &Дата
	|				И ЗаметкиИНапоминания.ПовторятьКаждыеЧислоЧасов = 0)";
	
	Запрос.УстановитьПараметр("Дата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("ТипЗаметка", Перечисления.ТипыЗаметок.Заметка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ЛишняяЗаметка = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
			ЛишняяЗаметка.Удалить(ИСТИНА);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьАктуальность()
	
	Список.Параметры.УстановитьЗначениеПараметра("НетОтбораПоАктуальным", НЕ ТолькоАктуальные);
	Список.Параметры.УстановитьЗначениеПараметра("ТипЗаметка", Перечисления.ТипыЗаметок.Заметка);
	Список.Параметры.УстановитьЗначениеПараметра("ПустаяДата", '00010101000000' );
	
КонецПроцедуры
