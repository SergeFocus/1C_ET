// sza160330-1632 автонаименование
// sza150219-2351
// sza140718-1624 +произвольные отчеты
// sza140522-0311
// sza131121-1833

&НаСервере
Процедура ВидимостьДополнительныхКолонок()
	
	Видимость = ЛОЖЬ;
	
	Для Каждого Задача Из Объект.ВыполняемыеЗадачи Цикл
		
		Если Задача.ТипЗадачи = перечисления.ТипыЗадач.СформироватьПроизвольныйОтчетИОтправитьПоEmail
			ИЛИ Задача.ТипЗадачи = перечисления.ТипыЗадач.СформироватьПроизвольныйОтчетИСохранитьВФайлExcel Тогда
			
			Видимость = ИСТИНА;			
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Элементы.ВыполняемыеЗадачиНаименованиеОтчета.Видимость  = Видимость;
	Элементы.ВыполняемыеЗадачиВариантОтчета.Видимость  		= Видимость;
	Элементы.ВыполняемыеЗадачиКодВариантОтчета.Видимость 	= Видимость;
	Элементы.ВыполняемыеЗадачиПериодОтчета.Видимость 		= Видимость;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидимостьПолучателей()
	
	ВидимостьПолучателей = НЕ Объект.ТипЗаметки = ТипЗадача И НЕ Объект.ТипЗаметки = ТипЗаметка;
	
	Если Объект.Получатели.Количество() = 0
		ИЛИ Объект.Получатели.Количество() = 1 Тогда
		
		Элементы.ГоризонтПолучателей.Видимость = ВидимостьПолучателей;
		Элементы.Получатели.Видимость = ЛОЖЬ;
	Иначе
		Элементы.ГоризонтПолучателей.Видимость = ЛОЖЬ;
		Элементы.Получатели.Видимость = ВидимостьПолучателей;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВКонцеМесяца(Команда)
	
	Дата = ТекущаяДата();
	КонецМесяца = КонецМесяца(ТекущаяДата());
	Объект.Дата = дата(год(КонецМесяца), месяц(КонецМесяца), День(КонецМесяца), час(дата), Минута(дата), 0);
	ИзменитьСледующийПериод();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьСейчас(Команда)
	
	Состояние(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Выполнение задач.."), , ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Ждите.."));
	Записать();
	ОбщийМодульСерверПривилегия.ВыполнитьЗадачку(Объект.Ссылка);
	ОбщегоНазначения.СообщитьПользователю(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Задачи выполнены"), Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполняемыеЗадачиАдресОткрытие(Элемент, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ВыполняемыеЗадачи.ТекущиеДанные;
	Адрес = ТекущиеДанные.Адрес;
	СтандартнаяОбработка = ЛОЖЬ;
	ДиалогДляВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогДляВыбораФайла.Заголовок				= ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Выберите каталог") + ": ";
	ДиалогДляВыбораФайла.МножественныйВыбор		= ЛОЖЬ;
	ДиалогДляВыбораФайла.ПредварительныйПросмотр	= ЛОЖЬ;
	ДиалогДляВыбораФайла.ИндексФильтра			= 0;
	ДиалогДляВыбораФайла.Каталог					= Адрес;
	ДиалогДляВыбораФайла.Показать(Новый ОписаниеОповещения("ВыполняемыеЗадачиАдресОткрытиеЗавершение", ЭтаФорма, Новый Структура("ДиалогДляВыбораФайла, ТекущиеДанные", ДиалогДляВыбораФайла, ТекущиеДанные)));
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполняемыеЗадачиАдресОткрытиеЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	ДиалогДляВыбораФайла = ДополнительныеПараметры.ДиалогДляВыбораФайла;
	ТекущиеДанные  = ДополнительныеПараметры.ТекущиеДанные;
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		ТекущиеДанные.Адрес = ДиалогДляВыбораФайла.Каталог;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполняемыеЗадачиКодИмяОтчетаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = ЛОЖЬ;
	ТекущиеДанные = Элементы.ВыполняемыеЗадачи.ТекущиеДанные;
	ПоказатьВводЗначения(Новый ОписаниеОповещения("ВыполняемыеЗадачиКодИмяОтчетаНачалоВыбораЗавершение", ЭтаФорма, Новый Структура("ТекущиеДанные, ТекстПрограммыИзСправочника", ТекущиеДанные, ТекстПрограммыИзСправочника)), ТекстПрограммыИзСправочника, ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Укажите текст программы.."));
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполняемыеЗадачиКодИмяОтчетаНачалоВыбораЗавершение(Значение, ДополнительныеПараметры) Экспорт
	
	ТекущиеДанные = ДополнительныеПараметры.ТекущиеДанные;
	ТекстПрограммыИзСправочника = ?(Значение = Неопределено, ДополнительныеПараметры.ТекстПрограммыИзСправочника, Значение);
	Если (Значение <> Неопределено) Тогда
		ТекущиеДанные.КодИмяОтчета = ЗагрузитьТекстИзСправочника(ТекстПрограммыИзСправочника);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполняемыеЗадачиКодИмяОтчетаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = ЛОЖЬ;
	ТекущиеДанные 		 = Элементы.ВыполняемыеЗадачи.ТекущиеДанные;
	КодИмяОтчета 		 = ТекущиеДанные.КодИмяОтчета;
	ПоказатьВводСтроки(Новый ОписаниеОповещения("ВыполняемыеЗадачиКодИмяОтчетаОткрытиеЗавершение", ЭтаФорма, Новый Структура("ТекущиеДанные, КодИмяОтчета", ТекущиеДанные, КодИмяОтчета)), КодИмяОтчета, ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Введите код / имя отчета") + ": ", , ИСТИНА);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполняемыеЗадачиКодИмяОтчетаОткрытиеЗавершение(Строка, ДополнительныеПараметры) Экспорт
	
	ТекущиеДанные = ДополнительныеПараметры.ТекущиеДанные;
	КодИмяОтчета = ?(Строка = Неопределено, ДополнительныеПараметры.КодИмяОтчета, Строка);
	Если (Строка <> Неопределено) Тогда
		ТекущиеДанные.КодИмяОтчета = КодИмяОтчета;
		ТекущиеДанные.КодВариантОтчета = "";
		ТекущиеДанные.ВариантОтчета = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполняемыеЗадачиТипЗадачиПриИзменении(Элемент)
	
	Если НЕ ФайловаяБД Тогда
		ТекущиеДанные = Элементы.ВыполняемыеЗадачи.ТекущиеДанные;
		
		Если НЕ ТекущиеДанные = Неопределено
			И ЗначениеЗаполнено(ТекущиеДанные.ТипЗадачи)
			И (ТекущиеДанные.ТипЗадачи = ПредопределенноеЗначение("Перечисление.ТипыЗадач.РезервированиеКаталогаСБазойДанных")
			ИЛИ ТекущиеДанные.ТипЗадачи = ПредопределенноеЗначение("Перечисление.ТипыЗадач.РезервированиеКаталогаСБазойДанныхВАрхив")) Тогда
			
			ОбщегоНазначения.СообщитьПользователю(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Для не-файловой базы данных резервирование каталога недоступно!"));
			ТекущиеДанные.ТипЗадачи = ПредопределенноеЗначение("Перечисление.ТипыЗадач.ПустаяСсылка");
		КонецЕсли;
	КонецЕсли;
	
	ВидимостьДополнительныхКолонок();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ИзменитьСледующийПериод();
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзображение(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ДобавитьИзображениеЗавершение", ЭтаФорма), ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Сначала следует записать этот элемент. Записать?"), РежимДиалогаВопрос.ДаНет);
		
		Возврат;
	КонецЕсли;
	
	ДобавитьИзображениеФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзображениеЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Записать();
	КонецЕсли;
	
	ДобавитьИзображениеФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзображениеФрагмент()
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрыФормы = Новый Структура("СвязанныйОбъект", Объект.Ссылка);
		ОткрытьФорму("Справочник.Изображения.ФормаОбъекта", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСебя(Команда)
	НесколькоПолучателейПриИзмененииНаСервере(ИСТИНА);
КонецПроцедуры

&НаКлиенте
Процедура ДополнительнаяИнформацияВидИнформацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура("ТипВладельца", ДопИнфоТипВладельца);
	ФормаВыбораВидаИнформации = ПолучитьФорму("Справочник.ДополнительныеРеквизиты.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
	ВидИнформации = ФормаВыбораВидаИнформации.ОткрытьМодально();
	Если ЗначениеЗаполнено(ВидИнформации) Тогда
		Элементы.ДополнительнаяИнформация.ТекущиеДанные.ВидИнформации = ВидИнформации;
		Элементы.ДополнительнаяИнформация.ТекущиеДанные.Информация = ОбщийМодульКлиент.ПолучитьЗначениеПоУмолчаниюПоляДополнительнойИнформации(ВидИнформации);
		СтандартнаяОбработка = ЛОЖЬ;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительнаяИнформацияПриИзменении(Элемент)
	
	ДопИнформацияИзменена = ИСТИНА;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавтраВЭтоВремя(Команда)
	
	Объект.Дата = ТекущаяДата() + 3600 * 24;
	ИзменитьСледующийПериод();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавтраУтром(Команда)
	
	Объект.Дата = КонецДня(ТекущаяДата()) + 3600 * 9;
	ИзменитьСледующийПериод();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция   ЗагрузитьТекстИзСправочника(Знач ТекстЭлементСправочника)
	
	Результат = Неопределено;
	
	Если ЗначениеЗаполнено(ТекстЭлементСправочника) Тогда
		Результат = ТекстЭлементСправочника.ТекстПрограммы + Символы.ПС +
		ТекстЭлементСправочника.ТекстПрограммы2 + Символы.ПС +
		ТекстЭлементСправочника.ТекстПрограммы3;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция   ИзменитьСледующийПериод()
	
	Если НЕ ЗначениеЗаполнено(Объект.ПовторятьКаждыеЧислоЧасов)
		ИЛИ не ЗначениеЗаполнено(Объект.Дата) Тогда
		
		Элементы.СледующийПериод.Видимость = ЛОЖЬ;
	Иначе
		СледующийПериод = ?(ЗначениеЗаполнено(Объект.ДатаПоследнегоНапоминания) И (Объект.ДатаПоследнегоНапоминания > Объект.Дата), Объект.ДатаПоследнегоНапоминания, Объект.Дата) + Объект.ПовторятьКаждыеЧислоЧасов * 3600;
		Элементы.СледующийПериод.Видимость = ИСТИНА;
	КонецЕсли;
	
	ЧислоМинут = (Объект.ПовторятьКаждыеЧислоЧасов - цел(Объект.ПовторятьКаждыеЧислоЧасов)) * 60;
	
	Если ЧислоМинут  > 0
		И ЧислоМинут < 1 Тогда
		
		ЧислоМинут = 1;
	КонецЕсли;
	
	Частота = Формат(ЧислоМинут, "ЧЦ=4; ЧГ=0") + " m";
	Элементы.Частота.Видимость = НЕ цел(Объект.ПовторятьКаждыеЧислоЧасов) = Объект.ПовторятьКаждыеЧислоЧасов;
	
КонецФункции // ИзменитьСледующийПериод

&НаКлиенте
Процедура ИзображенияПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("СписокПриАктивизацииСтрокиОповещение", 0.2, ИСТИНА);
КонецПроцедуры

&НаКлиенте
Процедура КаждуюНеделю(Команда)
	
	Объект.ПовторятьКаждыеЧислоЧасов = 24 * 7;
	ИзменитьСледующийПериод();
	
КонецПроцедуры

&НаКлиенте
Процедура Каждые15минут(Команда)
	
	Объект.ПовторятьКаждыеЧислоЧасов = 0.25;
	ИзменитьСледующийПериод();
	
КонецПроцедуры

&НаКлиенте
Процедура Каждые30Минут(Команда)
	
	Объект.ПовторятьКаждыеЧислоЧасов = 0.5;
	ИзменитьСледующийПериод();
	
КонецПроцедуры

&НаКлиенте
Процедура КаждыйГод(Команда)
	
	Объект.ПовторятьКаждыеЧислоЧасов = 24 * 7 * 365.25;
	ИзменитьСледующийПериод();
	
КонецПроцедуры

&НаКлиенте
Процедура КаждыйДень(Команда)
	
	Объект.ПовторятьКаждыеЧислоЧасов = 24;
	ИзменитьСледующийПериод();
	
КонецПроцедуры

&НаКлиенте
Процедура КаждыйМесяц(Команда)
	
	Объект.ПовторятьКаждыеЧислоЧасов = 24 * 30.4375;
	ИзменитьСледующийПериод();
	
КонецПроцедуры

&НаКлиенте
Процедура КаждыйЧас(Команда)
	
	Объект.ПовторятьКаждыеЧислоЧасов = 1;
	ИзменитьСледующийПериод();
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Объект.Наименование = Наименование;
	НаименованиеИзменяли = ИСТИНА;
	Если ПереводитьНаименованияАвтоматически Тогда
		ОбщийМодульКлиент.ПеревестиНаименованияАвтоматически(Наименование, НаименованияНаДругихЯзыках);
		БылиИзмененияЗначенияПолейНаЯзыках = ИСТИНА;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованияНаДругихЯзыкахНаЯзыкеПриИзменении(Элемент)
	БылиИзмененияЗначенияПолейНаЯзыках = ИСТИНА;
КонецПроцедуры

&НаКлиенте
Процедура НаименованияНаДругихЯзыкахПриИзменении(Элемент)
	БылиИзмененияЗначенияПолейНаЯзыках = ИСТИНА;
КонецПроцедуры

&НаКлиенте
Процедура НесколькоПолучателейПриИзменении(Элемент)
	
	Если НесколькоПолучателей Тогда
		Элементы.Получатели.Видимость = ИСТИНА;
		Элементы.ГоризонтПолучателей.Видимость = ЛОЖЬ;
		НесколькоПолучателейПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НесколькоПолучателейПриИзмененииНаСервере(Знач ДобавитьСебя = ЛОЖЬ)
	
	Если ЗначениеЗаполнено(Объект.Получатель) Тогда
		Объект.Получатели.Очистить();
		СтрокаПолучателей = Объект.Получатели.Добавить();
		Если ДобавитьСебя Тогда
			СтрокаПолучателей.Получатель = ПараметрыСеанса.ТекущийПользователь;
		Иначе
			СтрокаПолучателей.Получатель = Объект.Получатель;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если НЕ ЗначениеЗаполнено(Объект.ТекстЗаметки)
		И ЗначениеЗаполнено(Объект.Наименование) Тогда
		
		Объект.ТекстЗаметки = Объект.Наименование;
	ИначеЕсли НЕ ЗначениеЗаполнено(Объект.Наименование)
		И ЗначениеЗаполнено(Объект.ТекстЗаметки) Тогда
		
		Объект.Наименование = Объект.ТекстЗаметки;
	ИначеЕсли НЕ ЗначениеЗаполнено(Объект.ТекстЗаметки)
		И НЕ ЗначениеЗаполнено(Объект.Наименование) Тогда
		
		ОбщегоНазначения.СообщитьПользователю(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Нет текста заметки!"), Объект, "Наименование");
		Отказ = ИСТИНА;
	ИначеЕсли НЕ Объект.ТипЗаметки = ТипЗаметка
		И НЕ ЗначениеЗаполнено(Объект.Дата) Тогда
		
		Если Вопрос(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Дата не указана. Это просто заметка?"), РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
			Объект.ТипЗаметки = ТипЗаметка;
		Иначе
			Отказ = ИСТИНА;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ Отказ
		И Объект.Актуально
		И НЕ Объект.ТипЗаметки = ПредопределенноеЗначение("Перечисление.ТипыЗаметок.Заметка")
		И Объект.Дата < ОбщийМодульКлиент.ПользователяТекущаяДата()
		И Объект.ПовторятьКаждыеЧислоЧасов = 0 Тогда
		
		ОбщегоНазначения.СообщитьПользователю(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Дата в прошлом") + ": " + формат(Объект.Дата, "ДФ='dd.MM.yy ЧЧ:мм'") + " " + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Задайте дату в будущем."), Объект, "Дата");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПовторятьКаждыеЧислоЧасовПриИзменении(Элемент)
	
	ИзменитьСледующийПериод();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучателиПриИзменении(Элемент)
	
	Если Объект.Получатели.Количество() = 1
		И ЗначениеЗаполнено(Объект.Получатели[0].Получатель) Тогда
		
		Объект.Получатель = Объект.Получатели[0].Получатель;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	глЧислоОбъектов = глЧислоОбъектов + 1; глПроверятьСообщения = ИСТИНА;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПоддержкаДругихЯзыков Тогда
		Если НаименованиеИзменяли Тогда
			ОбщийМодульСервер.обновитьЗначениеНаЯзыке(Объект.Ссылка, Наименование);
		КонецЕсли;
		
		Если БылиИзмененияЗначенияПолейНаЯзыках Тогда
			Для Каждого СтрокаЯзыка Из НаименованияНаДругихЯзыках Цикл
				ОбщийМодульСервер.обновитьЗначениеНаЯзыке(Объект.Ссылка, СтрокаЯзыка.НаЯзыке, "Наименование", СтрокаЯзыка.Язык);
			КонецЦикла;
			
			ОбновитьПовторноИспользуемыеЗначения();
		КонецЕсли;
	КонецЕсли;
	
	Если ДопИнформацияИзменена Тогда
		ОбщийМодульСервисСервер.ЗаписатьДополнительнуюИнформацию(Объект.Ссылка, ДополнительнаяИнформация);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеНаДругихЯзыках(Команда)
	
	Элементы.НаименованияНаДругихЯзыках.Видимость  = НЕ Элементы.НаименованияНаДругихЯзыках.Видимость;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 1, Объект.Ссылка, ИСТИНА);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 0, Объект.ссылка, ИСТИНА);
	ПриСменеТипаЗаметки();
	ИзменитьСледующийПериод();
	ВидимостьДополнительныхКолонок();
	ВидимостьПолучателей();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриСменеТипаЗаметки()
	
	Если НЕ ЗначениеЗаполнено(Объект.ТипЗаметки) Тогда
		Объект.ТипЗаметки = ТипЗаметка ;
	КонецЕсли;
	
	Если Объект.ТипЗаметки = ТипЗаметка Тогда
		 Элементы.НаименованиеЗаметки.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Наименование заметки");
	ИначеЕсли Объект.ТипЗаметки = ТипЗадача Тогда
		 Элементы.НаименованиеЗаметки.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Наименование задачи");
	Иначе
		 Элементы.НаименованиеЗаметки.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Наименование напоминания");
	КонецЕсли;
	
	Элементы.Дата.АвтоОтметкаНезаполненного = НЕ Объект.ТипЗаметки = ТипЗаметка;
	Элементы.ГруппаДата.Видимость 			= НЕ Объект.ТипЗаметки = ТипЗаметка;
	Элементы.ВыполняемыеЗадачи.Видимость	= Объект.ТипЗаметки = ТипЗадача;
	Элементы.ТекстЗаметки.Видимость			= НЕ Объект.ТипЗаметки = ТипЗадача;
	ВидимостьПолучателей()
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Отказ Тогда
		Если ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьСобственныйПереводЭлементовИнтерфейса") Тогда
			ОбщийМодульСервер.ПеревестиРеквизитыФормы(ЭтаФорма);
		КонецЕсли;
		
		ЗначениеЗаполненоЗаписьСсылка = ЗначениеЗаполнено(Объект.Ссылка);
		
		Если ЗначениеЗаполнено(Параметры.ТипЗаметки) Тогда
			Объект.ТипЗаметки 	= Параметры.ТипЗаметки;
		ИначеЕсли НЕ ЗначениеЗаполнено(Объект.ТипЗаметки) Тогда
			Объект.ТипЗаметки 	= Перечисления.ТипыЗаметок.Заметка ;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
			Объект.Актуально 	= ИСТИНА;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Параметры.ПараметрКоманды) Тогда
			Объект.Актуально = ИСТИНА;
			Объект.Причина 	 = Параметры.ПараметрКоманды;
			ТипПараметра 	 = ТипЗнч(Параметры.ПараметрКоманды);
			
			Если ТипПараметра = Тип("ДокументСсылка.РасходыТовара") Тогда
				Объект.СвязаноС = Параметры.ПараметрКоманды.КлиентПоставщик;
				Объект.Комментарий = Параметры.ПараметрКоманды.Комментарий;
			ИначеЕсли ТипПараметра = Тип("ДокументСсылка.ДвиженияДенег") Тогда
				Если НЕ Параметры.ПараметрКоманды.НачислениеИВыплатаЗаработнойПлаты Тогда
					Объект.СвязаноС = Параметры.ПараметрКоманды.КлиентПоставщик;
				КонецЕсли;
				
				Объект.Комментарий = Параметры.ПараметрКоманды.Комментарий;
			ИначеЕсли ТипПараметра = Тип("ДокументСсылка.ПоступленияТовара") Тогда
				Объект.СвязаноС = Параметры.ПараметрКоманды.КлиентПоставщик;
			ИначеЕсли ТипПараметра = Тип("ДокументСсылка.Инвентаризации") Тогда
				Объект.СвязаноС = Параметры.ПараметрКоманды.Склад;
				Объект.Комментарий = Параметры.ПараметрКоманды.Комментарий;
			ИначеЕсли ТипПараметра = Тип("ДокументСсылка.КорректировкиИРегистрацияОстатков") Тогда
				Объект.Комментарий = Параметры.ПараметрКоманды.Комментарий;
			ИначеЕсли ТипПараметра = Тип("ДокументСсылка.ПеремещенияТовара") Тогда
				Объект.СвязаноС = Параметры.ПараметрКоманды.СкладКуда;
				Объект.Комментарий = "" + Параметры.ПараметрКоманды.Склад;
			ИначеЕсли ТипПараметра = Тип("ДокументСсылка.ПланыПродаж") Тогда
				Объект.Комментарий = Параметры.ПараметрКоманды.Комментарий;
			ИначеЕсли ТипПараметра = Тип("ДокументСсылка.УстановкиЦен") Тогда
				Объект.СвязаноС = Параметры.ПараметрКоманды.ВидЦен;
				Объект.Комментарий = Параметры.ПараметрКоманды.Комментарий;
			ИначеЕсли ТипПараметра = Тип("СправочникСсылка.ВидыЦен") Тогда
				Объект.Комментарий = Параметры.ПараметрКоманды.Комментарий;
			ИначеЕсли ТипПараметра = Тип("СправочникСсылка.Договора") Тогда
				Объект.СвязаноС = Параметры.ПараметрКоманды.КлиентПоставщик;
			ИначеЕсли ТипПараметра = Тип("СправочникСсылка.Клиенты") Тогда
				Объект.Комментарий = ОбщийМодульСервер.ПолучитьТекстТелефонов(Параметры.ПараметрКоманды);
			ИначеЕсли ТипПараметра = Тип("СправочникСсылка.Номенклатура") Тогда
			ИначеЕсли ТипПараметра = Тип("СправочникСсылка.НоменклатурныеГруппы") Тогда
			ИначеЕсли ТипПараметра = Тип("СправочникСсылка.Организации") Тогда
				Объект.Комментарий = ОбщийМодульСервер.ПолучитьТекстТелефонов(Параметры.ПараметрКоманды);				
			ИначеЕсли ТипПараметра = Тип("СправочникСсылка.ПартииПриобретения") Тогда
				Объект.Комментарий = "" + Параметры.ПараметрКоманды.Поставщик;
			ИначеЕсли ТипПараметра = Тип("СправочникСсылка.ПодсистемаИЭШаблоны") Тогда
			ИначеЕсли ТипПараметра = Тип("СправочникСсылка.Поставщики") Тогда
				Объект.Комментарий = ОбщийМодульСервер.ПолучитьТекстТелефонов(Параметры.ПараметрКоманды);
			ИначеЕсли ТипПараметра = Тип("СправочникСсылка.Производители") Тогда
			ИначеЕсли ТипПараметра = Тип("СправочникСсылка.Регионы") Тогда
			ИначеЕсли ТипПараметра = Тип("СправочникСсылка.Склады") Тогда
			ИначеЕсли ТипПараметра = Тип("СправочникСсылка.Сотрудники") Тогда
				Объект.Комментарий = ОбщийМодульСервер.ПолучитьТекстТелефонов(Параметры.ПараметрКоманды);
			ИначеЕсли ТипПараметра = Тип("СправочникСсылка.СтатьиДвиженияДенег") Тогда
			ИначеЕсли ТипПараметра = Тип("СправочникСсылка.ФормулыОписания") Тогда
				Объект.Комментарий = Параметры.ПараметрКоманды.Формула;
			КонецЕсли;
		КонецЕсли;
		
		Если Объект.Предопределенный
			И Объект.ВыполняемыеЗадачи.Количество() = 0 Тогда
			
			Задача = Объект.ВыполняемыеЗадачи.Добавить();
			Задача.ТипЗадачи  = перечисления.ТипыЗадач.РезервированиеКаталогаСБазойДанных;
			Задача.Адрес      = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Укажите здесь адрес (каталог) резервирования!");
			Объект.Актуально  = ЛОЖЬ;
			Объект.ТипЗаметки = Перечисления.ТипыЗаметок.Задача;
		КонецЕсли;
		
		ТипЗаметка 	= Перечисления.ТипыЗаметок.Заметка ;
		ТипЗадача	= Перечисления.ТипыЗаметок.Задача;
		ДопИнфоТипВладельца = ОбщийМодульПовтор.ПолучитьТипВладельца(Объект.Ссылка);
		ОбщийМодульСервисСервер.ЗаполнитьДополнительнуюИнформацию(Объект.Ссылка, ДополнительнаяИнформация, ДопИнфоТипВладельца);
		
		ПоддержкаДругихЯзыков = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ПоддерживатьИныеЯзыкиКромеРусского")
		И ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ПоддерживатьНаименованияНаДругихЯзыкахКромеРусского", ИСТИНА);
		
		Если ПоддержкаДругихЯзыков
			И ЗначениеЗаполненоЗаписьСсылка Тогда
			
			Наименование = ОбщийМодульПовтор.получитьПредставлениеНаЯзыке(Объект.Ссылка);
			
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ ЗначенияНаДругихЯзыках.Язык,
			|	ЗначенияНаДругихЯзыках.НаЯзыке,
			|	ЗначенияНаДругихЯзыках.Поле
			|ИЗ РегистрСведений.ЗначенияНаДругихЯзыках КАК ЗначенияНаДругихЯзыках
			|ГДЕ ЗначенияНаДругихЯзыках.ОбъектБазыДанных = &ОбъектБазыДанных";
			Запрос.УстановитьПараметр("ОбъектБазыДанных", Объект.Ссылка);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			Если НЕ РезультатЗапроса.Пустой() Тогда
				ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					Если ВыборкаДетальныеЗаписи.Поле = "Наименование" Тогда
						СтрокаЯзыка = НаименованияНаДругихЯзыках.Добавить();
						СтрокаЯзыка.Язык 	= ВыборкаДетальныеЗаписи.Язык;
						СтрокаЯзыка.НаЯзыке = ВыборкаДетальныеЗаписи.НаЯзыке;
					КонецЕсли;
					
				КонецЦикла;
			КонецЕсли;
		Иначе
			Наименование = Объект.Наименование;
		КонецЕсли;
		
		Если ПоддержкаДругихЯзыков Тогда
			ПереводитьНаименованияАвтоматически = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьРегистрТекстовНаДругихЯзыкахДляАвтоматическогоПереводаНаименований");
			Элементы.НаименованияНаДругихЯзыкахЯзык.РежимВыбораИзСписка = ИСТИНА;
			ОбщийМодульПовтор.УстановитьСписокДоступныхЯзыков(Элементы.НаименованияНаДругихЯзыкахЯзык.СписокВыбора, ИСТИНА);
		КонецЕсли;
		
		ПользователиСправочник = Справочники.Пользователи.Выбрать();
		
		Пока ПользователиСправочник.Следующий() Цикл
			Если НЕ ПользователиСправочник.Предопределенный Тогда
				Элементы.Получатель.СписокВыбора.Добавить(ПользователиСправочник.Ссылка, ПользователиСправочник.Наименование);
				Элементы.ПолучателиПолучатель.СписокВыбора.Добавить(ПользователиСправочник.Ссылка, ПользователиСправочник.Наименование);
			КонецЕсли;
		КонецЦикла;
		
		Изображения.Параметры.УстановитьЗначениеПараметра("Владелец", Объект.Ссылка);
		СтрокаСоединенияСБД = СтрокаСоединенияИнформационнойБазы();
		ФайловаяБД = НЕ Найти(ВРег(СтрокаСоединенияСБД), "FILE=") = 0;
		СписокВыбораПроизвольныхОтчетов = Новый СписокЗначений;
		
		Для Каждого Отчет Из МетаДанные.Отчеты Цикл
			Попытка
				ОсновнаяСхемаКомпоновкиДанных = Отчет.ОсновнаяСхемаКомпоновкиДанных;
				Если НЕ ОсновнаяСхемаКомпоновкиДанных = Неопределено Тогда
					Элементы.ВыполняемыеЗадачиНаименованиеОтчета.СписокВыбора.Добавить(Отчет.Имя, ОбщийМодульПовтор.ПолучитьТекстНаЯзыке(Отчет.Синоним));
				КонецЕсли;				
			Исключение
			КонецПопытки;
		КонецЦикла;
		
		Если ПараметрыСеанса.ВерсияПриложения < 803050000
			ИЛИ ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ВместоТумблеровПоказыватьГалочки") Тогда
			
			Если НЕ ПараметрыСеанса.ВерсияПриложения < 803050000 Тогда
				Выполнить(" Элементы.Актуально.ВидФлажка = ВидФлажка.Флажок; ");
			КонецЕсли;
			
			Элементы.Актуально.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Актуально");
			Элементы.Актуально.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
		КонецЕсли;
	КонецЕсли;
	
	ОбщийМодульСервисСервер.ОбработатьНовуюФорму(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура РеакцияНаПрочиеСобытияФормы(КомандаСобытияФормы) Экспорт // Для реакции на события для запрограммированных элементов
	ОбщийМодульКлиент.РеакцияНаПрочиеСобытияФормы(КомандаСобытияФормы, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура СегодняВечером(Команда)
	
	Дата = ТекущаяДата();
	Объект.Дата = дата(год(Дата), месяц(Дата), День(дата), 17, 50, 0);
	ИзменитьСледующийПериод();
	
КонецПроцедуры

&НаКлиенте
Процедура Сейчас(Команда)
	
	Объект.Дата = ТекущаяДата();
	ИзменитьСледующийПериод();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтрокиОповещение()
	
	ТекущийЭлементСписка = Элементы.Изображения.ТекущаяСтрока;
	
	Если ЗначениеЗаполнено(ТекущийЭлементСписка) Тогда
		СтруктураИзображения = ОбщийМодульСервер.ПолучитьСтруктуруИзображения(ТекущийЭлементСписка);
		ПодСсылку = СтруктураИзображения.ПодСсылку;
		Элементы.СсылкаНаИзображение.Видимость    = не СтруктураИзображения.ИзображениеВБазеДанных;
		Элементы.ИзображениеВБазеДанных.Видимость = СтруктураИзображения.ИзображениеВБазеДанных;
		
		Если СтруктураИзображения.РазмерПриОтображении = 1 Тогда
			Элементы.ИзображениеВБазеДанных.РазмерКартинки = РазмерКартинки.АвтоРазмер
		ИначеЕсли СтруктураИзображения.РазмерПриОтображении = 2 Тогда
			
			Элементы.ИзображениеВБазеДанных.РазмерКартинки = РазмерКартинки.Пропорционально
		ИначеЕсли СтруктураИзображения.РазмерПриОтображении = 3 Тогда
			
			Элементы.ИзображениеВБазеДанных.РазмерКартинки = РазмерКартинки.Растянуть
		ИначеЕсли СтруктураИзображения.РазмерПриОтображении = 4 Тогда
			
			Элементы.ИзображениеВБазеДанных.РазмерКартинки = РазмерКартинки.РеальныйРазмер
		ИначеЕсли СтруктураИзображения.РазмерПриОтображении = 5 Тогда
			
			Элементы.ИзображениеВБазеДанных.РазмерКартинки = РазмерКартинки.Черепица
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипЗаметкиПриИзменении(Элемент)
	ПриСменеТипаЗаметки();
КонецПроцедуры

&НаКлиенте
Процедура Через15Минут(Команда)
	
	Объект.Дата = ТекущаяДата() + 15 * 60;
	ИзменитьСледующийПериод();
	
КонецПроцедуры

&НаКлиенте
Процедура Через30Минут(Команда)
	
	Объект.Дата = ТекущаяДата() + 30 * 60;
	ИзменитьСледующийПериод();
	
КонецПроцедуры

&НаКлиенте
Процедура ЧерезМесяц(Команда)
	
	Объект.Дата = ДобавитьМесяц(ТекущаяДата(), 1);
	ИзменитьСледующийПериод();
	
КонецПроцедуры

&НаКлиенте
Процедура ЧерезНеделю(Команда)
	
	Объект.Дата = ТекущаяДата() + 3600 * 24 * 7;
	ИзменитьСледующийПериод();
	
КонецПроцедуры

&НаКлиенте
Процедура ЧерезЧас(Команда)
	
	Объект.Дата = ТекущаяДата() + 3600;
	ИзменитьСледующийПериод();
	
КонецПроцедуры

&НаКлиенте
Процедура АвтоНаименование(Команда)
	
	НаименованиеЗаписи = "";
	
	Если Объект.ТипЗаметки = ПредопределенноеЗначение("Перечисление.ТипыЗаметок.Задача") Тогда
		НаименованиеЗаписи = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Задача");
		
		Если НЕ Объект.ВыполняемыеЗадачи.Количество() = 0 Тогда
			НаименованиеЗаписи = НаименованиеЗаписи + " " + СокрЛП("" + Объект.ВыполняемыеЗадачи[0].ТипЗадачи + " " + Объект.ВыполняемыеЗадачи[0].КодИмяОтчета);
		КонецЕсли;
		
	ИначеЕсли Объект.ТипЗаметки = ПредопределенноеЗначение("Перечисление.ТипыЗаметок.Напоминание") Тогда
		НаименованиеЗаписи = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Напоминание");
	Иначе
		НаименованиеЗаписи = СокрЛП(Прав(СтрЗаменить(Объект.ТекстЗаметки, Символы.ПС, " "), 120));
	КонецЕсли;
	
	Если НЕ Объект.Получатели.Количество() = 0 Тогда
		НаименованиеЗаписи = НаименованиеЗаписи + " " + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("для нескольких получателей");
	ИначеЕсли ЗначениеЗаполнено(Объект.Получатель) Тогда
		НаименованиеЗаписи = НаименованиеЗаписи + " " + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("для") + " " + СокрЛП(Объект.Получатель);
	КонецЕсли;
	
	Если НЕ Объект.Дата = '00010101000000' Тогда
		НаименованиеЗаписи = НаименованиеЗаписи + " " + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("на") + " " + Формат(Объект.Дата, "ДФ='dd.MM.yy ЧЧ:мм'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ПовторятьКаждыеЧислоЧасов) Тогда
		НаименованиеЗаписи = НаименованиеЗаписи + " " + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("с повтором каждые") + " " + СокрЛП(Объект.ПовторятьКаждыеЧислоЧасов) + " " + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("ч.");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Причина) Тогда
		НаименованиеЗаписи = СокрЛП(НаименованиеЗаписи + " (" + Объект.Причина + ")");
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(НаименованиеЗаписи)
		И НЕ НаименованиеЗаписи = Наименование Тогда
		
		Наименование = НаименованиеЗаписи;
		Объект.Наименование = Наименование;
		НаименованиеИзменяли = ИСТИНА;
		
		Если ПереводитьНаименованияАвтоматически Тогда
			ОбщийМодульКлиент.ПеревестиНаименованияАвтоматически(НаименованиеЗаписи, НаименованияНаДругихЯзыках);
			БылиИзмененияЗначенияПолейНаЯзыках = ИСТИНА;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстЗаметкиПриИзменении(Элемент)
	глВремяПоследнегоСобытия = ТекущаяДата();
КонецПроцедуры

&НаКлиенте
Процедура ВыполняемыеЗадачиПриИзменении(Элемент)
	глВремяПоследнегоСобытия = ТекущаяДата();
КонецПроцедуры

&НаКлиенте
Процедура ВыполняемыеЗадачиВариантОтчетаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = ЛОЖЬ;
	ТекущиеДанные = Элементы.ВыполняемыеЗадачи.ТекущиеДанные;	
	СписокВариантовОтчета = ОбщийМодульСервисСервер.ПолучитьСписокВариантовОтчета(ТекущиеДанные.КодИмяОтчета);
	
	РезультатВыбора = Неопределено;
	
	ПоказатьВыборИзСписка(Новый ОписаниеОповещения("ВыполняемыеЗадачиВариантОтчетаНачалоВыбораЗавершение", ЭтотОбъект), СписокВариантовОтчета, Элемент, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполняемыеЗадачиВариантОтчетаНачалоВыбораЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	ТекущиеДанные = Элементы.ВыполняемыеЗадачи.ТекущиеДанные;	
	
	РезультатВыбора = ВыбранныйЭлемент;
	ТекущиеДанные.КодВариантОтчета = РезультатВыбора.Значение;
	ДанныеВыбора = РезультатВыбора.Представление;
	ТекущиеДанные.ВариантОтчета = ДанныеВыбора;
	
	Элементы.ВыполняемыеЗадачи.ЗакончитьРедактированиеСтроки(ЛОЖЬ);
	
КонецПроцедуры
