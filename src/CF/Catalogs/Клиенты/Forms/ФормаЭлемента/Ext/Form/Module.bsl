//sza131124-2328 SZA: 
//sza130905-1706 : 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)          // ПРИ СОЗДАНИИ НА СЕРВЕРЕ
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Если НЕ ЗначениеЗаполнено(Объект.ПравовойСтатус) Тогда
			Объект.ПравовойСтатус = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ПравовойСтатусПоУмолчанию");
		КонецЕсли;		
		
		Если НЕ ЗначениеЗаполнено(Объект.Регион) Тогда
			Объект.Регион = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("РегионПоУмолчанию");
		КонецЕсли;
		ЭтаФорма.Элементы.ГруппаВзаиморасчеты.видимость = ложь;
		ЭтаФорма.Элементы.ДокументыКлиента.Видимость 	= ложь;
		
	Иначе
		Элементы.ДокументыКлиента.Видимость = ОбщийМодульСервер.УКонтрагентаЕстьДокументы(Объект.Ссылка);
		ПроверитьДеньРождения();
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Родитель)
		и ЗначениеЗаполнено(Объект.Родитель.ВидЦен) Тогда
		
		Элементы.ВидЦен.ТолькоПросмотр = Истина;
		Элементы.ВидЦен.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Вид цен Группы Клиентов:");
		
		Если не Объект.ВидЦен = Объект.Родитель.ВидЦен Тогда
			Объект.ВидЦен = Объект.Родитель.ВидЦен;
		КонецЕсли;			
	КонецЕсли;
	
	ДокументыКлиента.Параметры.УстановитьЗначениеПараметра("Клиент", Объект.Ссылка);	
	ДокументыКлиента.Параметры.УстановитьЗначениеПараметра("Видприход", ВидДвиженияНакопления.Приход);		
	ЗаказыКлиента.Параметры.УстановитьЗначениеПараметра("КлиентПоставщик", Объект.Ссылка);	
	ЗаказыКлиента.Параметры.УстановитьЗначениеПараметра("Дата", ОбщийМодульСервисСервер.ПользователяТекущаяДата());	
	Изображения.Параметры.УстановитьЗначениеПараметра("Владелец", Объект.Ссылка);
	ДоговораКлиента.Параметры.УстановитьЗначениеПараметра("КлиентПоставщик", Объект.Ссылка);	
	
	элементы.ДоговораКлиента.Видимость 		= ПараметрыСеанса.ВестиУчетОтдельныхДоговоровСИКонтрагентамиПС И НЕ ОбщийМодульСервер.ЧислоДоговоровСКонтрагентом(Объект.Ссылка) = 0;
	элементы.ЗаказыКлиента.Видимость 		= ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетЗаказовКлиентов");
	Элементы.СоздатьНовыйДоговор.Видимость  = ПараметрыСеанса.ВестиУчетОтдельныхДоговоровСИКонтрагентамиПС И ЗначениеЗаполнено(Объект.Ссылка);
	
	ИспользоватьПодключаемоеОборудование 	= ПодключаемоеОборудованиеДСервер.ИспользоватьПодключаемоеОборудование();
	
	ИспользоватьМагнитныеКартыКлиентов 		= ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ИспользоватьМагнитныеКартыКлиентов");
	ЭтаФорма.Элементы.НомерКарты.видимость 	= ИспользоватьМагнитныеКартыКлиентов ;
	
	элементы.ГруппаПроцент.Видимость 		= параметрысеанса.ИспользоватьСкидкиПС и ПараметрыСеанса.ВключитьВозможностьУказыватьПроцентСкидкиДляКлиентов ;
	
	Если параметрысеанса.НеМожетМенятьЦены Тогда
		Элементы.ВидЦен.Доступность 		= Ложь;
		Элементы.ПроцентСкидки.Доступность 	= Ложь;
		Элементы.Процент15.Видимость 		= Ложь;
		Элементы.Процент5.Видимость 		= Ложь;
	КонецЕсли;
	
	НаименованиеОсновнойВалюты = Справочники.Валюты.ОсновнаяВалюта.Наименование ;
	ОбновитьДаныеОДолгеНаСервере();
	
	РодительПриИзмененииНаСервере();
	Элементы.ПроцентСкидки.Видимость = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ИспользоватьСкидки") 
	И ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВключитьВозможностьУказыватьПроцентСкидкиДляКлиентов");
	
    ПоддержкаДругихЯзыков = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ПоддерживатьИныеЯзыкиКромеРусского") ;
	
	если ПоддержкаДругихЯзыков 
		и ЗначениеЗаполнено(Объект.Ссылка) тогда
		
		Наименование = ОбщийМодульПовтор.получитьПредставлениеНаЯзыке(объект.Ссылка);
		НаименованиеДляПечати = ОбщийМодульПовтор.получитьПредставлениеНаЯзыке(объект.Ссылка, "НаименованиеДляПечати");
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ЗначенияНаДругихЯзыках.Язык,
		|	ЗначенияНаДругихЯзыках.НаЯзыке,
		|	ЗначенияНаДругихЯзыках.Поле
		|ИЗ РегистрСведений.ЗначенияНаДругихЯзыках КАК ЗначенияНаДругихЯзыках
		|ГДЕ ЗначенияНаДругихЯзыках.ОбъектБазыДанных = &ОбъектБазыДанных";
		
		Запрос.УстановитьПараметр("ОбъектБазыДанных", Объект.Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		если не РезультатЗапроса.Пустой() тогда
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				Если ВыборкаДетальныеЗаписи.Поле = "Наименование" Тогда
					СтрокаЯзыка = НаименованияНаДругихЯзыках.Добавить();
					СтрокаЯзыка.Язык 	= ВыборкаДетальныеЗаписи.Язык;
					СтрокаЯзыка.НаЯзыке = ВыборкаДетальныеЗаписи.НаЯзыке;	
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	иначе
		Наименование = объект.Наименование;
		НаименованиеДляПечати = Объект.НаименованиеДляПечати;
		
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
		
	объект.Наименование = Наименование;
	НаименованиеИзменяли = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеНаДругихЯзыках(Команда)
	Элементы.НаименованияНаДругихЯзыках.Видимость  = НЕ Элементы.НаименованияНаДругихЯзыках.Видимость;
КонецПроцедуры

&НаКлиенте
Процедура НаименованияНаДругихЯзыкахНаЯзыкеПриИзменении(Элемент)
	БылиИзмененияЗначенияПолейНаЯзыках = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НаименованияНаДругихЯзыкахПриИзменении(Элемент)
	БылиИзмененияЗначенияПолейНаЯзыках = Истина;
КонецПроцедуры
&НаКлиенте
Процедура НаименованиеДляПечатиНаДругихЯзыках(Команда)
	Элементы.НаименованияДляПечатиНаДругихЯзыках.Видимость = НЕ Элементы.НаименованияДляПечатиНаДругихЯзыках.Видимость;
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеДляПечатиПриИзменении(Элемент)
	
	объект.НаименованиеДляПечати = НаименованиеДляПечати;
	НаименованиеИзменяли = Истина;

КонецПроцедуры

&НаКлиенте
Процедура НаименованияДляПечатиНаДругихЯзыкахПриИзменении(Элемент)
	БылиИзмененияЗначенияПолейНаЯзыках = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НаименованияДляПечатиНаДругихЯзыкахНаЯзыкеПриИзменении(Элемент)
	БылиИзмененияЗначенияПолейНаЯзыках = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)                                                                    // ПРИ ОТКРЫТИИ	
	
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 0, Объект.Ссылка, истина);
	
	Если ИспользоватьПодключаемоеОборудование 
		И ИспользоватьМагнитныеКартыКлиентов
		И МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента () Тогда
		
		ОписаниеОшибки = "" ;
		ПоддерживаемыеТипыВО = Новый Массив ();
		ПоддерживаемыеТипыВО.Добавить("СчитывательМагнитныхКарт");
		
		Если Не МенеджерОборудованияКлиент.ПодключитьОборудованиеПоТипу(УникальныйИдентификатор, ПоддерживаемыеТипыВО, ОписаниеОшибки) Тогда
			ТекстСообщения = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("При подключении оборудования произошла ошибка:") + " " + ОписаниеОшибки + ".";
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтраницаПанели)Тогда
		
		СтраницыПанели  = Элементы.ГруппаСтраницСправа.ПодчиненныеЭлементы;
		ТекущаяСтраница = СтраницыПанели.получить(СтраницаПанели);
		элементы.ГруппаСтраницСправа.ТекущаяСтраница = ТекущаяСтраница;
	КонецЕсли;	
	
	если не ЗначениеЗаполнено(объект.Ссылка) Тогда
		ЭтаФорма.ТекущийЭлемент = Элементы.Наименование;	
	КонецЕсли;
	
	ВидимостьТаблицСправа();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()                                                  // ПРИ ЗАКРЫТИИ   	
	
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 1, Объект.Ссылка, Истина);
	
	Если ИспользоватьПодключаемоеОборудование 
		и ИспользоватьМагнитныеКартыКлиентов Тогда
		
		ПоддерживаемыеТипыВО = Новый Массив ();
		ПоддерживаемыеТипыВО.Добавить("СчитывательМагнитныхКарт");
		МенеджерОборудованияКлиент.ОтключитьОборудованиеПоТипу(УникальныйИдентификатор,ПоддерживаемыеТипыВО);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИспользоватьМагнитныеКартыКлиентов
		и Источник = "ПодключаемоеОборудование" 
		И ВводДоступен() Тогда
		
		Если ИмяСобытия = "TracksData" Тогда
			Если Параметр[1][3] <> Неопределено Тогда
				Объект.НомерКарты = Параметр[1][3][0].ДанныеДорожек[0].ЗначениеПоля;
			Иначе
				Объект.НомерКарты = Параметр[0];
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	если ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ИспользоватьМагнитныеКартыКлиентов") тогда
		
		Если не объект.НомерКарты = ""
			и НЕ ПодключаемоеОборудованиеДСервер.ПроверитьУникальностьМагнитногоКода(ДанныеФормыВЗначение(Объект, Тип("СправочникОбъект.Клиенты"))) Тогда
			
			ТекстСообщения = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Карта с данным номером уже зарегистрирована в системе");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект.Ссылка, "НомерКарты");
			Отказ = Истина;
		КонецЕсли;
		
		АдресЭП = Объект.ЭлектроннаяПочта;
		Если НЕ ПодключаемоеОборудованиеДСервер.ПроверитьКорректностьЗаписиАдресаЭлектроннойПочты(АдресЭП) Тогда
			ТекстСообщения = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Указан некорректный адрес электронной почты");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, "Элементы.ЭлектроннаяПочта");
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЭлектроннаяПочтаПриИзменении(Элемент)	
	Объект.ЭлектроннаяПочта = СокрЛП(Объект.ЭлектроннаяПочта);	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыКлиентаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(элемент.текущиеданные.регистратор) Тогда
		ОткрытьЗначение(элемент.текущиеданные.регистратор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Процент5(Команда)
	Объект.ПроцентСкидки = 5;
КонецПроцедуры

&НаКлиенте
Процедура Процент15(Команда)
	Объект.ПроцентСкидки = 15;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьФормулуТекстаЭтикетки(Команда)
	ПроверитьФормулуТекстаЭтикеткиНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПроверитьФормулуТекстаЭтикеткиНаСервере()
	
	Если ЗначениеЗаполнено(Объект.ФормулаТекстаЭтикетки) Тогда
		ОбщийМодульСервер.ПроверитьФормулу(Объект.ФормулаТекстаЭтикетки.Формула);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РодительПриИзменении(Элемент)
	РодительПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура РодительПриИзмененииНаСервере()
	
	элементы.ПроцентСкидки.ТолькоПросмотр 	= ложь;
	элементы.Процент5.Видимость 			= истина;
	элементы.Процент15.Видимость 			= истина;
	элементы.ВидЦен.ТолькоПросмотр 			= ложь;
	
	Родитель = Объект.Родитель;
	
	Если ЗначениеЗаполнено(Родитель) тогда
		
		если ЗначениеЗаполнено(Родитель.ВидЦен) Тогда
			
			элементы.ВидЦен.ТолькоПросмотр 	= истина;
			если не Объект.ВидЦен = Родитель.ВидЦен тогда
				Объект.ВидЦен = Родитель.ВидЦен;				
			КонецЕсли;
		КонецЕсли;	
		
		Если не объект.Родитель.ПроцентСкидки = 0 Тогда
			
			элементы.ПроцентСкидки.ТолькоПросмотр 	= истина;
			элементы.Процент5.Видимость 			= ложь;
			элементы.Процент15.Видимость 			= ложь;
			
			если не объект.ПроцентСкидки = Родитель.ПроцентСкидки тогда
				объект.ПроцентСкидки = Родитель.ПроцентСкидки;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтоСотрудникПриИзменении(Элемент)
	ЭтоСотрудникПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЭтоСотрудникПриИзмененииНаСервере()
	
	ВидЦенСотрудников = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВидЦенДляОтпускаТоваровСобственнымСотрудникам");
	
	Если ЗначениеЗаполнено(ВидЦенСотрудников)
		И Объект.ЭтоСотрудник Тогда
		
		объект.ВидЦен = ВидЦенСотрудников;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДаныеОДолге(Команда)
	ОбновитьДаныеОДолгеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбновитьДаныеОДолгеНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(РасчетыОстаткиИОбороты.СуммаПриход, 0) КАК Продано,
		|	ЕСТЬNULL(РасчетыОстаткиИОбороты.СуммаРасход, 0) КАК Оплачено,
		|	ЕСТЬNULL(РасчетыОстаткиИОбороты.СуммаКонечныйОстаток, 0) КАК ТекущийДолг
		|ИЗ
		|	РегистрНакопления.Расчеты.ОстаткиИОбороты(&ДатаНачала, &ДатаОкончания, , , Клиент = &Клиент) КАК РасчетыОстаткиИОбороты";
		
		Запрос.УстановитьПараметр("ДатаНачала", '00010101000000');
		Запрос.УстановитьПараметр("ДатаОкончания", ОбщийМодульСервисСервер.ПользователяТекущаяДата());
		Запрос.УстановитьПараметр("Клиент", Объект.Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		ТекущийДолг = 0;
		продано 	= 0;
		оплачено 	= 0;
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			продано 	= продано + ВыборкаДетальныеЗаписи.Продано;
			оплачено 	= оплачено + ВыборкаДетальныеЗаписи.оплачено;
			ТекущийДолг = ТекущийДолг + ВыборкаДетальныеЗаписи.ТекущийДолг;
		КонецЦикла;
		элементы.ТекущийДолг.Видимость 			= не ТекущийДолг = 0;
		элементы.НаименованиеОсновнойВалюты.Видимость = не ТекущийДолг = 0;
		элементы.Продано.Видимость 				= не Продано = 0;
		элементы.Оплачено.Видимость 			= не Оплачено = 0;
		элементы.ОбновитьДаныеОДолге.Видимость 	= не (ТекущийДолг = 0 И Оплачено = 0 И Продано = 0);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если НЕ ЗначениеЗаполнено(Объект.Наименование) 
		и ЗначениеЗаполнено(Объект.НаименованиеДляПечати) Тогда
		
		Объект.Наименование = Объект.НаименованиеДляПечати;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовыйДоговор(Команда)
	
	ПараметрыДляФормы = Новый Структура("КлиентПоставщик", Объект.Ссылка);
	ФормаДоговора = ПолучитьФорму("Справочник.Договора.ФормаОбъекта", ПараметрыДляФормы);
	ФормаДоговора.отКрыть();
	
	СоздатьНовыйДоговорНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура СоздатьНовыйДоговорНаСервере()
	элементы.ДоговораКлиента.Видимость = ПараметрыСеанса.ВестиУчетОтдельныхДоговоровСИКонтрагентамиПС И НЕ ОбщийМодульСервер.ЧислоДоговоровСКонтрагентом(Объект.Ссылка) = 0;
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере1(ТекущийОбъект, ПараметрыЗаписи)
	
	Элементы.СоздатьНовыйДоговор.Видимость = ПараметрыСеанса.ВестиУчетОтдельныхДоговоровСИКонтрагентамиПС И ЗначениеЗаполнено(Объект.Ссылка);	
	
	если ПоддержкаДругихЯзыков тогда
		если НаименованиеИзменяли тогда
			ОбщийМодульСервер.обновитьЗначениеНаЯзыке(Объект.Ссылка, Наименование);
			ОбщийМодульСервер.обновитьЗначениеНаЯзыке(Объект.Ссылка, НаименованиеДляПечати, "НаименованиеДляПечати");
		КонецЕсли;
		
		Если БылиИзмененияЗначенияПолейНаЯзыках Тогда
			Для Каждого СтрокаЯзыка Из НаименованияНаДругихЯзыках Цикл
				ОбщийМодульСервер.обновитьЗначениеНаЯзыке(Объект.Ссылка, СтрокаЯзыка.НаЯзыке, "Наименование", СтрокаЯзыка.Язык);	
			КонецЦикла;
			Для Каждого СтрокаЯзыка Из НаименованияДляПечатиНаДругихЯзыках Цикл
				ОбщийМодульСервер.обновитьЗначениеНаЯзыке(Объект.Ссылка, СтрокаЯзыка.НаЯзыке, "НаименованиеДляПечати", СтрокаЯзыка.Язык);	
			КонецЦикла;
		КонецЕсли;
	
	конецесли;
	        
КонецПроцедуры

&НаКлиенте
Процедура ДатаРожденияПриИзменении(Элемент)
	ПроверитьДеньРождения();
КонецПроцедуры

&НаСервере
Процедура ПроверитьДеньРождения()
	
	Если ЗначениеЗаполнено(Объект.ДатаРождения)
		и месяц(Объект.ДатаРождения) = Месяц(ОбщийМодульСервисСервер.ПользователяТекущаяДата())
		и День(Объект.ДатаРождения) = День(ОбщийМодульСервисСервер.ПользователяТекущаяДата()) Тогда
		
		Элементы.ДатаРожденияТорт.Видимость = Истина;
	Иначе
		Элементы.ДатаРожденияТорт.Видимость = Ложь;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СменитьВидОкна(Команда)
	
	НеПоказыватьТаблицыСправа = НЕ НеПоказыватьТаблицыСправа;
	ВидимостьТаблицСправа();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидимостьТаблицСправа()
	Элементы.ГруппаСправа.Видимость = НЕ НеПоказыватьТаблицыСправа;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	глЧислоОбъектов = глЧислоОбъектов + 1; глПроверятьСообщения = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовыйЗаказ(Команда)
	
	ПараметрыДляФормы = Новый Структура("КлиентПоставщик", Объект.Ссылка);
	ФормаЗаказа = ПолучитьФорму("Документ.РасходыТовара.ФормаОбъекта", ПараметрыДляФормы);
	ФормаЗаказа.отКрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	ТекущаяСтраница = Элементы.ГруппаСтраницСправа.ТекущаяСтраница;
	СтраницыПанели  = Элементы.ГруппаСтраницСправа.ПодчиненныеЭлементы;
	СтраницаПанели  = СтраницыПанели.Индекс(ТекущаяСтраница);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзображенияПриАктивизацииСтроки(Элемент)
	
	ТекущийЭлементСписка = Элементы.Изображения.ТекущаяСтрока;
	Если ЗначениеЗаполнено(ТекущийЭлементСписка) Тогда
		 СтруктураИзображения = ОбщийМодульСервер.ПолучитьСтруктуруИзображения(ТекущийЭлементСписка);
		 ПодСсылку = СтруктураИзображения.ПодСсылку;
		 
		 элементы.СсылкаНаИзображение.Видимость    = не СтруктураИзображения.ИзображениеВБазеДанных;
		 элементы.ИзображениеВБазеДанных.Видимость = СтруктураИзображения.ИзображениеВБазеДанных;
		 
		 если СтруктураИзображения.РазмерПриОтображении = 1 тогда
			 элементы.ИзображениеВБазеДанных.РазмерКартинки = РазмерКартинки.АвтоРазмер
		 иначеесли СтруктураИзображения.РазмерПриОтображении = 2 тогда
			 элементы.ИзображениеВБазеДанных.РазмерКартинки = РазмерКартинки.Пропорционально
		 иначеесли СтруктураИзображения.РазмерПриОтображении = 3 тогда
			 элементы.ИзображениеВБазеДанных.РазмерКартинки = РазмерКартинки.Растянуть
		 иначеесли СтруктураИзображения.РазмерПриОтображении = 4 тогда
			 элементы.ИзображениеВБазеДанных.РазмерКартинки = РазмерКартинки.РеальныйРазмер
		 иначеесли СтруктураИзображения.РазмерПриОтображении = 5 тогда
			 элементы.ИзображениеВБазеДанных.РазмерКартинки = РазмерКартинки.Черепица
		 конецесли; 		 
		 
	 КонецЕсли;
	 
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзображение(Команда)
	
	Если НЕ ЗначениеЗаполнено(объект.Ссылка) 
		и Вопрос(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Сначала следует записать этот элемент. Записать?"), РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
		
		Записать();			
	КонецЕсли;
		
	Если ЗначениеЗаполнено(объект.Ссылка) Тогда
		ПараметрыФормы = Новый Структура("СвязанныйОбъект", объект.Ссылка);
		формаИзображения = ПолучитьФорму("Справочник.Изображения.ФормаОбъекта", ПараметрыФормы);	
		формаИзображения.Открыть();
	КонецЕсли;
	
КонецПроцедуры
