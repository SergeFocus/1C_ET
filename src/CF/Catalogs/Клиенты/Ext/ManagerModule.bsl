//sza140601-2055 SZA: 
//sza131021-1345 : 

Процедура Печать(ТабДок, Ссылка) Экспорт
	
	ЭлементСправочника = ссылка[0];
	
	ТабДок.Очистить();
	
	ТабДок.Начатьавтогруппировкустрок();
	
	Макет = Справочники.Поставщики.ПолучитьМакет("Печать");
	Шапка = Макет.ПолучитьОбласть("Ш");
	Шапка.параметры.заполнить(ЭлементСправочника);
	
	СтруктураДополнительныПараметровМакета = ОбщийМодульСервер.ПолучитьСтруктуруДополнительныхПараметровМакетаПечати();
	Шапка.Параметры.ТекстВШапкеДокументовПриПечати   = СтруктураДополнительныПараметровМакета.ТекстВШапкеДокументовПриПечати;
	
	Если НЕ ЗначениеЗаполнено(ЭлементСправочника.НаименованиеДляПечати) Тогда
		Шапка.Параметры.НаименованиеДляПечати = ЭлементСправочника.наименование;
	КонецЕсли;                      		
	
	ТабДок.Вывести(Шапка);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ Расчеты.Период КАК ДАТА,
	|	Расчеты.Регистратор,
	|	ВЫБОР
	|		КОГДА Расчеты.ВидДвижения = &Приход
	|			ТОГДА Расчеты.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА Расчеты.ВидДвижения = &Приход
	|			ТОГДА 0
	|		ИНАЧЕ Расчеты.Сумма
	|	КОНЕЦ КАК Оплачено
	|ИЗ РегистрНакопления.Расчеты КАК Расчеты
	|ГДЕ Расчеты.Период <= &Период
	|	И Расчеты.Клиент = &Клиент
	|УПОРЯДОЧИТЬ ПО ДАТА";
	
	Запрос.УстановитьПараметр("Приход", ВидДвиженияНакопления.Приход);
	
	Запрос.УстановитьПараметр("Клиент", ЭлементСправочника);
	Запрос.УстановитьПараметр("Период", ОбщийМодульСервисСервер.ПользователяТекущаяДата());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	если не РезультатЗапроса .Пустой() тогда
		Шапка 	= Макет.ПолучитьОбласть("ШД");
		Подвал 	= Макет.ПолучитьОбласть("П");
		Строка 	= Макет.ПолучитьОбласть("С");
		
		ОсновнаяВалюта = ПредопределенноеЗначение("Справочник.Валюты.ОсновнаяВалюта");
		Шапка.Параметры.ВалютаПрограммы = ОсновнаяВалюта.Наименование;
		
		Шапка.Параметры.текущийдолг = ОбщийМодульСервер.ПолучитьДолгКлиентаПоставщика(ЭлементСправочника, ОбщийМодульСервисСервер.ПользователяТекущаяДата());
		ТабДок.Вывести(Шапка, 0);
		
		Если  ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетОтдельныхДоговоровСКонтрагентами") Тогда
		Шапка  = Макет.ПолучитьОбласть("ШДК");
		Шапка.Параметры.ПросроченныйДолг = ОбщийМодульСервер.ПолучитьДолгКлиентаПоставщикаСОтсрочкой(ЭлементСправочника, ОбщийМодульСервисСервер.ПользователяТекущаяДата());
		Шапка.Параметры.ВалютаПрограммы  = ОсновнаяВалюта.Наименование;
		ТабДок.Вывести(Шапка, 0);
		КонецЕсли;
		
		ТаблицаРезультат = РезультатЗапроса.Выгрузить();
		
		ТаблицаРезультат.Свернуть("Регистратор, Дата", "Сумма, Оплачено");
		ТаблицаРезультат.Сортировать("Дата");
		ТаблицаРезультат.Колонки.Добавить("Остаток");
		ТаблицаРезультат.Колонки.Добавить("Количество");
		остаток = 0;
		для каждого строкарезультата из ТаблицаРезультат цикл
			остаток = остаток + строкарезультата.сумма - строкарезультата.оплачено;
			строкарезультата.остаток = остаток;
			Если ТипЗнч(строкарезультата.регистратор) = Тип("ДокументСсылка.РасходыТовара") 
				или ТипЗнч(строкарезультата.регистратор) = Тип("ДокументСсылка.ПоступленияТовара") Тогда
				
				строкарезультата.количество = строкарезультата.регистратор.товары.итог("количество");
			КонецЕсли;
			строка.Параметры.Заполнить(строкарезультата);
			ТабДок.Вывести(строка, 1);
		КонецЦикла;
		
		Подвал.Параметры.Сумма = ТаблицаРезультат.Итог("Сумма");
		Подвал.Параметры.Количество = ТаблицаРезультат.Итог("Количество");
		Подвал.Параметры.Оплачено = ТаблицаРезультат.Итог("Оплачено");
	//	Подвал.Параметры.Остаток = ТаблицаРезультат.Итог("Остаток");
		Подвал.Параметры.ТекстВПодвалеДокументовПриПечати = СтруктураДополнительныПараметровМакета.ТекстВПодвалеДокументовПриПечати;
		ТабДок.Вывести(Подвал, 0);
		
	КонецЕсли;
	
	ТабДок.Закончитьавтогруппировкустрок();
	
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Истина;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;  	
	
КонецПроцедуры

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ДляУскоренияРаботыСБазойДанныхНеВыводитьДополнительныхДанных") Тогда
		
		Если ВидФормы = "ФормаВыбора" 
			или ВидФормы = "ФормаСписка" Тогда
			
			ВыбраннаяФорма = ВидФормы + "Упрощенная";
			СтандартнаяОбработка = Ложь;
		ИначеЕсли ВидФормы = "ФормаОбъекта" Тогда

			ВыбраннаяФорма = "ФормаЭлементаУпрощенная";
			СтандартнаяОбработка = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Если НЕ ОбщийМодульПовтор.ТекущийЯзыкРусский()
		и ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ПоддерживатьНаименованияНаДругихЯзыкахКромеРусского", Истина) Тогда
		
		ВозможноеПредставление = ОбщийМодульПовтор.получитьПредставлениеНаЯзыке(Данные.ссылка, , Истина);
		Если НЕ ВозможноеПредставление = Неопределено Тогда
			Представление = ВозможноеПредставление;
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

