//sza140521-0200 SZA: 
//sza140107-0143 : 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Отказ ТОгда
		
		ЗначениеЗаполненоОбъектСсылка = ЗначениеЗаполнено(Объект.Ссылка);
		
		Если ЗначениеЗаполнено(Параметры.ОбъектВладелец) Тогда
			
			СтруктураВладельца 	= ОбщийМодульПовтор.ПолучитьИмяДополнительногоРеквизита(параметры.ОбъектВладелец);
			Объект.ЭтоРеквизитДляОбъекта = СтруктураВладельца.ЭтоРеквизитДляОбъекта;
			Объект.НаименованиеОбъектаВладельца = СтруктураВладельца.НаименованиеОбъектаВладельца;
			
		ИначеЕсли ЗначениеЗаполнено(Параметры.ЭтоРеквизитДляОбъекта) Тогда
			Объект.ЭтоРеквизитДляОбъекта = Параметры.ЭтоРеквизитДляОбъекта;
			
		КонецЕсли;
		
		Если ЗначениеЗаполненоОбъектСсылка Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ ЗначенияДополнительныхРеквизитов.ЗначениеРеквизита
			|ИЗ РегистрСведений.ЗначенияДополнительныхРеквизитов КАК ЗначенияДополнительныхРеквизитов
			|ГДЕ ЗначенияДополнительныхРеквизитов.ДополнительныйРеквизит = &ДополнительныйРеквизит";
			
			Запрос.УстановитьПараметр("ДополнительныйРеквизит", Объект.Ссылка);
			
			РезультатЗапроса = Запрос.Выполнить();
			Если НЕ РезультатЗапроса.Пустой() тогда
				ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
				ЗначениеПоУмолчанию = Объект.ЗначениеПоУмолчанию;
				
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл				
					ЗначениеРеквизита = ВыборкаДетальныеЗаписи.ЗначениеРеквизита;
					
					Если ЗначениеЗаполнено(ЗначениеРеквизита) 
						И НЕ ЗначениеПоУмолчанию = ЗначениеРеквизита тогда
						
						элементы.ПараметрыДополнительногоРеквизита.Доступность = Ложь;
						элементы.ИзменениеПараметровНедоступноРеквизитУжеИспользуется.Видимость = Истина;
						
						Прервать;
						
					КонецЕсли;
				КонецЦикла;                        	
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Объект.ТипДополнительногоРеквизита) Тогда
				Элементы.ЗначениеПоУмолчанию.Доступность = Истина;
			КонецЕсли;
			
		Иначе		
			
			Элементы.ЗначениеПоУмолчанию.Доступность = Ложь;
		КонецЕсли;
		
		Если ПоддержкаДругихЯзыков 
			И ЗначениеЗаполненоОбъектСсылка тогда
			
			Наименование = ОбщийМодульПовтор.получитьПредставлениеНаЯзыке(объект.Ссылка);
			
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ ЗначенияНаДругихЯзыках.Язык,
			|	ЗначенияНаДругихЯзыках.НаЯзыке,
			|	ЗначенияНаДругихЯзыках.Поле
			|ИЗ РегистрСведений.ЗначенияНаДругихЯзыках КАК ЗначенияНаДругихЯзыках
			|ГДЕ ЗначенияНаДругихЯзыках.ОбъектБазыДанных = &ОбъектБазыДанных";
			
			Запрос.УстановитьПараметр("ОбъектБазыДанных", Объект.Ссылка);
			
			РезультатЗапроса = Запрос.Выполнить();
			Если НЕ РезультатЗапроса.Пустой() тогда
				ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
				
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					Если ВыборкаДетальныеЗаписи.Поле = "Наименование" Тогда
						СтрокаЯзыка = НаименованияНаДругихЯзыках.Добавить();
						СтрокаЯзыка.Язык 	= ВыборкаДетальныеЗаписи.Язык;
						СтрокаЯзыка.НаЯзыке = ВыборкаДетальныеЗаписи.НаЯзыке;	
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
			
		Иначе
			Наименование = объект.Наименование;
			
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 0, Объект.Ссылка, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 1, Объект.Ссылка, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ТипДополнительногоРеквизитаПриИзменении(Элемент)
	
	ТипДополнительногоРеквизита = Объект.ТипДополнительногоРеквизита;
	Если ЗначениеЗаполнено(ТипДополнительногоРеквизита) Тогда
		Объект.ЗначениеПоУмолчанию = ОбщийМодульПовтор.ПолучитьПустоеЗначениеДополнительногоРеквизита(ТипДополнительногоРеквизита);
		Элементы.ЗначениеПоУмолчанию.Доступность = Истина;
	иначе
		Объект.ЗначениеПоУмолчанию = Ложь;
		Элементы.ЗначениеПоУмолчанию.Доступность = Ложь;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	объект.Наименование  = Наименование;
	НаименованиеИзменяли = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеНаДругихЯзыках(Команда)
	Элементы.НаименованияНаДругихЯзыках.Видимость  = НЕ Элементы.НаименованияНаДругихЯзыках.Видимость;
КонецПроцедуры

&НаКлиенте
Процедура НаименованияНаДругихЯзыкахНаЯзыкеПриИзменении(Элемент)
	БылиИзмененияЗначенияПолейНаЯзыках = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НаименованияНаДругихЯзыкахПриИзменении(Элемент)
	БылиИзмененияЗначенияПолейНаЯзыках = Истина;
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	если ПоддержкаДругихЯзыков тогда
		если НаименованиеИзменяли тогда
			ОбщийМодульСервер.обновитьЗначениеНаЯзыке(Объект.Ссылка, Наименование);
		КонецЕсли;
		
		Если БылиИзмененияЗначенияПолейНаЯзыках Тогда
			Для Каждого СтрокаЯзыка Из НаименованияНаДругихЯзыках Цикл
				ОбщийМодульСервер.обновитьЗначениеНаЯзыке(Объект.Ссылка, СтрокаЯзыка.НаЯзыке, "Наименование", СтрокаЯзыка.Язык);	
			КонецЦикла;
			ОбновитьПовторноИспользуемыеЗначения();
		КонецЕсли;
		
	конецесли;
	
	Элементы.ЗначениеПоУмолчанию.Доступность = ЗначениеЗаполнено(Объект.ТипДополнительногоРеквизита);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	глЧислоОбъектов = глЧислоОбъектов + 1; глПроверятьСообщения = Истина;
КонецПроцедуры
