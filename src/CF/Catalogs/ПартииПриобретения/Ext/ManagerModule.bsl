// sza140104-0214 : 
// sza131111-0009 : 
Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Если ОбщийМодульПовтор.ВывестиНаименованияНаДругомЯзыке() Тогда
		
		ВозможноеПредставление = ОбщийМодульПовтор.получитьПредставлениеНаЯзыке(Данные.ссылка, , ИСТИНА);
		Если НЕ ВозможноеПредставление = Неопределено Тогда
			Представление = ВозможноеПредставление;
			СтандартнаяОбработка = ЛОЖЬ;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура Печать(ТабДок, Ссылка) Экспорт
	
	ТабДок.ИмяПараметровПечати  = "ПартияПриобретения" + СокрЛП(ИмяКомпьютера());
	ТабДок.КлючПараметровПечати = ТабДок.ИмяПараметровПечати;
	
	ТабДок.Очистить();
	
	СсылкаНаОбъектПечати = Ссылка[0];
	
	РазрядМетаданных = ""; ИмяМетаданных = "";
	ПроизвольнаяПечатнаяФорма = ОбщийМодульТекстСервер.ОпределитьПроизвольнуюПечатнуюФорму(, , СсылкаНаОбъектПечати, РазрядМетаданных, ИмяМетаданных);
	
	Если ЗначениеЗаполнено(ПроизвольнаяПечатнаяФорма) Тогда
		ТабДок = ОбщийМодульТекстСервер.СформироватьДокументПоПроизвольнойФорме(ПроизвольнаяПечатнаяФорма, , ТабДок, СсылкаНаОбъектПечати, РазрядМетаданных, ИмяМетаданных);
		
	Иначе		
		Макет = Справочники.ПартииПриобретения.ПолучитьМакет("Печать");
		Макет.КодЯзыкаМакета = ОбщийМодульПовтор.ПолучитьТекущийЯзыкДокументов();
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПартииПриобретения.Дата,
		|	ПартииПриобретения.Комментарий,
		|	ПартииПриобретения.Наименование,
		|	ПартииПриобретения.ОбщаяСтоимостьПартии,
		|	ПартииПриобретения.Поставщик,
		|	ПартииПриобретения.ПроцентНаценки,
		|	ПартииПриобретения.СтоимостьТовара,
		|	ПартииПриобретения.СуммаДополнительнойСтоимости,
		|	ПартииПриобретения.СуммаДопРасходов,
		|	ПартииПриобретения.ПроцентыНаценкиПоПроизводителям.(
		|		НомерСтроки,
		|		Производитель,
		|		ПроцентНаценки,
		|		ОСтроке ),
		|	ПартииПриобретения.ДополнительныеРасходы.(
		|		НомерСтроки,
		|		Статья,
		|		Сумма,
		|		Валюта,
		|		Курс,
		|		ОСтроке )
		|ИЗ Справочник.ПартииПриобретения КАК ПартииПриобретения
		|ГДЕ ПартииПриобретения.Ссылка В (&Ссылка)";
		
		Запрос.Параметры.Вставить("Ссылка", Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		
		ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
		Шапка = Макет.ПолучитьОбласть("Шапка");
		ОбластьПроцентыНаценкиПоПроизводителямШапка = Макет.ПолучитьОбласть("ПроцентыНаценкиПоПроизводителямШапка");
		ОбластьПроцентыНаценкиПоПроизводителям = Макет.ПолучитьОбласть("ПроцентыНаценкиПоПроизводителям");
		ОбластьДополнительныеРасходыШапка = Макет.ПолучитьОбласть("ДополнительныеРасходыШапка");
		ОбластьДополнительныеРасходы = Макет.ПолучитьОбласть("ДополнительныеРасходы");
		Подвал = Макет.ПолучитьОбласть("Подвал");
		
		ВставлятьРазделительСтраниц = ЛОЖЬ;
		Пока Выборка.Следующий() Цикл
			Если ВставлятьРазделительСтраниц Тогда
				ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьЗаголовок);
			ТабДок.Вывести(ОбластьЗаголовок);
			
			Шапка.Параметры.Заполнить(Выборка);
			ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(Шапка);
			ТабДок.Вывести(Шапка, Выборка.Уровень());
			
			ВыборкаПроцентыНаценкиПоПроизводителям = Выборка.ПроцентыНаценкиПоПроизводителям.Выбрать();
			Если НЕ ВыборкаПроцентыНаценкиПоПроизводителям.количество() = 0 Тогда
				ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьПроцентыНаценкиПоПроизводителямШапка);
				ТабДок.Вывести(ОбластьПроцентыНаценкиПоПроизводителямШапка);
				Пока ВыборкаПроцентыНаценкиПоПроизводителям.Следующий() Цикл
					ОбластьПроцентыНаценкиПоПроизводителям.Параметры.Заполнить(ВыборкаПроцентыНаценкиПоПроизводителям);
					ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьПроцентыНаценкиПоПроизводителям);
					ТабДок.Вывести(ОбластьПроцентыНаценкиПоПроизводителям, ВыборкаПроцентыНаценкиПоПроизводителям.Уровень());
				КонецЦикла;                                                     	
			КонецЕсли;
			
			ВыборкаДополнительныеРасходы = Выборка.ДополнительныеРасходы.Выбрать();
			Если НЕ ВыборкаДополнительныеРасходы.Количество() = 0 тогда
				ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьДополнительныеРасходыШапка);
				ТабДок.Вывести(ОбластьДополнительныеРасходыШапка);
				Пока ВыборкаДополнительныеРасходы.Следующий() Цикл
					ОбластьДополнительныеРасходы.Параметры.Заполнить(ВыборкаДополнительныеРасходы);
					ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьДополнительныеРасходы);
					ТабДок.Вывести(ОбластьДополнительныеРасходы, ВыборкаДополнительныеРасходы.Уровень());
				КонецЦикла;	
			КонецЕсли;
			
			Подвал.Параметры.Заполнить(Выборка);
			ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(Подвал);
			ТабДок.Вывести(Подвал);
			
			ВставлятьРазделительСтраниц = ИСТИНА;
		КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры
