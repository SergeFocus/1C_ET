// sza140411-1701 : 
// sza131110-2019 : 
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступленияТовара") Тогда
		Валюта  = ДанныеЗаполнения.Валюта;
		ВидЦен  = ДанныеЗаполнения.ВидЦен;
		Дата 	= ДанныеЗаполнения.Дата;
		Курс 	= ДанныеЗаполнения.Курс;
		Поставщик 	= ДанныеЗаполнения.КлиентПоставщик;
		Комментарий = ДанныеЗаполнения.Комментарий;
		СтоимостьТовара = ДанныеЗаполнения.ТовараНаСумму;
		Для Каждого ТекСтрокаДополнительныеРасходы Из ДанныеЗаполнения.ДополнительныеРасходы Цикл
			НоваяСтрока = ДополнительныеРасходы.Добавить();
			НоваяСтрока.Валюта  = ТекСтрокаДополнительныеРасходы.Валюта;
			НоваяСтрока.Курс 	= ТекСтрокаДополнительныеРасходы.Курс;
			НоваяСтрока.ОСтроке = ТекСтрокаДополнительныеРасходы.ОСтроке;
			НоваяСтрока.Статья 	= ТекСтрокаДополнительныеРасходы.Статья;
			НоваяСтрока.Сумма 	= ТекСтрокаДополнительныеРасходы.Сумма;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)  	
	
	Если НЕ Отказ 
		И НЕ ПометкаУдаления Тогда
		
		Отказ = ОбщийМодульСервер.ПроверитьУникальностьЗначенияРеквизита(Отказ, "ПартииПриобретения", Наименование, Ссылка);
		ОбщийМодульСервер.ПроверитьСменуНаименованияНаТекущемЯзыке(Наименование, Ссылка, , Отказ);	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если НЕ Отказ Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КоррекцияПартииПриобретенияРуководителем.Ссылка
		|ИЗ
		|	Документ.КоррекцияПартииПриобретенияРуководителем КАК КоррекцияПартииПриобретенияРуководителем
		|ГДЕ
		|	КоррекцияПартииПриобретенияРуководителем.ПартияПриобретения = &ПартияПриобретения";
		
		Запрос.УстановитьПараметр("ПартияПриобретения", Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				Коррекция = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
				Коррекция.Удалить();
			КонецЦикла;	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если НЕ Отказ 
		И НЕ ПараметрыСеанса.МногоФункциональныйФлаг
		И НЕ ОбщаяСтоимостьПартии = 0
		И (НЕ ОбщийМодульСерверПривилегия.ЕстьПользователи() 
		ИЛИ РольДоступна("ПолныеПрава")) Тогда 	
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1 КоррекцияПартииПриобретенияРуководителем.Ссылка
		|ИЗ Документ.КоррекцияПартииПриобретенияРуководителем КАК КоррекцияПартииПриобретенияРуководителем
		|ГДЕ КоррекцияПартииПриобретенияРуководителем.ПартияПриобретения = &ПартияПриобретения";
		
		Запрос.УстановитьПараметр("ПартияПриобретения", Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			
			ВыборкаДетальныеЗаписи.Следующий();		
			Коррекция = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();			
			
		Иначе
			Коррекция = Документы.КоррекцияПартииПриобретенияРуководителем.СоздатьДокумент();		
			Коррекция.ПартияПриобретения = ссылка;
			Коррекция.Дата = Дата;
			
		КонецЕсли;
		Коррекция.Записать(РежимЗаписиДокумента.Проведение);
		
	КонецЕсли;
	
	ПараметрыСеанса.МногоФункциональныйФлаг = ЛОЖЬ;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Комментарий = "";
КонецПроцедуры
