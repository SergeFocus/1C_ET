// sza131125-0344  
Процедура Печать(ТабДок, Ссылка) Экспорт
	
	Макет = Справочники.СерииНоменклатуры.ПолучитьМакет("Печать");
	Макет.КодЯзыкаМакета = ОбщийМодульПовтор.ПолучитьТекущийЯзыкДокументов();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ СерииНоменклатуры.КодПродукта,
	|	СерииНоменклатуры.Наименование,
	|	СерииНоменклатуры.Ссылка,
	|	СерииНоменклатуры.Владелец КАК Номенклатура
	|ИЗ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|ГДЕ СерииНоменклатуры.Ссылка В(&Ссылка)";
	
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	Шапка 	= Макет.ПолучитьОбласть("Шапка");
	ШК 		= Макет.ПолучитьОбласть("ШК");
	
	ТабДок.ИмяПараметровПечати  = "СерияНоменклатуры" + СокрЛП(ИмяКомпьютера());
	ТабДок.КлючПараметровПечати = ТабДок.ИмяПараметровПечати;
	
	ТабДок.Очистить();
	
	ВставлятьРазделительСтраниц = ЛОЖЬ;
	Пока Выборка.Следующий() Цикл
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьЗаголовок);
		ТабДок.Вывести(ОбластьЗаголовок);
		
		Шапка.Параметры.Заполнить(Выборка);
		ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(Шапка);
		ТабДок.Вывести(Шапка, Выборка.Уровень());
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ШтрихКоды.ШтрихКод
		|ИЗ РегистрСведений.ШтрихКоды КАК ШтрихКоды
		|ГДЕ ШтрихКоды.СерияНоменклатуры = &СерияНоменклатуры";
		
		Запрос.УстановитьПараметр("СерияНоменклатуры", Выборка.ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				ШК.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
				ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ШК);
				ТабДок.Вывести(ШК);
			КонецЦикла;                        	
		КонецЕсли;
		
		Если ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьДополнительныеКодыСерий") Тогда
			ДопКоды = Макет.ПолучитьОбласть("ДопКоды");
			ДопКоды.Параметры.Заполнить(Выборка);
			ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ДопКоды);
			ТабДок.Вывести(ДопКоды);	
		КонецЕсли;
		
		ВставлятьРазделительСтраниц = ИСТИНА;
	КонецЦикла;
	
КонецПроцедуры
