#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// sza151210-0212 про
// sza150210-2318
// sza141111-1338
// sza140604-1052  
// sza131121-1528  
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Клиенты") Тогда
		Комментарий  = ДанныеЗаполнения.Комментарий;
		Наименование = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Для") + " " + ДанныеЗаполнения.Наименование;
		ВалютаЦены   = ДанныеЗаполнения.ОсновнаяВалюта;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Поставщики") Тогда
		Комментарий  = ДанныеЗаполнения.Комментарий;
		Наименование = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("От") + " " + ДанныеЗаполнения.Наименование;
		ВалютаЦены   = ДанныеЗаполнения.ОсновнаяВалюта;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Склады") Тогда
		Комментарий  = ДанныеЗаполнения.Комментарий;
		Наименование = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("На") + " " + ДанныеЗаполнения.Наименование;
		
	КонецЕсли;
	
КонецПроцедуры
Процедура ПередЗаписью(Отказ)
	
	Если НЕ Отказ 
		И НЕ ПометкаУдаления
		И НЕ ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("МассоваяЗагрузка", ИСТИНА) Тогда
		
		ОбщийМодульСервер.ПроверитьСменуНаименованияНаТекущемЯзыке(Наименование, Ссылка, , Отказ);	
		
		Если НЕ Зависимая Тогда
			Если ЗначениеЗаполнено(ОсновнойВидЦен) Тогда
				ОсновнойВидЦен = ПредопределенноеЗначение("Справочник.ВидыЦен.ПустаяСсылка");
			КонецЕсли;
			Если ЗначениеЗаполнено(ФормулаРасчетаОтБазовогоВидаЦены) Тогда
				ФормулаРасчетаОтБазовогоВидаЦены = ПредопределенноеЗначение("Справочник.ФормулыОписания.ПустаяСсылка");
			КонецЕсли;
			Если НЕ ПроцентОтБазовойЦены = 0 Тогда
				ПроцентОтБазовойЦены = 0;
			КонецЕсли;
			
			СпособыФормированияЦеныДляНоменклатурныхГрупп.Очистить();
			ТаблицаЗависимости.Очистить();
			
		Иначе
			Если ОсновнойВидЦен = Ссылка Тогда
				ОсновнойВидЦен = ПредопределенноеЗначение("Справочник.ВидыЦен.ПустаяСсылка");
			КонецЕсли;
			
			Для Каждого СтрокаТаблицы Из СпособыФормированияЦеныДляНоменклатурныхГрупп Цикл
				Если СтрокаТаблицы.ОсновнойВидЦен = Ссылка Тогда
					СтрокаТаблицы.ОсновнойВидЦен = ПредопределенноеЗначение("Справочник.ВидыЦен.ПустаяСсылка");
				КонецЕсли;
			КонецЦикла;
			
			Для Каждого СтрокаТаблицы Из ТаблицаЗависимости Цикл
				Если СтрокаТаблицы.ОсновнойВидЦен = Ссылка Тогда
					СтрокаТаблицы.ОсновнойВидЦен = ПредопределенноеЗначение("Справочник.ВидыЦен.ПустаяСсылка");
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
		Если ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ФормироватьОписаниеТаблицОбъектовДляИхСписков") Тогда
			ОбщийМодульСервер.ОформитьОписаниеТаблицы(ЭтотОбъект, "СпособыФормированияЦеныДляНоменклатурныхГрупп", "ОсновнойВидЦен", ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("источников формирования цен номенклатурных групп"));
			ОбщийМодульСервер.ОформитьОписаниеТаблицы(ЭтотОбъект, "ТаблицаЗависимости", "ОсновнойВидЦен", ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("источников формирования цен"), "ОсновнойВидЦен");	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
Процедура ПриКопировании(ОбъектКопирования)
	Комментарий = "";
КонецПроцедуры

#КонецЕсли