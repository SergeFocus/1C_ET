#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// sza140104-0216 :
// sza131010-1848  

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Если ОбщийМодульПовтор.ВывестиНаименованияНаДругомЯзыке() Тогда
		
		ВозможноеПредставление = ОбщийМодульПовтор.получитьПредставлениеНаЯзыке(Данные.ссылка, , ИСТИНА);
		Если НЕ ВозможноеПредставление = Неопределено Тогда
			Представление = ВозможноеПредставление;
			СтандартнаяОбработка = ЛОЖЬ;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура Печать(ДокументДляПечати, Ссылка) Экспорт
	
	Макет = Справочники.ФормулыОписания.ПолучитьМакет("Печать");
	Макет.КодЯзыкаМакета = ОбщийМодульПовтор.ПолучитьТекущийЯзыкДокументов();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ФормулыОписания.Комментарий КАК СТРОКА(1000)) КАК Комментарий,
	|	ФормулыОписания.Наименование,
	|	ФормулыОписания.Формула
	|ИЗ Справочник.ФормулыОписания КАК ФормулыОписания
	|ГДЕ ФормулыОписания.Ссылка В (&Ссылка)";
	
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	Шапка = Макет.ПолучитьОбласть("Шапка");
	
	ДокументДляПечати.ИмяПараметровПечати  = "ФормулаОписания" + СокрЛП(ИмяКомпьютера());
	ДокументДляПечати.КлючПараметровПечати = ДокументДляПечати.ИмяПараметровПечати;
	
	ДокументДляПечати.Очистить();
	ВставлятьРазделительСтраниц = ЛОЖЬ;
	Пока Выборка.Следующий() Цикл
		Если ВставлятьРазделительСтраниц Тогда
			ДокументДляПечати.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьЗаголовок);
		ДокументДляПечати.Вывести(ОбластьЗаголовок);
		Шапка.Параметры.Заполнить(Выборка);
		ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(Шапка);
		ДокументДляПечати.Вывести(Шапка, Выборка.Уровень());
		ВставлятьРазделительСтраниц = ИСТИНА;
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли