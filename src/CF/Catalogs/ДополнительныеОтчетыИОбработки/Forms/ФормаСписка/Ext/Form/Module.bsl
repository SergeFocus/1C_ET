// sza151116-0245 
// sza151010-2159
&НаКлиенте
Перем ТекСтрока;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отказ = ОбщийМодульСервисСервер.ПроверитьОтказДоступа("003100", ЭтаФорма, Отказ, );
	
	Если НЕ Отказ Тогда
		Если ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьСобственныйПереводЭлементовИнтерфейса") Тогда
			ОбщийМодульСервер.ПеревестиРеквизитыФормы(ЭтаФорма);	
		КонецЕсли;
		
		ОбщийМодульСервер.ОбеспечитьСписокОтборов(Список);
		
		Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
			Возврат;
		КонецЕсли;
		
		Если Параметры.Свойство("Заголовок") Тогда
			АвтоЗаголовок = Ложь;
			Заголовок = Параметры.Заголовок;
		КонецЕсли;
		Если Параметры.Свойство("Отображение") Тогда
			Элементы.Список.Отображение = ОтображениеТаблицы[Параметры.Отображение];
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 0);
	
	Если ЗначениеЗаполнено(СтраницаПанели) Тогда
		
		СтраницыПанели  = Элементы.ГруппаСтраниц.ПодчиненныеЭлементы;
		ТекущаяСтраница = СтраницыПанели.получить(СтраницаПанели);
		Элементы.ГруппаСтраниц.ТекущаяСтраница = ТекущаяСтраница;
		
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ПослеИзображенияОкнаФормы", 0.3, ИСТИНА);	
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеИзображенияОкнаФормы() // Когда окно уже нарисовано пользователю
	ЭтаФорма.Элементы.ФормаЗакрыть.Видимость = НЕ ЭтаФорма.Окно = Неопределено;	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 1);
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьВыбранныйЭлемент(Команда)
	
	Если ЭтаФорма.ТекущийЭлемент = Элементы.ОтчетыИОбработкиСнаружи Тогда
		ТекущиеДанные = Элементы.ОтчетыИОбработкиСнаружи.ТекущиеДанные;	
		Если НЕ ТекущиеДанные = Неопределено Тогда
			ОбщийМодульКлиент.ВыполнитьОбработкуПоПараметрам(ТекущиеДанные.ИмяФайла, Неопределено, ИСТИНА);
		КонецЕсли;
	Иначе
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		
		Если НЕ ТекущиеДанные = Неопределено Тогда
			ОбщийМодульКлиент.ВыполнитьОбработкуПоПараметрам(ТекущиеДанные.Ссылка, Неопределено);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	ТекущаяСтраница = Элементы.ГруппаСтраниц.ТекущаяСтраница;
	СтраницыПанели  = Элементы.ГруппаСтраниц.ПодчиненныеЭлементы;
	СтраницаПанели  = СтраницыПанели.Индекс(ТекущаяСтраница);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетыИОбработкиСнаружиИмяФайлаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = ЛОЖЬ;
	ТекСтрока = Элементы.ОтчетыИОбработкиСнаружи.ТекущиеДанные;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Фильтр = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Внешние отчеты и обработки") + " (*.epf, *.erf)|*.epf;*.erf|" + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Внешние отчеты") + " (*.epf)|*.erf|" + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Внешние обработки") + " (*.epf)|*.erf";
	Диалог.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Выберите файл внешнего файла или обработки");
	Диалог.ПолноеИмяФайла = "";
	Диалог.ПредварительныйПросмотр = ЛОЖЬ;
	
	ДиалогВыбораФайлаМаршрута = Новый ОписаниеОповещения("ВыборВнешнийФайл", ЭтаФорма);
	
	Диалог.Показать(ДиалогВыбораФайлаМаршрута); 
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборВнешнийФайл(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если НЕ ВыбранныеФайлы = Неопределено 
		И НЕ ТекСтрока = Неопределено Тогда
		
		ТекСтрока.ИмяФайла = ВыбранныеФайлы[0];
		
		Файл = Новый Файл(ТекСтрока.ИмяФайла);
		ТекСтрока.ДатаФайла = Файл.ПолучитьВремяИзменения();
		ТекСтрока.РазмерФайла = Файл.Размер();		
		
		АдресХранилища = "";
		Результат = Неопределено;

		НачатьПомещениеФайла(Новый ОписаниеОповещения("ВыборВнешнийФайлЗавершение", ЭтаФорма, Новый Структура("АдресХранилища, Файл", АдресХранилища, Файл)), АдресХранилища, ТекСтрока.ИмяФайла, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборВнешнийФайлЗавершение(Результат1, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры1) Экспорт
	
	АдресХранилища = ДополнительныеПараметры1.АдресХранилища;
	Файл = ДополнительныеПараметры1.Файл;	
	
	Если НЕ Файл = Неопределено Тогда
		Результат =  ПоместитьФайл(АдресХранилища, Файл.ПолноеИмя, , Ложь);
		ТекСтрока.Описание = ПолучитьОписаниеОбработки(Файл.Имя, АдресХранилища);
		
		Элементы.ОтчетыИОбработкиСнаружи.ЗакончитьРедактированиеСтроки(ЛОЖЬ);	
		ЭтаФорма.ТекущийЭлемент = Элементы.ОтчетыИОбработкиСнаружиОписание;
		ТекСтрока = Неопределено;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция   ПолучитьОписаниеОбработки(Знач ФайлИмя, Знач АдресХранилища)
	
	Описание = "";
	
	ИмяОбъекта = СокрЛП(ВнешниеОбработки.Подключить(АдресХранилища, ФайлИмя, Истина));
	
	ВнешнийОбъект = ВнешниеОбработки.Создать(ИмяОбъекта);
	
	Попытка
		РегистрационныеДанные = ВнешнийОбъект.СведенияОВнешнейОбработке();
		Описание = "" + СокрЛП(РегистрационныеДанные.Наименование) + " " + СокрЛП(РегистрационныеДанные.Версия) + " " + СокрЛП(РегистрационныеДанные.Информация);
	Исключение
		Префикс = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("В файле нет сведений о внешней обработке") + ": СведенияОВнешнейОбработке()";
	КонецПопытки;		
	
	ВнешнийОбъект = Неопределено;
	
	Возврат Описание;
	
КонецФункции

ТекСтрока = Неопределено;
