// sza151020-2238 остатки набора
// sza150212-1817
// sza141226-0003
// sza141016-0226
// sza141015-1738 ключи учета
// sza140913-0237
// sza140831-0011
// sza140829-2328
// sza140619-0327
// sza130902-0029 :

&НаКлиенте
Процедура Аналоги1Выбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	ТекущиеДанные = Элементы.Аналоги1.ТекущиеДанные;

	Если НЕ ТекущиеДанные = Неопределено Тогда
		ПоказатьЗначение(Неопределено, ТекущиеДанные.Номенклатура);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура АртикулОткрытие(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = ЛОЖЬ;

	Если НЕ ПустаяСтрока(Объект.Артикул) Тогда
		ЗапуститьПриложение("http://www.google.com.ua/search?q=" + Объект.Артикул + "%20" + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке(Элемент.имя));
	КонецЕсли;

КонецПроцедуры

Процедура ВидимостьСоставаНабора()

	Элементы.ГруппаСоставИСуммы.Видимость 						= Объект.ЭтоНабор;
	Элементы.РекомендуемыйМинимальныйОстатокТовара.Видимость 	= НЕ Объект.ЭтоНабор
	И ВключитьВозможностьУказыватьРекомендованныйМинимальныйОстатокНоменклатуры;
	Элементы.ПересчитатьЦенуНабораПоСуммеЦенВсехЭлементовСостава.Видимость = НЕ ИспользоватьСложныйМеханизмЦен
	И Объект.ЭтоНабор = ИСТИНА;

	Элементы.КоличествоНаборовКотороеМожноСобратьНаТекущийМомент.Видимость = ЗначениеЗаполнено(Объект.Ссылка);

КонецПроцедуры

&НаКлиенте
Процедура ВидимостьТаблицСправа()

	КолонкаСправаВидимость = ЗначениеЗаполнено(Объект.Ссылка) И НЕ НеПоказыватьТаблицыСправа;
	Элементы.КолонкаСправа.Видимость = КолонкаСправаВидимость;
	Элементы.Цены.Видимость = ИспользоватьСложныйМеханизмЦен И КолонкаСправаВидимость;

КонецПроцедуры

&НаКлиенте
Процедура ВключитьВГруппуАналогов(Команда)

	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ВключитьВГруппуАналоговЗавершение1", ЭтаФорма), ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Сначала следует записать этот элемент. Записать?"), РежимДиалогаВопрос.ДаНет);

		Возврат;
	КонецЕсли;

	ВключитьВГруппуАналоговФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ВключитьВГруппуАналоговЗавершение(Значение, ДополнительныеПараметры) Экспорт

	ГруппаАналогов = ?(Значение = Неопределено, ДополнительныеПараметры.ГруппаАналогов, Значение);

	Если (Значение <> Неопределено) Тогда
		ВключитьВГруппуАналоговНаСервере(ГруппаАналогов);
		Элементы.Аналоги1.Обновить();
		Элементы.ГруппыАналогов.Обновить();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВключитьВГруппуАналоговЗавершение1(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Записать();
	КонецЕсли;

	ВключитьВГруппуАналоговФрагмент();

КонецПроцедуры

&НаСервере
Процедура ВключитьВГруппуАналоговНаСервере(Знач ГруппаАналогов)

	ГруппаАналоговОбъект = ГруппаАналогов.ПолучитьОбъект();
	СтрокаТовара = ГруппаАналоговОбъект.Товары.Добавить();
	СтрокаТовара.Номенклатура = Объект.Ссылка;
	СтрокаТовара.количество   = ?(Объект.КоличествоПоУмолчанию = 0, 1, Объект.КоличествоПоУмолчанию);
	ГруппаАналоговОбъект.Записать();

КонецПроцедуры

&НаКлиенте
Процедура ВключитьВГруппуАналоговФрагмент()

	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ГруппаАналогов = ПредопределенноеЗначение("Справочник.ГруппыАналогов.ПустаяСсылка");
		ПоказатьВводЗначения(Новый ОписаниеОповещения("ВключитьВГруппуАналоговЗавершение", ЭтаФорма, Новый Структура("ГруппаАналогов", ГруппаАналогов)), ГруппаАналогов, ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Укажите группу аналогов этой номенклатуры") + ": ", тип("СправочникСсылка.ГруппыАналогов"));
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыводНадписиПоФормуле()

	ПроизвольнаяНадписьФормыВидно = ЛОЖЬ;

	Если ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьФормулыАвтоНаименованияФормированияШтрихКодаИТекстаСШтрихКодом") Тогда
		СтруктураФормулы = ОбщийМодульПовтор.НайтиФормулуДляНадписиНаФорме(ЭтаФорма.ИмяФормы);
		Если НЕ СтруктураФормулы = Неопределено Тогда
			Элементы.ОбновитьЗначениеПоФормулеНадписиФормы.Видимость = СтруктураФормулы.ВыводитьКнопкуОбновленияНадписи;

			Если СтруктураФормулы.ВыполнитьНаСервереСОбъектом Тогда
				Элементы.ПроизвольнаяНадписьФормы.Заголовок = ВыводНадписиПоФормулеНаСервере(СтруктураФормулы.Формула);
			Иначе
				Попытка
					Выполнить(" Элементы.ПроизвольнаяНадписьФормы.Заголовок = " + СтруктураФормулы.Формула + ";");
				Исключение
				КонецПопытки;
			КонецЕсли;

			ПроизвольнаяНадписьФормыВидно = ИСТИНА;
		КонецЕсли;
	КонецЕсли;

	Элементы.ПроизвольнаяНадписьФормы.Видимость = ПроизвольнаяНадписьФормыВидно;

КонецПроцедуры

&НаСервере
Функция   ВыводНадписиПоФормулеНаСервере(Знач Формула)

	Результат = "";
	Попытка
		Выполнить(" Результат = " + Формула + ";");
	Исключение
	КонецПопытки;

	Возврат Результат;

КонецФункции //ВыводНадписиПоФормулеНаСервере

&НаКлиенте
Процедура ВысотаПриИзменении(Элемент)

	ОбновитьРазмеры();

КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницКлючейУчетаПриСменеСтраницы(Элемент, ТекущаяСтраница)
	СменитьСтраницуКлючейУчетаНаСервере(ТекущаяСтраница.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ГруппыАналоговВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	ТекущиеДанные = Элементы.ГруппыАналогов.ТекущиеДанные;

	Если НЕ ТекущиеДанные = Неопределено Тогда
		ПоказатьЗначение(Неопределено, ТекущиеДанные.ГруппаАналогов);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДлинаПриИзменении(Элемент)
	ОбновитьРазмеры();
КонецПроцедуры

&НаСервере
Процедура ДобавитьВМассивСпецификаций(Знач Состав, ТаблОписанияНабора, Знач Коэффициент)

	Для Каждого СтрокаСоставаНабора Из Состав цикл
		Если СтрокаСоставаНабора.Номенклатура.ЭтоНабор  = ИСТИНА Тогда
			ДобавитьВМассивСпецификаций(СтрокаСоставаНабора.Номенклатура.Состав, ТаблОписанияНабора, СтрокаСоставаНабора.Количество * Коэффициент);
		Иначе
			СтрокаСпециф = ТаблОписанияНабора.добавить();
			СтрокаСпециф.Номенклатура 	= СтрокаСоставаНабора.Номенклатура;
			СтрокаСпециф.Количество 	= СтрокаСоставаНабора.Количество * Коэффициент;

			Если СтрокаСпециф.количество = 0 Тогда
				СтрокаСпециф.количество = 1;
			КонецЕсли;
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры // ДобавитьВМассивСпецификаций

&НаКлиенте
Процедура ДобавитьВсеВидыЦен(Команда)

	ДобавитьВсеВидыЦенНаСервере();

КонецПроцедуры

&НаСервере
Процедура ДобавитьВсеВидыЦенНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ВидыЦен.Ссылка
	|ИЗ Справочник.ВидыЦен КАК ВидыЦен
	|ГДЕ ВидыЦен.ПометкаУдаления = ЛОЖЬ
	| И НЕ ВидыЦен.Ссылка В (&Ссылка)";
	Запрос.УстановитьПараметр("Ссылка", ЦеныДляНовойНоменклатуры.Выгрузить().ВыгрузитьКолонку("ВидЦен"));

	РезультатЗапроса = Запрос.Выполнить();

	Если НЕ РезультатЗапроса.Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Дата = ОбщийМодульСервисСервер.ПользователяТекущаяДата();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			НоваяЦена = ЦеныДляНовойНоменклатуры.Добавить();
			НоваяЦена.ДатаЦены = Дата;
			НоваяЦена.ВидЦен   = ВыборкаДетальныеЗаписи.Ссылка;

		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДопШтрихКод(Команда)

	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ДобавитьДопШтрихКодЗавершение", ЭтаФорма), ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Сначала следует записать этот элемент. Записать?"), РежимДиалогаВопрос.ДаНет);

		Возврат;
	КонецЕсли;

	ДобавитьДопШтрихКодФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДопШтрихКодЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Записать();
	КонецЕсли;

	ДобавитьДопШтрихКодФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДопШтрихКодФрагмент()

	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрыФормы = Новый Структура("Номенклатура", Объект.Ссылка);
		ОткрытьФорму("РегистрСведений.ДополнительныеШтрихКоды.Форма.ФормаЗаписи", ПараметрыФормы);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЕдиницуИзмерения(Команда)

	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ДобавитьЕдиницуИзмеренияЗавершение", ЭтаФорма), ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Сначала следует записать этот элемент. Записать?"), РежимДиалогаВопрос.ДаНет);

		Возврат;
	КонецЕсли;

	ДобавитьЕдиницуИзмеренияФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЕдиницуИзмеренияЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Записать();
	КонецЕсли;

	ДобавитьЕдиницуИзмеренияФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЕдиницуИзмеренияФрагмент()

	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрыФормы = Новый Структура("Номенклатура", Объект.Ссылка);
		ОткрытьФорму("Справочник.ЕдиницыИзмерения.Форма.ФормаЭлемента", ПараметрыФормы);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзображение(Команда)

	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ДобавитьИзображениеЗавершение", ЭтаФорма), ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Сначала следует записать этот элемент. Записать?"), РежимДиалогаВопрос.ДаНет);

		Возврат;
	КонецЕсли;

	ДобавитьИзображениеФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзображениеЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Записать();
	КонецЕсли;

	ДобавитьИзображениеФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзображениеФрагмент()

	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрыФормы = Новый Структура("СвязанныйОбъект", Объект.Ссылка);
		ОткрытьФорму("Справочник.Изображения.Форма.ФормаЭлемента", ПараметрыФормы);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПредставлениеКонтрагента(Команда)

	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ДобавитьПредставлениеКонтрагентаЗавершение", ЭтаФорма), ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Сначала следует записать этот элемент. Записать?"), РежимДиалогаВопрос.ДаНет);

		Возврат;
	КонецЕсли;

	ДобавитьПредставлениеКонтрагентаФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПредставлениеКонтрагентаЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Записать();
	КонецЕсли;

	ДобавитьПредставлениеКонтрагентаФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПредставлениеКонтрагентаФрагмент()

	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрыФормы = Новый Структура("Номенклатура", Объект.Ссылка);
		ОткрытьФорму("РегистрСведений.ПредставлениеТоваровКонтрагентами.Форма.ФормаЗаписи", ПараметрыФормы);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСпецификацию(Команда)

	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ДобавитьСпецификациюЗавершение", ЭтаФорма), ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Сначала следует записать этот элемент. Записать?"), РежимДиалогаВопрос.ДаНет);

		Возврат;
	КонецЕсли;

	ДобавитьСпецификациюФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСпецификациюЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Записать();
	КонецЕсли;

	ДобавитьСпецификациюФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСпецификациюФрагмент()

	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрыФормы = Новый Структура("Номенклатура", Объект.Ссылка);
		ОткрытьФорму("Справочник.Спецификации.Форма.ФормаЭлемента", ПараметрыФормы);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЦену(Команда)

	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ДобавитьЦенуЗавершение", ЭтаФорма), ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Сначала следует записать этот элемент. Записать?"), РежимДиалогаВопрос.ДаНет);

		Возврат;
	КонецЕсли;

	ДобавитьЦенуФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЦенуЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Записать();
	КонецЕсли;

	ДобавитьЦенуФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЦенуФрагмент()

	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Номенклатура", Объект.Ссылка);
		ПараметрыФормы.Вставить("Период", НачалоДня(ОбщийМодульКлиент.ПользователяТекущаяДата()));
		ОткрытьФорму("РегистрСведений.Цены.ФормаЗаписи", ПараметрыФормы);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьШтрихКод(Команда)

	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ДобавитьШтрихКодЗавершение", ЭтаФорма), ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Сначала следует записать этот элемент. Записать?"), РежимДиалогаВопрос.ДаНет);

		Возврат;
	КонецЕсли;

	ДобавитьШтрихКодФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьШтрихКодЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Записать();
	КонецЕсли;

	ДобавитьШтрихКодФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьШтрихКодФрагмент()

	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрыФормы = Новый Структура("Номенклатура", Объект.Ссылка);
		ФормаШтрихКода = ПолучитьФорму("РегистрСведений.ШтрихКоды.ФормаЗаписи", ПараметрыФормы);
		ФормаШтрихКода.Открыть();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДополнительнаяИнформацияВидИнформацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ПараметрыФормы = Новый Структура("ТипВладельца", ДопИнфоТипВладельца);
	ФормаВыбораВидаИнформации = ПолучитьФорму("Справочник.ДополнительныеРеквизиты.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
	ВидИнформации = ФормаВыбораВидаИнформации.ОткрытьМодально();
	Если ЗначениеЗаполнено(ВидИнформации) Тогда
		Элементы.ДополнительнаяИнформация.ТекущиеДанные.ВидИнформации = ВидИнформации;
		Элементы.ДополнительнаяИнформация.ТекущиеДанные.Информация = ОбщийМодульКлиент.ПолучитьЗначениеПоУмолчаниюПоляДополнительнойИнформации(ВидИнформации);
		СтандартнаяОбработка = ЛОЖЬ;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДополнительнаяИнформацияПриИзменении(Элемент)

	ДопИнформацияИзменена = ИСТИНА;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЦеныНовойНоменклатуры()

	ЦеныДляНовойНоменклатуры.Очистить();

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ВидыЦен.Ссылка
	|ИЗ Справочник.ВидыЦен КАК ВидыЦен
	|ГДЕ ВидыЦен.ПометкаУдаления = ЛОЖЬ
	|УПОРЯДОЧИТЬ ПО
	|	ВидыЦен.Предопределенный УБЫВ,
	|	ВидыЦен.Наименование";

	РезультатЗапроса = Запрос.Выполнить();

	Если НЕ РезультатЗапроса.Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ДатаЗапретаИзмененияДанных = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ДатаЗапретаИзмененияДанных");
		Если ДатаЗапретаИзмененияДанных = '00010101000000' Тогда
			СрокЗапретаДанныхВЧасах = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("СрокЗапретаДанныхВЧасах");

			Если СрокЗапретаДанныхВЧасах = 0 Тогда
				дата = НачалоМесяца(ОбщийМодульСервисСервер.ПользователяТекущаяДата());
			Иначе
				дата = ОбщийМодульСервисСервер.ПользователяТекущаяДата() - СрокЗапретаДанныхВЧасах * 3600 + 600;
			КонецЕсли;
		Иначе
			дата = КонецДня(ДатаЗапретаИзмененияДанных + 3600);
		КонецЕсли;

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			СтрокаЦен = ЦеныДляНовойНоменклатуры.Добавить();
			СтрокаЦен.ДатаЦены 	= дата;
			СтрокаЦен.ВидЦен 	= ВыборкаДетальныеЗаписи.ссылка;
			СтрокаЦен.Цена 		= 0;

		КонецЦикла;

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Изображения1ПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("СписокПриАктивизацииСтрокиОповещение", 0.2, ИСТИНА);
КонецПроцедуры

&НаКлиенте
Процедура КоличествоНаборовКотороеМожноСобратьНаТекущийМомент(Команда)

	ДокументДляПечати = Новый ТабличныйДокумент;
	ДокументДляПечати.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЧИСЛОНАБОРОВ";
	ДокументДляПечати.КлючПараметровПечати = ДокументДляПечати.ИмяПараметровПечати;

	КоличествоНаборовКотороеМожноСобратьНаТекущийМоментНаСервере(ДокументДляПечати);

	ДокументДляПечати.ОтображатьСетку 	= ЛОЖЬ;
	ДокументДляПечати.Защита 			= ЛОЖЬ;
	ДокументДляПечати.ТолькоПросмотр 	= ЛОЖЬ;
	ДокументДляПечати.ОтображатьЗаголовки = ЛОЖЬ;

	ДокументДляПечати.Показать(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Компоновка наборов") + " " + Объект.Наименование);

КонецПроцедуры

&НаСервере
Процедура КоличествоНаборовКотороеМожноСобратьНаТекущийМоментНаСервере(ДокументДляПечати)

	Макет 	= Справочники.Номенклатура.ПолучитьМакет("МакетНабора");
	Макет.КодЯзыкаМакета = ОбщийМодульПовтор.ПолучитьТекущийЯзыкДокументов();
	Дата 	= ОбщийМодульСервисСервер.ПользователяТекущаяДата();
	ТаблОписанияНабора = Новый ТаблицаЗначений;
	ТаблОписанияНабора.Колонки.Добавить("Номенклатура");
	ТаблОписанияНабора.Колонки.Добавить("Количество");
	ДобавитьВМассивСпецификаций(Объект.Состав, ТаблОписанияНабора, 1);
	ТаблОписанияНабора.Свернуть("Номенклатура", "Количество");
	ТаблОписанияНабора.Колонки.Добавить("Цена");
	ТаблОписанияНабора.Колонки.Добавить("Остаток");
	МинимальныйДелегат  = -1;

	Для Каждого СтрокаСоставаНабора Из ТаблОписанияНабора Цикл
		Если НЕ ОбщийМодульПовтор.ТоварНеУчитываетсяПоКоличеству(СтрокаСоставаНабора.номенклатура) Тогда
			СтрокаСоставаНабора.Цена 	= ОбщийМодульСервер.ПолучитьЦенуСписания(СтрокаСоставаНабора.номенклатура, СтрокаСоставаНабора.количество, Дата);
			СтрокаСоставаНабора.остаток = ОбщийМодульСервер.ПолучитьОстатокТовара(СтрокаСоставаНабора.номенклатура, , Дата);
			МожноПроизвести = цел(СтрокаСоставаНабора.остаток / СтрокаСоставаНабора.количество);

			Если МинимальныйДелегат  = -1
				ИЛИ МожноПроизвести < МинимальныйДелегат  Тогда

				МинимальныйДелегат = МожноПроизвести;
			КонецЕсли;
		Иначе
			СтрокаСоставаНабора.Цена = ОбщийМодульСервер.ПолучитьЦенуНаСервере(СтрокаСоставаНабора.номенклатура, , Дата, ЛОЖЬ, , , , , Объект.Ссылка, , СтрокаСоставаНабора.ЕдиницаИзмерения);
			СтрокаСоставаНабора.остаток = 0;
		КонецЕсли;

	КонецЦикла;

	ТаблОписанияНабора.Колонки.Добавить("Использовано");
	ТаблОписанияНабора.Колонки.Добавить("НаСумму");
	Для Каждого СтрокаСоставаНабора Из ТаблОписанияНабора Цикл
		СтрокаСоставаНабора.Использовано = МинимальныйДелегат * СтрокаСоставаНабора.Количество;
		СтрокаСоставаНабора.НаСумму 		= СтрокаСоставаНабора.Использовано * СтрокаСоставаНабора.цена;

	КонецЦикла;

	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьПодвал 			= Макет.ПолучитьОбласть("Подвал");
	ОбластьШапкаТаблицы 	= Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьПодвалТаблицы 	= Макет.ПолучитьОбласть("ПодвалТаблицы");
	ОбластьДетальныхЗаписей = Макет.ПолучитьОбласть("Детали");
	ДокументДляПечати.ИмяПараметровПечати  = "КоличествоНаборов" + СокрЛП(ИмяКомпьютера());
	ДокументДляПечати.КлючПараметровПечати = ДокументДляПечати.ИмяПараметровПечати;

	ДокументДляПечати.Очистить();
	ОбластьЗаголовок.Параметры.Дата = ОбщийМодульСервисСервер.ПользователяТекущаяДата();
	ОбластьЗаголовок.Параметры.Номенклатура = ?(ЗначениеЗаполнено(Объект.НаименованиеДляПечати), Объект.НаименованиеДляПечати, Объект.Наименование);
	ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьЗаголовок);
	ДокументДляПечати.Вывести(ОбластьЗаголовок);

	ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьШапкаТаблицы);
	ДокументДляПечати.Вывести(ОбластьШапкаТаблицы);
	ДокументДляПечати.НачатьАвтогруппировкуСтрок();

	Для Каждого СтрокаСоставаНабора Из ТаблОписанияНабора Цикл
		ОбластьДетальныхЗаписей.Параметры.Заполнить(СтрокаСоставаНабора);
		ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьДетальныхЗаписей);
		ДокументДляПечати.Вывести(ОбластьДетальныхЗаписей);
	КонецЦикла;

	ДокументДляПечати.ЗакончитьАвтогруппировкуСтрок();
	ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьПодвалТаблицы);
	ДокументДляПечати.Вывести(ОбластьПодвалТаблицы);
	ОбластьПодвал.параметры.ИтогоНаборов = МинимальныйДелегат;
	НаСумму = ТаблОписанияНабора.Итог("НаСумму");
	ОбластьПодвал.параметры.НаСумму = НаСумму;

	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Цена = ОбщийМодульСервер.ПолучитьЦенуНаСервере(Объект.Ссылка, ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ВидЦенРасходованияПоУмолчанию"), Дата, ЛОЖЬ, МинимальныйДелегат, , , , Объект.Ссылка);
	Иначе
		Цена = Объект.Цена;
	КонецЕсли;

	ОбластьПодвал.параметры.Цена 			= Цена;
	ОбластьПодвал.параметры.НаборНаСумму 	= Цена * МинимальныйДелегат;
	ОбластьПодвал.параметры.Выручка 		= (Цена * МинимальныйДелегат) - НаСумму;
	ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьПодвал);
	ДокументДляПечати.Вывести(ОбластьПодвал);

КонецПроцедуры

&НаКлиенте
Процедура НаименованиеДляПечатиНаДругихЯзыках(Команда)
	Элементы.НаименованияДляПечатиНаДругихЯзыках.Видимость = НЕ Элементы.НаименованияДляПечатиНаДругихЯзыках.Видимость;
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеДляПечатиПриИзменении(Элемент)

	Объект.НаименованиеДляПечати = НаименованиеДляПечати;
	НаименованиеИзменяли = ИСТИНА;
	Если ПереводитьНаименованияАвтоматически Тогда
		ОбщийМодульКлиент.ПеревестиНаименованияАвтоматически(НаименованиеДляПечати, НаименованияДляПечатиНаДругихЯзыках);
		БылиИзмененияЗначенияПолейНаЯзыках = ИСТИНА;
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция  НаименованиеПриИзмененииНаСервере(Знач ОбъектНаименование, Знач ОбъектРодитель)

	ГруппаПоПервомуСлову = ОбщийМодульТоварСервер.ПолучитьНаименованиеГруппыПоПервомуСлову(ОбъектНаименование, ИСТИНА);

	Если НЕ ГруппаПоПервомуСлову = Неопределено
		И ГруппаПоПервомуСлову.ЭтоГруппа
		И НЕ ГруппаПоПервомуСлову = ОбъектРодитель Тогда

		Возврат ГруппаПоПервомуСлову;
	Иначе

		Возврат Неопределено;
	КонецЕсли;

КонецФункции

&НаКлиенте
Процедура НаименованияДляПечатиНаДругихЯзыкахНаЯзыкеПриИзменении(Элемент)
	БылиИзмененияЗначенияПолейНаЯзыках = ИСТИНА;
КонецПроцедуры

&НаКлиенте
Процедура НаименованияДляПечатиНаДругихЯзыкахПриИзменении(Элемент)
	БылиИзмененияЗначенияПолейНаЯзыках = ИСТИНА;
КонецПроцедуры

&НаКлиенте
Процедура НаименованияНаДругихЯзыкахНаЯзыкеПриИзменении(Элемент)
	БылиИзмененияЗначенияПолейНаЯзыках = ИСТИНА;
КонецПроцедуры

&НаКлиенте
Процедура НаименованияНаДругихЯзыкахПриИзменении(Элемент)
	БылиИзмененияЗначенияПолейНаЯзыках = ИСТИНА;
КонецПроцедуры

&НаКлиенте
Процедура НеОтслеживатьОстатокПриИзменении(Элемент)
	ОбновитьВидимостьУслуги();
КонецПроцедуры

&НаКлиенте
Процедура НоменклатурнаяГруппаПриИзменении(Элемент)
	НоменклатурнаяГруппаПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура НоменклатурнаяГруппаПриИзмененииНаСервере(Знач ПриОткрытии = ЛОЖЬ)

	Если НЕ ПриОткрытии Тогда
		ОбщийМодульТоварСервер.УстановитьРеквизитНоменклатурыПоФормуле(Объект);
	КонецЕсли;

	Элементы.Родитель.ТолькоПросмотр 	 = ЛОЖЬ;
	Элементы.СерийныйУчет.ТолькоПросмотр = ЛОЖЬ;
	НоменклатурнаяГруппа = Объект.НоменклатурнаяГруппа;
	Если ЗначениеЗаполнено(НоменклатурнаяГруппа) Тогда
		Если НоменклатурнаяГруппа.СвязатьОдноименнуюГруппуНоменклатуры Тогда
			Родитель = Справочники.Номенклатура.НайтиПоНаименованию(НоменклатурнаяГруппа.Наименование);

			Если НЕ Объект.Родитель = Родитель
				И Родитель.ЭтоГруппа Тогда

				Объект.Родитель = Родитель;
			КонецЕсли;

			Элементы.Родитель.ТолькоПросмотр = ИСТИНА;
		КонецЕсли;

		Если НоменклатурнаяГруппа.НоменклатураЭтойГруппыССерийнымУчетом Тогда
			Если НЕ Объект.СерийныйУчет Тогда
				Объект.СерийныйУчет = ИСТИНА;
			КонецЕсли;

			Элементы.СерийныйУчет.ТолькоПросмотр = ИСТИНА;
		КонецЕсли;

		Если ИспользоватьЕдиницыИзмеренияНоменклатуры
			И Объект.ОсновнаяЕдиницаИзмерения = ""
			И НЕ ПустаяСтрока(НоменклатурнаяГруппа.ОбщееНаименованиеЕдиницыИзмерения) Тогда

			Объект.ОсновнаяЕдиницаИзмерения = НоменклатурнаяГруппа.ОбщееНаименованиеЕдиницыИзмерения;
		КонецЕсли;

		Если ЗначениеЗаполнено(НоменклатурнаяГруппа.НаправлениеДеятельности) Тогда
			Объект.НаправлениеДеятельности = НоменклатурнаяГруппа.НаправлениеДеятельности;
		КонецЕсли;
	КонецЕсли;

	Если НЕ ПриОткрытии Тогда
		ОбновитьДополнительныеРеквизитыНоменклатурнойГруппы(ИСТИНА);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьАртикулПоФормуле(Команда)
	ОбновитьАртикулПоФормулеНаСервере(ИСТИНА);
КонецПроцедуры

&НаСервере
Процедура ОбновитьАртикулПоФормулеНаСервере(Знач Принудительно = ЛОЖЬ)

	Если Принудительно
		ИЛИ ПустаяСтрока(Объект.Артикул) Тогда

		Объект.Артикул = ОбщийМодульТоварСервер.УстановитьАртикулНоменклатурыПоФормуле(Объект);
	КонецЕсли;

КонецПроцедуры

Процедура ОбновитьВидимостьУслуги()

	Элементы.РекомендуемыйМинимальныйОстатокТовара.Видимость = НЕ УслугаНЕТовар И ВключитьВозможностьУказыватьРекомендованныйМинимальныйОстатокНоменклатуры И НЕ Объект.НеОтслеживатьОстаток ;
	Элементы.НеОтслеживатьОстаток.Видимость 	= НЕ ЭтотЭлементПредопределенный И НоменклатураНеИспользовалась И НЕ УслугаНЕТовар И Объект.РекомендуемыйМинимальныйОстатокТовара = 0;
	Элементы.Артикул.Видимость 					= НЕ УслугаНЕТовар;
	Элементы.Производитель.Видимость 			= НЕ УслугаНЕТовар;
	Элементы.СредняяЦенаПриобретения.Видимость 	= НЕ УслугаНЕТовар И НЕ СредняяЦенаПриобретения = 0 И НЕ Объект.НеОтслеживатьОстаток;
	Элементы.ГруппаСерийногоУчета.Доступность	= НоменклатураНеИспользовалась;
	Элементы.ГруппаСерийногоУчета.Видимость		= НЕ УслугаНЕТовар И НЕ Объект.НеОтслеживатьОстаток;
	Элементы.ГруппаГарантии.Видимость 			= НЕ УслугаНЕТовар И ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ПоказыватьКнопкуПечатиГарантийногоТалонаВРасходномДокументе");
	Элементы.ПериодОказанияУслугиЗаВремя.Видимость = УслугаНЕТовар И ВестиУчетОказанияУслугЗаВремя;

	Если УслугаНЕТовар
		ИЛИ НЕ (Элементы.ОстаткиДляНабора.Видимость ИЛИ Элементы.ОстаткиБезСклада.Видимость ИЛИ Элементы.ГруппаКвоИСуммаГоризонт.Видимость ИЛИ Элементы.ОстаткиНоменклатуры.Видимость) Тогда

		Элементы.СтраницаОстаткиИДанные.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Цены");
	Иначе
		Элементы.СтраницаОстаткиИДанные.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Остатки и Цены");
	КонецЕсли;

	Если (УслугаНЕТовар ИЛИ Объект.НеОтслеживатьОстаток)
		И Объект.СерийныйУчет Тогда

		Объект.СерийныйУчет = ЛОЖЬ;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеОбОстатке(Команда)

	ОбновитьДанныеОбОстаткеНаСервере();

КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеОбОстаткеНаСервере()

	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Дата = ОбщийМодульСервисСервер.ПользователяТекущаяДата();

		Запрос = Новый Запрос;

		Если Объект.ЭтоНабор Тогда
			Запрос.Текст = "ВЫБРАТЬ
			|	МИНИМУМ(ВЫРАЗИТЬ(ЕСТЬNULL(ЗапросКОстаткамЭлементовНабора.КоличествоОстаток, 0) / ВЫБОР
			|				КОГДА НоменклатураСостав.Количество = 0
			|					ТОГДА 0

			|				ИНАЧЕ НоменклатураСостав.Количество
			|			КОНЕЦ КАК ЧИСЛО(10, 0))) КАК Остаток,
			|	НоменклатураСостав.Ссылка КАК НоменклатураНабора
			|ИЗ
			|	Справочник.Номенклатура.Состав КАК НоменклатураСостав
			|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
			|			ТоварыОстатки.КоличествоОстаток КАК КоличествоОстаток,
			|			ТоварыОстатки.Номенклатура КАК НоменклатураОстатка
			|		ИЗ
			|			РегистрНакопления.Товары.Остатки(
			|					&ДатаОстатка) КАК ТоварыОстатки) КАК ЗапросКОстаткамЭлементовНабора
			|		ПО НоменклатураСостав.Номенклатура = ЗапросКОстаткамЭлементовНабора.НоменклатураОстатка
			|ГДЕ
			|	НоменклатураСостав.Ссылка = &Номенклатура
			|
			|СГРУППИРОВАТЬ ПО
			|	НоменклатураСостав.Ссылка";
			Запрос.УстановитьПараметр("ДатаОстатка", Дата);
			Запрос.УстановитьПараметр("Номенклатура", Объект.Ссылка);
			ДоступныйОстаток = 0;

			РезультатЗапроса = Запрос.Выполнить();

			Если НЕ РезультатЗапроса.Пустой() Тогда
				ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					ДоступныйОстаток = ДоступныйОстаток + ВыборкаДетальныеЗаписи.Остаток;

				КонецЦикла;
			КонецЕсли;

			Если ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ВестиУчетПоСкладам") Тогда
				ОстаткиНабораПоСкладам.Очистить();
				Запрос.Текст = "ВЫБРАТЬ
				|	МИНИМУМ(ВЫРАЗИТЬ(ЕСТЬNULL(ЗапросКОстаткамЭлементовНабора.КоличествоОстаток, 0) / ВЫБОР
				|				КОГДА НоменклатураСостав.Количество = 0
				|					ТОГДА 0

				|				ИНАЧЕ НоменклатураСостав.Количество
				|			КОНЕЦ КАК ЧИСЛО(10, 0))) КАК Остаток,
				|	НоменклатураСостав.Ссылка КАК НоменклатураНабора
				|ИЗ
				|	Справочник.Номенклатура.Состав КАК НоменклатураСостав
				|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
				|			ТоварыОстатки.КоличествоОстаток КАК КоличествоОстаток,
				|			ТоварыОстатки.Номенклатура КАК НоменклатураОстатка
				|		ИЗ
				|			РегистрНакопления.Товары.Остатки(&ДатаОстатка, Склад = &Склад) КАК ТоварыОстатки) КАК ЗапросКОстаткамЭлементовНабора
				|		ПО НоменклатураСостав.Номенклатура = ЗапросКОстаткамЭлементовНабора.НоменклатураОстатка
				|ГДЕ
				|	НоменклатураСостав.Ссылка = &Номенклатура
				|
				|СГРУППИРОВАТЬ ПО
				|	НоменклатураСостав.Ссылка";
				Запрос.УстановитьПараметр("ДатаОстатка", Дата);
				Запрос.УстановитьПараметр("Номенклатура", Объект.Ссылка);

				ЗапросПоСкладам = Новый Запрос;
				ЗапросПоСкладам.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ Склады.Ссылка
				|ИЗ Справочник.Склады КАК Склады
				|ГДЕ Склады.Виртуальный = ЛОЖЬ
				|	И Склады.ПометкаУдаления = ЛОЖЬ";

				РезультатЗапросаПоСкладам = ЗапросПоСкладам.Выполнить();
				ВыборкаДетальныеЗаписиПоСкладам = РезультатЗапросаПоСкладам.Выбрать();
				Пока ВыборкаДетальныеЗаписиПоСкладам.Следующий() Цикл
					Запрос.УстановитьПараметр("Склад", ВыборкаДетальныеЗаписиПоСкладам.Ссылка);

					РезультатЗапроса = Запрос.Выполнить();

					Если НЕ РезультатЗапроса.Пустой() Тогда
						ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
						Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
							СтрокаОстаткаНабора = ОстаткиНабораПоСкладам.Добавить();
							СтрокаОстаткаНабора.Склад = ВыборкаДетальныеЗаписиПоСкладам.Ссылка;
							СтрокаОстаткаНабора.ДоступныйОстаток = ВыборкаДетальныеЗаписи.Остаток;

						КонецЦикла;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		Иначе
			Запрос.Текст = "ВЫБРАТЬ ЕСТЬNULL(ТоварыОстаткиИОбороты.КоличествоПриход, 0) КАК КоличествоПриход,
			|	ЕСТЬNULL(ТоварыОстаткиИОбороты.КоличествоРасход, 0) КАК КоличествоРасход,
			|	ЕСТЬNULL(ТоварыОстаткиИОбороты.СуммаПриход, 0) КАК СуммаПриход,
			|	ЕСТЬNULL(ТоварыОстаткиИОбороты.СуммаРасход, 0) КАК СуммаРасход,
			|	ЕСТЬNULL(ТоварыОстаткиИОбороты.КоличествоКонечныйОстаток, 0) КАК КоличествоКонечныйОстаток,
			|	ЕСТЬNULL(ТоварыОстаткиИОбороты.СуммаКонечныйОстаток, 0) КАК СуммаКонечныйОстаток
			|ИЗ РегистрНакопления.Товары.ОстаткиИОбороты(&ДатаНачала, &ДатаОкончания, , , Номенклатура = &Номенклатура) КАК ТоварыОстаткиИОбороты";
			Запрос.УстановитьПараметр("ДатаНачала", '00010101000000');
			Запрос.УстановитьПараметр("ДатаОкончания", Дата);
			Запрос.УстановитьПараметр("Номенклатура", Объект.Ссылка);
			Остаток = 0;
			НаСумму = 0;
			КоличествоПриход = 0;
			КоличествоРасход = 0;
			СуммаПриход = 0;
			СуммаРасход = 0;

			РезультатЗапроса = Запрос.Выполнить();

			Если НЕ РезультатЗапроса.Пустой() Тогда
				ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					Остаток 		 = Остаток + ВыборкаДетальныеЗаписи.КоличествоКонечныйОстаток;
					НаСумму 		 = НаСумму + ВыборкаДетальныеЗаписи.СуммаКонечныйОстаток;
					КоличествоПриход = КоличествоПриход + ВыборкаДетальныеЗаписи.КоличествоПриход;
					КоличествоРасход = КоличествоРасход + ВыборкаДетальныеЗаписи.КоличествоРасход;
					СуммаПриход 	 = СуммаПриход + ВыборкаДетальныеЗаписи.СуммаПриход;
					СуммаРасход 	 = СуммаРасход + ВыборкаДетальныеЗаписи.СуммаРасход;

				КонецЦикла;
			КонецЕсли;

			Элементы.Остаток.Видимость 			= (НЕ КоличествоПриход = 0) ИЛИ (НЕ Остаток = 0);
			Элементы.НаСумму.Видимость 			= НЕ НаСумму = 0;
			Элементы.КоличествоПриход.Видимость = НЕ КоличествоПриход = 0;
			Элементы.СуммаПриход.Видимость 		= НЕ СуммаПриход = 0;
			Элементы.КоличествоРасход.Видимость = НЕ КоличествоРасход = 0;
			Элементы.СуммаРасход.Видимость 		= НЕ СуммаРасход = 0;
			Элементы.НаименованиеОсновнойВалюты1.Видимость = НЕ НаСумму = 0;
			СредняяЦенаПриобретения = ОбщийМодульСервер.ПолучитьЦенуСписания(Объект.Ссылка, 1, Дата);
			Элементы.СредняяЦенаПриобретения.Видимость = ИспользоватьСложныйМеханизмЦен И НЕ СредняяЦенаПриобретения = 0;
			ПоследнийПоставщик = ОбщийМодульТоварСервер.ПолучитьПоследнегоПоставщика(Объект.Ссылка, , КоличествоПриход);
			Элементы.ПоследнийПоставщик.Видимость = ЗначениеЗаполнено(ПоследнийПоставщик);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДляВаучеров()

	Элементы.ГруппаДляВаучера.Видимость = Объект.ЭтоВаучер;
	Элементы.КоэффициентНакопленияНаВаучер.Видимость = НЕ Объект.ЭтоВаучер;

КонецПроцедуры

&НаСервере
Процедура ОбновитьДополнительныеРеквизитыНоменклатурнойГруппы(Знач НоваяНоменклатурнаяГруппа = ЛОЖЬ)

	Если ИспользоватьДополнительныеРеквизитыНоменклатурныхГрупп Тогда
		Счетчик = 1;
		Если ЗначениеЗаполнено(Объект.НоменклатурнаяГруппа) Тогда
			НаименованияДополнительныхРеквизитов = ОбщийМодульПовтор.ПолучитьДополнительныеРеквизитыНоменклатурныхГрупп(Объект.НоменклатурнаяГруппа);

			Если НЕ НаименованияДополнительныхРеквизитов = Неопределено Тогда
				Для Каждого ДопРеквизит Из НаименованияДополнительныхРеквизитов Цикл
					Если Счетчик <= 10 Тогда
						ДополнительныйРеквизит = ДопРеквизит.ДополнительныйРеквизит;
						НаименованиеДопРеквизита = ОбщийМодульПовтор.получитьПредставлениеНаЯзыке(ДополнительныйРеквизит);
						Выполнить(" ДопРеквизитНГ" + СокрЛП(Счетчик) + " = ДопРеквизит.ДополнительныйРеквизит;");
						Выполнить(" Элементы.РеквизитНоменклатурнойГруппы" + СокрЛП(Счетчик) + ".Видимость = ИСТИНА;");
						Выполнить(" Элементы.РеквизитНоменклатурнойГруппы" + СокрЛП(Счетчик) + ".Заголовок = """ + НаименованиеДопРеквизита + """;");
						Выполнить(" Элементы.РеквизитНоменклатурнойГруппы" + СокрЛП(Счетчик) + ".Подсказка = """ + НаименованиеДопРеквизита + """;");

						Если ДополнительныйРеквизит.ТипДополнительногоРеквизита = ПредопределенноеЗначение("Перечисление.ТипыДополнительныхРеквизитов.ЗначениеИзСправочника")
							ИЛИ ДополнительныйРеквизит.ТипДополнительногоРеквизита = ПредопределенноеЗначение("Перечисление.ТипыДополнительныхРеквизитов.СсылкаНаЭлементСправочника")
							ИЛИ ДополнительныйРеквизит.ТипДополнительногоРеквизита = ПредопределенноеЗначение("Перечисление.ТипыДополнительныхРеквизитов.СсылкаНаДокумент") Тогда

							Выполнить(" Элементы.РеквизитНоменклатурнойГруппы" + СокрЛП(Счетчик) + ".КнопкаВыбора  = ИСТИНА;");
							Выполнить(" Элементы.РеквизитНоменклатурнойГруппы" + СокрЛП(Счетчик) + ".КнопкаОчистки = ИСТИНА;");
						КонецЕсли;

						ТипЗначенияРеквизитНоменклатурнойГруппы = Неопределено;

						Если НоваяНоменклатурнаяГруппа
							И НЕ ТипЗначенияРеквизитНоменклатурнойГруппы = ТипЗнч(ДопРеквизит.ЗначениеПоУмолчанию)  Тогда

							Выполнить(" Объект.РеквизитНоменклатурнойГруппы" + СокрЛП(Счетчик) + " = ДопРеквизит.ЗначениеПоУмолчанию;");
						КонецЕсли;

						Выполнить(" ТипЗначенияРеквизитНоменклатурнойГруппы = ТипЗнч(Объект.РеквизитНоменклатурнойГруппы" + СокрЛП(Счетчик) + ");");

						Если ТипЗначенияРеквизитНоменклатурнойГруппы = Тип("Дата")
							ИЛИ ТипЗначенияРеквизитНоменклатурнойГруппы = Тип("Булево")
							ИЛИ ТипЗначенияРеквизитНоменклатурнойГруппы = Тип("Число") Тогда

							Выполнить(" Элементы.РеквизитНоменклатурнойГруппы" + СокрЛП(Счетчик) + ".РастягиватьПоГоризонтали = ЛОЖЬ;");
						КонецЕсли;
					КонецЕсли;

					Счетчик = Счетчик + 1;

				КонецЦикла;
			КонецЕсли;

			Пока счетчик <= 10 Цикл
				Выполнить(" Элементы.РеквизитНоменклатурнойГруппы" + СокрЛП(Счетчик) + ".Видимость = ЛОЖЬ;");
				Если НоваяНоменклатурнаяГруппа Тогда
					Выполнить(" Объект.РеквизитНоменклатурнойГруппы" + СокрЛП(Счетчик) + " = Неопределено;");
				КонецЕсли;

				счетчик = счетчик + 1;

			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗначениеПоФормулеНадписиФормы(Команда)
	ВыводНадписиПоФормуле();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьРазмеры()

	Если ЗначениеЗаполнено(Объект.Высота) Тогда
		Размеры = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Объем") + " " + (Объект.Ширина * Объект.Длина * Объект.Высота);
	ИначеЕсли ЗначениеЗаполнено(Объект.Ширина) Тогда
		Размеры = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Площадь") + " " + (Объект.Ширина * Объект.Длина);
	Иначе
		Размеры = "";
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбновитьСвязанныеКлючиУчетаДляФормыНоменклатуры()

	ЕстьКлючиУчета = ЛОЖЬ;

	Если ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьКлючиУчетаНоменклатурыИИхСвязанныеСписки") Тогда
		КлючиУчета.Очистить();
		СвязанныеКлючиУчета.Параметры.УстановитьЗначениеПараметра("Номенклатура", Объект.Ссылка);
		ТаблицаКлючей = Новый ТаблицаЗначений;
		ТаблицаКлючей.Колонки.Добавить("НомерКлюча");
		ТаблицаКлючей.Колонки.Добавить("Наименование");

		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ДополнительныеРеквизиты.Ссылка
		|ИЗ Справочник.ДополнительныеРеквизиты КАК ДополнительныеРеквизиты
		|ГДЕ ДополнительныеРеквизиты.ЭтоКлючУчетаНоменклатуры = ИСТИНА";

		РезультатЗапроса = Запрос.Выполнить();

		Если НЕ РезультатЗапроса.Пустой() Тогда
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			НомерКлюча = 1;
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				СтрокаТаблицыКлючей = ТаблицаКлючей.Добавить();
				СтрокаТаблицыКлючей.НомерКлюча = НомерКлюча;
				НомерКлюча = НомерКлюча + 1;
				СтрокаТаблицыКлючей.Наименование = ОбщийМодульПовтор.ПолучитьПредставлениеНаЯзыке(ВыборкаДетальныеЗаписи.Ссылка);
				ТекущийКлючУчета = ВыборкаДетальныеЗаписи.ссылка;
				КлючиУчета.Добавить(ВыборкаДетальныеЗаписи.ссылка);

			КонецЦикла;

			СтраницыКлючей = Элементы.ГруппаСтраницКлючейУчета;
			Для Каждого СтрокаТаблицыКлючей Из ТаблицаКлючей Цикл
				СтраницаКлюча = Элементы.Найти("СтраницаКлюча" + СтрокаТаблицыКлючей.НомерКлюча);
				Если СтраницаКлюча = Неопределено Тогда
					СтраницаКлюча = Элементы.Добавить("СтраницаКлюча" + СтрокаТаблицыКлючей.НомерКлюча, тип("ГруппаФормы"), СтраницыКлючей);
					СтраницаКлюча.Вид = ВидГруппыФормы.Страница;
					СтраницаКлюча.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
					СтраницаКлюча.Заголовок = СтрокаТаблицыКлючей.Наименование;
					НоваяКоманда = Команды.Найти("КнопкаНовогоЗначенияКлючаУчета" + СтрокаТаблицыКлючей.НомерКлюча);

					Если НоваяКоманда = Неопределено Тогда
						НоваяКоманда = Команды.Добавить("КнопкаНовогоЗначенияКлючаУчета" + СтрокаТаблицыКлючей.НомерКлюча);
					КонецЕсли;

					НоваяКоманда.Действие  = "ОткрытьКнопкуКоманды";
					НоваяКоманда = Элементы.Найти("КнопкаНовогоЗначенияКлючаУчета" + СтрокаТаблицыКлючей.НомерКлюча);
					Если НоваяКоманда = Неопределено Тогда
						НоваяКоманда = Элементы.Добавить("КнопкаНовогоЗначенияКлючаУчета" + СтрокаТаблицыКлючей.НомерКлюча, тип("КнопкаФормы"), СтраницаКлюча);
					КонецЕсли;

					НоваяКоманда.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Добавить") + " " + СтрокаТаблицыКлючей.Наименование;
					НоваяКоманда.ИмяКоманды = "КнопкаНовогоЗначенияКлючаУчета" + СтрокаТаблицыКлючей.НомерКлюча;
					НоваяКоманда.КнопкаПоУмолчанию = ЛОЖЬ;
					ТаблицаКлюча = Элементы.Найти("ТаблицаКлюча" + СтрокаТаблицыКлючей.НомерКлюча);

					Если ТаблицаКлюча = Неопределено Тогда
						ТаблицаКлюча = Элементы.Добавить("ТаблицаКлюча" + СтрокаТаблицыКлючей.НомерКлюча, тип("ТаблицаФормы"), СтраницаКлюча);
						ТаблицаКлюча.ПутьКДанным = "СвязанныеКлючиУчета";
					КонецЕсли;

					ТаблицаКлюча.КоманднаяПанель.Видимость = ЛОЖЬ;
					ТаблицаКлюча.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
					ТаблицаКлюча.ЧередованиеЦветовСтрок = ИСТИНА;
					НоваяКолонка = Элементы.Найти("Значение" + СтрокаТаблицыКлючей.НомерКлюча);

					Если НоваяКолонка = Неопределено Тогда
						НоваяКолонка = Элементы.Добавить("Значение" + СтрокаТаблицыКлючей.НомерКлюча, Тип("ПолеФормы"), ТаблицаКлюча);
					КонецЕсли;

					НоваяКолонка.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Значения") + " " + СтрокаТаблицыКлючей.Наименование;
					НоваяКолонка.ПутьКДанным = "СвязанныеКлючиУчета.ЗначениеКлючаУчета";
					НоваяКолонка.Вид = ВидПоляФормы.ПолеВвода;
					НоваяКолонка.РежимРедактирования = РежимРедактированияКолонки.ВходПриВводе;
					НоваяКолонка = Элементы.Найти("Комментарий" + СтрокаТаблицыКлючей.НомерКлюча);

					Если НоваяКолонка = Неопределено Тогда
						НоваяКолонка = Элементы.Добавить("Комментарий" + СтрокаТаблицыКлючей.НомерКлюча, Тип("ПолеФормы"), ТаблицаКлюча);
					КонецЕсли;

					НоваяКолонка.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Комментарий");
					НоваяКолонка.ПутьКДанным = "СвязанныеКлючиУчета.Комментарий";
					НоваяКолонка.Вид = ВидПоляФормы.ПолеВвода;
					НоваяКолонка.РежимРедактирования = РежимРедактированияКолонки.ВходПриВводе;
					НоваяКолонка.Высота = 1;
				КонецЕсли;

			КонецЦикла;

			ЕстьКлючиУчета = ИСТИНА;
			СменитьСтраницуКлючейУчетаНаСервере("1");
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // ОбновитьСвязанныеКлючиУчетаДляФормыНоменклатуры

&НаКлиенте
Процедура ОбновитьШтрихКодПоФормуле(Команда)
	
	ОбновитьШтрихКодПоФормулеНаСервере(ИСТИНА);
	Элементы.ОбновитьШтрихКодПоФормуле.Видимость 	= ИспользоватьШтрихКоды И ПустаяСтрока(Объект.ОсновнойШтрихКод);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьШтрихКодПоФормулеНаСервере(Знач Принудительно)

	Если Принудительно
		ИЛИ ПустаяСтрока(Объект.ОсновнойШтрихКод) Тогда

		Объект.ОсновнойШтрихКод = ОбщийМодульТоварСервер.УстановитьШтрихКодНоменклатурыПоФормуле(Объект);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборДопРеквизитаНГ(Знач НомерДопРеквизита = "1", СтандартнаяОбработка)

	ТипДополнительногоРеквизита = Неопределено;
	Выполнить(" ТипДополнительногоРеквизита = ОбщийМодульКлиент.получитьЗначениеРеквизита(ДопРеквизитНГ" + НомерДопРеквизита + ", ""ТипДополнительногоРеквизита"");");
	Если ТипДополнительногоРеквизита = ПредопределенноеЗначение("Перечисление.ТипыДополнительныхРеквизитов.СсылкаНаЭлементСправочника") Тогда
		СтандартнаяОбработка = ЛОЖЬ;
		РеквизитНоменклатурнойГруппы = Неопределено;
		Выполнить(" РеквизитНоменклатурнойГруппы = Объект.РеквизитНоменклатурнойГруппы" + НомерДопРеквизита + ";");

		Если РеквизитНоменклатурнойГруппы = Неопределено Тогда
			ТипРеквизита = Неопределено;
			ПоказатьВыборИзСписка(Новый ОписаниеОповещения("ОбработатьВыборДопРеквизитаНГЗавершение1", ЭтаФорма, Новый Структура("НомерДопРеквизита, ТипДополнительногоРеквизита", НомерДопРеквизита, ТипДополнительногоРеквизита)), ОбщийМодульКлиент.ПолучитьСписокИзМетаданных(ИСТИНА));

			Возврат;
		КонецЕсли;

		ОбработатьВыборДопРеквизитаНГФрагмент1(НомерДопРеквизита);
	ИначеЕсли ТипДополнительногоРеквизита = ПредопределенноеЗначение("Перечисление.ТипыДополнительныхРеквизитов.СсылкаНаДокумент") Тогда
		СтандартнаяОбработка = ЛОЖЬ;
		РеквизитНоменклатурнойГруппы = Неопределено;
		Выполнить(" РеквизитНоменклатурнойГруппы = Объект.РеквизитНоменклатурнойГруппы" + НомерДопРеквизита + ";");

		Если РеквизитНоменклатурнойГруппы = Неопределено Тогда
			ПоказатьВыборИзСписка(Новый ОписаниеОповещения("ОбработатьВыборДопРеквизитаНГЗавершение", ЭтаФорма, Новый Структура("НомерДопРеквизита, ТипДополнительногоРеквизита", НомерДопРеквизита, ТипДополнительногоРеквизита)), ОбщийМодульКлиент.ПолучитьСписокИзМетаданных(ЛОЖЬ));

			Возврат;
		КонецЕсли;

		ОбработатьВыборДопРеквизитаНГФрагмент(НомерДопРеквизита);
	ИначеЕсли ТипДополнительногоРеквизита = ПредопределенноеЗначение("Перечисление.ТипыДополнительныхРеквизитов.ЗначениеИзСправочника")
		И Объект["РеквизитНоменклатурнойГруппы" + НомерДопРеквизита] = Неопределено Тогда

		СтандартнаяОбработка = ЛОЖЬ;
		Объект["РеквизитНоменклатурнойГруппы" + НомерДопРеквизита] = ПредопределенноеЗначение("Справочник.НаборЗначенийДополнительныхРеквизитов.ПустаяСсылка");
		ПараметрыФормы = Новый Структура;
		Выполнить(" ПараметрыФормы.Вставить(""Владелец"", ДопРеквизитНГ" + НомерДопРеквизита + ");");
		ФормаВыбора = ПолучитьФорму("Справочник.НаборЗначенийДополнительныхРеквизитов.Форма.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
		ФормаВыбора.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Укажите значение") + ":";
		Объект["РеквизитНоменклатурнойГруппы" + НомерДопРеквизита] = ФормаВыбора.ОткрытьМодально();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборДопРеквизитаНГЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт

	НомерДопРеквизита = ДополнительныеПараметры.НомерДопРеквизита;
	ТипДополнительногоРеквизита = ДополнительныеПараметры.ТипДополнительногоРеквизита;
	ТипРеквизита = ВыбранныйЭлемент;
	Если НЕ ТипРеквизита = Неопределено Тогда
		Выполнить(" Объект.РеквизитНоменклатурнойГруппы" + НомерДопРеквизита + " = ПредопределенноеЗначение(""Документ."" + ТипРеквизита.Значение + "".ПустаяСсылка"");");
	КонецЕсли;

	ОбработатьВыборДопРеквизитаНГФрагмент(НомерДопРеквизита);

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборДопРеквизитаНГЗавершение1(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт

	НомерДопРеквизита = ДополнительныеПараметры.НомерДопРеквизита;
	ТипДополнительногоРеквизита = ДополнительныеПараметры.ТипДополнительногоРеквизита;
	ТипРеквизита = ВыбранныйЭлемент;
	Если НЕ ТипРеквизита = Неопределено Тогда
		Выполнить(" Объект.РеквизитНоменклатурнойГруппы" + НомерДопРеквизита + " = ПредопределенноеЗначение(""Справочник."" + ТипРеквизита.Значение + "".ПустаяСсылка"");");
	КонецЕсли;

	ОбработатьВыборДопРеквизитаНГФрагмент1(НомерДопРеквизита);

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборДопРеквизитаНГФрагмент(Знач НомерДопРеквизита)

	Выполнить(" ВвестиЗначение(Объект.РеквизитНоменклатурнойГруппы" + НомерДопРеквизита + ", ОбщийМодульПовтор.ПолучитьТекстНаЯзыке(""Укажите значение"") + "":"");");

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборДопРеквизитаНГФрагмент1(Знач НомерДопРеквизита)

	Выполнить(" ВвестиЗначение(Объект.РеквизитНоменклатурнойГруппы" + НомерДопРеквизита + ", ОбщийМодульПовтор.ПолучитьТекстНаЯзыке(""Укажите значение"") + "":"");");

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОчисткуДопРеквизитаНГ(Знач НомерДопРеквизита = "1", СтандартнаяОбработка)

	ТипДополнительногоРеквизита = Неопределено;
	Выполнить(" ТипДополнительногоРеквизита = ОбщийМодульКлиент.получитьЗначениеРеквизита(ДопРеквизитНГ" + НомерДопРеквизита + ", ""ТипДополнительногоРеквизита"");");

	Если ТипДополнительногоРеквизита = ПредопределенноеЗначение("Перечисление.ТипыДополнительныхРеквизитов.СсылкаНаЭлементСправочника")
		ИЛИ ТипДополнительногоРеквизита = ПредопределенноеЗначение("Перечисление.ТипыДополнительныхРеквизитов.СсылкаНаДокумент") Тогда

		СтандартнаяОбработка = ЛОЖЬ;
		Выполнить(" Объект.РеквизитНоменклатурнойГруппы" + НомерДопРеквизита + " = Неопределено;");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПолученныйШтрихкодНаКлиенте(Знач ТекКод)

	Если УстанавливатьШтрихКодИзКарточкиНоменклатуры Тогда
		Объект.ОсновнойШтрихКод = ТекКод;
	Иначе
		ПоказатьВопрос(Новый ОписаниеОповещения("ОбработатьПолученныйШтрихкодНаКлиентеЗавершение", ЭтаФорма, Новый Структура("ТекКод", ТекКод)), ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Установить Штрих-Код основным для этой номенклатуры?"), РежимДиалогаВопрос.ДаНет);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПолученныйШтрихкодНаКлиентеЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	ТекКод = ДополнительныеПараметры.ТекКод;

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.ОсновнойШтрихКод = ТекКод;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если Источник = "ПодключаемоеОборудование"
		И ВводДоступен() Тогда

		Если ИмяСобытия = "ScanData" Тогда
			Если Параметр[1] = Неопределено Тогда
				ТекКод = Параметр[0];
			Иначе
				ТекКод = Параметр[1][1];
			КонецЕсли;

			ЭтаФорма.ТекущийЭлемент = Элементы.ОсновнойШтрихКод;
			ОбработатьПолученныйШтрихкодНаКлиенте(ТекКод);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОсновнойШтрихКодОткрытие(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = ЛОЖЬ;

	Если НЕ ПустаяСтрока(Объект.ОсновнойШтрихКод) Тогда
		ЗапуститьПриложение("http://www.google.com.ua/search?q=" + Объект.ОсновнойШтрихКод);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКнопкуКоманды(Команда)

	НомерКлючаУчета = Число(СтрЗаменить(Команда.Имя, "КнопкаНовогоЗначенияКлючаУчета", ""));
	ПараметрыФормы  = Новый Структура("КлючУчета, Номенклатура", КлючиУчета[НомерКлючаУчета - 1].Значение, Объект.Ссылка);
	ФормаНовойСвязи = ПолучитьФорму("РегистрСведений.СвязанныеКлючиУчетаНоменклатуры.ФормаЗаписи", ПараметрыФормы);
	ФормаНовойСвязи.Открыть();

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)

	ТекущаяСтраница = Элементы.ГруппаСтраницСправа.ТекущаяСтраница;
	Если НЕ ТекущаяСтраница = Неопределено Тогда
		СтраницыПанели  = Элементы.ГруппаСтраницСправа.ПодчиненныеЭлементы;
		СтраницаПанели  = СтраницыПанели.Индекс(ТекущаяСтраница);
	КонецЕсли;

	ТекущаяСтраница = Элементы.ГруппаСтраниц.ТекущаяСтраница;
	Если НЕ ТекущаяСтраница = Неопределено Тогда
		СтраницыПанели  = Элементы.ГруппаСтраниц.ПодчиненныеЭлементы;
		СтраницаОсновноеДополнительно = СтраницыПанели.Индекс(ТекущаяСтраница);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	Если НЕ Отказ Тогда

		Если ПустаяСтрока(Объект.Наименование)
			И ЗначениеЗаполнено(Объект.НаименованиеДляПечати) Тогда

			Объект.Наименование = Объект.НаименованиеДляПечати;
		КонецЕсли;

		Если ПустаяСтрока(Объект.Наименование)
			И ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьФормулыАвтоНаименованияФормированияШтрихКодаИТекстаСШтрихКодом") Тогда

			Объект.Наименование = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Номенклатура") + " " + Формат(ТекущаяДата(), "ДФ=ггггММддЧЧммсс");
		КонецЕсли;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Объект.Родитель)
		И ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ЗапретитьЗаписьНоменклатурыБезУказанияГруппы") Тогда

		ОбщегоНазначения.СообщитьПользователю(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Не указана группа, а она обязательно должна быть указана!"), Объект.Ссылка, "Родитель", , Отказ, , СтатусСообщения.Важное);
	КонецЕсли;

	глПроверятьСообщения = ИСТИНА;

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	Если НЕ Отказ Тогда
		Отказ = ПроверитьНаОтказ();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьЦенуНабораПоСуммеЦенВсехЭлементовСостава(Команда)
	ПересчитатьЦенуНабораПоСуммеЦенВсехЭлементовСоставаНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПересчитатьЦенуНабораПоСуммеЦенВсехЭлементовСоставаНаСервере()

	Объект.Цена = 0;

	Для Каждого СтрокаСоставаНабора Из Объект.Состав цикл
		Если ЗначениеЗаполнено(СтрокаСоставаНабора.номенклатура) Тогда
			Объект.Цена = Объект.Цена + СтрокаСоставаНабора.количество * ОбщийМодульСервер.ПолучитьЦенуНаСервере(СтрокаСоставаНабора.номенклатура, , ОбщийМодульСервисСервер.ПользователяТекущаяДата(), ИСТИНА, СтрокаСоставаНабора.количество, , , , Объект.Ссылка, , СтрокаСоставаНабора.ЕдиницаИзмерения);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	Если Объект.ЭтоНабор Тогда
		Элементы.ОстаткиДляНабора.Видимость = ИСТИНА;
		ОбновитьДанныеОбОстаткеНаСервере();
	КонецЕсли;

	ЗначениеЗаполненоОбъектСсылка = ЗначениеЗаполнено(Объект.Ссылка);
	Элементы.ШтрихКоды.Видимость 	= ИспользоватьШтрихКоды И ЗначениеЗаполненоОбъектСсылка;
	Элементы.ДопШтрихКоды.Видимость = ИспользоватьШтрихКоды И ЗначениеЗаполненоОбъектСсылка И ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьДополнительныеУстаревшиеШтрихКодыНоменклатуры");
	СерийныйУчетПриИзмененииК();
	ВидимостьТаблицСправа();
	глЧислоОбъектов = глЧислоОбъектов + 1; глПроверятьСообщения = ИСТИНА;
	ЭтаФорма.ОбновитьОтображениеДанных();

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	ОбъектСсылка = Объект.Ссылка;
	ЗначениеЗаполненоОбъектСсылка = ЗначениеЗаполнено(объектСсылка);
	Элементы.КлючиУчетаБудутДоступныПослеЗаписиНоменклатуры.Видимость = ЛОЖЬ;
	ВидимостьСоставаНабора();

	Если ЗначениеЗаполненоОбъектСсылка Тогда
		ОбновитьСвязанныеКлючиУчетаДляФормыНоменклатуры();
		СтрокаТовара = Новый Структура;
		СтрокаТовара.Вставить("Номенклатура", ОбъектСсылка);
		СтрокаТовара.Вставить("Цена", 0);

		Для Каждого СтрокаЦен Из ЦеныДляНовойНоменклатуры цикл
			Если НЕ СтрокаЦен.цена = 0 Тогда
				СтрокаТовара.цена = СтрокаЦен.цена;
				ОбщийМодульСервер.УстановитьЦенуИВсеЗависимые(СтрокаЦен.ВидЦен, СтрокаТовара, , СтрокаЦен.остроке, СтрокаЦен.датацены);
			КонецЕсли;

		КонецЦикла;
	КонецЕсли;

	Если ПоддержкаДругихЯзыков Тогда
		Если НаименованиеИзменяли Тогда
			ОбщийМодульСервер.обновитьЗначениеНаЯзыке(ОбъектСсылка, ПредставлениеНаименования);
			ОбщийМодульСервер.обновитьЗначениеНаЯзыке(ОбъектСсылка, НаименованиеДляПечати, "НаименованиеДляПечати");
		КонецЕсли;

		Если БылиИзмененияЗначенияПолейНаЯзыках Тогда
			Для Каждого СтрокаЯзыка Из НаименованияНаДругихЯзыках Цикл
				ОбщийМодульСервер.обновитьЗначениеНаЯзыке(ОбъектСсылка, СтрокаЯзыка.НаЯзыке, "Наименование", СтрокаЯзыка.Язык);
			КонецЦикла;

			Для Каждого СтрокаЯзыка Из НаименованияДляПечатиНаДругихЯзыках Цикл
				ОбщийМодульСервер.обновитьЗначениеНаЯзыке(ОбъектСсылка, СтрокаЯзыка.НаЯзыке, "НаименованиеДляПечати", СтрокаЯзыка.Язык);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

	Если ДопИнформацияИзменена Тогда
		ОбщийМодульСервисСервер.ЗаписатьДополнительнуюИнформацию(ОбъектСсылка, ДополнительнаяИнформация);
	КонецЕсли;

	ПредставлениеНаименования 	 = ОбщийМодульПовтор.получитьПредставлениеНаЯзыке(ОбъектСсылка);
	НаименованиеДляПечати 		 = ОбщийМодульПовтор.получитьПредставлениеНаЯзыке(ОбъектСсылка, "НаименованиеДляПечати");
	Попытка
		Штрихкоды.Параметры.УстановитьЗначениеПараметра("Номенклатура", ОбъектСсылка);
		ДополнительныеШтрихКоды.Параметры.УстановитьЗначениеПараметра("Номенклатура", ОбъектСсылка);
	Исключение
	КонецПопытки;
	Спецификации.Параметры.УстановитьЗначениеПараметра("Номенклатура", ОбъектСсылка);
	Аналоги.Параметры.УстановитьЗначениеПараметра("Номенклатура", ОбъектСсылка);
	ГруппыАналогов.Параметры.УстановитьЗначениеПараметра("Номенклатура", ОбъектСсылка);
	ПредставленияКонтрагентов.Параметры.УстановитьЗначениеПараметра("Номенклатура", ОбъектСсылка);
	Элементы.ПредставленияКонтрагентов.Видимость = ИспользоватьПредставлениеТоваровКонтрагентами И ЗначениеЗаполненоОбъектСсылка;
	Элементы.Спецификации.Видимость = ИспользоватьКомплектацию И ЗначениеЗаполненоОбъектСсылка;
	Элементы.Аналоги.Видимость = ИспользоватьМеханизмАналоговДляНоменклатуры И ЗначениеЗаполненоОбъектСсылка;
	Элементы.ГруппыАналогов.Видимость = ИспользоватьМеханизмАналоговДляНоменклатуры И ЗначениеЗаполненоОбъектСсылка;
	Элементы.ГруппаЦеныДляНовойНоменклатуры.Видимость = НЕ ЗначениеЗаполненоОбъектСсылка;
	ЕдиницыИзмеренияНоменклатуры.Параметры.УстановитьЗначениеПараметра("Номенклатура", ОбъектСсылка);

	Элементы.ЕдиницыИзмеренияНоменклатуры.Видимость = ИспользоватьЕдиницыИзмеренияНоменклатуры;
	Элементы.ОсновнаяЕдиницаИзмерения1.Видимость = ИспользоватьЕдиницыИзмеренияНоменклатуры;

КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеКонтрагентов(Команда)

	ПараметрыФормы = Новый Структура("Номенклатура", Объект.Ссылка);
	ОткрытьФорму("РегистрСведений.ПредставлениеТоваровКонтрагентами.ФормаСписка", ПараметрыФормы);

КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеНаДругихЯзыках(Команда)
	Элементы.НаименованияНаДругихЯзыках.Видимость = НЕ Элементы.НаименованияНаДругихЯзыках.Видимость;
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеНаименованияПриИзменении(Элемент)

	Объект.Наименование = ПредставлениеНаименования;
	НаименованиеИзменяли = ИСТИНА;

	Если СоздаватьГруппыНоменклатурыПоПервомуСловуИОтноситьКНим Тогда
		ОбъектРодитель = НаименованиеПриИзмененииНаСервере(ПредставлениеНаименования, Объект.Родитель);
		Если НЕ ОбъектРодитель = Неопределено Тогда
			Объект.Родитель = ОбъектРодитель;
		КонецЕсли;
	КонецЕсли;

	Если ПереводитьНаименованияАвтоматически Тогда
		ОбщийМодульКлиент.ПеревестиНаименованияАвтоматически(ПредставлениеНаименования, НаименованияНаДругихЯзыках);
		БылиИзмененияЗначенияПолейНаЯзыках = ИСТИНА;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()                                              // ПРИ ЗАКРЫТИИ

	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 1, Объект.Ссылка, ИСТИНА);

	Если ИспользоватьПодключаемоеОборудование Тогда
		ПоддерживаемыеТипыВО = Новый Массив ();
		ПоддерживаемыеТипыВО.Добавить("СчитывательМагнитныхКарт");
		ПоддерживаемыеТипыВО.Добавить("СканерШтрихКода");
		ПоддерживаемыеТипыВО.Добавить("ДисплейПокупателя");
		МенеджерОборудованияКлиент.ОтключитьОборудованиеПоТипу(УникальныйИдентификатор, ПоддерживаемыеТипыВО);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)                                         // ПРИ ОТКРЫТИИ

	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 0, Объект.Ссылка, ИСТИНА);

	Если ИспользоватьПодключаемоеОборудование
		И МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда

		ОписаниеОшибки = "" ;
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("СканерШтрихКода");
		Если НЕ МенеджерОборудованияКлиент.ПодключитьОборудованиеПоТипу(УникальныйИдентификатор, ПоддерживаемыеТипыВО, ОписаниеОшибки) Тогда
			ТекстСообщения = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("При подключении оборудования произошла ошибка") + ": " + ОписаниеОшибки + ".";
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;

	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если ЗначениеЗаполнено(СтраницаПанели) Тогда
			СтраницыПанели  = Элементы.ГруппаСтраницСправа.ПодчиненныеЭлементы;
			ТекущаяСтраница = СтраницыПанели.получить(СтраницаПанели);
			Если НЕ ТекущаяСтраница = Неопределено Тогда
				Элементы.ГруппаСтраницСправа.ТекущаяСтраница = ТекущаяСтраница;
			КонецЕсли;
		КонецЕсли;

		Если ЗначениеЗаполнено(СтраницаОсновноеДополнительно) Тогда
			СтраницыПанели  = Элементы.ГруппаСтраниц.ПодчиненныеЭлементы;
			ТекущаяСтраница = СтраницыПанели.получить(СтраницаОсновноеДополнительно);
			Если НЕ ТекущаяСтраница = Неопределено Тогда
				Элементы.ГруппаСтраниц.ТекущаяСтраница = ТекущаяСтраница;
			КонецЕсли;
		КонецЕсли;
	Иначе
		ЭтаФорма.ТекущийЭлемент = Элементы.ПредставлениеНаименования;
	КонецЕсли;

	ОбновитьРазмеры();
	СерийныйУчетПриИзмененииК();
	ВидимостьТаблицСправа();
	ОбновитьДляВаучеров();

	Если ЕстьКлючиУчета Тогда
		СменитьСтраницуКлючейУчетаНаСервере("1");
	КонецЕсли;

	ВыводНадписиПоФормуле();

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)          // ПРИ СОЗДАНИИ НА СЕРВЕРЕ

	Отказ = ОбщийМодульСервисСервер.ПроверитьОтказДоступа("000300", ЭтаФорма, Отказ, Объект);

	Если НЕ Отказ Тогда
		Если ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьСобственныйПереводЭлементовИнтерфейса") Тогда
			ОбщийМодульСервер.ПеревестиРеквизитыФормы(ЭтаФорма);
		КонецЕсли;

		ОбъектСсылка = Объект.Ссылка;
		НоменклатураНеИспользовалась 	= ИСТИНА;
		ЗначениеЗаполненоОбъектСсылка 	= ЗначениеЗаполнено(ОбъектСсылка);
		ВестиУчетПоСкладам 				= ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ВестиУчетПоСкладам");
		ВестиУчетОказанияУслугЗаВремя   = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ВестиУчетОказанияУслугЗаВремя");
		СоздаватьГруппыНоменклатурыПоПервомуСловуИОтноситьКНим = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("СоздаватьГруппыНоменклатурыПоПервомуСловуИОтноситьКНим");
		ИспользоватьСложныйМеханизмЦен 	= ОбщийМодульПовтор.ПолучитьПараметрСеанса("ИспользоватьСложныйМеханизмЦенПС");

		Если НЕ ИспользоватьСложныйМеханизмЦен Тогда
			Элементы.СтраницаОстаткиИДанные.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Остатки");
		КонецЕсли;

		ВестиУчетНоменклатурыВРазрезеНоменклатурныхГрупп = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ВестиУчетНоменклатурыВРазрезеНоменклатурныхГрупп");
		УчетПоСериям = ОбщийМодульПовтор.ПолучитьПараметрСеанса("ВестиУчетПоСериямНоменклатуры")
		ИЛИ ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ВестиУчетСерийНоменклатурыТолькоПриПоступлении");

		Дата = ОбщийМодульСервисСервер.ПользователяТекущаяДата();
		
		ПоддержкаДругихЯзыков = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ПоддерживатьИныеЯзыкиКромеРусского")
		И ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ПоддерживатьНаименованияНаДругихЯзыкахКромеРусского", ИСТИНА);
		
		Элементы.ЦеныДобавитьЦену.Видимость = ИспользоватьСложныйМеханизмЦен И ЗначениеЗаполненоОбъектСсылка;
		ВключитьВозможностьУказыватьРекомендованныйМинимальныйОстатокНоменклатуры = ИСТИНА; // для алгоритма
		РегулироватьКоличествоНоменклатурыПоУмолчаниюДляУпаковок = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("РегулироватьКоличествоНоменклатурыПоУмолчаниюДляУпаковок");
		ИспользоватьДополнительныеРеквизитыНоменклатурныхГрупп   = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьДополнительныеРеквизитыНоменклатурныхГрупп");
		ИспользоватьЕдиницыИзмеренияНоменклатуры				 = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьЕдиницыИзмеренияНоменклатуры");
		ИспользоватьШтрихКоды 		= ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьШтрихКоды");
		ИспользоватьКомплектацию 	= ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьКомплектацию");
		ИспользоватьМеханизмАналоговДляНоменклатуры 	= ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьМеханизмАналоговДляНоменклатуры");
		ИспользоватьПредставлениеТоваровКонтрагентами 	= ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьПредставлениеТоваровКонтрагентами");
		Элементы.ОбновитьШтрихКодПоФормуле.Видимость 	= ИспользоватьШтрихКоды И ПустаяСтрока(Объект.ОсновнойШтрихКод);
		Элементы.ЕдиницыИзмеренияНоменклатуры.Видимость = ИспользоватьЕдиницыИзмеренияНоменклатуры;
		Элементы.Изображения.Видимость 		= ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("СопровождатьНоменклатуруИзображениями");
		Элементы.ШтрихКоды.Видимость 		= ИспользоватьШтрихКоды;
		ЭтотЭлементПредопределенный = Объект.Предопределенный;

		Если НЕ ЗначениеЗаполненоОбъектСсылка Тогда
			Если ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьКлючиУчетаНоменклатурыИИхСвязанныеСписки") Тогда
				Элементы.КлючиУчетаБудутДоступныПослеЗаписиНоменклатуры.Видимость = ИСТИНА;
			КонецЕсли;

			Если НЕ ЗначениеЗаполнено(Объект.Родитель)
				И Параметры.Родитель.ЭтоГруппа
				И ЗначениеЗаполнено(Параметры.Родитель) Тогда

				Объект.Родитель = Параметры.Родитель;
			КонецЕсли;

			Если НЕ ЗначениеЗаполнено(Объект.ОсновнаяЕдиницаИзмерения) Тогда
				Объект.ОсновнаяЕдиницаИзмерения = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("НаименованиеОсновнойЕдиницыИзмеренияПоУмолчанию");
			КонецЕсли;

			Объект.СерийныйУчет = Параметры.СерийныйУчет;

			Если НЕ ЗначениеЗаполнено(Объект.НоменклатурнаяГруппа) Тогда
				Объект.НоменклатурнаяГруппа = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("НоменклатурнаяГруппаПоУмолчанию");

				Если ЗначениеЗаполнено(Объект.НоменклатурнаяГруппа)
					И ЗначениеЗаполнено(Объект.НоменклатурнаяГруппа.ОбщееНаименованиеЕдиницыИзмерения) Тогда

					Объект.ОсновнаяЕдиницаИзмерения = Объект.НоменклатурнаяГруппа.ОбщееНаименованиеЕдиницыИзмерения;
				КонецЕсли;
			КонецЕсли;

			Если НЕ ЗначениеЗаполнено(Объект.Производитель) Тогда
				Объект.Производитель = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ПроизводительПоУмолчанию");
			КонецЕсли;

			Если Объект.ПланкаКоличестваДляЛьготнойЦены = 0 Тогда
				Объект.ПланкаКоличестваДляЛьготнойЦены = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("РазмерПланкиКоличестваТовараДляЛьготнойЦены");
			КонецЕсли;

			Элементы.Цены.Видимость = ЛОЖЬ;
			ОбъектРодитель = Объект.Родитель;
			Если ЗначениеЗаполнено(ОбъектРодитель) Тогда
				Если ОбъектРодитель.НеОтслеживатьОстаток Тогда
					Элементы.ТоварИлиУслуга.Видимость = ЛОЖЬ;
					Объект.НеОтслеживатьОстаток = ИСТИНА;
					УслугаНЕТовар = ИСТИНА;
				Иначе
					Элементы.ТоварИлиУслуга.Видимость = ИСТИНА;
				КонецЕсли;

				Если ВестиУчетНоменклатурыВРазрезеНоменклатурныхГрупп
					И НЕ ЗначениеЗаполнено(Объект.НоменклатурнаяГруппа) Тогда

					Если ЗначениеЗаполнено(Объект.Родитель.НоменклатурнаяГруппа) Тогда
						Объект.НоменклатурнаяГруппа = Объект.Родитель.НоменклатурнаяГруппа;
					Иначе

						Запрос = Новый Запрос;
						Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1 НоменклатурныеГруппы.Ссылка
						|ИЗ Справочник.НоменклатурныеГруппы КАК НоменклатурныеГруппы
						|ГДЕ НоменклатурныеГруппы.СвязатьОдноименнуюГруппуНоменклатуры = ИСТИНА
						|	И НоменклатурныеГруппы.Наименование = &Наименование";
						Запрос.УстановитьПараметр("Наименование", ОбъектРодитель.Наименование);

						РезультатЗапроса = Запрос.Выполнить();

						Если НЕ РезультатЗапроса.Пустой() Тогда
							ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
							Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
								Объект.НоменклатурнаяГруппа = ВыборкаДетальныеЗаписи.ссылка;

							КонецЦикла;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;

			Элементы.КолонкаСправа.Видимость 			= ЛОЖЬ;
			Элементы.ОстаткиБезСклада.Видимость 		= ЛОЖЬ;
			Элементы.ГруппаКвоИСуммаГоризонт.Видимость 	= ЛОЖЬ;
			Элементы.СредняяЦенаПриобретения.Видимость 	= ЛОЖЬ;
			Элементы.Аналоги.Видимость 					= ЛОЖЬ;
			Элементы.ГруппыАналогов.Видимость 			= ЛОЖЬ;
			ЗаполнитьЦеныНовойНоменклатуры();

			Элементы.ГруппаЦеныДляНовойНоменклатуры.Видимость = ИспользоватьСложныйМеханизмЦен
			И НЕ ЦеныДляНовойНоменклатуры.Количество() = 0;
			Элементы.ЗависимыеЦеныБудутРасчитаныАвтоматически.Видимость = ИспользоватьСложныйМеханизмЦен
			И ОбщийМодульСервер.СуществуютЗависимыеВидыЦен();
			ОбновитьДополнительныеРеквизитыНоменклатурнойГруппы(ИСТИНА);
			// Элементы.ЕдиницыИзмеренияНоменклатуры.Видимость   = ЛОЖЬ;
		ИначеЕсли НЕ Отказ Тогда // существующая
			УслугаНЕТовар = Объект.НеОтслеживатьОстаток;
			Элементы.ГруппаЦеныДляНовойНоменклатуры.Видимость = ЛОЖЬ;
			НоменклатураНеИспользовалась = ОбщийМодульСервер.ПроверитьЧтоНоменклатураНеИспользовалась(ОбъектСсылка);
			СкоростьПродажВидимость	= НЕ НоменклатураНеИспользовалась;

			Если Объект.Предопределенный
				ИЛИ Объект.НеОтслеживатьОстаток Тогда // Товар на сумму

				Элементы.Родитель.Видимость 		= ЛОЖЬ;
				Элементы.ГруппаНабора.Видимость 	= ЛОЖЬ;
				Элементы.ОстаткиБезСклада.Видимость = ЛОЖЬ;
				Элементы.ТоварИлиУслуга.Видимость 	= ЛОЖЬ;
				Элементы.ОстаткиНоменклатуры.Видимость 		= ЛОЖЬ;
				Элементы.СредняяЦенаПриобретения.Видимость 	= ЛОЖЬ;
				Элементы.ГруппаКвоИСуммаГоризонт.Видимость 	= ЛОЖЬ;
				Элементы.РекомендуемыйМинимальныйОстатокТовара.Видимость = ЛОЖЬ;
				ВключитьВозможностьУказыватьРекомендованныйМинимальныйОстатокНоменклатуры = ЛОЖЬ;
				СкоростьПродажВидимость	= ЛОЖЬ;

			ИначеЕсли Объект.ЭтоНабор = ИСТИНА
				И ОбщийМодульПовтор.ПолучитьПараметрСеанса("ИспользоватьНаборыТоваровПС") Тогда

				ДоступностьСостава = ОбщийМодульСервер.ПроверитьЧтоНоменклатураНеИспользовалась(ОбъектСсылка);

				Если ДоступностьСостава Тогда
					Элементы.Состав.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Состав набора");
				Иначе
					Элементы.Состав.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Состав набора нельзя изменить после указания в документах. Создавайте новые наборы для иного состава.");
				КонецЕсли;

				Элементы.ЭтоНабор.ТолькоПросмотр 			= НЕ ДоступностьСостава;
				Элементы.Состав.ТолькоПросмотр 				= НЕ ДоступностьСостава;
				Элементы.ОстаткиБезСклада.Видимость 		= ЛОЖЬ;
				Элементы.ОстаткиНоменклатуры.Видимость 		= ЛОЖЬ;
				Элементы.ГруппаКвоИСуммаГоризонт.Видимость 	= ЛОЖЬ;
				Элементы.СредняяЦенаПриобретения.Видимость 	= ЛОЖЬ;
				Элементы.ТоварИлиУслуга.Видимость 			= ЛОЖЬ;
				Элементы.ОстаткиДляНабора.Видимость			= ИСТИНА;
				Элементы.РекомендуемыйМинимальныйОстатокТовара.Видимость = ЛОЖЬ;
				ОбновитьДанныеОбОстаткеНаСервере();
			Иначе
				Элементы.ТоварИлиУслуга.Видимость 	= ЛОЖЬ;
				Элементы.ГруппаНабора.Видимость 	= НоменклатураНеИспользовалась;
				Элементы.ОстатокНеОтслеживается.Видимость	= ЭтотЭлементПредопределенный ИЛИ НЕ НоменклатураНеИспользовалась;
				Элементы.НеОтслеживатьОстаток.Видимость		= НоменклатураНеИспользовалась И НЕ ЭтотЭлементПредопределенный;
				Элементы.СерийныйУчет.Доступность			= НоменклатураНеИспользовалась;
				Элементы.ГруппаСерийногоУчета.Доступность 	= НоменклатураНеИспользовалась;
				Элементы.ОстаткиНоменклатурыСклад.Видимость = ВестиУчетПоСкладам;
				ОбновитьДанныеОбОстаткеНаСервере();
				Элементы.ОстаткиБезСклада.Видимость 		= НЕ ВестиУчетПоСкладам;
				Элементы.ОстаткиНоменклатуры.Видимость 		= ВестиУчетПоСкладам;
			КонецЕсли;

			Элементы.Цены.Видимость = ИспользоватьСложныйМеханизмЦен;

			Если СкоростьПродажВидимость Тогда
				ВыводитьВКарточкеНоменклатурыСкоростьПродажВ = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ВыводитьВКарточкеНоменклатурыСкоростьПродажВ");
				Если ЗначениеЗаполнено(ВыводитьВКарточкеНоменклатурыСкоростьПродажВ) Тогда
					СкоростьПродаж = ОбщийМодульТоварСервер.ПолучитьСкоростьРеализации(ОбъектСсылка, ВыводитьВКарточкеНоменклатурыСкоростьПродажВ, ИСТИНА);
					Элементы.СкоростьПродаж.Видимость = ИСТИНА;
				КонецЕсли;
			КонецЕсли;

			Если НЕ НоменклатураНеИспользовалась
				И ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("БлокироватьКарточкуНоменклатурыПослеУказанияЕеВДокументах") Тогда

				Элементы.ПредставлениеНаименования.ТолькоПросмотр = ИСТИНА;
				Элементы.ГруппаВторогоРяда.ТолькоПросмотр 	  	  = ИСТИНА;
			КонецЕсли;

			Если (Объект.СерийныйУчет
				И НЕ Найти(Объект.Наименование, ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Новый серийный товар")) = 0)
				ИЛИ НЕ Найти(Объект.Наименование, ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Копия товара с ценами")) = 0
				ИЛИ НЕ Найти(Объект.Наименование, ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Новый товар")) = 0 Тогда

				Объект.Наименование = "";
				ЭтаФорма.ТекущийЭлемент = Элементы.ПредставлениеНаименования;
			КонецЕсли;

			ОбновитьДополнительныеРеквизитыНоменклатурнойГруппы();
		КонецЕсли;

		ЕдиницыИзмеренияНоменклатуры.Параметры.УстановитьЗначениеПараметра("Номенклатура", ОбъектСсылка);
		Штрихкоды.Параметры.УстановитьЗначениеПараметра("Номенклатура", ОбъектСсылка);
		ДополнительныеШтрихКоды.Параметры.УстановитьЗначениеПараметра("Номенклатура", ОбъектСсылка);
		Если ВключитьВозможностьУказыватьРекомендованныйМинимальныйОстатокНоменклатуры Тогда
			ПоведениеПрограммыПриРасходеТоваровНижеРекомендованногоМинимальногоОстатка = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ПоведениеПрограммыПриРасходеТоваровНижеРекомендованногоМинимальногоОстатка");
			ВключитьВозможностьУказыватьРекомендованныйМинимальныйОстатокНоменклатуры = ОбщийМодульПовтор.ПолучитьПараметрСеанса("ВключитьВозможностьУказыватьРекомендованныйМинимальныйОстатокНоменклатуры");
		КонецЕсли;

		Элементы.ГруппаГарантии.Видимость = НЕ УслугаНЕТовар
		И ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ПоказыватьКнопкуПечатиГарантийногоТалонаВРасходномДокументе");
		Элементы.ПериодОказанияУслугиЗаВремя.Видимость = УслугаНЕТовар И ВестиУчетОказанияУслугЗаВремя;
		Аналоги.Параметры.УстановитьЗначениеПараметра("Номенклатура", ОбъектСсылка);
		ГруппыАналогов.Параметры.УстановитьЗначениеПараметра("Номенклатура", ОбъектСсылка);
		Элементы.Аналоги.Видимость = ИспользоватьМеханизмАналоговДляНоменклатуры;
		Элементы.ГруппыАналогов.Видимость = ИспользоватьМеханизмАналоговДляНоменклатуры;
		Спецификации.Параметры.УстановитьЗначениеПараметра("Номенклатура", ОбъектСсылка);
		Элементы.Спецификации.Видимость = ИспользоватьКомплектацию И ЗначениеЗаполненоОбъектСсылка;
		НоменклатурнаяГруппаПриИзмененииНаСервере(ИСТИНА);
		ИспользоватьПодключаемоеОборудование 		= ПодключаемоеОборудованиеДСервер.ИспользоватьПодключаемоеОборудование();
		УстанавливатьШтрихКодИзКарточкиНоменклатуры = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьШтрихКоды")
		И ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("УстанавливатьШтрихКодИзКарточкиНоменклатуры");

		ОстаткиНоменклатуры.Параметры.УстановитьЗначениеПараметра("Номенклатура", ОбъектСсылка);
		ОстаткиНоменклатуры.Параметры.УстановитьЗначениеПараметра("ДатаОстатка", Дата);
		СерииВНаличии.Параметры.УстановитьЗначениеПараметра("ДатаОстатка", Дата);
		ПредставленияКонтрагентов.Параметры.УстановитьЗначениеПараметра("Номенклатура", ОбъектСсылка);
		Элементы.ПредставленияКонтрагентов.Видимость = ИспользоватьПредставлениеТоваровКонтрагентами И ЗначениеЗаполненоОбъектСсылка;
		ОбщийМодульПовтор.ЗаполнитьСписокВыбораЕдиницИзмерения(Элементы.ОсновнаяЕдиницаИзмерения.СписокВыбора);
		Цены.Параметры.УстановитьЗначениеПараметра("Номенклатура", ОбъектСсылка);
		Цены.Параметры.УстановитьЗначениеПараметра("ДатаОстатка", Дата);
		СерииВНаличии.Параметры.УстановитьЗначениеПараметра("Номенклатура", ОбъектСсылка);
		Изображения.Параметры.УстановитьЗначениеПараметра("Владелец", ОбъектСсылка);
		Элементы.Цена.Видимость = НЕ ИспользоватьСложныйМеханизмЦен;

		Если НЕ Отказ
			И Объект.SKU = 0 Тогда

			Объект.SKU = ПодключаемоеОборудованиеДСервер.ПолучитьМаксимальныйКодSKU();
		КонецЕсли;

		НаименованиеОсновнойВалюты = ПредопределенноеЗначение("Справочник.Валюты.ОсновнаяВалюта").Наименование ;
		Элементы.НаименованиеОсновнойВалюты.Видимость = НЕ ОбщийМодульПовтор.ПолучитьПараметрСеанса("ИспользоватьСложныйМеханизмЦенПС");
		Элементы.КоличествоПоУмолчанию.Видимость 	  = НЕ Объект.СерийныйУчет
		И РегулироватьКоличествоНоменклатурыПоУмолчаниюДляУпаковок;
		Элементы.ПроцентСкидки.Видимость = ОбщийМодульПовтор.ПолучитьПараметрСеанса("ИспользоватьСкидкиПС")
		И ОбщийМодульПовтор.ПолучитьПараметрСеанса("ВключитьВозможностьУказыватьПроцентСкидкиДляНоменклатуры");
		ВидимостьСоставаНабора();

		Если НЕ Отказ
			И ОбщийМодульПовтор.ПолучитьПараметрСеанса("НеМожетМенятьЦены") Тогда

			Элементы.ПересчитатьЦенуНабораПоСуммеЦенВсехЭлементовСостава.Видимость = ЛОЖЬ;
			Элементы.ПроцентСкидки.Доступность 	= ЛОЖЬ;
			Элементы.Цены.ТолькоПросмотр 		= ИСТИНА;
			Элементы.ЦеныДобавитьЦену.Видимость = ЛОЖЬ;
			Элементы.ПланкаКоличестваДляЛьготнойЦены.ТолькоПросмотр = ИСТИНА;
		КонецЕсли;

		ДопИнфоТипВладельца = ОбщийМодульПовтор.ПолучитьТипВладельца(ОбъектСсылка);
		ОбщийМодульСервисСервер.ЗаполнитьДополнительнуюИнформацию(ОбъектСсылка, ДополнительнаяИнформация, ДопИнфоТипВладельца);
		ОбновитьВидимостьУслуги();
		РодительПриИзмененииНаСервере();

		Элементы.НоменклатурнаяГруппа.ОтметкаНезаполненного = ВестиУчетНоменклатурыВРазрезеНоменклатурныхГрупп
		И НЕ ЗначениеЗаполнено(Объект.НоменклатурнаяГруппа)
		И ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("НоменклатурнаяГруппаОбязательныйРеквизитЛюбойНоменклатуры");
		Элементы.ОбновитьАртикулПоФормуле.Видимость = НЕ Объект.НеОтслеживатьОстаток
		И ЗначениеЗаполнено(ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ФормулаФормированияАртикула"));

		Если НЕ Отказ
			И ПоддержкаДругихЯзыков
			И ЗначениеЗаполненоОбъектСсылка Тогда

			ПредставлениеНаименования = ОбщийМодульПовтор.получитьПредставлениеНаЯзыке(ОбъектСсылка);
			НаименованиеДляПечати 	  = ОбщийМодульПовтор.получитьПредставлениеНаЯзыке(ОбъектСсылка, "НаименованиеДляПечати");

			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ ЗначенияНаДругихЯзыках.Язык,
			|	ЗначенияНаДругихЯзыках.НаЯзыке,
			|	ЗначенияНаДругихЯзыках.Поле
			|ИЗ РегистрСведений.ЗначенияНаДругихЯзыках КАК ЗначенияНаДругихЯзыках
			|ГДЕ ЗначенияНаДругихЯзыках.ОбъектБазыДанных = &ОбъектБазыДанных";
			Запрос.УстановитьПараметр("ОбъектБазыДанных", ОбъектСсылка);

			РезультатЗапроса = Запрос.Выполнить();

			Если НЕ РезультатЗапроса.Пустой() Тогда
				ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					Если ВыборкаДетальныеЗаписи.Поле = "Наименование" Тогда
						СтрокаЯзыка = НаименованияНаДругихЯзыках.Добавить();
						СтрокаЯзыка.Язык 	= ВыборкаДетальныеЗаписи.Язык;
						СтрокаЯзыка.НаЯзыке = ВыборкаДетальныеЗаписи.НаЯзыке;
					ИначеЕсли ВыборкаДетальныеЗаписи.Поле = "НаименованиеДляПечати" Тогда
						СтрокаЯзыка = НаименованияДляПечатиНаДругихЯзыках.Добавить();
						СтрокаЯзыка.Язык 	= ВыборкаДетальныеЗаписи.Язык;
						СтрокаЯзыка.НаЯзыке = ВыборкаДетальныеЗаписи.НаЯзыке;
					КонецЕсли;

				КонецЦикла;
			КонецЕсли;
		Иначе
			ПредставлениеНаименования = Объект.Наименование;
			НаименованиеДляПечати 	  = Объект.НаименованиеДляПечати;
		КонецЕсли;

		Если ПоддержкаДругихЯзыков Тогда
			ПереводитьНаименованияАвтоматически = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьРегистрТекстовНаДругихЯзыкахДляАвтоматическогоПереводаНаименований");
			Элементы.НаименованияНаДругихЯзыкахЯзык.РежимВыбораИзСписка = ИСТИНА;
			ОбщийМодульПовтор.УстановитьСписокДоступныхЯзыков(Элементы.НаименованияНаДругихЯзыкахЯзык.СписокВыбора, ИСТИНА);
			Элементы.НаименованияДляПечатиНаДругихЯзыкахЯзык.РежимВыбораИзСписка = ИСТИНА;
			ОбщийМодульПовтор.УстановитьСписокДоступныхЯзыков(Элементы.НаименованияДляПечатиНаДругихЯзыкахЯзык.СписокВыбора, ИСТИНА);
		КонецЕсли;

		ОбщийМодульСервисСервер.ОформитьФорматКоличества(Элементы.ПланкаКоличестваДляЛьготнойЦены, ИСТИНА);
		ОбщийМодульСервисСервер.ОформитьФорматКоличества(Элементы.РекомендуемыйМинимальныйОстатокТовара, ИСТИНА);
		ОбщийМодульСервисСервер.ОформитьФорматКоличества(Элементы.КоличествоПоУмолчанию, ИСТИНА);
		ОбщийМодульСервисСервер.ОформитьФорматКоличества(Элементы.ОстаткиНоменклатурыОстаток, , ИСТИНА);
		ОбщийМодульСервисСервер.ОформитьФорматКоличества(Элементы.СоставКоличество);
		ОбщийМодульСервисСервер.ОформитьФорматКоличества(Элементы.ЕдиницыИзмеренияНоменклатурыКоличество, , ИСТИНА);
		ОбщийМодульСервисСервер.ОформитьФорматКоличества(Элементы.НаОдноМесто, ИСТИНА);
		ОбщийМодульСервисСервер.ОформитьФорматКоличества(Элементы.ДоступныйОстаток, , ИСТИНА);
		ОбщийМодульСервисСервер.ОформитьФорматКоличества(Элементы.Остаток, , ИСТИНА);
		ОбщийМодульСервисСервер.ОформитьФорматКоличества(Элементы.ОстаткиНабораПоСкладамДоступныйОстаток);
		Элементы.ДопШтрихКоды.Видимость = ИспользоватьШтрихКоды И ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьДополнительныеУстаревшиеШтрихКодыНоменклатуры");

		Если ЗначениеЗаполнено(ОбъектСсылка) Тогда
			ОбновитьСвязанныеКлючиУчетаДляФормыНоменклатуры();
		КонецЕсли;

		ОбщийМодульСервер.ОбеспечитьСписокОтборов(Цены, , "РегистрСведений.Цены");
		ОбщийМодульСервер.ОбеспечитьСписокОтборов(ШтрихКоды);
		ОбщийМодульСервер.ОбеспечитьСписокОтборов(Спецификации);
		ОбщийМодульСервер.ОбеспечитьСписокОтборов(Аналоги);
		ОбщийМодульСервер.ОбеспечитьСписокОтборов(ОстаткиНоменклатуры, , "РегистрНакопления.Товары");
		ГруппаВаучера = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьВаучеры") И НЕ ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("НеОтслеживатьВаучерыВКачествеНоменклатуры");
		Элементы.ГруппаВаучера.Видимость = ГруппаВаучера;
		Элементы.КоэффициентНакопленияНаВаучер.Видимость = ГруппаВаучера И ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ВаучерыНакопительнымиКартами");
		Элементы.РегистрСведенийСебестоимостьТоваровБезКоличественногоУчетаИУслугОткрытьСписок.Видимость = ЗначениеЗаполненоОбъектСсылка И Объект.НеОтслеживатьОстаток;

		Если ПараметрыСеанса.ВерсияПриложения < 803050000
			ИЛИ ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ВместоТумблеровПоказыватьГалочки") Тогда

			Если НЕ ПараметрыСеанса.ВерсияПриложения < 803050000 Тогда
				Выполнить(" Элементы.ТоварИлиУслуга.ВидФлажка = ВидФлажка.Флажок;
				|Элементы.НеОтслеживатьОстаток.ВидФлажка = ВидФлажка.Флажок; ");
			КонецЕсли;

			Элементы.ТоварИлиУслуга.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Тип новой номенклатуры: Услуга");
			Элементы.ТоварИлиУслуга.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
			Элементы.НеОтслеживатьОстаток.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Не отслеживать количество для позиции (услуги)");
			Элементы.НеОтслеживатьОстаток.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
		КонецЕсли;
	КонецЕсли;

	ОбщийМодульСервисСервер.ОбработатьНовуюФорму(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура РеакцияНаПрочиеСобытияФормы(КомандаСобытияФормы) Экспорт // Для реакции на события для запрограммированных элементов
	ОбщийМодульКлиент.РеакцияНаПрочиеСобытияФормы(КомандаСобытияФормы, ЭтаФорма);
КонецПроцедуры

&НаСервере
Функция   ПроверитьНаОтказ()

	Отказ = ЛОЖЬ;
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ПодготовкаКПроведению(Отказ);
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");

	Возврат Отказ;

КонецФункции

&НаКлиенте
Процедура ПроизводительПриИзменении(Элемент)
	// НоменклатурнаяГруппаПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура РеквизитНоменклатурнойГруппы10НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбработатьВыборДопРеквизитаНГ("10", СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РеквизитНоменклатурнойГруппы10Очистка(Элемент, СтандартнаяОбработка)
	ОбработатьОчисткуДопРеквизитаНГ("10", СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РеквизитНоменклатурнойГруппы1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбработатьВыборДопРеквизитаНГ("1", СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РеквизитНоменклатурнойГруппы1Очистка(Элемент, СтандартнаяОбработка)
	ОбработатьОчисткуДопРеквизитаНГ("1", СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РеквизитНоменклатурнойГруппы2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбработатьВыборДопРеквизитаНГ("2", СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РеквизитНоменклатурнойГруппы2Очистка(Элемент, СтандартнаяОбработка)
	ОбработатьОчисткуДопРеквизитаНГ("2", СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РеквизитНоменклатурнойГруппы3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбработатьВыборДопРеквизитаНГ("3", СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РеквизитНоменклатурнойГруппы3Очистка(Элемент, СтандартнаяОбработка)
	ОбработатьОчисткуДопРеквизитаНГ("3", СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РеквизитНоменклатурнойГруппы4НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбработатьВыборДопРеквизитаНГ("4", СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РеквизитНоменклатурнойГруппы4Очистка(Элемент, СтандартнаяОбработка)
	ОбработатьОчисткуДопРеквизитаНГ("4", СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РеквизитНоменклатурнойГруппы5НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбработатьВыборДопРеквизитаНГ("5", СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РеквизитНоменклатурнойГруппы5Очистка(Элемент, СтандартнаяОбработка)
	ОбработатьОчисткуДопРеквизитаНГ("5", СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РеквизитНоменклатурнойГруппы6НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбработатьВыборДопРеквизитаНГ("6", СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РеквизитНоменклатурнойГруппы6Очистка(Элемент, СтандартнаяОбработка)
	ОбработатьОчисткуДопРеквизитаНГ("6", СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РеквизитНоменклатурнойГруппы7НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбработатьВыборДопРеквизитаНГ("7", СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РеквизитНоменклатурнойГруппы7Очистка(Элемент, СтандартнаяОбработка)
	ОбработатьОчисткуДопРеквизитаНГ("7", СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РеквизитНоменклатурнойГруппы8НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбработатьВыборДопРеквизитаНГ("8", СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РеквизитНоменклатурнойГруппы8Очистка(Элемент, СтандартнаяОбработка)
	ОбработатьОчисткуДопРеквизитаНГ("8", СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РеквизитНоменклатурнойГруппы9НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбработатьВыборДопРеквизитаНГ("9", СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РеквизитНоменклатурнойГруппы9Очистка(Элемент, СтандартнаяОбработка)
	ОбработатьОчисткуДопРеквизитаНГ("9", СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РекомендуемыйМинимальныйОстатокТовараПриИзменении(Элемент)
	ОбновитьВидимостьУслуги();
КонецПроцедуры

&НаКлиенте
Процедура РодительПриИзменении(Элемент)
	РодительПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура РодительПриИзмененииНаСервере()

	Элементы.ПланкаКоличестваДляЛьготнойЦены.Доступность 		= ИСТИНА;
	Элементы.РекомендуемыйМинимальныйОстатокТовара.Доступность 	= ИСТИНА;
	Элементы.ОстатокНеОтслеживается.Видимость	= ЭтотЭлементПредопределенный ИЛИ (Объект.НеОтслеживатьОстаток И НЕ НоменклатураНеИспользовалась);
	Элементы.НеОтслеживатьОстаток.Видимость 	= НЕ ЭтотЭлементПредопределенный И НоменклатураНеИспользовалась;
	Элементы.ГруппаСерийногоУчета.Доступность 	= НоменклатураНеИспользовалась;
	Элементы.ГруппаСерийногоУчета.Видимость		= НЕ УслугаНЕТовар И НЕ Объект.НеОтслеживатьОстаток;
	Родитель = Объект.Родитель;

	Если ЗначениеЗаполнено(Родитель) Тогда
		Если ВестиУчетНоменклатурыВРазрезеНоменклатурныхГрупп ТОгда

			Если НЕ ЗначениеЗаполнено(Объект.НоменклатурнаяГруппа)
				И ЗначениеЗаполнено(Родитель.НоменклатурнаяГруппа) Тогда

				Объект.НоменклатурнаяГруппа = Родитель.НоменклатурнаяГруппа;
				НоменклатурнаяГруппаПриИзмененииНаСервере();
			Иначе

				Запрос = Новый Запрос;
				Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1 НоменклатурныеГруппы.Ссылка
				|ИЗ Справочник.НоменклатурныеГруппы КАК НоменклатурныеГруппы
				|ГДЕ НоменклатурныеГруппы.СвязатьОдноименнуюГруппуНоменклатуры = ИСТИНА
				|	И НоменклатурныеГруппы.Наименование = &Наименование";
				Запрос.УстановитьПараметр("Наименование", Родитель.Наименование);

				РезультатЗапроса = Запрос.Выполнить();

				Если НЕ РезультатЗапроса.Пустой() Тогда
					ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
					Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
						Объект.НоменклатурнаяГруппа = ВыборкаДетальныеЗаписи.ссылка;

					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

		Если НЕ родитель.КоличествоПоУмолчанию = 0 Тогда
			Элементы.КоличествоПоУмолчанию.Доступность = ЛОЖЬ;
			Если НЕ Объект.КоличествоПоУмолчанию = родитель.КоличествоПоУмолчанию Тогда
				Объект.КоличествоПоУмолчанию = родитель.КоличествоПоУмолчанию = ИСТИНА;
			КонецЕсли;
		КонецЕсли;

		Если УчетПоСериям
			И Родитель.СерийныйУчет Тогда

			Если НоменклатураНеИспользовалась
				И НЕ Объект.СерийныйУчет Тогда

				Объект.СерийныйУчет = ИСТИНА;
			КонецЕсли;

			Если Объект.СерийныйУчет ТОгда
				Элементы.СерийныйУчет.Видимость 		  = ИСТИНА;
				Элементы.ГруппаСерийногоУчета.Доступность = ЛОЖЬ;
			КонецЕсли;

			Если Родитель.ЗапретитьУказаниеБезСерии Тогда

				Если НоменклатураНеИспользовалась
					И НЕ Объект.ЗапретитьУказаниеБезСерии Тогда

					Объект.ЗапретитьУказаниеБезСерии = ИСТИНА;
				КонецЕсли;

				Если Объект.ЗапретитьУказаниеБезСерии Тогда
					Элементы.ЗапретитьУказаниеБезСерии.ТолькоПросмотр = ИСТИНА;
					Элементы.СерийныйУчет.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Обязательно сопровождается серией");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

		Если Родитель.НеОтслеживатьОстаток Тогда

			Если НоменклатураНеИспользовалась
				И НЕ Объект.НеОтслеживатьОстаток Тогда

				Объект.НеОтслеживатьОстаток = ИСТИНА;
			КонецЕсли;

			Если Объект.НеОтслеживатьОстаток
				ИЛИ ЭтотЭлементПредопределенный Тогда

				Элементы.ОстатокНеОтслеживается.Видимость	= ИСТИНА;
				Элементы.НеОтслеживатьОстаток.Видимость 	= ЛОЖЬ;
			КонецЕсли;
		КонецЕсли;

		Если НЕ Родитель.ПланкаКоличестваДляЛьготнойЦены = 0 Тогда
			Элементы.ПланкаКоличестваДляЛьготнойЦены.ТолькоПросмотр = ИСТИНА;
			Если НЕ Объект.ПланкаКоличестваДляЛьготнойЦены = Родитель.ПланкаКоличестваДляЛьготнойЦены Тогда
				Объект.ПланкаКоличестваДляЛьготнойЦены = Родитель.ПланкаКоличестваДляЛьготнойЦены;
			КонецЕсли;
		КонецЕсли;

		Если НЕ Родитель.РекомендуемыйМинимальныйОстатокТовара = 0 Тогда
			Элементы.РекомендуемыйМинимальныйОстатокТовара.ТолькоПросмотр = ИСТИНА;
			Если НЕ Объект.РекомендуемыйМинимальныйОстатокТовара = Родитель.РекомендуемыйМинимальныйОстатокТовара Тогда
				Объект.РекомендуемыйМинимальныйОстатокТовара = Родитель.РекомендуемыйМинимальныйОстатокТовара;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СерийныйУчетПриИзменении(Элемент)
	СерийныйУчетПриИзмененииК();
КонецПроцедуры

&НаКлиенте
Процедура СерийныйУчетПриИзмененииК()

	ОбъектСерийныйУчет = Объект.СерийныйУчет;
	Элементы.ЗапретитьУказаниеБезСерии.Видимость = НоменклатураНеИспользовалась И УчетПоСериям И ОбъектСерийныйУчет;
	Элементы.СерииВНаличии.Видимость 			 = УчетПоСериям И ОбъектСерийныйУчет и ЗначениеЗаполнено(Объект.Ссылка);
	Элементы.НеОтслеживатьОстаток.Видимость 	 = НЕ ЭтотЭлементПредопределенный И (НоменклатураНеИспользовалась И НЕ ОбъектСерийныйУчет);
	Элементы.ОсновнойШтрихКод.Заголовок			 = ?(ОбъектСерийныйУчет, ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Общий для серий Штрих код"), ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Основной штрихкод"));
	Элементы.КоличествоПоУмолчанию.Видимость     = НЕ ОбъектСерийныйУчет И РегулироватьКоличествоНоменклатурыПоУмолчаниюДляУпаковок;

	Если ОбъектСерийныйУчет
		И Объект.НеОтслеживатьОстаток Тогда

		Объект.НеОтслеживатьОстаток = ЛОЖЬ;
	КонецЕсли;

	Элементы.ДопШтрихКодыСерияНоменклатуры.Видимость = УчетПоСериям И ИспользоватьШтрихКоды И ОбъектСерийныйУчет;
	Элементы.ШтрихКодыСерияНоменклатуры.Видимость	 = УчетПоСериям И ИспользоватьШтрихКоды И ОбъектСерийныйУчет;

КонецПроцедуры

&НаКлиенте
Процедура СменитьВидОкна(Команда)

	НеПоказыватьТаблицыСправа = НЕ НеПоказыватьТаблицыСправа;
	ВидимостьТаблицСправа();

КонецПроцедуры

&НаСервере
Процедура СменитьСтраницуКлючейУчетаНаСервере(Знач ИмяТекущейСтраницы)

	ПолучитьНомерКлюча = Число(СтрЗаменить(ИмяТекущейСтраницы, "СтраницаКлюча", ""));
	СвязанныеКлючиУчета.Параметры.УстановитьЗначениеПараметра("КлючУчета", КлючиУчета[ПолучитьНомерКлюча - 1].Значение);

КонецПроцедуры

&НаКлиенте
Процедура СоставНоменклатураПриИзменении(Элемент)

	ТекущиеДанные = Элементы.Состав.ТекущиеДанные;

	Если ТекущиеДанные.Количество = 0 Тогда
		ТекущиеДанные.Количество = ОбщийМодульСервер.ПолучитьКоличествоПоУмолчанию(ТекущиеДанные.Номенклатура);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СпецификацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	ТекущиеДанные = Элементы.Спецификации.ТекущиеДанные;

	Если НЕ ТекущиеДанные = Неопределено Тогда
		ПоказатьЗначение(Неопределено, ТекущиеДанные.Ссылка);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтрокиОповещение()

	ТекущийЭлементСписка = Элементы.Изображения.ТекущаяСтрока;

	Если ЗначениеЗаполнено(ТекущийЭлементСписка) Тогда
		СтруктураИзображения = ОбщийМодульСервер.ПолучитьСтруктуруИзображения(ТекущийЭлементСписка);
		ПодСсылку = СтруктураИзображения.ПодСсылку;
		Элементы.СсылкаНаИзображение.Видимость    = НЕ СтруктураИзображения.ИзображениеВБазеДанных;
		Элементы.ИзображениеВБазеДанных.Видимость = СтруктураИзображения.ИзображениеВБазеДанных;

		Если СтруктураИзображения.РазмерПриОтображении = 1 Тогда
			Элементы.ИзображениеВБазеДанных.РазмерКартинки = РазмерКартинки.АвтоРазмер
		ИначеЕсли СтруктураИзображения.РазмерПриОтображении = 2 Тогда

			Элементы.ИзображениеВБазеДанных.РазмерКартинки = РазмерКартинки.Пропорционально
		ИначеЕсли СтруктураИзображения.РазмерПриОтображении = 3 Тогда

			Элементы.ИзображениеВБазеДанных.РазмерКартинки = РазмерКартинки.Растянуть
		ИначеЕсли СтруктураИзображения.РазмерПриОтображении = 4 Тогда

			Элементы.ИзображениеВБазеДанных.РазмерКартинки = РазмерКартинки.РеальныйРазмер
		ИначеЕсли СтруктураИзображения.РазмерПриОтображении = 5 Тогда

			Элементы.ИзображениеВБазеДанных.РазмерКартинки = РазмерКартинки.Черепица
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТоварИлиУслугаПриИзменении(Элемент)

	Объект.НеОтслеживатьОстаток = УслугаНЕТовар;

	Если УслугаНЕТовар Тогда
		Объект.ПланкаКоличестваДляЛьготнойЦены = 0;
		Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
			Объект.ОсновнаяЕдиницаИзмерения = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("усл.");
		КонецЕсли;
	ИначеЕсли Объект.ОсновнаяЕдиницаИзмерения = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("усл.") Тогда
		Объект.ОсновнаяЕдиницаИзмерения = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("шт.");
	КонецЕсли;

	ОбновитьВидимостьУслуги();

КонецПроцедуры

&НаКлиенте
Процедура УмножитьНаГод(Команда)

	Если Объект.ГарантийныйСрокПроизводителя = 0 Тогда
		Объект.ГарантийныйСрокПроизводителя = 1;
	КонецЕсли;

	Объект.ГарантийныйСрокПроизводителя = Объект.ГарантийныйСрокПроизводителя * 365.259636;

КонецПроцедуры

&НаКлиенте
Процедура УмножитьНаМесяц(Команда)

	Если Объект.ГарантийныйСрокПроизводителя = 0 Тогда
		Объект.ГарантийныйСрокПроизводителя = 1;
	КонецЕсли;

	Объект.ГарантийныйСрокПроизводителя = Объект.ГарантийныйСрокПроизводителя * 30.43685;

КонецПроцедуры

&НаКлиенте
Процедура ЦенаПриИзменении(Элемент)
	НоменклатурнаяГруппаПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ШиринаПриИзменении(Элемент)
	ОбновитьРазмеры();
КонецПроцедуры

&НаКлиенте
Процедура ЭтоВаучерПриИзменении(Элемент)
	ОбновитьДляВаучеров();
КонецПроцедуры

&НаКлиенте
Процедура ЭтоНаборПриИзменении(Элемент)
	ВидимостьСоставаНабора();
КонецПроцедуры

&НаКлиенте
Процедура ИзображениеВИнтернет(Команда)

	АдресВИнтернете = """https://www.google.com.ua/search?q=" + Объект.Наименование + ?(ПустаяСтрока(Объект.Артикул), "", "+" + СокрЛП(Объект.Артикул)) + "&source=lnms&tbm=isch""";

	Если глВерсияПлатформы < 806010000 Тогда
		Выполнить(" ЗапуститьПриложение(" + АдресВИнтернете + "); ");
	Иначе
		Выполнить(" НачатьЗапускПриложения(Неопределено, " + АдресВИнтернете + "); ");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СоставПриИзменении(Элемент)
	глВремяПоследнегоСобытия = ТекущаяДата();
КонецПроцедуры

&НаКлиенте
Процедура ОсновнойШтрихКодПриИзменении(Элемент)
	Элементы.ОбновитьШтрихКодПоФормуле.Видимость = ИспользоватьШтрихКоды И ПустаяСтрока(Объект.ОсновнойШтрихКод);
КонецПроцедуры
