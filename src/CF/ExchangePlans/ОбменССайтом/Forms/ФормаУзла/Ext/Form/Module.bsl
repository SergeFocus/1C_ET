// Суров С.В <s_surov@mail.ru>
// sza150518-0419 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	УзелОбменаЭтаИБ = ПроверитьУзелОбменаЭтаИБСервер();
	
	Если УзелОбменаЭтаИБ Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		ПриСозданииЧтенииНаСервере(ЭтаФорма, Объект);
	КонецЕсли;
	
	ОбменССайтомПереопределяемый.ФормаУзлаПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Задание = ТекущийОбъект.РегламентноеЗадание();
	
	Если НЕ Задание = Неопределено Тогда
		РасписаниеРегламентногоЗадания = Задание.Расписание;
	КонецЕсли;
	
	ПриСозданииЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	ВыполнитьДействияПриЧтенииНаСервере();
	
	ВосстановитьРеквизитыПрикладногоРешения(ТекущийОбъект);
	
	ОбменССайтомПереопределяемый.ФормаУзлаПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ИспользоватьРегламентныеЗадания
		И (РасписаниеРегламентногоЗадания = Неопределено) Тогда
		
		ТекущийОбъект.ИспользоватьРегламентныеЗадания = ЛОЖЬ;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(ИСТИНА);
	
	Если ТекущийОбъект.ИспользоватьРегламентныеЗадания Тогда
		
		Задание = ТекущийОбъект.РегламентноеЗадание();
		Если Задание = НеОпределено Тогда
			Задание = РегламентныеЗадания.СоздатьРегламентноеЗадание("ОбменССайтом");
			Задание.Использование = ИСТИНА;
			Задание.Ключ = Строка(Новый УникальныйИдентификатор);
			Задание.Наименование = ТекущийОбъект.Наименование;
			ТекущийОбъект.ИдентификаторРегламентногоЗадания = Задание.УникальныйИдентификатор;
		КонецЕсли;
		ПараметрыЗадания = Новый Массив;
		ПараметрыЗадания.Добавить(ТекущийОбъект.Код);
		Задание.Параметры = ПараметрыЗадания;
		Задание.Расписание = РасписаниеРегламентногоЗадания;
		
		Задание.Записать();
		
	Иначе
		
		ТекущийОбъект.УдалитьРегламентноеЗадание();
		ТекущийОбъект.ИдентификаторРегламентногоЗадания = НеОпределено;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(ЛОЖЬ);
	
	ТаблицаКаталоговТЗ = РеквизитФормыВЗначение("ТаблицаКаталогов");
	ТаблицаКаталоговТЗ.Колонки.Добавить("ХранилищеНастроекКаталогПакет", Новый ОписаниеТипов("ХранилищеЗначения"));
	ТаблицаКаталоговТЗ.Колонки.Добавить("ХранилищеНастроекИзмененияПакета", Новый ОписаниеТипов("ХранилищеЗначения"));
	ТаблицаКаталоговТЗ.Колонки.Добавить("ХранилищеНастроекКаталог", Новый ОписаниеТипов("ХранилищеЗначения"));
	ТаблицаКаталоговТЗ.Колонки.Добавить("ХранилищеНастроекПакетПредложений", Новый ОписаниеТипов("ХранилищеЗначения"));
	
	Для Каждого СтрокаТаблицыКаталогов Из ТаблицаКаталоговТЗ Цикл
		
		Если ЗначениеЗаполнено(СтрокаТаблицыКаталогов.АдресНастроекКаталогПакет) Тогда
			НастройкиКаталогПакет = ПолучитьИзВременногоХранилища(СтрокаТаблицыКаталогов.АдресНастроекКаталогПакет);
			СтрокаТаблицыКаталогов.ХранилищеНастроекКаталогПакет = Новый ХранилищеЗначения(НастройкиКаталогПакет);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТаблицыКаталогов.АдресНастроекИзмененияПакета) Тогда
			НастройкиОбновленияПакета = ПолучитьИзВременногоХранилища(СтрокаТаблицыКаталогов.АдресНастроекИзмененияПакета);
			СтрокаТаблицыКаталогов.ХранилищеНастроекИзмененияПакета = Новый ХранилищеЗначения(НастройкиОбновленияПакета);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТаблицыКаталогов.АдресНастроекКаталог) Тогда
			НастройкиКаталог = ПолучитьИзВременногоХранилища(СтрокаТаблицыКаталогов.АдресНастроекКаталог);
			СтрокаТаблицыКаталогов.ХранилищеНастроекКаталог = Новый ХранилищеЗначения(НастройкиКаталог);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТаблицыКаталогов.АдресНастроекПакетПредложений) Тогда
			НастройкиПакетПредложений = ПолучитьИзВременногоХранилища(СтрокаТаблицыКаталогов.АдресНастроекПакетПредложений);
			СтрокаТаблицыКаталогов.ХранилищеНастроекПакетПредложений = Новый ХранилищеЗначения(НастройкиПакетПредложений);
		КонецЕсли;		
		
	КонецЦикла;
	
	Если Не ТаблицаКаталоговТЗ.Колонки.Найти("АдресНастроекКомпоновки") = Неопределено Тогда
		ТаблицаКаталоговТЗ.Колонки.Удалить("АдресНастроекКомпоновки");
	КонецЕсли;
	ТаблицаКаталоговТЗ.Колонки.Удалить("АдресНастроекКаталогПакет");
	ТаблицаКаталоговТЗ.Колонки.Удалить("АдресНастроекИзмененияПакета");
	ТаблицаКаталоговТЗ.Колонки.Удалить("АдресНастроекКаталог");
	ТаблицаКаталоговТЗ.Колонки.Удалить("АдресНастроекПакетПредложений");
	
	ТекущийОбъект.СохраненнаяТаблицаКаталогов = Новый ХранилищеЗначения(ТаблицаКаталоговТЗ);
	
	СохраняемыеРеквизиты = Новый Структура;
	
	МассивРеквизитов = Новый Массив;
	ЗаполнитьМассивРеквизитовФормыПрикладногоРешения(МассивРеквизитов);
	
	СохранитьРеквизиты(ТекущийОбъект, МассивРеквизитов, СохраняемыеРеквизиты);
	
	ОбменССайтомПереопределяемый.ФормаУзлаПередЗаписьюНаСервере(ЭтаФорма, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	ОбменССайтомПереопределяемый.ФормаУзлаПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 0);
	
	Если УзелОбменаЭтаИБ Тогда
		
		ОчиститьСообщения();
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Узел соответствует этой информационной базе и не может использоваться в обмене с сайтом. 
		|Используйте другой узел обмена или создайте новый."));
		
		Отказ = ИСТИНА;
		Возврат;
		
	КонецЕсли;
	
	СформироватьЗаголовокПараметрыВыгрузки();
	
	ПриИзмененииРежимВыгрузки();
	
	ДоступностьРеквизитовГруппы();
	
	ОбменССайтомКлиентПереопределяемый.ФормаУзлаПриОткрытии(ЭтаФорма, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Объект.ОбменТоварами И ТаблицаКаталогов.Количество() = 0 Тогда
		
		НоваяСтрока = ТаблицаКаталогов.Добавить();
		ДозаполнитьТаблицуКаталогов(НоваяСтрока);
		
	КонецЕсли;
	
	ОбменССайтомКлиентПереопределяемый.ФормаУзлаПередЗаписью(ЭтаФорма, Отказ, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииОбменТоварами()
	
	УстановитьВидимостьСтраницФормы();
	УстановитьДоступностьПоляКаталогВыгрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииОбменЗаказами()
	
	УстановитьВидимостьСтраницФормы();
	УстановитьДоступностьПолейОбменЗаказами();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПереключательНазначениеОбмена()
	
	Объект.ВыгружатьНаСайт = ПереключательНазначениеОбмена = 0;
	УстановитьСтраницуТипаОбмена();
	Модифицированность = ИСТИНА;
	
КонецПроцедуры

// Процедура - обработчик события НачалоВыбора поля ввода КаталогВыгрузки.
// 
&НаКлиенте
Процедура КаталогВыгрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = ЛОЖЬ;
	
	НачатьПодключениеРасширенияРаботыСФайлами(Новый ОписаниеОповещения("КаталогВыгрузкиНачалоВыбораЗавершение", ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогВыгрузкиНачалоВыбораЗавершение(Подключено, ДополнительныеПараметры) Экспорт
	
	Если Не Подключено Тогда
		ТекстПредупреждения = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Для данной операции необходимо установить расширение работы с файлами!");
		Если глВерсияПлатформы < 803040000  Тогда
			ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
		Иначе
			Выполнить(" ПоказатьПредупреждение( , ТекстПредупреждения); ");
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	
	Диалог.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Укажите каталог обмена");
	Диалог.Каталог = Объект.КаталогВыгрузки;
	
	Диалог.Показать(Новый ОписаниеОповещения("КаталогВыгрузкиНачалоВыбораЗавершениеЗавершение", ЭтаФорма, Новый Структура("Диалог", Диалог)));

КонецПроцедуры

&НаКлиенте
Процедура КаталогВыгрузкиНачалоВыбораЗавершениеЗавершение(ВыбранныеФайлы, ДополнительныеПараметры1) Экспорт
	
	Диалог = ДополнительныеПараметры1.Диалог;	
	
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		Объект.КаталогВыгрузки = Диалог.Каталог;
	КонецЕсли;

КонецПроцедуры

// Процедура - обработчик события Открытие поля ввода КаталогВыгрузки.
// 
&НаКлиенте
Процедура КаталогВыгрузкиОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = ЛОЖЬ;
	
	ПолноеИмяФайла = Объект.КаталогВыгрузки;
	
	Если ПустаяСтрока(ПолноеИмяФайла) Тогда
		Возврат;
	КонецЕсли;
	
	Если глВерсияПлатформы < 806010000 Тогда 
		Выполнить(" ЗапуститьПриложение(ПолноеИмяФайла); ");
		Иначе
	Выполнить(" НачатьЗапускПриложения(Неопределено, ПолноеИмяФайла); ");	
																		 КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события НачалоВыбора поля ввода ФайлЗагрузки.
// 
&НаКлиенте
Процедура ФайлЗагрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = ЛОЖЬ;
	
	НачатьПодключениеРасширенияРаботыСФайлами(Новый ОписаниеОповещения("ФайлЗагрузкиНачалоВыбораЗавершение", ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлЗагрузкиНачалоВыбораЗавершение(Подключено, ДополнительныеПараметры) Экспорт
	
	Если Не Подключено Тогда
		
		ТекстПредупреждения = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Для данной операции необходимо установить расширение работы с файлами!");
		Если глВерсияПлатформы < 803040000  Тогда
			ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
		Иначе
			Выполнить(" ПоказатьПредупреждение( , ТекстПредупреждения); ");
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	Диалог.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Выберите xml-файл с заказами");
	Диалог.ПолноеИмяФайла = Объект.ФайлЗагрузки;
	Диалог.Фильтр = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Документ XML") + " (*.xml)|*.xml";
	
	Диалог.Показать(Новый ОписаниеОповещения("ФайлЗагрузкиНачалоВыбораЗавершениеЗавершение", ЭтаФорма, Новый Структура("Диалог", Диалог)));

КонецПроцедуры

&НаКлиенте
Процедура ФайлЗагрузкиНачалоВыбораЗавершениеЗавершение(ВыбранныеФайлы, ДополнительныеПараметры1) Экспорт
	
	Диалог = ДополнительныеПараметры1.Диалог;	
	
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		
		Объект.ФайлЗагрузки = Диалог.ПолноеИмяФайла;
		
	КонецЕсли;

КонецПроцедуры

// Процедура - обработчик события Открытие поля ввода ФайлЗагрузки.
// 
&НаКлиенте
Процедура ФайлЗагрузкиОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = ЛОЖЬ;
	
	ПолноеИмяФайла = Объект.ФайлЗагрузки;
	Если ПустаяСтрока(ПолноеИмяФайла) Тогда
		Возврат;
	КонецЕсли;
	
	Если глВерсияПлатформы < 806010000 Тогда 
		Выполнить(" ЗапуститьПриложение(""explorer.exe /select, "" + ПолноеИмяФайла); ");
		Иначе
	Выполнить(" НачатьЗапускПриложения(Неопределено, ""explorer.exe /select, "" + ПолноеИмяФайла); ");	
																									  КонецЕсли;
	
КонецПроцедуры


// Процедура - обработчик события ПриИзменении поля переключателя ПереключательНазначениеОбмена.
// 
&НаКлиенте
Процедура ПереключательНазначениеОбменаПриИзменении(Элемент)
	
	ПриИзмененииПереключательНазначениеОбмена();
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля флажка ИспользоватьРегламентныеЗадания.
// 
&НаКлиенте
Процедура ИспользоватьРегламентныеЗаданияПриИзменении(Элемент)
	
	УстановитьДоступностьРасписанияОбмена();
	
	Если Объект.ИспользоватьРегламентныеЗадания Тогда
		ВыполнитьНастройкуРасписанияОбмена();
		
		УстановитьНадписьРасписанияОбмена();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнтервалОбменаССайтомПриИзменении(Элемент)
	
	УстановитьРасписаниеРегламентногоЗадания();
	УстановитьНадписьРасписанияОбмена();
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля флажка ОбменТоварами.
// 
&НаКлиенте
Процедура ОбменТоварамиПриИзменении(Элемент)
	
	ПриИзмененииОбменТоварами();
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля флажка ОбменЗаказами.
// 
&НаКлиенте
Процедура ОбменЗаказамиПриИзменении(Элемент)
	
	ПриИзмененииОбменЗаказами();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключательВыгрузкаИзмененийПриИзменении(Элемент)
	
	ПриИзмененииПереключательВыгрузкаИзменений()
	
КонецПроцедуры

&НаКлиенте
Процедура СписокТочекСамовывозаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = ЛОЖЬ;
	
	ИмяФормыВыбора = ВыбратьФормуРедактирования("ФормаВыбораТочекСамовывоза");
	
	Если ИмяФормыВыбора= Неопределено Тогда
		Возврат ;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("МножественныйВыбор", ИСТИНА);
	ПараметрыОткрытия.Вставить("РежимВыбора", ИСТИНА);
	
	Если НЕ ОбщийМодульСервисСервер.ПолучитьВерсиюПлатформы() < 803040000 Тогда
		Выполнить(" Обработчик = Новый ОписаниеОповещения(""ДобавитьТочкиСамовывоза"", ЭтаФорма); ");		
		Выполнить(" Режим = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца; ");
		Выполнить(" ОткрытьФорму(ИмяФормыВыбора, ПараметрыОткрытия,,,,, Обработчик, Режим); ");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьТоварыПриИзменении(Элемент)
	
	Если Не ВыгружатьТовары
		И Не ВыгружатьЦеныОстатки Тогда
		
		ВыгружатьТовары = ИСТИНА;
		
		СообщитьОбОшибке("ВыгружатьТовары");
		
	КонецЕсли;
	
	ПриИзмененииРежимВыгрузки();
	
	ДоступностьРеквизитовГруппы();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьЦеныОстаткиПриИзменении(Элемент)
	
	Если Не ВыгружатьЦеныОстатки
		И Не ВыгружатьТовары Тогда
		
		ВыгружатьЦеныОстатки = ИСТИНА;
		СообщитьОбОшибке("ВыгружатьЦеныОстатки");
		
	КонецЕсли;
	
	ПриИзмененииРежимВыгрузки();
	
КонецПроцедуры

// Процедура - обработчик команды КомандаПроверитьСоединение.
// 
&НаКлиенте
Процедура КомандаПроверитьСоединение(Команда)
	
	НастройкиПодключения = Новый Структура;
	НастройкиПодключения.Вставить("Пользователь", Объект.ИмяПользователя);
	НастройкиПодключения.Вставить("Пароль", Объект.Пароль);
	НастройкиПодключения.Вставить("АдресСайта", Объект.АдресСайта);
	
	ТекстСообщения = "";
	
	ПроверитьПодключение(НастройкиПодключения, ТекстСообщения);
	
	Если глВерсияПлатформы < 803040000  Тогда			
		Предупреждение(ТекстСообщения);
	Иначе
		Выполнить(" ПоказатьПредупреждение( , ТекстСообщения); ");
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик команды НастроитьРасписаниеОбмена.
// 
&НаКлиенте
Процедура НастроитьРасписаниеОбмена(Команда)
	
	ВыполнитьНастройкуРасписанияОбмена();
	
КонецПроцедуры

&НаКлиенте
Процедура РасширеннаяНастройка(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РазмерПорции", Объект.РазмерПорции);
	ПараметрыФормы.Вставить("КоличествоПовторений", Объект.КоличествоПовторений);
	ПараметрыФормы.Вставить("РежимОткрытияОкна", РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	Если НЕ ОбщийМодульСервисСервер.ПолучитьВерсиюПлатформы() < 803040000 Тогда
		Выполнить(" ОбработчикОповещения = Новый ОписаниеОповещения(""РасширеннаяНастройкаЗавершить"", ЭтаФорма); ");	
		Выполнить(" ОткрытьФорму(""ПланОбмена.ОбменССайтом.Форма.ФормаРасширенныеНастройки"", ПараметрыФормы, ЭтаФорма, , , ,	ОбработчикОповещения); ");	
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ПередОкончаниемРедактирования таблицы ТаблицаКаталогов.
// 
&НаКлиенте
Процедура ТаблицаКаталоговПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если Не ИдентификаторУникален() Тогда
		Отказ = ИСТИНА;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКаталоговНастройкиКаталогаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = ЛОЖЬ;
	
	ОткрытьФормуОтбораКаталога();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКаталоговНастройкиКаталогаОчистка(Элемент, СтандартнаяОбработка)
	
	Если Элементы.ТаблицаКаталогов.ТекущиеДанные = НеОпределено Тогда
		Возврат;
	КонецЕсли;
	
	РежимВыгрузки = Объект.РежимВыгрузки;
	
	Если РежимВыгрузки = 0 Тогда
		
		Если ЗначениеЗаполнено(Элементы.ТаблицаКаталогов.ТекущиеДанные.АдресНастроекКаталогПакет) Тогда
			УдалитьИзВременногоХранилища(Элементы.ТаблицаКаталогов.ТекущиеДанные.АдресНастроекКаталогПакет);
			Элементы.ТаблицаКаталогов.ТекущиеДанные.АдресНастроекКаталогПакет= "";
		КонецЕсли;
		
	ИначеЕсли РежимВыгрузки = 1 Тогда
		
		Если ЗначениеЗаполнено(Элементы.ТаблицаКаталогов.ТекущиеДанные.АдресНастроекКаталог) Тогда
			УдалитьИзВременногоХранилища(Элементы.ТаблицаКаталогов.ТекущиеДанные.АдресНастроекКаталог);
			Элементы.ТаблицаКаталогов.ТекущиеДанные.АдресНастроекКаталог = "";
		КонецЕсли;
		
	ИначеЕсли РежимВыгрузки = 2 Тогда
		
		Если ЗначениеЗаполнено(Элементы.ТаблицаКаталогов.ТекущиеДанные.АдресНастроекПакетПредложений) Тогда
			УдалитьИзВременногоХранилища(Элементы.ТаблицаКаталогов.ТекущиеДанные.АдресНастроекПакетПредложений);
			Элементы.ТаблицаКаталогов.ТекущиеДанные.АдресНастроекПакетПредложений= "";
		КонецЕсли;
		
	ИначеЕсли РежимВыгрузки = 3 Тогда
		
		Если ЗначениеЗаполнено(Элементы.ТаблицаКаталогов.ТекущиеДанные.АдресНастроекИзмененияПакета) Тогда
			УдалитьИзВременногоХранилища(Элементы.ТаблицаКаталогов.ТекущиеДанные.АдресНастроекИзмененияПакета);
			Элементы.ТаблицаКаталогов.ТекущиеДанные.АдресНастроекИзмененияПакета = "";
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ПриОкончанииРедактирования таблицы ТаблицаКаталогов.
// 
&НаКлиенте
Процедура ТаблицаКаталоговПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные = НеОпределено Тогда
		Возврат;
	КонецЕсли;
	
	ДозаполнитьТаблицуКаталогов(Элемент.ТекущиеДанные);
	Модифицированность = ИСТИНА;
	
КонецПроцедуры

// Процедура - обработчик события ПослеУдаления таблицы ТаблицаКаталогов.
// 
&НаКлиенте
Процедура ТаблицаКаталоговПослеУдаления(Элемент)
	
	Модифицированность = ИСТИНА;
	
КонецПроцедуры

// Процедура - обработчик события ПриНачалеРедактирования таблицы ТаблицаКаталогов.
// 
&НаКлиенте
Процедура ТаблицаКаталоговПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Копирование Тогда
		
		Элемент.ТекущиеДанные.ИдентификаторКаталога = "";
		
	КонецЕсли;
	
	Если (Элемент.ТекущиеДанные.Группы.Количество() = 1
		И НЕ ЗначениеЗаполнено(Элемент.ТекущиеДанные.Группы[0].Значение))
		ИЛИ Элемент.ТекущиеДанные.Группы.Количество() = 0 Тогда
		
		НовыйСписокГрупп = Новый СписокЗначений;
		
		ИмяСправочникаНоменклатура = Неопределено;
		ЗаполнитьИмяПрикладногоСправочника(ИмяСправочникаНоменклатура);
		Если ИмяСправочникаНоменклатура = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		
		ТипЗначений = Новый ОписаниеТипов("СправочникСсылка."+ ИмяСправочникаНоменклатура);
		
		НовыйСписокГрупп.ТипЗначения = ТипЗначений;
		НовыйСписокГрупп.Добавить(Неопределено, НадписьВсеЭлементыСписка);
		Элемент.ТекущиеДанные.Группы = НовыйСписокГрупп;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	ОбменССайтомПереопределяемый.УстановитьУсловноеОформлениеФормаУзлаОбмена(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПереключательВыгрузкаИзменений()
	
	Объект.ВыгружатьИзменения = (ПереключательВыгрузкаИзменений = 1);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьТочекСамовывозаСервер(Форма, Объект)
	
	Элементы = Форма.Элементы;
	Если Объект.РежимВыгрузки = 0
		Или Объект.РежимВыгрузки = 2 Тогда
		Элементы.СписокТочекСамовывоза.Доступность = ИСТИНА;
	Иначе
		Элементы.СписокТочекСамовывоза.Доступность = ЛОЖЬ;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьВыгружатьКартинкиСервер(Форма, Объект)
	
	Элементы = Форма.Элементы;
	Если Объект.РежимВыгрузки = 0
		Или Объект.РежимВыгрузки = 1 Тогда
		Элементы.ВыгружатьКартинки.Доступность = ИСТИНА;
	Иначе
		Элементы.ВыгружатьКартинки.Доступность = ЛОЖЬ;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНадписьРасписанияОбменаСервер()
	
	// Если Не ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
	
	Если РасписаниеРегламентногоЗадания = Неопределено Тогда
		ТекстЗаголовка = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Настроить расписание обмена");
	Иначе
		ТекстЗаголовка = РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	Элементы.НастроитьРасписаниеОбмена.Заголовок = ТекстЗаголовка;
	
	// Иначе
	// 	
	// 	Если РасписаниеРегламентногоЗадания = Неопределено Тогда
	// 		
	// 		ИнтервалОбменаССайтом = НСтр("ru='Один раз в 30 минут");
	// 		
	// 	Иначе
	// 		
	// 		ЗначениеПериода = РасписаниеРегламентногоЗадания.ПериодПовтораВТечениеДня;
	// 		Если ЗначениеПериода = 0 Тогда
	// 			
	// 			ИнтервалОбменаССайтом = НСтр("ru='Один раз в 30 минут");
	// 			
	// 		ИначеЕсли ЗначениеПериода <= 300 Тогда
	// 			
	// 			ИнтервалОбменаССайтом = НСтр("ru='Один раз в 5 минут");
	// 			
	// 		ИначеЕсли ЗначениеПериода <= 900 Тогда
	// 			
	// 			ИнтервалОбменаССайтом = НСтр("ru='Один раз в 15 минут");
	// 			
	// 		ИначеЕсли ЗначениеПериода <= 1800 Тогда
	// 			
	// 			ИнтервалОбменаССайтом = НСтр("ru='Один раз в 30 минут");
	// 			
	// 		ИначеЕсли ЗначениеПериода <= 3600 Тогда
	// 			
	// 			ИнтервалОбменаССайтом = НСтр("ru='Один раз в час");
	// 			
	// 		ИначеЕсли ЗначениеПериода <= 10800 Тогда
	// 			
	// 			ИнтервалОбменаССайтом = НСтр("ru='Один раз в 3 часа");
	// 			
	// 		ИначеЕсли ЗначениеПериода <= 21600 Тогда
	// 			
	// 			ИнтервалОбменаССайтом = НСтр("ru='Один раз в 6 часов");
	// 			
	// 		ИначеЕсли ЗначениеПериода <= 43200 Тогда
	// 			
	// 			ИнтервалОбменаССайтом = НСтр("ru='Один раз в 12 часов");
	// 			
	// 		КонецЕсли;
	
	// 		
	// 	КонецЕсли;
	// 	
	// КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтраницуТипаОбмена()
	
	Если Объект.ВыгружатьНаСайт Тогда
		Элементы.СтраницыТипОбмена.ТекущаяСтраница = Элементы.СтраницаВыгрузкаНаСайт;
	Иначе
		Элементы.СтраницыТипОбмена.ТекущаяСтраница = Элементы.СтраницаВыгрузкаВКаталог;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьСтраницФормы()
	
	Элементы.СтраницаВыгрузкаТоваров.Видимость = Объект.ОбменТоварами;
	Элементы.СтраницаОбменЗаказами.Видимость = Объект.ОбменЗаказами;
	
КонецПроцедуры

&НаКлиенте
Процедура СообщитьОбОшибке(ИмяПоля)
	
	ОчиститьСообщения();
	
	ТекстСообщения = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Установлен признак ""Выгрузка товаров"".
	|Должны быть указаны выгружаемые данные");
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,ИмяПоля);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступностьРеквизитовГруппы()
	
	Элементы.ВыгружатьКартинки.Доступность = ВыгружатьТовары;
	Элементы.ВыгружатьФайлы.Доступность = ВыгружатьТовары;
	Элементы.КлассифицироватьПоВидамНоменклатуры.Доступность = ВыгружатьТовары;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция  ВыбратьФормуРедактирования(ИмяФормыРедактирования);
	
	Возврат ОбменССайтомПовтИсп.ИмяПрикладнойФормы(ИмяФормыРедактирования);
	
КонецФункции

&НаСервере
Процедура ПриСозданииЧтенииНаСервере(ЭтаФорма, Объект)
	
	УстановитьВидимостьЭлементовФормы();
	УстановитьДоступностьЭлементовФормы(ЭтаФорма, Объект);
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьДействияПриЧтенииНаСервере()
	
	НастроитьЭлементыАвтообмен();
	УстановитьНадписьРасписанияОбменаСервер();
	
	ЗаполнитьТаблицуКаталоговСервер();
	
	УстановитьПараметрыТаблицыКаталоговСервер();
	
	УстановитьТипЗначенийСпискаГруппТаблицыКаталоговСервер();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементовФормы()
	
	// Если ПолучитьФункциональнуюОпцию("РаботаВЛокальномРежиме") Тогда
	Элементы.ГруппаВыгружатьНаСайтГоризонтально.Видимость = ИСТИНА;
	// Иначе
	// 	Элементы.ГруппаВыгружатьНаСайтГоризонтально.Видимость = ЛОЖЬ;
	// 	Если Не Объект.ВыгружатьНаСайт Тогда
	// 		Объект.ВыгружатьНаСайт = ИСТИНА;
	// 		Модифицированность = ИСТИНА;
	// 	КонецЕсли;
	// КонецЕсли;
	
	УстановитьВидимостьСтраницФормы();
	
	Видимость = ЛОЖЬ;
	ОбменССайтомПереопределяемый.УстановитьВидимостьТабличнойЧастиСоотвествиеЗаказовФормыУзлаПланаОбмена(Видимость);
	Элементы.ГруппаСоответствиеСтатусов.Видимость = Видимость;
	
	ОбменССайтомПереопределяемый.УстановитьВидимостьЭлементовФормыУзла(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Процедура УстановитьДоступностьЭлементовФормы(Форма, Объект)
	
	Элементы = Форма.Элементы;
	
	Если Объект.ВыгружатьНаСайт Тогда
		Элементы.СтраницыТипОбмена.ТекущаяСтраница = Элементы.СтраницаВыгрузкаНаСайт;
		Форма.ПереключательНазначениеОбмена = 0;
	Иначе
		Элементы.СтраницыТипОбмена.ТекущаяСтраница = Элементы.СтраницаВыгрузкаВКаталог;
		Форма.ПереключательНазначениеОбмена = 1;
	КонецЕсли;
	
	УстановитьЗначениеФлаговВыгрузкиДанных(Форма, Объект);
	
	Форма.ПереключательВыгрузкаИзменений = 0;
	Если Объект.ВыгружатьИзменения Тогда
		Форма.ПереключательВыгрузкаИзменений = 1;
	КонецЕсли;
	
	Элементы.ФайлЗагрузки.Доступность = Объект.ОбменЗаказами;
	
	УстановитьДоступностьТочекСамовывозаСервер(Форма, Объект);
	УстановитьДоступностьВыгружатьКартинкиСервер(Форма, Объект);
	
	ОбменССайтомКлиентСерверПереопределяемый.УстановитьДоступностьЭлементовФормыУзла(Форма, Объект);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыАвтообмен()
	
	// Если Не ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
	Элементы.ГруппаАвтообменСтраницы.ТекущаяСтраница = Элементы.ГруппаАвтообменСтраница;
	Элементы.НастроитьРасписаниеОбмена.Доступность = Объект.ИспользоватьРегламентныеЗадания;
	// Иначе
	// 	Элементы.ГруппаАвтообменСтраницы.ТекущаяСтраница = Элементы.ГруппаАвтообменСтраницаИнтервал;
	// 	Элементы.ИнтервалОбменаССайтом.Доступность = Объект.ИспользоватьРегламентныеЗадания;
	// КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТипЗначенийСпискаГруппТаблицыКаталоговСервер()
	
	ИмяСправочникаНоменклатура = ОбменССайтомПовтИсп.ИмяПрикладногоСправочника("Номенклатура");
	
	ТипЗначений = Новый ОписаниеТипов("СправочникСсылка." + ИмяСправочникаНоменклатура);
	
	Для Каждого СтрокаТаблицыКаталогов Из ТаблицаКаталогов Цикл
		
		СтрокаТаблицыКаталогов.Группы.ТипЗначения = ТипЗначений;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыТаблицыКаталоговСервер()
	
	ЗаголовокТаблицы = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Таблица каталогов (соответствие групп номенклатуры каталогам на сайте)");
	ЗаголовокКолонки = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Группы номенклатуры");
	ВыборГруппИЭлементов = ГруппыИЭлементы.Группы;
	
	Элементы.ГруппаТаблицаКаталогов.Заголовок = ЗаголовокТаблицы;
	Элементы.ТаблицаКаталоговГруппы.Заголовок = ЗаголовокКолонки;
	Элементы.ТаблицаКаталоговГруппы.ВыборГруппИЭлементов = ВыборГруппИЭлементов;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуКаталоговСервер()
	
	НадписьВсеЭлементыСписка = "(" + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Все") + ")";
	
	СохраненнаяТаблицаКаталогов = РеквизитФормыВЗначение("Объект").СохраненнаяТаблицаКаталогов.Получить();
	
	Если НЕ ТипЗнч(СохраненнаяТаблицаКаталогов) = Тип("ТаблицаЗначений") Тогда
		
		СоздатьКаталогПоУмолчаниюСервер();
		
	Иначе
		
		ТаблицаКаталогов.Очистить();
		Для Каждого СтрокаСохраненнойТаблицыКаталогов Из СохраненнаяТаблицаКаталогов Цикл
			
			НоваяСтрока = ТаблицаКаталогов.Добавить();
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСохраненнойТаблицыКаталогов);
			
			Если Не СохраненнаяТаблицаКаталогов.Колонки.Найти("ХранилищеНастроекКомпоновки") = Неопределено Тогда
				Если Не СтрокаСохраненнойТаблицыКаталогов.ХранилищеНастроекКомпоновки = Неопределено Тогда
					
					ХранилищеНастроекКаталогПакет = СтрокаСохраненнойТаблицыКаталогов.ХранилищеНастроекКомпоновки.Получить();
					НоваяСтрока.АдресНастроекКаталогПакет = ПоместитьВоВременноеХранилище(ХранилищеНастроекКаталогПакет,
					УникальныйИдентификатор);
				КонецЕсли;
			КонецЕсли;
			
			Если Не СохраненнаяТаблицаКаталогов.Колонки.Найти("ХранилищеНастроекКаталогПакет") = Неопределено Тогда
				
				Если Не СтрокаСохраненнойТаблицыКаталогов.ХранилищеНастроекКаталогПакет = Неопределено Тогда
					ХранилищеНастроекКаталогПакет = СтрокаСохраненнойТаблицыКаталогов.ХранилищеНастроекКаталогПакет.Получить();
					Если ЗначениеЗаполнено(ХранилищеНастроекКаталогПакет) Тогда
						НоваяСтрока.АдресНастроекКаталогПакет = ПоместитьВоВременноеХранилище(ХранилищеНастроекКаталогПакет,
						УникальныйИдентификатор);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если Не СохраненнаяТаблицаКаталогов.Колонки.Найти("ХранилищеНастроекИзмененияПакета") = Неопределено Тогда
				Если Не СтрокаСохраненнойТаблицыКаталогов.ХранилищеНастроекИзмененияПакета = Неопределено Тогда
					ХранилищеНастроекИзмененияПакета = СтрокаСохраненнойТаблицыКаталогов.ХранилищеНастроекИзмененияПакета.Получить();
					НоваяСтрока.АдресНастроекИзмененияПакета = ПоместитьВоВременноеХранилище(ХранилищеНастроекИзмененияПакета,
					УникальныйИдентификатор);
				КонецЕсли;
			КонецЕсли;
			
			Если Не СохраненнаяТаблицаКаталогов.Колонки.Найти("ХранилищеНастроекКаталог") = Неопределено Тогда
				Если Не СтрокаСохраненнойТаблицыКаталогов.ХранилищеНастроекКаталог = Неопределено Тогда
					
					ХранилищеНастроекКаталог = СтрокаСохраненнойТаблицыКаталогов.ХранилищеНастроекКаталог.Получить();
					НоваяСтрока.АдресНастроекКаталог = ПоместитьВоВременноеХранилище(ХранилищеНастроекКаталог,
					УникальныйИдентификатор);
				КонецЕсли;
			КонецЕсли;
			
			Если Не СохраненнаяТаблицаКаталогов.Колонки.Найти("ХранилищеНастроекПакетПредложений") = Неопределено Тогда
				Если Не СтрокаСохраненнойТаблицыКаталогов.ХранилищеНастроекПакетПредложений = Неопределено Тогда
					
					ХранилищеНастроекПакетПредложений = СтрокаСохраненнойТаблицыКаталогов.ХранилищеНастроекПакетПредложений.Получить();
					НоваяСтрока.АдресНастроекПакетПредложений = ПоместитьВоВременноеХранилище(ХранилищеНастроекПакетПредложений,
					УникальныйИдентификатор);
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		Если ТаблицаКаталогов.Количество() = 0 Тогда
			
			СоздатьКаталогПоУмолчаниюСервер();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьКаталогПоУмолчаниюСервер()
	
	НоваяСтрока = ТаблицаКаталогов.Добавить();
	НоваяСтрока.Каталог = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Основной каталог товаров");
	НоваяСтрока.Группы.Добавить(Неопределено, НадписьВсеЭлементыСписка);
	НоваяСтрока.ИдентификаторКаталога = Строка(Новый УникальныйИдентификатор);
	
КонецПроцедуры 

&НаСервере
Функция ПроверитьУзелОбменаЭтаИБСервер()
	
	ЭтотУзел = ПланыОбмена.ОбменССайтом.ЭтотУзел();
	Возврат Объект.Ссылка = ЭтотУзел;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ОбработатьВыбранныеГруппыСерверБезКонтекста(СписокГрупп, НадписьВсеЭлементыСписка)
	
	ГруппыВыбраны = ЛОЖЬ;
	
	// Удаляем не группы номенклатуры.
	
	МассивУдалить = Новый Массив;
	
	Для Каждого ЭлементСЗ Из СписокГрупп Цикл
		
		ТекГруппа = ЭлементСЗ.Значение;
		
		Если НЕ ЗначениеЗаполнено(ТекГруппа)
			ИЛИ НЕ ОбщийМодульПовтор.ЭтоГруппа(ТекГруппа) Тогда
			
			МассивУдалить.Добавить(ЭлементСЗ);			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ЭлементМУ Из МассивУдалить Цикл
		
		СписокГрупп.Удалить(ЭлементМУ);
		
	КонецЦикла;
	
	// Удаляем дубли и подчиненные элементы.
	
	МассивУдалить = Новый Массив;
	
	Для Каждого ЭлементСЗ Из СписокГрупп Цикл
		
		Если НЕ МассивУдалить.Найти(ЭлементСЗ) = НеОпределено Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ТекГруппа = ЭлементСЗ.Значение;
		
		Для Каждого ЭлементСЗВложенный Из СписокГрупп Цикл
			
			Если НЕ МассивУдалить.Найти(ЭлементСЗВложенный) = НеОпределено Тогда
				
				Продолжить;
				
			КонецЕсли;
			
			Если НЕ ЭлементСЗВложенный = ЭлементСЗ
				И ЭлементСЗВложенный.Значение = ТекГруппа Тогда
				
				МассивУдалить.Добавить(ЭлементСЗВложенный);
				
			Иначе
				
				Если ЭлементСЗВложенный.Значение.ПринадлежитЭлементу(ТекГруппа) Тогда
					
					МассивУдалить.Добавить(ЭлементСЗВложенный);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого ЭлементМУ Из МассивУдалить Цикл
		
		СписокГрупп.Удалить(ЭлементМУ);
		
	КонецЦикла;
	
	Для Каждого ЭлементСЗ Из СписокГрупп Цикл
		
		Если ЗначениеЗаполнено(ЭлементСЗ.Значение) Тогда
			
			ГруппыВыбраны = ИСТИНА;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ ГруппыВыбраны Тогда
		
		СписокГрупп.Очистить();
		СписокГрупп.Добавить(НеОпределено, НадписьВсеЭлементыСписка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьПолейОбменЗаказами()
	
	Элементы.ФайлЗагрузки.Доступность = Объект.ОбменЗаказами;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьПоляКаталогВыгрузки()
	
	Элементы.КаталогВыгрузки.Доступность = Объект.ОбменТоварами;
	
КонецПроцедуры

&НаКлиенте
// Функция возвращает ПериодПовтораВТечениеДня в секундах
// 
Функция ПолучитьПериодПовтораВТечениеДня()
	
	ЗначенияВыбора = СоответствиеЗначенийВыбораККоличествуСекунд();
	
	ПериодПовтораВТечениеДня = ЗначенияВыбора.Получить(ИнтервалОбменаССайтом);
	Возврат ?(ПериодПовтораВТечениеДня = Неопределено, 1800, ПериодПовтораВТечениеДня);
	
КонецФункции // ПолучитьПериодПовтораВТечениеДня()

&НаКлиенте
// Функция возвращает соответствие надписей выбора к количеству секунд
// 
Функция СоответствиеЗначенийВыбораККоличествуСекунд()
	
	СоответствиеНадписей = Новый Соответствие;
	СоответствиеНадписей.Вставить(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Один раз в 5 минут"), 300);
	СоответствиеНадписей.Вставить(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Один раз в 15 минут"), 900);
	СоответствиеНадписей.Вставить(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Один раз в 30 минут"), 1800);
	СоответствиеНадписей.Вставить(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Один раз в час"), 3600);
	СоответствиеНадписей.Вставить(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Один раз в 3 часа"), 10800);
	СоответствиеНадписей.Вставить(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Один раз в 6 часов"), 21600);
	СоответствиеНадписей.Вставить(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Один раз в 12 часов"), 43200);
	
	Возврат СоответствиеНадписей;
	
КонецФункции // СоответствиеЗначенийВыбораККоличествуСекунд()

&НаКлиенте
// Заполняет значения расписания регламентного задания.
// 
Процедура УстановитьРасписаниеРегламентногоЗадания()
	
	Месяцы = Новый Массив;
	Месяцы.Добавить(1);
	Месяцы.Добавить(2);
	Месяцы.Добавить(3);
	Месяцы.Добавить(4);
	Месяцы.Добавить(5);
	Месяцы.Добавить(6);
	Месяцы.Добавить(7);
	Месяцы.Добавить(8);
	Месяцы.Добавить(9);
	Месяцы.Добавить(10);
	Месяцы.Добавить(11);
	Месяцы.Добавить(12);
	
	ДниНедели = Новый Массив;
	ДниНедели.Добавить(1);
	ДниНедели.Добавить(2);
	ДниНедели.Добавить(3);
	ДниНедели.Добавить(4);
	ДниНедели.Добавить(5);
	ДниНедели.Добавить(6);
	ДниНедели.Добавить(7);
	
	ПериодПовтораВТечениеДня = ПолучитьПериодПовтораВТечениеДня();
	
	Если ПериодПовтораВТечениеДня > 0 Тогда
		
		Расписание = Новый РасписаниеРегламентногоЗадания;
		Расписание.Месяцы					= Месяцы;
		Расписание.ДниНедели				= ДниНедели;
		Расписание.ПериодПовтораВТечениеДня = ПериодПовтораВТечениеДня; // секунды
		Расписание.ПериодПовтораДней		= 1; // каждый день
		
		РасписаниеРегламентногоЗадания = Расписание;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНадписьРасписанияОбмена()
	
	
	Если РасписаниеРегламентногоЗадания = Неопределено Тогда
		ТекстЗаголовка = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Настроить расписание обмена");
	Иначе
		ТекстЗаголовка = РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	Элементы.НастроитьРасписаниеОбмена.Заголовок = ТекстЗаголовка;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьРасписанияОбмена()
	
	Элементы.НастроитьРасписаниеОбмена.Доступность = Объект.ИспользоватьРегламентныеЗадания;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьНастройкуРасписанияОбмена()
	
	Если РасписаниеРегламентногоЗадания = Неопределено Тогда
		РасписаниеРегламентногоЗадания = Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	Если НЕ ОбщийМодульСервисСервер.ПолучитьВерсиюПлатформы() < 803040000 Тогда
		Выполнить(" ОписаниеОповещения = Новый ОписаниеОповещения(""ИзменитьРасписаниеОбмена"", ЭтаФорма); ");	
		Выполнить(" Диалог = Новый ДиалогРасписанияРегламентногоЗадания(РасписаниеРегламентногоЗадания); ");
		Выполнить(" Диалог.Показать(ОписаниеОповещения); ");	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРасписаниеОбмена(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("РасписаниеРегламентногоЗадания") Тогда
		РасписаниеРегламентногоЗадания = Результат;
		
		УстановитьНадписьРасписанияОбмена();
		
		Модифицированность = ИСТИНА;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИмяПрикладногоСправочника(ИмяСправочникаНоменклатура)
	
	ИмяСправочникаНоменклатура = ОбменССайтомПовтИсп.ИмяПрикладногоСправочника("Номенклатура");
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКаталоговГруппыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Группы = Элементы.ТаблицаКаталогов.ТекущиеДанные.Группы;
	
	Если Группы.Количество() = 1 Тогда
		
		Если НЕ ЗначениеЗаполнено(Группы[0].Значение) Тогда
			
			Группы.Очистить();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуОтбораКаталога()
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("АдресНастроекКомпоновки", АдресНастроекКаталога());
	ПараметрыФормы.Вставить("РежимВыгрузки", Объект.РежимВыгрузки);
	
	Если НЕ ОбщийМодульСервисСервер.ПолучитьВерсиюПлатформы() < 803040000 Тогда
		Выполнить(" ОписаниеОповещения = Новый ОписаниеОповещения(""НастроитьКомпоновщик"", ЭтаФорма); ");
		Выполнить(" РежимОткрытия = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца; ");	
		Выполнить(" ФормаНастройки = ""ПланОбмена.ОбменССайтом.Форма.ФормаНастройкиОтбора""; ");
		Выполнить(" ОткрытьФорму(ФормаНастройки, ПараметрыФормы,,,,, ОписаниеОповещения, РежимОткрытия); ");	
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Функция АдресНастроекКаталога()
	
	РежимВыгрузки = Объект.РежимВыгрузки;
	
	Если РежимВыгрузки = 0 Тогда
		
		АдресНастроек = Элементы.ТаблицаКаталогов.ТекущиеДанные.АдресНастроекКаталогПакет;
		
	ИначеЕсли РежимВыгрузки = 1 Тогда
		
		АдресНастроек = Элементы.ТаблицаКаталогов.ТекущиеДанные.АдресНастроекКаталог;
		
	ИначеЕсли РежимВыгрузки = 2 Тогда
		
		АдресНастроек = Элементы.ТаблицаКаталогов.ТекущиеДанные.АдресНастроекПакетПредложений;
		
	ИначеЕсли РежимВыгрузки = 3 Тогда
		
		АдресНастроек = Элементы.ТаблицаКаталогов.ТекущиеДанные.АдресНастроекИзмененияПакета;
		
	КонецЕсли;
	
	Возврат АдресНастроек;
	
КонецФункции

&НаКлиенте
Процедура НастроитьКомпоновщик(НастройкиКомпоновки, Параметры) Экспорт
	
	Если НастройкиКомпоновки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьНастройкиКаталога(НастройкиКомпоновки);
	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНастройкиКаталога(НастройкиКомпоновки)
	
	ТекущиеДанные = Элементы.ТаблицаКаталогов.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РежимВыгрузки = Объект.РежимВыгрузки;
	
	Если РежимВыгрузки = 0 Тогда
		
		ТекущиеДанные.АдресНастроекКаталогПакет = ПоместитьВоВременноеХранилище(НастройкиКомпоновки,
		УникальныйИдентификатор);;
		
	ИначеЕсли РежимВыгрузки = 1 Тогда
		
		ТекущиеДанные.АдресНастроекКаталог = ПоместитьВоВременноеХранилище(НастройкиКомпоновки,
		УникальныйИдентификатор);
		
	ИначеЕсли РежимВыгрузки = 2 Тогда
		
		ТекущиеДанные.АдресНастроекПакетПредложений = ПоместитьВоВременноеХранилище(НастройкиКомпоновки,
		УникальныйИдентификатор);
		
	ИначеЕсли РежимВыгрузки = 3 Тогда
		
		ТекущиеДанные.АдресНастроекИзмененияПакета = ПоместитьВоВременноеХранилище(НастройкиКомпоновки,
		УникальныйИдентификатор);
		
	КонецЕсли;
	
	ТекущиеДанные.НастройкиКаталога = Строка(НастройкиКомпоновки.Отбор);
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьДублированиеСтатусов(ИмяКолонки)
	
	ТекущиеДанные = Элементы.СоответствиеСтатусовЗаказов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат ЛОЖЬ;
	КонецЕсли;
	
	СтатусЗаказаНаСайте       = ТекущиеДанные.СтатусЗаказаНаСайте;
	СостояниеЗаказаПокупателя = ТекущиеДанные.СтатусЗаказаВБазе;
	
	Если НЕ ПустаяСтрока(СтатусЗаказаНаСайте) Тогда
		Найдено = СоответствиеСтатусовЗаказов.НайтиСтроки(Новый Структура("СтатусЗаказаНаСайте",
		СтатусЗаказаНаСайте));
		Если Найдено.Количество() > 1 Тогда
			ИмяКолонки = "СтатусЗаказаНаСайте";
			Возврат ЛОЖЬ;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ИСТИНА;
	
КонецФункции

&НаКлиенте
Функция ИдентификаторУникален()
	
	ИдентификаторКаталога = Элементы.ТаблицаКаталогов.ТекущиеДанные.ИдентификаторКаталога;
	Найдено = ТаблицаКаталогов.НайтиСтроки(Новый Структура("ИдентификаторКаталога", ИдентификаторКаталога));
	ИдентификаторыУникальны = Найдено.Количество() = 1;
	
	Если НЕ ИдентификаторыУникальны Тогда
		
		ОчиститьСообщения();
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Идентификатор каталога должен быть уникальным!"),
		Объект.Ссылка,
		ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
		"ТаблицаКаталогов", ТаблицаКаталогов.Индекс(Элементы.ТаблицаКаталогов.ТекущиеДанные) + 1,
		"ИдентификаторКаталога"));
		
	КонецЕсли;
	
	Возврат ИдентификаторыУникальны;
	
КонецФункции

&НаСервере
Процедура ВосстановитьРеквизитыПрикладногоРешения(ОбъектУзла)
	
	СохраненныеНастройки = Новый Структура;
	
	Если Не ОбъектУзла = Неопределено Тогда
		
		СохраненныеНастройки = ОбъектУзла.ПараметрыПрикладногоРешения.Получить();
		
		Если Не ТипЗнч(СохраненныеНастройки) = Тип("Структура") Тогда
			СохраненныеНастройки = Новый Структура;
		КонецЕсли;
		
	КонецЕсли;
	
	Если СохраненныеНастройки.Свойство("ПараметрыПрикладногоРешения") Тогда
		
		ПараметрыПрикладногоРешения = СохраненныеНастройки.ПараметрыПрикладногоРешения;
		Если ТипЗнч(ПараметрыПрикладногоРешения) = Тип("Структура") Тогда
			Для Каждого КлючЗначение Из ПараметрыПрикладногоРешения Цикл
				Попытка
					Если ТипЗнч(КлючЗначение.Значение) = Тип("ТаблицаЗначений") Тогда
						ЭтаФорма[КлючЗначение.Ключ].Загрузить(КлючЗначение.Значение);
					Иначе
						ЭтаФорма[КлючЗначение.Ключ] = КлючЗначение.Значение;
					КонецЕсли;
				Исключение
				КонецПопытки;
			КонецЦикла;
		КонецЕсли;
	Иначе
		МассивРеквизитовПрикладногоРешения = Новый Массив;
		ЗаполнитьМассивРеквизитовФормыПрикладногоРешения(МассивРеквизитовПрикладногоРешения);
		Для Каждого ЭлементМассива Из МассивРеквизитовПрикладногоРешения Цикл
			Попытка
				ЭтаФорма[ЭлементМассива] = "1";
			Исключение
			КонецПопытки;
		КонецЦикла;
		
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ДозаполнитьТаблицуКаталогов(СтрокаТаблицыКаталогов)
	
	ОбработатьВыбранныеГруппыСерверБезКонтекста(СтрокаТаблицыКаталогов.Группы, НадписьВсеЭлементыСписка);
	
	Если ПустаяСтрока(СтрокаТаблицыКаталогов.ИдентификаторКаталога) Тогда
		СтрокаТаблицыКаталогов.ИдентификаторКаталога = Строка(Новый УникальныйИдентификатор);
	КонецЕсли;
	
	Если ПустаяСтрока(СтрокаТаблицыКаталогов.Каталог) Тогда
		СтрокаТаблицыКаталогов.Каталог = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Каталог товаров") + " "
		+ ВРег(СокрЛП(Лев(СтрокаТаблицыКаталогов.ИдентификаторКаталога, 8)));
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
// Заполняет массив реквизитами, которые были добавлены на форму
// 
Процедура ЗаполнитьМассивРеквизитовФормыПрикладногоРешения(МассивРеквизитов)
	
	ОбменССайтомПереопределяемый.ЗаполнитьПрикладныеРеквизитыФормыУзлаОбменСсайтом(МассивРеквизитов);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьРеквизиты(ТекущийОбъект, МассивЗначений, СохраненныеНастройки)
	
	Если Не ТипЗнч(МассивЗначений) = Тип("Массив") Тогда
		Массив = Новый Массив;
		Массив.Добавить(МассивЗначений);
	Иначе
		Массив = МассивЗначений;
	КонецЕсли;
	
	ДополнительныеРеквизиты = Новый Структура;
	Для Каждого ЭлементМассива Из Массив Цикл
		Если ТипЗнч(ЭтаФорма[ЭлементМассива]) = Тип("ДанныеФормыКоллекция") Тогда
			ЗначениеРеквизита = РеквизитФормыВЗначение(ЭлементМассива);
		Иначе
			ЗначениеРеквизита = ЭтаФорма[ЭлементМассива];
		КонецЕсли;
		ДополнительныеРеквизиты.Вставить(ЭлементМассива, ЗначениеРеквизита);
		
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ДополнительныеРеквизиты) Тогда
		Возврат;
	КонецЕсли;
	
	СохраненныеНастройки.Вставить("ПараметрыПрикладногоРешения", ДополнительныеРеквизиты);
	ТекущийОбъект.ПараметрыПрикладногоРешения = Новый ХранилищеЗначения(СохраненныеНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьТочекСамовывоза()
	
	Если Объект.РежимВыгрузки = 0 
		Или Объект.РежимВыгрузки = 2 Тогда
		Элементы.СписокТочекСамовывоза.Доступность = ИСТИНА;
	Иначе
		Элементы.СписокТочекСамовывоза.Доступность = ЛОЖЬ;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьВыгружатьКартики()
	
	Если Объект.РежимВыгрузки = 0
		Или Объект.РежимВыгрузки = 1 Тогда
		Элементы.ВыгружатьКартинки.Доступность = ИСТИНА;
	Иначе
		Элементы.ВыгружатьКартинки.Доступность = ЛОЖЬ;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьПредставлениеОтбораВТаблицеКаталогов()
	
	РежимВыгрузки = объект.РежимВыгрузки;
	
	Если РежимВыгрузки = 0 Тогда
		ИмяКолонкиСОтбором = "АдресНастроекКаталогПакет";
		
	ИначеЕсли РежимВыгрузки = 1 Тогда
		ИмяКолонкиСОтбором = "АдресНастроекКаталог";
		
	ИначеЕсли РежимВыгрузки = 2 Тогда
		ИмяКолонкиСОтбором = "АдресНастроекПакетПредложений";
		
	ИначеЕсли РежимВыгрузки = 3 Тогда
		ИмяКолонкиСОтбором = "АдресНастроекИзмененияПакета";
		
	КонецЕсли;
	
	Для Каждого СтрокаКаталога Из ТаблицаКаталогов Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаКаталога[ИмяКолонкиСОтбором]) Тогда
			СтрокаКаталога.НастройкиКаталога = "";
			Продолжить;
		КонецЕсли;
		
		НастройкиКомпоновки = ПолучитьИзВременногоХранилища(СтрокаКаталога[ИмяКолонкиСОтбором]);;
		Если Не ЗначениеЗаполнено(НастройкиКомпоновки) Тогда
			СтрокаКаталога.НастройкиКаталога = "";
			Продолжить;
		КонецЕсли;
		
		ПредставлениеОтбора = "";
		Попытка
			ПредставлениеОтбора = Строка(НастройкиКомпоновки.Отбор);
		Исключение
		КонецПопытки;
		СтрокаКаталога.НастройкиКаталога = ПредставлениеОтбора;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьТочкиСамовывоза(ТочкиСамовывоза, Параметры) Экспорт
	
	Если Не ТочкиСамовывоза = Неопределено Тогда
		
		СписокТочекСамовывоза.ЗагрузитьЗначения(ТочкиСамовывоза);
		Модифицированность = ИСТИНА;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасширеннаяНастройкаЗавершить(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.РазмерПорции = Результат.РазмерПорции;
	Объект.КоличествоПовторений = Результат.КоличествоПовторений;
	Модифицированность = ИСТИНА;
	
	СформироватьЗаголовокПараметрыВыгрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьЗаголовокПараметрыВыгрузки()
	
	ШаблонЗаголовка = "";
	Если Объект.РазмерПорции > 0 Тогда
		ШаблонЗаголовка = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Количество товаров в порции %1;");
	КонецЕсли;
	
	Если Объект.КоличествоПовторений > 0 Тогда
		ШаблонЗаголовка = ШаблонЗаголовка + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Количество попыток обмена %2.");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ШаблонЗаголовка) Тогда
		ЗаголовокКнопки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовка, Объект.РазмерПорции,
		Объект.КоличествоПовторений);
		Элементы.РасширеннаяНастройка.Заголовок = ЗаголовокКнопки;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьПодключение(НастройкиПодключения, ТекстПредупреждения)
	
	ОбменССайтом.ВыполнитьТестовоеПодключениеКСайту(НастройкиПодключения, ТекстПредупреждения);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ОбменССайтомПереопределяемый.СкоректироватьПроверяемыеРеквизитыФормыУзла(ЭтаФорма,
	ПроверяемыеРеквизиты,
	Объект.ОбменТоварами,
	Объект.ОбменЗаказами);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииРежимВыгрузки()
	
	РежимВыгрузки = РежимВыгрузкиДанных();
	
	Объект.РежимВыгрузки = РежимВыгрузки;
	
	УстановитьДоступностьТочекСамовывоза();
	
	УстановитьДоступностьВыгружатьКартики();
	
	СформироватьПредставлениеОтбораВТаблицеКаталогов();
	
КонецПроцедуры

&НаКлиенте
Функция РежимВыгрузкиДанных()
	
	Если ВыгружатьТовары И ВыгружатьЦеныОстатки Тогда
		РежимВыгрузки = 0;
		
	ИначеЕсли ВыгружатьТовары И Не ВыгружатьЦеныОстатки Тогда
		РежимВыгрузки = 1;
		
	ИначеЕсли ВыгружатьЦеныОстатки И Не ВыгружатьТовары Тогда
		РежимВыгрузки = 2;
		
	КонецЕсли;
	
	Возврат РежимВыгрузки;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗначениеФлаговВыгрузкиДанных(Форма, Объект)
	
	РежимВыгрузки = Объект.РежимВыгрузки;
	
	Если РежимВыгрузки = 0 Тогда
		
		Форма.ВыгружатьТовары = ИСТИНА;
		Форма.ВыгружатьЦеныОстатки = ИСТИНА;
		
	ИначеЕсли РежимВыгрузки = 1 Тогда
		Форма.ВыгружатьТовары = ИСТИНА;
		Форма.ВыгружатьЦеныОстатки = ЛОЖЬ;
		
	ИначеЕсли РежимВыгрузки = 2 Тогда
		Форма.ВыгружатьТовары = ЛОЖЬ;
		Форма.ВыгружатьЦеныОстатки = ИСТИНА;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоответствиеСтатусовЗаказовПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ИмяКолонки = "";
	Если НЕ ПроверитьДублированиеСтатусов(ИмяКолонки) Тогда
		Отказ = ИСТИНА;
		
		ОчиститьСообщения();
		
		Статус = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
		"СоответствиеСтатусовЗаказов",
		СоответствиеСтатусовЗаказов.Индекс(Элементы.СоответствиеСтатусовЗаказов.ТекущиеДанные) + 1,
		ИмяКолонки);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Такой статус заказа на сайте уже указан в другой строке таблицы!"),
		Объект.Ссылка, Статус);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоглашениеПриИзменении(Элемент)
	
	СоглашениеПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура СоглашениеПриИзмененииНаСервере()
	
	// ОбменССайтамиУТ.ФормаУзлаОбменаСоглашениеПриИзмененииНаСервере(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздаватьПартнеровДляНовыхКонтрагентовПриИзменении(Элемент)
	
	ОбменССайтомКлиентСерверПереопределяемый.УстановитьДоступностьЭлементовФормыУзла(ЭтаФорма, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 1);
КонецПроцедуры



