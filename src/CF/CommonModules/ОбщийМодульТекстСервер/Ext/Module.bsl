//sza140517-0221 SZA: 
//sza131211-1456 SZA: 

&НаСервере
Функция   ЗначениеЗаполненоПараметрыСеансаТекущийЯзык() Экспорт
	попытка
		Возврат ЗначениеЗаполнено(ОбщийМодульПовтор.ПолучитьПараметрСеанса("ТекущийЯзыкИнтерфейсаПрограммы"));
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции //ЗначениеЗаполненоПараметрыСеансаТекущийЯзык

&НаСервере
Функция   ПолучитьТекстНаЯзыке(Знач ТекстНаРусском, Знач ОпределенныйЯзык = Неопределено) Экспорт
	
	ТекстНаЯзыке = ТекстНаРусском;
	Если ОпределенныйЯзык = Неопределено Тогда
		ОпределенныйЯзык = ОбщийМодульПовтор.ПолучитьПараметрСеанса("ТекущийЯзыкИнтерфейсаПрограммы");
	КонецЕсли;
	
	Если не ОпределенныйЯзык = ОбщийМодульПовтор.ЗначениеПредопределенного("Справочники.Языки.Русский") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1 ТекстыНаДругихЯзыках.ТекстНаЯзыке ИЗ РегистрСведений.ТекстыНаДругихЯзыках КАК ТекстыНаДругихЯзыках
		|ГДЕ ТекстыНаДругихЯзыках.ТекстНаРусском = &ТекстНаРусском И ТекстыНаДругихЯзыках.Язык = &Язык";
		
		Запрос.УстановитьПараметр("ТекстНаРусском", ТекстНаРусском);
		Запрос.УстановитьПараметр("Язык", ОпределенныйЯзык);
		
		РезультатЗапроса = Запрос.Выполнить();
		если РезультатЗапроса.Пустой() тогда
			//	ДобавитьТекстыДляЯзыковДляСообщения(ТекстНаРусском)
			
		иначе
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			
			ВыборкаДетальныеЗаписи.Следующий();
			ТекстНаЯзыкеТут = ВыборкаДетальныеЗаписи.ТекстНаЯзыке;
			Если ЗначениеЗаполнено(ТекстНаЯзыкеТут) Тогда
				ТекстНаЯзыке = "" + ТекстНаЯзыкеТут ;
			КонецЕсли;
			
		конецесли; 		
	КонецЕсли;		
	
	Возврат ТекстНаЯзыке;
	
КонецФункции 

&НаСервере
Процедура ДобавитьТекстыДляЯзыковДляСообщения(Знач ТекстНаРусском) Экспорт
	
	Язык = Справочники.Языки.Выбрать() ;
	пока Язык.Следующий() Цикл
		если не язык.Ссылка = ОбщийМодульПовтор.ЗначениеПредопределенного("справочники.Языки.Русский") тогда
			СоздатьЗаписьОТексте(ТекстНаРусском, Язык.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьЗаписьОТексте(Знач ТекстНаРусском, Знач Язык, Знач ЗначениеПеревода = "", Знач Насильно = Ложь) Экспорт
	
	ТекстНаЯзыке = РегистрыСведений.ТекстыНаДругихЯзыках.СоздатьМенеджерЗаписи();
	ТекстНаЯзыке.Активность 	= Истина;
	ТекстНаЯзыке.ТекстНаРусском = ТекстНаРусском;
	ТекстНаЯзыке.ТекстНаЯзыке	= ЗначениеПеревода;
	ТекстНаЯзыке.Язык 			= Язык;
	
	Попытка
		ТекстНаЯзыке.Записать(Насильно);		
	Исключение
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Функция   ЯзыкПользователяЗаданВКонфигураторе() Экспорт
	
	ДанныеТекущегоПользователя = ПользователиИнформационнойБазы.ТекущийПользователь();
	
	Возврат НЕ ДанныеТекущегоПользователя.Язык = Неопределено;
	
КонецФункции //ЯзыкПользователяЗаданВКонфигураторе

&НаСервере
Функция   НетНиОднойЗаписиЭтогоЯзыка(Знач Язык) Экспорт
	
	Запрос = Новый Запрос;	
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1 ТекстыНаДругихЯзыках.ТекстНаЯзыке ИЗ РегистрСведений.ТекстыНаДругихЯзыках КАК ТекстыНаДругихЯзыках ГДЕ ТекстыНаДругихЯзыках.Язык = &Язык";	
	Запрос.УстановитьПараметр("Язык", Язык);	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Пустой();
	
КонецФункции

&наСервере
процедура ЗагрузитьТекстыЯзыкаИзМакета(Знач ИмяМакета, Знач язык, Знач УжеМонопольный = Ложь) Экспорт
	
	если не УжеМонопольный Тогда
		Попытка 
			УстановитьМонопольныйРежим(истина);
		Исключение 	
		КонецПопытки;               	
	КонецЕсли;
	
	Макет 	= ПолучитьОбщийМакет(ИмяМакета);
	КоличествоСтрок = 999999;
	счетчик = 1;
	пусто 	= 0;
	НачатьТранзакцию();
	
	пока КоличествоСтрок > Счетчик цикл
		
		R = сокрлп(формат(счетчик, "ЧГ=0"));
		ТекстНаРусском  = Макет.Область("R" + R + "C1").Текст;
		если значениезаполнено(ТекстНаРусском) тогда
			
			ТЯ = РегистрыСведений.ТекстыНаДругихЯзыках.СоздатьМенеджерЗаписи();
			ТЯ.Активность 	  = Истина;   						
			ТЯ.ТекстНаРусском = ТекстНаРусском ;
			ТЯ.ТекстНаЯзыке   = Макет.Область("R" + R + "C2").Текст ;
			тя.Язык 		  = язык;
			
			Попытка 
				ТЯ.Записать(ложь);
				пусто = 0;	
			Исключение 
			КонецПопытки;
			
		иначе 
			пусто = пусто + 1;
		конецесли;
		
		если пусто = 10 тогда
			прервать;
		конецесли;
		
		счетчик = счетчик + 1;
	КонецЦикла;
	ЗафиксироватьТранзакцию();
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Запрос = Новый Запрос;
	Для Каждого СправочникДляПеревода Из Метаданные.Справочники Цикл
		
		Запрос.Текст = "ВЫБРАТЬ Н.Ссылка, Н.Наименование ИЗ Справочник." + СправочникДляПеревода.Имя + " КАК Н ГДЕ Н.Предопределенный = ИСТИНА ";
		
		РезультатЗапроса = Запрос.Выполнить();
		если не РезультатЗапроса.Пустой() тогда
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				
				НаЯзыке  			= ОбщийМодульПовтор.ПолучитьТекстНаЯзыке(ВыборкаДетальныеЗаписи.Наименование, язык);
				
				Если Не НаЯзыке = "" Тогда
					ЗначениеПеревода = РегистрыСведений.ЗначенияНаДругихЯзыках.СоздатьМенеджерЗаписи();
					ЗначениеПеревода.Активность 		= Истина;
					ЗначениеПеревода.НаЯзыке  			= НаЯзыке;
					ЗначениеПеревода.ОбъектБазыДанных   = ВыборкаДетальныеЗаписи.ссылка;
					ЗначениеПеревода.Поле				= "Наименование";
					ЗначениеПеревода.Язык               = язык;
					ЗначениеПеревода.Записать(Истина);	
				КонецЕсли;
				
			КонецЦикла;	
		КонецЕсли;
		
	КонецЦикла;
	
	если не УжеМонопольный Тогда
		Попытка 
			УстановитьМонопольныйРежим(Ложь);
		Исключение 	
		КонецПопытки;	
	КонецЕсли;
	
КонецПроцедуры

&наСервере
функция   ТекущийЯзыкРусский() Экспорт
	Возврат ОбщийМодульПовтор.ТекущийЯзыкРусский();
КонецФункции

&НаСервере
Функция   ПолучитьОбщееНаименование(Знач Ссылка) Экспорт
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		
		ПоддерживатьИныеЯзыкиКромеРусского = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ПоддерживатьИныеЯзыкиКромеРусского");
		Если Не ПоддерживатьИныеЯзыкиКромеРусского Тогда
			Если ЗначениеЗаполнено(Ссылка.НаименованиеДляПечати) Тогда
				Результат = Ссылка.НаименованиеДляПечати;
			Иначе
				Результат = Ссылка.Наименование;
			КонецЕсли;        
			
		иначе
			Результат = ОбщийМодульПовтор.ПолучитьПредставлениеНаЯзыке(Ссылка, "НаименованиеДляПечати");
			Если Результат = "" Тогда
				Результат = ОбщийМодульПовтор.ПолучитьПредставлениеНаЯзыке(Ссылка, "Наименование");
			КонецЕсли;
		КонецЕсли;
		
		Возврат Результат;
	Иначе
		Возврат "";
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ПередатьПереводыДругомуОбъекту(Знач Источник, Знач Приемник) Экспорт
	
	Если ЗначениеЗаполнено(Источник)
		и ЗначениеЗаполнено(Приемник) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ЗначенияНаДругихЯзыках.Поле,
		|	ЗначенияНаДругихЯзыках.Язык,
		|	ЗначенияНаДругихЯзыках.НаЯзыке
		|ИЗ РегистрСведений.ЗначенияНаДругихЯзыках КАК ЗначенияНаДругихЯзыках
		|ГДЕ ЗначенияНаДругихЯзыках.ОбъектБазыДанных = &ОбъектБазыДанных";
		
		Запрос.УстановитьПараметр("ОбъектБазыДанных", Источник);
		
		РезультатЗапроса = Запрос.Выполнить();
		если не РезультатЗапроса.Пустой() тогда
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				//Запись Зндя для ЗначенияНаДругихЯзыках
				ЗначенияНаДругихЯзыках = РегистрыСведений.ЗначенияНаДругихЯзыках.СоздатьМенеджерЗаписи();
				ЗначенияНаДругихЯзыках.Активность = Истина;
				ЗначенияНаДругихЯзыках.НаЯзыке = ВыборкаДетальныеЗаписи.НаЯзыке;
				ЗначенияНаДругихЯзыках.ОбъектБазыДанных = Приемник;
				ЗначенияНаДругихЯзыках.Поле = ВыборкаДетальныеЗаписи.Поле;
				ЗначенияНаДругихЯзыках.Язык = ВыборкаДетальныеЗаписи.Язык;
				
				Попытка //Записи в регистр сведений 
					ЗначенияНаДругихЯзыках.Записать(Истина);
					
				Исключение //Записи в регистр сведений 
				КонецПопытки; //Записи в регистр сведений
				
			КонецЦикла;
			
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция   СформироватьСуммуПрописью(Знач Сумма, Знач Валюта = Неопределено) Экспорт
	
	Если Валюта = Неопределено Тогда
		Валюта = ПредопределенноеЗначение("Справочник.Валюты.ОсновнаяВалюта");
	КонецЕсли;
	
	ГривнаНаРусском 	= "гривна, гривны, гривен, ж, копейка, копейки, копеек, ж, 2";
	ГривнаНаУкраинском 	= "гривна, гривні, гривень, ж, копійка, копійки, копійок, ж, 2";
	ДолларНаРусском 	= "доллар, доллара, долларов, м, цент, цента, центов, м, 2";
	ДолларНаУкраинском 	= "долар, долари, доларів, м, цент, центи, центів, м, 2";
	ЕвроНаРусском 		= "евро, евро, евро, м, цент, цента, центов, м, 2";
	ЕвроНаУкраинском 	= "євро, євро, євро, м, цент, центи, центів, м, 2";
	РубльНаРусском 		= "рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2";
	РубльНаУкраинском 	= "рубль, рубля, рублiв, м, копійка, копійки, копійок, ж, 2";
	
	ПервыйСимволВалюты 	= лев(сокрлп(врег(валюта)), 1);
	КодВалюты 			= Валюта.Код;
	
	Если КодВалюты = "980"
		ИЛи ПервыйСимволВалюты = "Г" Тогда
		
		Если ОбщийМодульПовтор.ТекущийЯзыкРусский() Тогда
			ПараметрыПрописи = ГривнаНаРусском;
			КодЯзыка = "ru_RU";
		Иначе
			ПараметрыПрописи = ГривнаНаУкраинском;
			КодЯзыка = "uk_UA";
		КонецЕсли;
		
	ИначеЕсли КодВалюты = "840"
		ИЛи ПервыйСимволВалюты = "U" 
		ИЛи ПервыйСимволВалюты = "Д" Тогда
		
		Если ОбщийМодульПовтор.ТекущийЯзыкРусский() Тогда
			ПараметрыПрописи = ДолларНаРусском;
			КодЯзыка = "ru_RU";
		Иначе
			ПараметрыПрописи = ДолларНаУкраинском;
			КодЯзыка = "uk_UA";
		КонецЕсли;
		
	ИначеЕсли КодВалюты = "978"
		ИЛи ПервыйСимволВалюты = "Е" 
		ИЛи ПервыйСимволВалюты = "E" Тогда
		
		Если ОбщийМодульПовтор.ТекущийЯзыкРусский() Тогда
			ПараметрыПрописи = ЕвроНаРусском;
			КодЯзыка = "ru_RU";
		Иначе
			ПараметрыПрописи = ЕвроНаУкраинском;
			КодЯзыка = "uk_UA";
		КонецЕсли;
		
	Иначе //643		
		
		Если ОбщийМодульПовтор.ТекущийЯзыкРусский() Тогда
			ПараметрыПрописи = РубльНаРусском;
			КодЯзыка = "ru_RU";
		Иначе
			ПараметрыПрописи = РубльНаУкраинском;
			КодЯзыка = "uk_UA";
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЧислоПрописью(Сумма,"Л=" + КодЯзыка, ПараметрыПрописи);
	
КонецФункции // СформироватьСуммуПрописью()
