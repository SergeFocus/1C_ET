//sza140117-2231 : 
//sza130901-0137 : 
Перем глЧислоОбъектов Экспорт;
Перем глПодключаемоеОборудование Экспорт;
Перем глКонфигурацияВРаботе Экспорт;
Перем глОтборСтруктура Экспорт;
Перем глПроверятьСообщения Экспорт;
Перем глТекущийПользователь Экспорт;
Перем глВерсияПлатформы Экспорт;

Процедура ВыполнениеРегламентныхЗаданий() Экспорт
    ОбщийМодульСерверПривилегия.ВыполнитьЗадачи();
КонецПроцедуры

Процедура ОбработкаВнешнегоСобытия(Источник, Событие, Данные)

	ОписаниеСобытия = Новый Структура();
	ОписаниеОшибки  = "";

	ОписаниеСобытия.Вставить("Источник", Источник);
	ОписаниеСобытия.Вставить("Событие",  Событие);
	ОписаниеСобытия.Вставить("Данные",   Данные);

	Результат = МенеджерОборудованияКлиент.ОбработатьСобытиеОтУстройства(ОписаниеСобытия, ОписаниеОшибки);
	Если НЕ Результат Тогда
		ОбщегоНазначения.СообщитьПользователю(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("При обработке внешнего события от устройства произошла ошибка.")
		                                                 + Символы.ПС + ОписаниеОшибки);
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗавершениемРаботыСистемы(Отказ)
	
	СледуетВыполнитьОперацииПередЗавершением = ОбщийМодульСервисСервер.СледуетВыполнитьОперацииПередЗавершением();
	Если НЕ СледуетВыполнитьОперацииПередЗавершением.количество() = 0 Тогда
		
		Количество = СледуетВыполнитьОперацииПередЗавершением.Количество();
		Процент = ?(Количество = 0, 100, 100 /Количество);
		ТекущийПроцент = Процент;
		Для Каждого ОперацияПередЗавершением Из СледуетВыполнитьОперацииПередЗавершением Цикл
			
			Если ОперацияПередЗавершением = "Восстановление Валовой Прибыли" 
				И Вопрос(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Необходимо восстановить показатели валовой прибыли. Восстановить сейчас?"), РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
				
			Состояние(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Операция") + ": " + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке(ОперацияПередЗавершением), Процент, ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Ждите.."), БиблиотекаКартинок.GrayGIf);
			ОбщийМодульСервисСервер.ПередЗавершениемРаботыСистемы(ОперацияПередЗавершением);	
			
			КонецЕсли;
			
			ТекущийПроцент = ТекущийПроцент + Процент;
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("НеСпрашиватьПодтверждениеПриВыходеИзПрограммы") Тогда
		
		КомандыВопроса = Новый Массив(3, 3);
		КомандыВопроса[0][0] = "НЕТ";
		КомандыВопроса[0][1] = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Нет");
		КомандыВопроса[0][2] = "Результат = ЛОЖЬ;";
		КомандыВопроса[1][0] = "ДА";
		КомандыВопроса[1][1] = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Да");
		КомандыВопроса[1][2] = "Результат = ИСТИНА;";
		КомандыВопроса[2][0] = "БС";
		КомандыВопроса[2][1] = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Да, и больше не спрашивать");
		КомандыВопроса[2][2] = "ОбщийМодульСервисСервер.УстановитьЗначениеПараметраИлиКонстанты(""НеСпрашиватьПодтверждениеПриВыходеИзПрограммы"", ИСТИНА); Результат = ИСТИНА;";
		ПараметрыФормы = Новый Структура("СтруктураКнопокИПоведения", КомандыВопроса);
		ПараметрыФормы.Вставить("ЗаголовокФормы", "");
		ПараметрыФормы.Вставить("ТекстВопроса", ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Закрыть программу?"));
		ФормаВопроса = ПолучитьФорму("ОбщаяФорма.ФормаВопроса", ПараметрыФормы);
		ОтветПользователя = ФормаВопроса.ОткрытьМодально();
		Если НЕ ОтветПользователя = Неопределено Тогда
			Отказ = НЕ ОтветПользователя;		
		КонецЕсли;
	КонецЕсли;
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.ПередЗавершениемРаботыСистемы();
	// Конец ПодключаемоеОборудование
	
	ОбщийМодульСервисСервер.УстановитьЗначениеКонстанты("ЧислоОбъектов", глЧислоОбъектов);
	
КонецПроцедуры

Процедура ПриНачалеРаботыСистемы()
	
	глТекущийПользователь = ОбщийМодульСерверПривилегия.ОпределитьПользователя();
	
	глКонфигурацияВРаботе = ИСТИНА;
	глЧислоОбъектов = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ЧислоОбъектов", ИСТИНА);
	ОбщийМодульКлиент.ПриНачалеРаботыСистемы();
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.ПриНачалеРаботыСистемы();
	// Конец ПодключаемоеОборудование
	
	ПроверитьПодключениеОбработчикаОжидания();
	ДобавитьНапоминанияПриСтартеСистемы();
		
КонецПроцедуры

Процедура ПроверитьПодключениеОбработчикаОжидания() Экспорт
	
	Если ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьМеханизмЗаметокИНапоминаний") Тогда
		ПодключитьОбработчикОжидания("ВыполнениеРегламентныхЗаданий", 60);
	Иначе
		ОтключитьОбработчикОжидания("ВыполнениеРегламентныхЗаданий");
	КонецЕсли; 	
	
	глОтборСтруктура = Новый Структура("Пользователь", ОбщийМодульПовтор.ПолучитьПараметрСеанса("ТекущийПользователь"));
	ПодключитьОбработчикОжидания("СообщенияПользователюОтСистемы", 1);
		
КонецПроцедуры

Процедура СообщенияПользователюОтСистемы() Экспорт
	
	Если глПроверятьСообщения Тогда
    	ОбщийМодульКлиент.ПроверитьВывестиСообщенияПользователю(глОтборСтруктура);	
		глПроверятьСообщения = ЛОЖЬ;
	КонецЕсли;
	
КонецПроцедуры

глПроверятьСообщения = ЛОЖЬ;
глВерсияПлатформы = 803050001;