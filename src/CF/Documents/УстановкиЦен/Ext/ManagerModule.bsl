// sza150727-1821  цены поставщиков
// sza131213-2337 : 
// sza131003-2248 :

Процедура Печать(ТабДок, Ссылка) Экспорт
	
	ТабДок.ИмяПараметровПечати  = "ДокУстановкаЦен" + СокрЛП(ИмяКомпьютера());
	ТабДок.КлючПараметровПечати = ТабДок.ИмяПараметровПечати;
	
	ТабДок.Очистить();
	
	СсылкаНаОбъектПечати = Ссылка[0];
	
	РазрядМетаданных = ""; ИмяМетаданных = "";
	ПроизвольнаяПечатнаяФорма = ОбщийМодульТекстСервер.ОпределитьПроизвольнуюПечатнуюФорму(, , СсылкаНаОбъектПечати, РазрядМетаданных, ИмяМетаданных);
	
	Если ЗначениеЗаполнено(ПроизвольнаяПечатнаяФорма) Тогда
		ТабДок = ОбщийМодульТекстСервер.СформироватьДокументПоПроизвольнойФорме(ПроизвольнаяПечатнаяФорма, , ТабДок, СсылкаНаОбъектПечати, РазрядМетаданных, ИмяМетаданных);
		
	Иначе		
		Макет = Документы.УстановкиЦен.ПолучитьМакет("Печать");
		Макет.КодЯзыкаМакета = ОбщийМодульПовтор.ПолучитьТекущийЯзыкДокументов();
		
		Запрос = Новый Запрос;	
		Запрос.Текст = "ВЫБРАТЬ УстановкиЦен.ВидЦен,
		|	УстановкиЦен.Дата,
		|	УстановкиЦен.Комментарий,
		|	УстановкиЦен.Номер,
		|	УстановкиЦен.Товары.(
		|		Ссылка,
		|		НомерСтроки,
		|		Номенклатура,
		|		ЕдиницаИзмерения,
		|		СтараяЦена,
		|		Цена,
		|		РазницаЦены,
		|		Комментарий
		|	),
		|	УстановкиЦен.ДляЦенПоставщика,
		|	УстановкиЦен.Поставщик,
		|	УстановкиЦен.Договор
		|ИЗ Документ.УстановкиЦен КАК УстановкиЦен
		|ГДЕ УстановкиЦен.Ссылка В(&Ссылка)";
		
		Запрос.Параметры.Вставить("Ссылка", Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Шапка 				= Макет.ПолучитьОбласть("Шапка");
		ОбластьТоварыШапка 	= Макет.ПолучитьОбласть("ТоварыШапка");
		ОбластьТовары 	= Макет.ПолучитьОбласть("Товары");
		Подвал 			= Макет.ПолучитьОбласть("Подвал");
		Поставщик 		= Макет.ПолучитьОбласть("Поставщик");
		
		СтруктураДополнительныПараметровМакета = ОбщийМодульСервер.ПолучитьСтруктуруДополнительныхПараметровМакетаПечати();
		
		ВставлятьРазделительСтраниц = ЛОЖЬ;
		Пока Выборка.Следующий() Цикл
			Если ВставлятьРазделительСтраниц Тогда
				ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			Если Выборка.ДляЦенПоставщика Тогда
				ОбластьЗаголовок = Макет.ПолучитьОбласть("ЗаголовокПоставщика");
			Иначе
				ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
			КонецЕсли;		
			
			ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьЗаголовок);
			ТабДок.Вывести(ОбластьЗаголовок);
			
			Шапка.Параметры.Заполнить(Выборка);
			Шапка.Параметры.валюта = Выборка.ВидЦен.ВалютаЦены;
			Шапка.Параметры.ТекстВШапкеДокументовПриПечати   = СтруктураДополнительныПараметровМакета.ТекстВШапкеДокументовПриПечати;			
			ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(Шапка);
			ТабДок.Вывести(Шапка, Выборка.Уровень());
			
			Если Выборка.ДляЦенПоставщика Тогда
				Поставщик.Параметры.Заполнить(Выборка);
				ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(Поставщик);
				ТабДок.Вывести(Поставщик, Выборка.Уровень());
			КонецЕсли;
			
			ВыборкаТовары = Выборка.Товары.Выбрать();
			Если НЕ ВыборкаТовары.Количество() = 0 Тогда
				ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьТоварыШапка);
				ТабДок.Вывести(ОбластьТоварыШапка);		
				
				Пока ВыборкаТовары.Следующий() Цикл
					ОбластьТовары.Параметры.Заполнить(ВыборкаТовары);			
					ОбластьТовары.Параметры.НоменклатураНаименование = ОбщийМодульТекстСервер.ПолучитьОбщееНаименование(ВыборкаТовары.Номенклатура) + " " + ВыборкаТовары.ЕдиницаИзмерения;
					ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьТовары);
					ТабДок.Вывести(ОбластьТовары, ВыборкаТовары.Уровень());
				КонецЦикла;	
			КонецЕсли;
			
			Подвал.Параметры.Заполнить(Выборка);
			Подвал.Параметры.ТекстВПодвалеДокументовПриПечати = СтруктураДополнительныПараметровМакета.ТекстВПодвалеДокументовПриПечати;
			ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(Подвал);
			ТабДок.Вывести(Подвал);
			
			ВставлятьРазделительСтраниц = ИСТИНА;
		КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	ДанныеСсылка  = Данные.Ссылка;
	Если ДанныеСсылка.ДляЦенПоставщика Тогда
		Представление = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Цены поставщика") + " " + ДанныеСсылка.Поставщик + ДанныеСсылка.Номер + " - " + формат(ДанныеСсылка.Дата, "ДФ='dd.MM.yy ЧЧ:мм'");
		СтандартнаяОбработка = ЛОЖЬ;	
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	ЗначенияЗаполнения = Неопределено;
	Попытка 
		ДляЦенПоставщика = Параметры.Ключ.ДляЦенПоставщика;
	Исключение 	
		ДляЦенПоставщика = ЛОЖЬ;		
	КонецПопытки;
	
	Если Параметры.свойство("ДляЦенПоставщика")
		ИЛИ ДляЦенПоставщика Тогда
		
		СтандартнаяОбработка = ЛОЖЬ;
		
		Если ВидФормы = "ФормаСписка" Тогда
			ВыбраннаяФорма = "ФормаСпискаДляЦенПоставщика";
			
		ИначеЕсли НЕ ВидФормы = "ФормаОбъекта" Тогда
			ВыбраннаяФорма = "ФормаВыбораДляЦенПоставщика";
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
