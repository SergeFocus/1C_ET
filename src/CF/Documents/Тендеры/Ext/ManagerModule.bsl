// sza151218-1733

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	Процедура НаПечать(ДокументДляПечати, Ссылка) Экспорт
		
		ДокументДляПечати.ИмяПараметровПечати  = "ДокТендер" + СокрЛП(ИмяКомпьютера());
		ДокументДляПечати.КлючПараметровПечати = ДокументДляПечати.ИмяПараметровПечати;
		
		ДокументДляПечати.Очистить();
		
		СсылкаНаОбъектПечати = Ссылка[0];
		
		РазрядМетаданных = ""; ИмяМетаданных = "";
		ПроизвольнаяПечатнаяФорма = ОбщийМодульТекстСервер.ОпределитьПроизвольнуюПечатнуюФорму(, , СсылкаНаОбъектПечати, РазрядМетаданных, ИмяМетаданных);
		
		Если ЗначениеЗаполнено(ПроизвольнаяПечатнаяФорма) Тогда
			ДокументДляПечати = ОбщийМодульТекстСервер.СформироватьДокументПоПроизвольнойФорме(ПроизвольнаяПечатнаяФорма, , ДокументДляПечати, СсылкаНаОбъектПечати, РазрядМетаданных, ИмяМетаданных);
			
		Иначе		
			Макет = Документы.Тендеры.ПолучитьМакет("НаПечать");
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			|	Тендеры.Дата,
			|	Тендеры.ДатаПоставки,
			|	Тендеры.Договор,
			|	ВЫРАЗИТЬ(Тендеры.Комментарий КАК СТРОКА(1000)) КАК Комментарий,
			|	Тендеры.НаправлениеДеятельности,
			|	Тендеры.Номер,
			|	Тендеры.Организация,
			|	Тендеры.Поставщик,
			|	Тендеры.Склад,
			|	Тендеры.Сотрудник,
			|	Тендеры.СтатусТендера,
			|	Тендеры.Товары.(
			|		НомерСтроки,
			|		Номенклатура,
			|		ЕдиницаИзмерения,
			|		Количество,
			|		ЗаказКлиента,
			|		ОСтроке
			|	),
			|	Тендеры.Предложения.(
			|		НомерСтроки,
			|		Поставщик,
			|		Договор,
			|		ДатаПоставки,
			|		Сумма,
			|		СуммаВВалюте,
			|		Валюта,
			|		Курс,
			|		Тендер,
			|		Условия,
			|		ОСтроке
			|	)
			|ИЗ Документ.Тендеры КАК Тендеры
			|ГДЕ Тендеры.Ссылка В (&Ссылка)";
			
			Запрос.Параметры.Вставить("Ссылка", Ссылка);
			Выборка = Запрос.Выполнить().Выбрать();
			
			ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
			Шапка = Макет.ПолучитьОбласть("Шапка");
			ОбластьТоварыШапка = Макет.ПолучитьОбласть("ТоварыШапка");
			ОбластьТовары = Макет.ПолучитьОбласть("Товары");
			ОбластьПредложенияШапка = Макет.ПолучитьОбласть("ПредложенияШапка");
			ОбластьПредложения = Макет.ПолучитьОбласть("Предложения");
			Подвал = Макет.ПолучитьОбласть("Подвал");
			
			ДокументДляПечати.Очистить();
			
			ВставлятьРазделительСтраниц = Ложь;
			Пока Выборка.Следующий() Цикл
				Если ВставлятьРазделительСтраниц Тогда
					ДокументДляПечати.ВывестиГоризонтальныйРазделительСтраниц();
				КонецЕсли;
				
				ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьЗаголовок);
				ДокументДляПечати.Вывести(ОбластьЗаголовок);
				
				Шапка.Параметры.Заполнить(Выборка);
				СтруктураДополнительныхПараметровМакета = ОбщийМодульСервер.ПолучитьСтруктуруДополнительныхПараметровМакетаПечати();
				Шапка.Параметры.ТекстВШапкеДокументовПриПечати   = СтруктураДополнительныхПараметровМакета.ТекстВШапкеДокументовПриПечати ;
				ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(Шапка);
				ДокументДляПечати.Вывести(Шапка, Выборка.Уровень());
				
				ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьТоварыШапка);
				ДокументДляПечати.Вывести(ОбластьТоварыШапка);
				
				ВыборкаТовары = Выборка.Товары.Выбрать();
				Пока ВыборкаТовары.Следующий() Цикл
					ОбластьТовары.Параметры.Заполнить(ВыборкаТовары);
					ОбластьТовары.Параметры.НоменклатураНаименование = ОбщийМодульТекстСервер.ПолучитьОбщееНаименование(ВыборкаТовары.Номенклатура);
					ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ВыборкаТовары);
					ДокументДляПечати.Вывести(ОбластьТовары, ВыборкаТовары.Уровень());
				КонецЦикла;
				
				ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьПредложенияШапка);
				ДокументДляПечати.Вывести(ОбластьПредложенияШапка);
				
				ВыборкаПредложения = Выборка.Предложения.Выбрать();
				Пока ВыборкаПредложения.Следующий() Цикл
					ОбластьПредложения.Параметры.Заполнить(ВыборкаПредложения);
					ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьПредложения);
					ДокументДляПечати.Вывести(ОбластьПредложения, ВыборкаПредложения.Уровень());
				КонецЦикла;
				
				Подвал.Параметры.Заполнить(Выборка);
				ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(Подвал);
				ДокументДляПечати.Вывести(Подвал);
				
				ВставлятьРазделительСтраниц = ИСТИНА;
			КонецЦикла;	
		КонецЕсли;
		
	КонецПроцедуры
	
#КонецЕсли