// sza140119-0200
// sza131003-0226 :

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

		Если ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
			Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступленияТовара") Тогда
				НаправлениеДеятельности = ДанныеЗаполнения.НаправлениеДеятельности;
				Комментарий = ДанныеЗаполнения.Комментарий;

				Для Каждого ТекСтрокаТовара Из ДанныеЗаполнения.Товары Цикл
					НоваяСтрока = Товары.Добавить();
					НоваяСтрока.Количество 				= ТекСтрокаТовара.Количество;
					НоваяСтрока.НоменклатураИлиГруппа 	= ТекСтрокаТовара.Номенклатура;

				КонецЦикла;

			ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.События") Тогда
				НаправлениеДеятельности = ДанныеЗаполнения.Направление;
				Комментарий = ДанныеЗаполнения.Комментарий;
				Для Каждого ТекСтрокаТовара Из ДанныеЗаполнения.Товары Цикл
					НоваяСтрока = Товары.Добавить();
					НоваяСтрока.Количество 				= ТекСтрокаТовара.Количество;
					НоваяСтрока.НоменклатураИлиГруппа 	= ТекСтрокаТовара.Номенклатура;

				КонецЦикла;
			ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.УстановкиЦен") Тогда
				Комментарий = ДанныеЗаполнения.Комментарий;
				Для Каждого ТекСтрокаТовара Из ДанныеЗаполнения.Товары Цикл
					НоваяСтрока = Товары.Добавить();
					НоваяСтрока.НоменклатураИлиГруппа = ТекСтрокаТовара.Номенклатура;

				КонецЦикла;
			ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Инвентаризации") Тогда
				НаправлениеДеятельности = ДанныеЗаполнения.НаправлениеДеятельности;
				Комментарий = ДанныеЗаполнения.Комментарий;

				Для Каждого ТекСтрокаТовара Из ДанныеЗаполнения.Товары Цикл
					НоваяСтрока = Товары.Добавить();
					НоваяСтрока.Количество 				= ТекСтрокаТовара.КоличествоПоФакту;
					НоваяСтрока.НоменклатураИлиГруппа 	= ТекСтрокаТовара.Номенклатура;

				КонецЦикла;
			ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПланыПродаж") Тогда
				НаправлениеДеятельности = ДанныеЗаполнения.НаправлениеДеятельности;
				ДатаНачала 		= НачалоДня(ДанныеЗаполнения.ДатаОкончания + 3600 * 24 + 1);
				ДатаОкончания 	= ДатаНачала + (ДанныеЗаполнения.ДатаОкончания - ДанныеЗаполнения.ДатаНачала);
				Комментарий 	= ДанныеЗаполнения.Комментарий;
				ПланНеАктуален 	= ДанныеЗаполнения.ПланНеАктуален;

				Для Каждого ТекСтрокаТовара Из ДанныеЗаполнения.Товары Цикл
					НоваяСтрока = Товары.Добавить();
					НоваяСтрока.Количество 				= ТекСтрокаТовара.Количество;
					НоваяСтрока.НоменклатураИлиГруппа 	= ТекСтрокаТовара.НоменклатураИлиГруппа;
					НоваяСтрока.Сумма 					= ТекСтрокаТовара.Сумма;

				КонецЦикла;
			КонецЕсли;
		КонецЕсли;

	КонецПроцедуры

	Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

		Если ОбменДанными.Загрузка Тогда
			Возврат;
		КонецЕсли;

		Если НЕ Отказ
			И НЕ ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("МассоваяЗагрузка", ИСТИНА) Тогда

			МассивПустыхСтрок  = Новый Массив;
			МассивНоменклатуры = Новый Массив;
			Для Каждого СтрокаТовары Из Товары Цикл
				Если НЕ ЗначениеЗаполнено(СтрокаТовары.НоменклатураИлиГруппа) Тогда
					МассивПустыхСтрок.Добавить(СтрокаТовары);
				ИначеЕсли НЕ МассивНоменклатуры.Найти(СтрокаТовары.НоменклатураИлиГруппа) = Неопределено Тогда
					Если ОбщийМодульПовторВТеченииСервера.ВыводитьСообщенияМожно() Тогда
						ОбщийМодульСервисСервер.ДобавитьСообщениеПользователю(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("На строке") + " №" + СтрокаТовары.НомерСтроки + " " + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("повторяется номенклатура") + " " + СтрокаТовары.НоменклатураИлиГруппа, , Ссылка);
					КонецЕсли;
				Иначе
					МассивНоменклатуры.Добавить(СтрокаТовары.НоменклатураИлиГруппа);
				КонецЕсли;

			КонецЦикла;

			Для Каждого СтрокаТовары Из МассивПустыхСтрок Цикл
				Товары.Удалить(СтрокаТовары);
			КонецЦикла;

			Попытка // ЭтотОбъект
				ОбщийМодульТоварСервер.ПроверитьИСортироватьТаблицуТовары(ЭтотОбъект, ИСТИНА);
			Исключение
			КонецПопытки;
			ТовараВКоличестве = Товары.Итог("Количество");
			ТовараНаСумму 	  = Товары.Итог("Сумма");

			Если НЕ Отказ
				И ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ФормироватьОписаниеТаблицОбъектовДляИхСписков") Тогда

				ОбщийМодульСервер.ОформитьОписаниеТаблицы(ЭтотОбъект, , "НоменклатураИлиГруппа", "-");
			КонецЕсли;
		КонецЕсли;

	КонецПроцедуры

	Процедура ПриКопировании(ОбъектКопирования)
		Комментарий = "";
	КонецПроцедуры

#КонецЕсли
