//sza140605-0107 SZA: 
//sza140216-2249 SZA: 

&НаКлиенте
Процедура ДатаНачалаОказанияУслугПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ДатаОкончанияОказанияУслуг)
		и объект.ДатаОкончанияОказанияУслуг < Объект.ДатаНачалаОказанияУслуг Тогда
		
		объект.ДатаОкончанияОказанияУслуг = Объект.ДатаНачалаОказанияУслуг;
	КонецЕсли;
	
	ПересчетКоличестваУслуги();
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	ПериодОказанияУслугиЗаВремя = ОбщийМодульСервер.получитьЗначениеРеквизита(Объект.Номенклатура, "ПериодОказанияУслугиЗаВремя");
	
	Если ЗначениеЗаполнено(Объект.Номенклатура) Тогда  		
		
		если Объект.ТовараВКоличестве = 0 тогда
			Объект.ТовараВКоличестве = ОбщийМодульСервер.ПолучитьКоличествоПоУмолчанию(Объект.Номенклатура);
		КонецЕсли;
		
		Объект.Цена = ПолучитьЦенуНаСервере(Объект.Номенклатура, Объект.ТовараВКоличестве);
		
		если ИспользоватьСкидки тогда
			Объект.СуммаБезСкидки = Объект.Цена * Объект.ТовараВКоличестве;
			Объект.ПроцентСкидки 	= ПолучитьПроцентСкидкиНаСервере(Объект.Номенклатура, Объект.ТовараВКоличестве);
		иначе
			Объект.ТовараНаСумму = Объект.Цена * Объект.ТовараВКоличестве;
		КонецЕсли;    
		
	КонецЕсли;
	
	ОбновитьКнопкиВыбораПериодаУслуги();
	ПересчетКоличестваУслуги();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидЦенПриИзменении(Элемент)
	
	ПересчетКоличестваУслуги();
	Если ЗначениеЗаполнено(объект.ВидЦен) Тогда
		ВалютаЦены = ОбщийМодульСервер.получитьЗначениеРеквизита(объект.ВидЦен, "ВалютаЦены");
	Иначе
		ВалютаЦены = ПредопределенноеЗначение("Справочник.Валюты.ОсновнаяВалюта");
	КонецЕсли;
	
	Объект.цена = ОбщийМодульСервер.ПолучитьЦенуНаСервере(Объект.Номенклатура, Объект.ВидЦен, Объект.Дата, Истина, Объект.ТовараВКоличестве, Объект.Договор, , Объект.Цена, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЦенаПриИзменении(Элемент)
	ПересчетКоличестваУслуги();
КонецПроцедуры

&НаКлиенте
Процедура ТовараНаСуммуПриИзменении(Элемент)
	ПересчетКоличестваУслуги(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПересчетКоличестваУслуги(ИзСуммы = Ложь)
	
	ПериодУслуги.ДатаНачала 	= Объект.ДатаНачалаОказанияУслуг;
	ПериодУслуги.ДатаОкончания 	= объект.ДатаОкончанияОказанияУслуг;
	
	Если Не ИзСуммы ТОгда
		Объект.ТовараВКоличестве = ОбщийМодульСервер.ВычислитьКоличествоУслугВПериоде(Объект.Номенклатура, Объект.ДатаНачалаОказанияУслуг, Объект.ДатаОкончанияОказанияУслуг);
		Объект.Цена 			 = ОбщийМодульСервер.ПолучитьЦенуНаСервере(Объект.Номенклатура, Объект.ВидЦен, Объект.Дата, Истина, Объект.ТовараВКоличестве, Объект.Договор, , Объект.Цена, Объект.Ссылка);
		объект.ПроцентСкидки  	 = ОбщийМодульСервер.ПолучитьПроцентСкидкиНаСервере(Объект.Номенклатура, Объект.ТовараВКоличестве, Объект.ВидЦен, Объект.Клиент);
		
		КоличествоНоменклатурыМожетБытьДробным = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("КоличествоНоменклатурыМожетБытьДробным");
		Если КоличествоНоменклатурыМожетБытьДробным Тогда
			ЧислоЗнаковПослеЗапятойДляКоличества = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ЧислоЗнаковПослеЗапятойДляКоличества");
			
			Если ЧислоЗнаковПослеЗапятойДляКоличества < 1 Тогда
				Объект.ТовараВКоличестве = Окр(Объект.ТовараВКоличестве);	
			Иначе
				Коэффициент = pow(10, ЧислоЗнаковПослеЗапятойДляКоличества);
				Объект.ТовараВКоличестве = Окр(Объект.ТовараВКоличестве * Коэффициент) / Коэффициент;
			КонецЕсли;
			
		Иначе
			Объект.ТовараВКоличестве = Окр(Объект.ТовараВКоличестве);
		КонецЕсли;		
		
		объект.СуммаБезСкидки = Объект.ТовараВКоличестве * Объект.Цена;
		Объект.ТовараНаСумму  = объект.СуммаБезСкидки - ((объект.СуммаБезСкидки / 100) * Объект.ПроцентСкидки);
		
	Иначе
		Если объект.ТовараВКоличестве = 0 Тогда
			объект.ТовараВКоличестве = 1;
		КонецЕсли;
		объект.СуммаБезСкидки = 0;
		Объект.ПроцентСкидки  = 0;
		Объект.Цена = объект.ТовараНаСумму / объект.ТовараВКоличестве;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоНаСейчас(Команда)
	
	Объект.ДатаНачалаОказанияУслуг = ОбщийМодульСервисСервер.ПользователяТекущаяДата();
	ОкруглитьДаты();
	
	Если ЗначениеЗаполнено(Объект.ДатаОкончанияОказанияУслуг)
		и объект.ДатаОкончанияОказанияУслуг < Объект.ДатаНачалаОказанияУслуг Тогда
		
		объект.ДатаОкончанияОказанияУслуг = Объект.ДатаНачалаОказанияУслуг;
	КонецЕсли;
	
	ПересчетКоличестваУслуги();
	
КонецПроцедуры

&НаКлиенте
Процедура КлиентПриИзменении(Элемент)
	ПриСменеКонтрагента();
КонецПроцедуры

&НаСервере
Процедура КлиентПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Клиент) Тогда
		Если ЗначениеЗаполнено(Объект.Клиент.ВидЦен) Тогда
			Объект.ВидЦен = Объект.Клиент.ВидЦен;
			ВалютаЦены 	  = объект.ВидЦен.ВалютаЦены;			
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьДатуОтсрочки();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияОказанияУслугПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ДатаОкончанияОказанияУслуг) Тогда
		Объект.УслугаОказана = Истина;
		ОбновитьВидДокумента();
		
		Если объект.ДатаОкончанияОказанияУслуг < Объект.ДатаНачалаОказанияУслуг Тогда 		
			объект.ДатаНачалаОказанияУслуг = Объект.ДатаОкончанияОказанияУслуг;
		КонецЕсли;
		
		ПересчетКоличестваУслуги();
	Иначе
		Объект.УслугаОказана = Ложь;
	КонецЕсли;
	
	ОбновитьДатуОтсрочки();
	
КонецПроцедуры

&НаКлиенте
Процедура КонецНаСейчас(Команда)
	
	Объект.УслугаОказана = Истина;
	объект.ДатаОкончанияОказанияУслуг = ОбщийМодульСервисСервер.ПользователяТекущаяДата();
	ОкруглитьДаты();
	
	Если ЗначениеЗаполнено(Объект.ДатаОкончанияОказанияУслуг)
		и объект.ДатаОкончанияОказанияУслуг < Объект.ДатаНачалаОказанияУслуг Тогда
		
		объект.ДатаНачалаОказанияУслуг = Объект.ДатаОкончанияОказанияУслуг;
	КонецЕсли;
	
	ПересчетКоличестваУслуги();
	ОбновитьВидДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодУслугиПриИзменении(Элемент)
	
	Объект.ДатаНачалаОказанияУслуг = ПериодУслуги.ДатаНачала;
	Объект.ДатаОкончанияОказанияУслуг = ПериодУслуги.ДатаОкончания;	
	ОкруглитьДаты();
	
	ПересчетКоличестваУслуги();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКнопкиВыбораПериодаУслуги()
	
	Элементы.ГруппаСутки.Видимость = ПериодОказанияУслугиЗаВремя = ПредопределенноеЗначение("Перечисление.ПериодыОказанияУслугиЗаВремя.ЗаСутки");
	Элементы.ГруппаМесяца.Видимость = ПериодОказанияУслугиЗаВремя = ПредопределенноеЗначение("Перечисление.ПериодыОказанияУслугиЗаВремя.ЗаМесяц");
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отказ = ОбщийМодульСервисСервер.ПроверитьОтказДоступа("001290", ЭтаФорма, Отказ, Объект);
	
	Если НЕ Отказ Тогда
		ОбъектСсылка = Объект.Ссылка;
		
		ДопИнфоТипВладельца = ОбщийМодульПовтор.ПолучитьТипВладельца(ОбъектСсылка); 	
		ОбщийМодульСервисСервер.ЗаполнитьДополнительнуюИнформацию(ОбъектСсылка, ДополнительнаяИнформация, ДопИнфоТипВладельца);
		
		ИспользоватьСкидки 		= ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ИспользоватьСкидки") ;
		ВестиУчетДвиженияДенег 	= ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетДвиженияДенег") ;
		ИспользоватьРегистрациюПриемаИВыдачиДенегПрямоВДокументахРасходаИПрихода = ВестиУчетДвиженияДенег И ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ИспользоватьРегистрациюПриемаИВыдачиДенегПрямоВДокументахРасходаИПрихода") ;
		Элементы.Оплата.Видимость = ИспользоватьРегистрациюПриемаИВыдачиДенегПрямоВДокументахРасходаИПрихода;
		
		ВестиУчетОказанияУслугЗаВремяСОпределеннымиПозициямиНомерами = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетОказанияУслугЗаВремяСОпределеннымиПозициямиНомерами");
		ИспользоватьПодключаемоеОборудование = ПодключаемоеОборудованиеДСервер.ИспользоватьПодключаемоеОборудование();
		
		ВестиУчетДенегВНесколькихХранилищах = ВестиУчетДвиженияДенег И ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетДенегВНесколькихХранилищах") ;
		Элементы.ФормаОплаты.Видимость 		= ВестиУчетДвиженияДенег И НЕ ВестиУчетДенегВНесколькихХранилищах;
		Элементы.ПечатьЧека.Видимость 		= ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ПечататьЧек");
		Элементы.ГруппаДополнительнаяИнформация.Видимость = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиДополнительнуюИнформациюДляОказанияУслугЗаВремя");
		
		Если НЕ ЗначениеЗаполнено(ОбъектСсылка) Тогда
			
			Если Параметры.ПоУмолчаниюУслугаОказана Тогда
				Объект.УслугаОказана = Истина;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Объект.ХранилищеДенег) Тогда					
				Объект.ХранилищеДенег = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ОсновноеХранилищеДенег");
				ХранилищеДенегПриИзмененииНаСервере();
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Объект.ФормаОплаты) Тогда
				Объект.ФормаОплаты = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ФормаОплатыПоУмолчанию");
			КонецЕсли;
			
			Если Параметры.ОткрыватьНовыйДокументОказанияУслугПриЗавершении Тогда
				Объект.ОткрыватьНовыйДокументОказанияУслугПриЗавершении = Истина;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
				Если ЗначениеЗаполнено(Параметры.Организация) Тогда
					Объект.Организация = Параметры.Организация;
				Иначе
					Объект.Организация = ПредопределенноеЗначение("Справочник.Организации.ОсновноеПредприятие");
				КонецЕсли;			
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Объект.Сотрудник) Тогда
				Объект.Сотрудник = Параметры.Сотрудник;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Объект.Клиент) Тогда
				Если ЗначениеЗаполнено(Параметры.Клиент) Тогда
					Объект.Клиент = Параметры.Клиент;
				Иначе
					Объект.Клиент = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("КлиентПоУмолчанию");
				КонецЕсли;			
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Объект.ВидЦен) Тогда
				
				Если ЗначениеЗаполнено(Параметры.ВидЦен) Тогда
					Объект.ВидЦен = Параметры.ВидЦен;
					
				Иначе			
					Если ЗначениеЗаполнено(Объект.Клиент)
						и ЗначениеЗаполнено(Объект.Клиент.ВидЦен) Тогда
						
						Объект.ВидЦен = Объект.Клиент.ВидЦен;
					Иначе
						Объект.ВидЦен = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВидЦенРасходованияПоУмолчанию") ;
						Если НЕ ЗначениеЗаполнено(Объект.ВидЦен) Тогда
							Объект.ВидЦен = ПредопределенноеЗначение("Справочник.ВидыЦен.ОсновнойВидЦен");
						КонецЕсли;
						
					КонецЕсли;	
				КонецЕсли;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Объект.Номенклатура) 
				и ЗначениеЗаполнено(Параметры.Номенклатура) Тогда
				
				Объект.Номенклатура = Параметры.Номенклатура;
				Объект.Цена = ОбщийМодульСервер.ПолучитьЦенуНаСервере(Объект.Номенклатура, Объект.ВидЦен, Объект.Дата);
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Объект.Валюта) Тогда
				Если ЗначениеЗаполнено(Объект.ВидЦен) Тогда
					Объект.Валюта = Объект.ВидЦен.ВалютаЦены;
				Иначе
					Объект.Валюта = ПредопределенноеЗначение("Справочник.Валюты.ОсновнаяВалюта");
				КонецЕсли;			
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Объект.Дата) Тогда
				Объект.Дата = ОбщийМодульСервисСервер.ПользователяТекущаяДата();
			КонецЕсли;
			
			Если не объект.УслугаОказана Тогда
				Если параметры.услугаоказана Тогда
					объект.УслугаОказана = Истина;
					Объект.ДатаОкончанияОказанияУслуг = ОбщийМодульСервисСервер.ПользователяТекущаяДата();
					
					Если параметры.МинимальныйОбъемОказанияУслуг > объект.ТовараВКоличестве Тогда
						объект.ТовараВКоличестве = параметры.МинимальныйОбъемОказанияУслуг;
						объект.ТовараНаСумму  	 = объект.ТовараВКоличестве * Объект.Цена;
						объект.СуммаБезСкидки 	 = объект.ТовараНаСумму;
						Объект.ПроцентСкидки  	 = 0;
					КонецЕсли;
					
					если параметры.МинимальнаяСуммаОказанияУслуг > объект.ТовараНаСумму тогда
						объект.ТовараНаСумму = параметры.МинимальнаяСуммаОказанияУслуг;
						Если объект.ТовараВКоличестве = 0 Тогда
							объект.ТовараВКоличестве = 1;
						КонецЕсли;
						
						объект.СуммаБезСкидки = объект.ТовараНаСумму;
						Объект.ПроцентСкидки  = 0;
						Объект.Цена = объект.ТовараНаСумму / объект.ТовараВКоличестве;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ИспользоватьМеханизмОтсрочкиПлатежа")
				И не ЗначениеЗаполнено(Объект.ДатаОтсрочки)
				И ЗначениеЗаполнено(Объект.ДатаОкончанияОказанияУслуг) Тогда
				
				Объект.ДатаОтсрочки = Объект.ДатаОкончанияОказанияУслуг + ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ЧислоДнейОтсрочкиКлиентамПоУмолчанию") * 3600 * 24;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(объект.ВидЦен) Тогда
			ВалютаЦены = объект.ВидЦен.ВалютаЦены;
		Иначе
			ВалютаЦены = ПредопределенноеЗначение("Справочник.Валюты.ОсновнаяВалюта");
		КонецЕсли;
		
		Если ОбщийМодульПовтор.ПолучитьПараметрСеанса("НеМожетМенятьЦены") тогда
			если ЗначениеЗаполнено(объект.ВидЦен) Тогда
				элементы.ВидЦен.Доступность = Ложь;	
			КонецЕсли;
			элементы.Цена.Доступность = Ложь;
		КонецЕсли;
		
		ПериодОказанияУслугиЗаВремя = ОбщийМодульСервер.получитьЗначениеРеквизита(Объект.Номенклатура, "ПериодОказанияУслугиЗаВремя");
		ОбщийМодульСервисСервер.ОформитьФорматКоличества(Элементы.ТовараВКоличестве, Истина);	
		
		Если ВестиУчетДвиженияДенег ТОгда
			СтруктураОтказа = ОбщийМодульСервисСервер.ПроверитьОтказДоступа("000892", , , , Истина);
			Если СтруктураОтказа.УровеньДоступа = Перечисления.УровниДоступа.ТолькоЧтение Тогда
				Элементы.ПоДокументу.Видимость = Ложь;
				Элементы.Оплата.ТолькоПросмотр = Истина;
				
			ИначеЕсли СтруктураОтказа.Отказ Тогда
				Элементы.Оплата.Видимость = Ложь;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцентСкидкиПриИзменении(Элемент)
	ПересчетКоличестваУслуги();
КонецПроцедуры

&НаКлиенте
Процедура СуммаБезСкидкиПриИзменении(Элемент)
	ПересчетКоличестваУслуги();
КонецПроцедуры

&НаКлиенте
Процедура ТовараВКоличествеПриИзменении(Элемент)
	ПересчетКоличестваУслуги();
КонецПроцедуры

&НаКлиенте
Процедура ВалютаПриИзменении(Элемент)
	Объект.курс = ОбщийМодульСервер.ПолучитьТекущийКурс(Объект.Валюта, ?(ЗначениеЗаполнено(Объект.ДатаОкончанияОказанияУслуг), Объект.ДатаОкончанияОказанияУслуг, Объект.ДатаНачалаОказанияУслуг));
КонецПроцедуры

&НаКлиенте
Процедура ПоДокументу(Команда)
	
	Объект.ПоступилоДенег = Объект.ТовараНаСумму;
	объект.Валюта = ВалютаЦены;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 0, Объект.Ссылка, истина);
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОбработатьБлокировку(истина);		
	КонецЕсли;
	
	Если ИспользоватьПодключаемоеОборудование И 
		МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда
		
		ОписаниеОшибки = "" ;
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("СчитывательМагнитныхКарт");
		ПоддерживаемыеТипыВО.Добавить("СканерШтрихКода");
		
		Если Не МенеджерОборудованияКлиент.ПодключитьОборудованиеПоТипу(УникальныйИдентификатор, ПоддерживаемыеТипыВО, ОписаниеОшибки) Тогда
			ТекстСообщения = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке( "При подключении оборудования произошла ошибка") + ": " + ОписаниеОшибки + ".";
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьКнопкиВыбораПериодаУслуги();
	ОбновитьВидДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 1, Объект.Ссылка, истина);
	
	Если ИспользоватьПодключаемоеОборудование Тогда
		ПоддерживаемыеТипыВО = Новый Массив ();
		ПоддерживаемыеТипыВО.Добавить("СчитывательМагнитныхКарт");
		ПоддерживаемыеТипыВО.Добавить("СканерШтрихКода");
		ПоддерживаемыеТипыВО.Добавить("ДисплейПокупателя");
		МенеджерОборудованияКлиент.ОтключитьОборудованиеПоТипу(УникальныйИдентификатор, ПоддерживаемыеТипыВО);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
процедура ОбработатьБлокировку(ПриСозданииФормы = ложь);
	
	Если ОбщийМодульСервер.ОбработатьБлокировку(Объект, ЭтаФорма, ПриСозданииФормы) Тогда		
		элементы.ПоДокументу.Видимость = Ложь;
		элементы.НачалоНаСейчас.Видимость = Ложь;
		элементы.КонецНаСейчас.Видимость = Ложь;
		элементы.ПериодУслуги.Видимость = ложь;
		элементы.ГруппаПредПериода.Видимость = Ложь;
		
	иначе          	
		элементы.ПоДокументу.Видимость = Истина;
		элементы.НачалоНаСейчас.Видимость = истина;
		элементы.КонецНаСейчас.Видимость = истина;
		элементы.ПериодУслуги.Видимость = истина;
		элементы.ГруппаПредПериода.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	если НЕ ПараметрыЗаписи.режимзаписи = РежимЗаписиДокумента.Запись тогда
		ОбработатьБлокировку();                                                	
	КонецЕсли;
	
	Если ДопИнформацияИзменена Тогда
		ОбщийМодульСервисСервер.ЗаписатьДополнительнуюИнформацию(Объект.Ссылка, ДополнительнаяИнформация);
	КонецЕсли; 	
	
КонецПроцедуры

&НаСервере
Функция   ПолучитьЦенуНаСервере(Знач Номенклатура, Знач Количество = 0)	
	Возврат ОбщийМодульСервер.ПолучитьЦенуНаСервере(Номенклатура, Объект.ВидЦен, Объект.Дата, истина, Количество, Объект.Договор, , , Объект.Ссылка);	
КонецФункции

&НаСервере
Функция   ПолучитьПроцентСкидкиНаСервере(Знач НоменклатураИлиКлиентПоставщик, Знач Количество = 0)
	
	если ИспользоватьСкидки тогда
		Возврат ОбщийМодульСервер.ПолучитьПроцентСкидкиНаСервере(НоменклатураИлиКлиентПоставщик, Количество, объект.ВидЦен, объект.Клиент);
		
	иначе
		возврат 0;
	КонецЕсли;
	
КонецФункции //ПолучитьПроцентСкидкиНаСервере

&НаКлиенте
Процедура ХранилищеДенегПриИзменении(Элемент)
	ХранилищеДенегПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ХранилищеДенегПриИзмененииНаСервере()
	
	если ЗначениеЗаполнено(Объект.ХранилищеДенег) Тогда		
		Объект.Валюта = Объект.ХранилищеДенег.Валюта;
		Объект.курс = ОбщийМодульСервер.ПолучитьТекущийКурс(Объект.Валюта, ?(ЗначениеЗаполнено(Объект.ДатаОкончанияОказанияУслуг), Объект.ДатаОкончанияОказанияУслуг, Объект.ДатаНачалаОказанияУслуг));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если НЕ Отказ
		И ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		если ЗначениеЗаполнено(Объект.ХранилищеДенег) Тогда		
			Объект.ФормаОплаты = Объект.ХранилищеДенег.ФормаОплаты;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Вчера(Команда)
	
	Дата = ОбщийМодульСервисСервер.ПользователяТекущаяДата();
	
	Объект.ДатаНачалаОказанияУслуг = НачалоДня(НачалоДня(Дата) - 3600);
	Объект.ДатаОкончанияОказанияУслуг = КонецДня(Объект.ДатаНачалаОказанияУслуг);	
	ОкруглитьДаты();
	
	ПересчетКоличестваУслуги();
	
КонецПроцедуры

&НаКлиенте
Процедура Сегодня(Команда)
	
	Дата = ОбщийМодульСервисСервер.ПользователяТекущаяДата();
	
	Объект.ДатаНачалаОказанияУслуг = НачалоДня(Дата);
	Объект.ДатаОкончанияОказанияУслуг = КонецДня(Объект.ДатаНачалаОказанияУслуг);	
	ОкруглитьДаты();
	
	ПересчетКоличестваУслуги();
	
КонецПроцедуры

&НаКлиенте
Процедура Завтра(Команда)
	
	Дата = ОбщийМодульСервисСервер.ПользователяТекущаяДата();
	
	Объект.ДатаНачалаОказанияУслуг = НачалоДня(КонецДня(Дата) + 3600);
	Объект.ДатаОкончанияОказанияУслуг = КонецДня(Объект.ДатаНачалаОказанияУслуг);	
	ОкруглитьДаты();
	
	ПересчетКоличестваУслуги();
	
КонецПроцедуры

&НаКлиенте
Процедура ПрошлыйМесяц(Команда)
	
	Дата = ОбщийМодульСервисСервер.ПользователяТекущаяДата();
	
	Объект.ДатаНачалаОказанияУслуг = НачалоМесяца(НачалоМесяца(Дата) - 3600);
	Объект.ДатаОкончанияОказанияУслуг = КонецМесяца(Объект.ДатаНачалаОказанияУслуг);	
	ОкруглитьДаты();
	
	ПересчетКоличестваУслуги();
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтотМесяц(Команда)
	
	Дата = ОбщийМодульСервисСервер.ПользователяТекущаяДата();
	
	Объект.ДатаНачалаОказанияУслуг = НачалоМесяца(Дата);
	Объект.ДатаОкончанияОказанияУслуг = КонецМесяца(Объект.ДатаНачалаОказанияУслуг);	
	ОкруглитьДаты();
	
	ПересчетКоличестваУслуги();
	
КонецПроцедуры

&НаКлиенте
Процедура СледующийМесяц(Команда)
	
	Дата = ОбщийМодульСервисСервер.ПользователяТекущаяДата();
	
	Объект.ДатаНачалаОказанияУслуг = НачалоМесяца(КонецМесяца(Дата) + 3600);
	Объект.ДатаОкончанияОказанияУслуг = КонецМесяца(Объект.ДатаНачалаОказанияУслуг);	
	ОкруглитьДаты();
	
	ПересчетКоличестваУслуги();
	
КонецПроцедуры

&НаКлиенте
Процедура ОкруглитьДаты()
	
	Если ПериодОказанияУслугиЗаВремя = ПредопределенноеЗначение("Перечисление.ПериодыОказанияУслугиЗаВремя.ЗаГод")
		ИЛИ ПериодОказанияУслугиЗаВремя = ПредопределенноеЗначение("Перечисление.ПериодыОказанияУслугиЗаВремя.ЗаМесяц") Тогда
		
		Объект.ДатаНачалаОказанияУслуг = НачалоДня(Объект.ДатаНачалаОказанияУслуг);
		Объект.ДатаОкончанияОказанияУслуг = КонецДня(Объект.ДатаОкончанияОказанияУслуг);
		Элементы.ДатаНачалаОказанияУслуг.ФорматРедактирования = "ДФ=dd.MMM.yyyy";
		Элементы.ДатаОкончанияОказанияУслуг.ФорматРедактирования = "ДФ=dd.MMM.yyyy";
		
	ИначеЕсли ПериодОказанияУслугиЗаВремя = ПредопределенноеЗначение("Перечисление.ПериодыОказанияУслугиЗаВремя.ЗаЧас")
		ИЛИ ПериодОказанияУслугиЗаВремя = ПредопределенноеЗначение("Перечисление.ПериодыОказанияУслугиЗаВремя.ЗаСутки") Тогда
		
		Элементы.ДатаНачалаОказанияУслуг.ФорматРедактирования = "ДФ='dd.MM.yy ЧЧ:мм'";
		Элементы.ДатаОкончанияОказанияУслуг.ФорматРедактирования = "ДФ='dd.MM.yy ЧЧ:мм'";
	Иначе
		Элементы.ДатаНачалаОказанияУслуг.ФорматРедактирования = "";
		Элементы.ДатаОкончанияОказанияУслуг.ФорматРедактирования = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УслугаОказанаПриИзменении(Элемент)
	
	Если объект.УслугаОказана 
		и НЕ ЗначениеЗаполнено(объект.ДатаОкончанияОказанияУслуг) Тогда
		
		объект.ДатаОкончанияОказанияУслуг = ОбщийМодульСервисСервер.ПользователяТекущаяДата();
	КонецЕсли;
	
	ОбновитьВидДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидДокумента() 
	
	Если Объект.УслугаОказана Тогда
		Элементы.ФормаПровестиИЗакрыть.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Провести и закрыть");
	Иначе
		Элементы.ФормаПровестиИЗакрыть.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Сохранить данные и закрыть");			
	КонецЕсли;
	
КонецПроцедуры 

&НаСервере
Функция   ПроверитьЧтоПозицияСвободна()
	
	Отказ = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗагрузкаПозицийНомеровПриОказанииУслугЗаВремя.Период,
	|	ЗагрузкаПозицийНомеровПриОказанииУслугЗаВремя.Регистратор,
	|	ЗагрузкаПозицийНомеровПриОказанииУслугЗаВремя.НомерСтроки,
	|	ЗагрузкаПозицийНомеровПриОказанииУслугЗаВремя.Активность,
	|	ЗагрузкаПозицийНомеровПриОказанииУслугЗаВремя.ПозицияНомер,
	|	ЗагрузкаПозицийНомеровПриОказанииУслугЗаВремя.Клиент,
	|	ЗагрузкаПозицийНомеровПриОказанииУслугЗаВремя.Комментарий
	|ИЗ РегистрСведений.ЗагрузкаПозицийНомеровПриОказанииУслугЗаВремя КАК ЗагрузкаПозицийНомеровПриОказанииУслугЗаВремя
	|ГДЕ ЗагрузкаПозицийНомеровПриОказанииУслугЗаВремя.ПозицияНомер = &ПозицияНомер
	|	И ЗагрузкаПозицийНомеровПриОказанииУслугЗаВремя.Регистратор.Ссылка <> &Ссылка
	|	И ЗагрузкаПозицийНомеровПриОказанииУслугЗаВремя.Период > &ДатаНачала
	|	И ЗагрузкаПозицийНомеровПриОказанииУслугЗаВремя.Период < &ДатаОкончания";
	
	Запрос.УстановитьПараметр("ДатаНачала", Объект.ДатаНачалаОказанияУслуг);
	Запрос.УстановитьПараметр("ДатаОкончания", Объект.ДатаОкончанияОказанияУслуг);
	Запрос.УстановитьПараметр("ПозицияНомер", Объект.ПозицияНомер);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	если НЕ РезультатЗапроса.Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Отказ = Истина;
		
		ВыборкаДетальныеЗаписи.Следующий();	
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Данная позиция занята:") + " " + ВыборкаДетальныеЗаписи.Клиент;
		Сообщение.Поле  = "ПозицияНомер";
		Сообщение.УстановитьДанные(Объект);
		Сообщение.Сообщить();
		
	КонецЕсли;
	
	Возврат Отказ;
	
КонецФункции

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если не отказ
		И ВестиУчетОказанияУслугЗаВремяСОпределеннымиПозициямиНомерами Тогда
		
		Если НЕ ЗначениеЗаполнено(Объект.ПозицияНомер)Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Укажите Позицию (номер)!");
			Сообщение.Поле  = "ПозицияНомер";
			Сообщение.УстановитьДанные(Объект);
			Сообщение.Сообщить();
			
			Отказ = Истина;
			
		Иначе
			Отказ = ПроверитьЧтоПозицияСвободна();
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.УслугаОказана 
		и (НЕ ЗначениеЗаполнено(Объект.ДатаОкончанияОказанияУслуг)
		ИЛИ не ЗначениеЗаполнено(объект.ДатаНачалаОказанияУслуг)) Тогда
		
		Объект.УслугаОказана = Ложь;
	КонецЕсли;	
	
	Если Объект.УслугаОказана 
		и ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение тогда
		
		МинимальныйОбъемОказанияУслуг = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("МинимальныйОбъемОказанияУслуг") ;
		Если Объект.ТовараВКоличестве < МинимальныйОбъемОказанияУслуг Тогда
			
			сообщить(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Объем услуг ниже минимального") + " - " + СокрЛП(МинимальныйОбъемОказанияУслуг));
			Отказ = Истина;
		КонецЕсли;
		
		Сумма = ОбщийМодульСервер.ПоКурсу(Объект.ТовараНаСумму, , ВалютаЦены, Объект.Дата);
		МинимальнаяСуммаОказанияУслуг = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("МинимальнаяСуммаОказанияУслуг") ;
		Если Сумма < МинимальнаяСуммаОказанияУслуг Тогда
			
			сообщить(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Сумма услуг ниже минимальной") + " - " + СокрЛП(МинимальнаяСуммаОказанияУслуг));
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПозицияНомерПриИзменении(Элемент)
	ПозицияНомерПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПозицияНомерПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(объект.ПозицияНомер)
		и ЗначениеЗаполнено(Объект.ПозицияНомер.ВидЦен) Тогда
		
		Объект.ВидЦен = Объект.ПозицияНомер.ВидЦен;
		
		Если ЗначениеЗаполнено(объект.ВидЦен) Тогда
			ВалютаЦены = ОбщийМодульСервер.получитьЗначениеРеквизита(объект.ВидЦен, "ВалютаЦены");
		Иначе
			ВалютаЦены = ПредопределенноеЗначение("Справочник.Валюты.ОсновнаяВалюта");
		КонецЕсли;
		
		Объект.цена = ОбщийМодульСервер.ПолучитьЦенуНаСервере(Объект.Номенклатура, Объект.ВидЦен, Объект.Дата, Истина, Объект.ТовараВКоличестве, Объект.Договор, , Объект.Цена, Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	глЧислоОбъектов = глЧислоОбъектов + 1; глПроверятьСообщения = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Источник = "ПодключаемоеОборудование"
		И ВводДоступен() Тогда
		
		Если ИмяСобытия = "ScanData" Тогда
			Если Параметр[ 1 ] = Неопределено Тогда
				ТекКод = Параметр[ 0 ];
			Иначе
				ТекКод = Параметр[ 1 ][ 1 ];
			КонецЕсли;
			
			ОбработатьПолученныйШКНаКлиенте(ТекКод);
			
		ИначеЕсли ИмяСобытия = "TracksData" Тогда
			ПолученКодИзСМК(Параметр);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПолученныйШКНаклиенте(ТекКод)
	
	если не ОбработатьПолученныйШКНаСервере(ТекКод) тогда
		ОбщийМодульКлиент.ВыдатьСигнал(ТекКод);
	конецесли;
	
КонецПроцедуры

&НаСервере
Функция   ОбработатьПолученныйШКНаСервере(Знач ТекКод, Количество = 1)
	
	Результат = Истина;
	
	Если не ДокументЗаблокирован тогда
		РезультатОбработки = ОбщийМодульТоварСервер.ПолучитьНоменклатуруПоШтрихКоду(ТекКод, Ложь, , Объект.Дата);
		
		Если ЗначениеЗаполнено(РезультатОбработки) Тогда
			Если ТипЗнч(РезультатОбработки) = Тип("СправочникСсылка.Номенклатура") Тогда
				Объект.Номенклатура = РезультатОбработки;
			Иначе
				Объект.Номенклатура = РезультатОбработки.Номенклатура;
			КонецЕсли;
			
		Иначе		
			ОстановитьПоиск = Ложь;
			
			Если ВестиУчетОказанияУслугЗаВремяСОпределеннымиПозициямиНомерами Тогда
				РезультатОбработки = ОбщийМодульСервер.ПолучитьКонтрагентаПоШтрихКоду(ТекКод, , , , Истина);
				если ЗначениеЗаполнено(РезультатОбработки) Тогда
					объект.ПозицияНомер = РезультатОбработки;
					ПозицияНомерПриИзмененииНаСервере();
					ОстановитьПоиск = Истина;
				КонецЕсли;	
			КонецЕсли;
			
			Если НЕ ОстановитьПоиск
				И ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетПродажПоСотрудникам") тогда
				
				РезультатОбработки = ОбщийМодульСервер.ПолучитьСотрудникаПоШтрихКоду(ТекКод);
				если ЗначениеЗаполнено(РезультатОбработки) Тогда
					объект.Сотрудник = РезультатОбработки;
					ОстановитьПоиск = Истина;
				КонецЕсли;	
			КонецЕсли;
			
			Если НЕ ОстановитьПоиск
				И ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ИспользоватьШтрихКодыДляИдентификацииКонтрагентов") Тогда
				
				РезультатОбработки = ОбщийМодульСервер.ПолучитьКонтрагентаПоШтрихКоду(ТекКод, Истина);				
				Если ЗначениеЗаполнено(РезультатОбработки) Тогда
					Объект.Клиент = РезультатОбработки;					
					КлиентПриИзмененииНаСервере();
					ОстановитьПоиск = Истина;
				КонецЕсли;		
			КонецЕсли;
			
			Если НЕ ОстановитьПоиск
				И ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетСобственныхЮридическихЛиц") Тогда
				
				РезультатОбработки = ОбщийМодульСервер.ПолучитьКонтрагентаПоШтрихКоду(ТекКод, , , Истина);				
				Если ЗначениеЗаполнено(РезультатОбработки) Тогда
					Объект.Организация = РезультатОбработки;
					ОстановитьПоиск = Истина;
				КонецЕсли;	
			КонецЕсли;
			
			Если НЕ ОстановитьПоиск Тогда
				ОбщегоНазначения.СообщитьПользователю(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Объект по штрих-коду не найден (") + ТекКод + ").");
				Результат = Ложь;	
			КонецЕсли;
		КонецЕсли; 	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПолученКодИзСМК(Параметр)
	
	Если Параметр[1][3] <> Неопределено Тогда
		МКод = Параметр[1][3][0].ДанныеДорожек[0].ЗначениеПоля;
	Иначе
		МКод = Параметр[0];
	КонецЕсли;
	ПолучитьКлиентаНаСервере(МКод);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьКлиентаНаСервере(МКод)
	
	если не ДокументЗаблокирован Тогда
		
		Клиент = ПодключаемоеОборудованиеДСервер.НайтиКлиентаПоМК(МКод);
		Если Клиент <> Неопределено Тогда
			Объект.Клиент = Клиент;
			
		Иначе
			ПустаяСсылка = Новый (Тип("СправочникСсылка.Клиенты"));
			Объект.Клиент = ПустаяСсылка;
			ОбщегоНазначения.СообщитьПользователю(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Клиент не найден."));
			
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЧастныйПокупатель(Команда)
	
	объект.Клиент = ПредопределенноеЗначение("Справочник.Клиенты.ФизическоеЛицо");
	ПриСменеКонтрагента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриСменеКонтрагента()
	
	КлиентПриИзмененииНаСервере();
	ПересчетКоличестваУслуги();
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЧека(Команда)
	
	Текст = ПечатьТоварногоЧекаНаСервере();
	
	Если ИспользоватьПодключаемоеОборудование
		И НЕ Текст = "" Тогда
		
		ОбщийМодульКлиент.ПечатьЧека(Текст, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция   ПечатьТоварногоЧекаНаСервере()
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат ОбщийМодульСервер.ПечатьТоварногоЧека(Объект.Ссылка, Ложь, Объект.Клиент, "", ?(ЗначениеЗаполнено(Объект.ХранилищеДенег), Объект.ХранилищеДенег.ФормаОплаты, Объект.ФормаОплаты), "");
	Иначе
		Возврат "";
	КонецЕсли;		
	
КонецФункции 

&НаКлиенте
Процедура ДополнительнаяИнформацияПриИзменении(Элемент)
	ДопИнформацияИзменена = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДополнительнаяИнформацияВидИнформацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура("ТипВладельца", ДопИнфоТипВладельца);
	ФормаВыбораВидаИнформации = ПолучитьФорму("Справочник.ДополнительныеРеквизиты.ФормаВыбора", ПараметрыФормы, ЭтаФорма);	
	ВидИнформации = ФормаВыбораВидаИнформации.ОткрытьМодально();
	Если ЗначениеЗаполнено(ВидИнформации) Тогда
		Элементы.ДополнительнаяИнформация.ТекущиеДанные.ВидИнформации = ВидИнформации;
		Элементы.ДополнительнаяИнформация.ТекущиеДанные.Информация = ОбщийМодульКлиент.ПолучитьЗначениеПоУмолчаниюПоляДополнительнойИнформации(ВидИнформации);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорПриИзменении(Элемент)
	ОбновитьДатуОтсрочки();
КонецПроцедуры

&НаСервере
Процедура ОбновитьДатуОтсрочки()
	
	Если ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ИспользоватьМеханизмОтсрочкиПлатежа") Тогда
		
		Если ЗначениеЗаполнено(Объект.Клиент)
			и не объект.Клиент.СрокОплатыВДняхПоУмолчанию = 0 Тогда
			
			Объект.ДатаОтсрочки = Объект.ДатаОкончанияОказанияУслуг + объект.Клиент.СрокОплатыВДняхПоУмолчанию * 3600 * 24;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Объект.Договор) Тогда
			
			Если Объект.Договор.УстанавливаетСрокОплатыВДнях = 0 Тогда
				Элементы.ДатаОтсрочки.Доступность = Истина;
			Иначе
				Объект.ДатаОтсрочки = Объект.ДатаОкончанияОказанияУслуг + Объект.Договор.УстанавливаетСрокОплатыВДнях * 3600 * 24;
				Элементы.ДатаОтсрочки.Доступность = Ложь;
			КонецЕсли;
			
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОрганизацияПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Организация) тогда
		если ОбщийМодульПовтор.ПолучитьПараметрСеанса("ВестиУчетОтдельныхДоговоровСКонтрагентамиПС") Тогда
			
			Если ЗначениеЗаполнено(Объект.Договор) Тогда
				Если не объект.Договор.Организация = Объект.Организация Тогда		
					объект.Договор = Справочники.Договора.ПустаяСсылка() ;	
				КонецЕсли;
				
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Объект.Договор)
				И ЗначениеЗаполнено(Объект.Клиент) Тогда
				
				Объект.Договор = ОбщийМодульСервер.ПодобратьДоговор(Объект.Организация, Объект.КлиентПоставщик, Объект.Дата, Объект.ВидЦен);
			КонецЕсли;	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
