//sza140613-1417 : 
//sza140612-1253 : 

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НеПроводить Тогда
		НеПроводить = Ложь;
	Иначе
		Если Не Отказ Тогда
			Движения.ВаловаяПрибыль.Записать();
			Движения.Деньги.Записать();
			Движения.Продажи.Записать();
			Движения.ПродажиСотрудников.Записать();
			Движения.Расчеты.Записать();
			Движения.РасчетыСОтсрочкой.Записать();
			Движения.Товары.Записать();
			движения.ТоварыПереданныеНаКомиссию.Записать();
			
			ВидДвиженияНакопленияПриход = ВидДвиженияНакопления.Приход;
			ВидДвиженияНакопленияРасход = ВидДвиженияНакопления.Расход;
			ТаблицаЦенСписания = Неопределено;
			ЗначениеЗаполненоКлиент = ЗначениеЗаполнено(клиент);
			
			Если ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетВалют")
				и ЗначениеЗаполнено(ВидЦен)
				и ЗначениеЗаполнено(ВидЦен.ВалютаЦены)
				и не ВидЦен.ВалютаЦены = ПредопределенноеЗначение("Справочник.Валюты.ОсновнаяВалюта") Тогда
				
				ВалютаЦены = ВидЦен.ВалютаЦены;
			Иначе
				ВалютаЦены = Неопределено;
			КонецЕсли;
			
			ИспользоватьМеханизмОтсрочкиПлатежа = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ИспользоватьМеханизмОтсрочкиПлатежа");
			ВестиУчетПродажСотрудников 			= ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетЗарплатыСотрудников");
			НЕЗначениеЗаполненоВалютаЦены 		= НЕ ЗначениеЗаполнено(ВалютаЦены);
			ЗачислятьТоварПереданныйКомиссионерамВКачествеПроданногоПриОтчетеКомиссионера = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ЗачислятьТоварПереданныйКомиссионерамВКачествеПроданногоПриОтчетеКомиссионера");
			
			ВестиУчетВаловойПрибыли = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетВаловойПрибыли");
			Если ВестиУчетВаловойПрибыли тогда 			
				СтруктураТаблиц    = ОбщийМодульСервер.ПолучитьТаблицыЦенСписанияИОстатков(Ссылка, Склад, ДатаВозврата, ложь, "ВозвратНепроданногоТовара");
				ТаблицаЦенСписания = СтруктураТаблиц.ТаблицаЦенСписания;			
			КонецЕсли;
			
			ВестиУчетДвиженияДенег = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетДвиженияДенег");
			Если ВестиУчетДвиженияДенег Тогда
				
				СтатьяДоходовОтРеализации = ПредопределенноеЗначение("Справочник.СтатьиДвиженияДенег.ДоходОтРеализации");
				
				Для Каждого ТекСтрокаДеньгиЗаТовар Из ДеньгиЗаТовар Цикл
					Движение = Движения.Деньги.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопленияПриход;
					Движение.Период 	 = ТекСтрокаДеньгиЗаТовар.ДатаОплаты;
					Движение.ФормаОплаты = ТекСтрокаДеньгиЗаТовар.ФормаОплаты;
					Движение.Валюта 	 = ТекСтрокаДеньгиЗаТовар.Валюта;
					Движение.Сумма 		 = ОбщийМодульСервер.ПоКурсу(ТекСтрокаДеньгиЗаТовар.Сумма, , ТекСтрокаДеньгиЗаТовар.Валюта, ТекСтрокаДеньгиЗаТовар.ДатаОплаты);
					Движение.СуммаВВалюте   = ТекСтрокаДеньгиЗаТовар.Сумма;
					Движение.ХранилищеДенег = ТекСтрокаДеньгиЗаТовар.ХранилищеДенег;
					Движение.Статья 	 = СтатьяДоходовОтРеализации;
				КонецЦикла;
				
				СтатьяОплатыКомиссионогоВознаграждения = ПредопределенноеЗначение("Справочник.СтатьиДвиженияДенег.ОплатаКомиссионногоВознаграждения");
				
				Для Каждого ТекСтрокаКомиссионноеВознаграждение Из КомиссионноеВознаграждение Цикл
					Движение = Движения.Деньги.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопленияРасход;
					Движение.Период 	 = ТекСтрокаКомиссионноеВознаграждение.ДатаОплаты;
					Движение.ФормаОплаты = ТекСтрокаКомиссионноеВознаграждение.ФормаОплаты;
					Движение.Валюта 	 = ТекСтрокаКомиссионноеВознаграждение.Валюта;
					Движение.Сумма  	 =  ОбщийМодульСервер.ПоКурсу(ТекСтрокаКомиссионноеВознаграждение.Сумма, , ТекСтрокаКомиссионноеВознаграждение.Валюта, ТекСтрокаКомиссионноеВознаграждение.ДатаОплаты);
					Движение.СуммаВВалюте   = ТекСтрокаКомиссионноеВознаграждение.Сумма;
					Движение.ХранилищеДенег = ТекСтрокаКомиссионноеВознаграждение.ХранилищеДенег;
					Движение.Статья		 = СтатьяОплатыКомиссионогоВознаграждения;
				КонецЦикла;
			КонецЕсли;	
			
			Для Каждого ТекСтрокаТовары Из Товары Цикл
				Движение = Движения.ТоварыПереданныеНаКомиссию.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопленияРасход;
				Движение.Период 	 	= Дата;
				Движение.Номенклатура 	= ТекСтрокаТовары.Номенклатура;
				Движение.СерияНоменклатуры = ТекСтрокаТовары.СерияНоменклатуры;
				Движение.Клиент 		= Клиент;
				Движение.Договор 		= Договор;
				Движение.Количество 	= ТекСтрокаТовары.Количество;
				Движение.Сумма 			= ТекСтрокаТовары.Сумма;
				Движение.ДатаОтчетаПоКомиссии = Дата;
				Движение.Комментарий 	= ТекСтрокаТовары.ОСтроке;
				
				Если ЗачислятьТоварПереданныйКомиссионерамВКачествеПроданногоПриОтчетеКомиссионера Тогда
					Движение = Движения.Продажи.Добавить();
					Движение.Период 		= Дата;
					Движение.Номенклатура 	= ТекСтрокаТовары.Номенклатура;
					Движение.Клиент 		= ТекСтрокаТовары.Клиент;
					Движение.Договор 		= ТекСтрокаТовары.Договор;
					Движение.Количество 	= ТекСтрокаТовары.Количество;
					Движение.Сумма 			= ТекСтрокаТовары.Сумма;
					Движение.Комментарий	= ТекСтрокаТовары.ОСтроке;	
				КонецЕсли;
				
			КонецЦикла;
			
			Если ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетПоКлиентам") тогда
				Для Каждого ТекСтрокаТовары Из Товары Цикл
					Движение = Движения.Расчеты.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопленияРасход;
					Движение.Период 	 = Дата;
					Движение.Клиент 	 = Клиент;
					Движение.Договор 	 = Договор;
					Движение.Сумма 		 = ТекСтрокаТовары.СуммаКомиссионногоВознаграждения;
					Движение.Комментарий = ТекСтрокаТовары.ОСтроке;
					
					Если ИспользоватьМеханизмОтсрочкиПлатежа Тогда
						Движение 			 = Движения.РасчетыСОтсрочкой.Добавить();
						Движение.ВидДвижения = ВидДвиженияНакопленияРасход;
						Движение.Период 	 = Дата;
						Движение.Клиент 	 = Клиент;
						Движение.Договор 	 = Договор;
						Движение.Сумма 		 = ТекСтрокаТовары.СуммаКомиссионногоВознаграждения;
						Движение.Комментарий = ТекСтрокаТовары.ОСтроке;
					КонецЕсли;
				КонецЦикла;
				
				Для Каждого ТекСтрокаДеньгиЗаТовар Из ДеньгиЗаТовар Цикл
					Движение = Движения.Расчеты.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопленияРасход;
					Движение.Период 	 = Дата;
					Движение.Клиент  	 = Клиент;
					Движение.Договор 	 = Договор;
					Движение.Сумма 		 = ТекСтрокаДеньгиЗаТовар.Сумма * ТекСтрокаДеньгиЗаТовар.Курс;
					Движение.Комментарий = ТекСтрокаДеньгиЗаТовар.ОСтроке;
					
					Если ИспользоватьМеханизмОтсрочкиПлатежа Тогда
						Движение = Движения.РасчетыСОтсрочкой.Добавить();
						Движение.ВидДвижения = ВидДвиженияНакопленияРасход;
						Движение.Период 	 = Дата;
						Движение.Клиент  	 = Клиент;
						Движение.Договор 	 = Договор;
						Движение.Сумма 		 = ТекСтрокаДеньгиЗаТовар.Сумма * ТекСтрокаДеньгиЗаТовар.Курс;
						Движение.Комментарий = ТекСтрокаДеньгиЗаТовар.ОСтроке;
					КонецЕсли;
				КонецЦикла;
				
				Для Каждого ТекСтрокаКомиссионноеВознаграждение Из КомиссионноеВознаграждение Цикл
					Движение = Движения.Расчеты.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопленияПриход;
					Движение.Период 	 = Дата;
					Движение.Клиент 	 = Клиент;
					Движение.Договор 	 = Договор;
					Движение.Сумма 		 = ТекСтрокаКомиссионноеВознаграждение.Сумма * ТекСтрокаКомиссионноеВознаграждение.Курс;
					Движение.Комментарий = ТекСтрокаКомиссионноеВознаграждение.ОСтроке;
					
					Если ИспользоватьМеханизмОтсрочкиПлатежа Тогда
						Движение = Движения.РасчетыСОтсрочкой.Добавить();
						Движение.ВидДвижения = ВидДвиженияНакопленияПриход;
						Движение.Период 	 = Дата;
						Движение.Клиент 	 = Клиент;
						Движение.Договор 	 = Договор;
						Движение.Сумма 		 = ТекСтрокаКомиссионноеВознаграждение.Сумма * ТекСтрокаКомиссионноеВознаграждение.Курс;
						Движение.Комментарий = ТекСтрокаКомиссионноеВознаграждение.ОСтроке;
					КонецЕсли;
				КонецЦикла;
				
				Для Каждого ТекСтрокаВозвратНепроданногоТовара Из ВозвратНепроданногоТовара Цикл
					Движение = Движения.Расчеты.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопленияРасход;
					Движение.Период 	 = ДатаВозврата;
					Движение.Клиент 	 = Клиент;
					Движение.Договор 	 = Договор;
					Движение.Сумма 		 = ТекСтрокаВозвратНепроданногоТовара.Сумма;
					Движение.Комментарий = ТекСтрокаВозвратНепроданногоТовара.ОСтроке;
					
					Если ИспользоватьМеханизмОтсрочкиПлатежа Тогда
						Движение = Движения.РасчетыСОтсрочкой.Добавить();
						Движение.ВидДвижения = ВидДвиженияНакопленияРасход;
						Движение.Период 	 = ДатаВозврата;
						Движение.Клиент 	 = Клиент;
						Движение.Договор 	 = Договор;
						Движение.Сумма 		 = ТекСтрокаВозвратНепроданногоТовара.Сумма;
						Движение.Комментарий = ТекСтрокаВозвратНепроданногоТовара.ОСтроке;
					КонецЕсли;
				КонецЦикла;	
			КонецЕсли;
			
			Для Каждого ТекСтрокаТовара Из ВозвратНепроданногоТовара Цикл
				
				Если НЕЗначениеЗаполненоВалютаЦены Тогда
					СуммаТов = ТекСтрокаТовара.Сумма;								
				Иначе
					СуммаТов = ОбщийМодульСервер.ПоКурсу(ТекСтрокаТовара.Сумма, , ВалютаЦены, ДатаВозврата);
				КонецЕсли;							
				
				Номенклатура = ТекСтрокаТовара.Номенклатура;
				КомментарийСтроки = ?(ЗначениеЗаполнено(ТекСтрокаТовара.ОСтроке), ТекСтрокаТовара.ОСтроке, Комментарий);
				Количество 		  = ?(ЗначениеЗаполнено(ТекСтрокаТовара.ЕдиницаИзмерения), ТекСтрокаТовара.ЕдиницаИзмерения.Количество * ТекСтрокаТовара.Количество, ТекСтрокаТовара.Количество);
				
				Если НЕ ОбщийМодульПовтор.ЭтоНабор(Номенклатура) Тогда
					
					ПредопределенныйТовар = ОбщийМодульПовтор.ТоварНеУчитываетсяПоКоличеству(Номенклатура);
					ЦенаСписания = 0;
					
					Если ВестиУчетВаловойПрибыли Тогда
						
						Если не ТаблицаЦенСписания = Неопределено тогда
							СтрокаЦеныСписания = ТаблицаЦенСписания.найти(номенклатура, "Номенклатура");
							
							Если не СтрокаЦеныСписания = Неопределено Тогда
								ЦенаСписания = СтрокаЦеныСписания.Цена;
							иначе
								ЦенаСписания = ОбщийМодульСервер.ПолучитьЦенуНаСервере(номенклатура, истина, ДатаВозврата, ложь, , , , ТекСтрокаТовара.Цена, Ссылка, , ТекСтрокаТовара.ЕдиницаИзмерения);
							КонецЕсли;	
							
						Иначе
							ЦенаСписания = ОбщийМодульСервер.ПолучитьЦенуНаСервере(номенклатура, истина, ДатаВозврата, ложь, , , , ТекСтрокаТовара.Цена, Ссылка, , ТекСтрокаТовара.ЕдиницаИзмерения);
						КонецЕсли;
						
						если ЦенаСписания = 0 
							или не (СуммаТов - (ЦенаСписания * Количество)) = 0 тогда
							
							Движение = Движения.ВаловаяПрибыль.Добавить();
							Движение.ВидДвижения  = ВидДвиженияНакопленияРасход;
							Движение.Период 	  = ДатаВозврата;
							Движение.Номенклатура = ТекСтрокаТовара.Номенклатура;
							Движение.Склад		  = Склад;
							Движение.Комментарий  = КомментарийСтроки;
							
							Если ЦенаСписания = 0 
								или ТекСтрокаТовара.Цена = 0
								или ПредопределенныйТовар Тогда
								
								Движение.Сумма 	= СуммаТов;
								Движение.ПроцентНаценки = 100;
								
							Иначе
								ЦенаСписания 	= СуммаТов - (ЦенаСписания * Количество);
								Движение.Сумма 	= ЦенаСписания;
								Цена = ОбщийМодульСервер.ПоКурсу(ТекСтрокаТовара.Цена, , ВалютаЦены, ДатаВозврата);
								Движение.ПроцентНаценки = (Цена / (ЦенаСписания / 100)) - 100 ;
								Движение.РентабельностьПродаж = (ЦенаСписания / ?(Цена = 0, 1, Цена)) * 100;
								
							КонецЕсли; 				
						КонецЕсли;
						
					КонецЕсли;
					
					Если НЕ ПредопределенныйТовар 
						и не Отказ Тогда
						
						Движение = Движения.Товары.Добавить();
						Движение.ВидДвижения  = ВидДвиженияНакопленияПриход;
						Движение.Период 	  = ДатаВозврата;
						Движение.Номенклатура = Номенклатура;
						Движение.Склад 		  = Склад;  				
						Движение.Количество   = Количество;					
						Движение.Сумма 		  = СуммаТов - ЦенаСписания;
						Движение.Комментарий  = КомментарийСтроки;
						Движение.СерияНоменклатуры = ТекСтрокаТовара.СерияНоменклатуры;
						Движение.СписаниеИлиОприходование = НЕ ЗначениеЗаполненоКлиент;
					КонецЕсли;
					
				Иначе //это набор         			
					ОбработатьДвиженияДляНабора(ТекСтрокаТовара.Номенклатура, Количество, СуммаТов, ВестиУчетВаловойПрибыли, Истина, ЗначениеЗаполненоКлиент, ВалютаЦены);
					
				КонецЕсли;
				
				Если ВестиУчетПродажСотрудников Тогда
					Движение = Движения.ПродажиСотрудников.Добавить();
					Движение.Комментарий  = КомментарийСтроки; 
					Движение.Номенклатура = Номенклатура;
					Движение.Период 	  = ДатаВозврата;
					Движение.Сотрудник    = Сотрудник;
					Движение.Сумма  	  = -СуммаТов;
				КонецЕсли;
				
				Движение = Движения.Продажи.Добавить();
				Движение.Период 		= ДатаВозврата;
				Движение.Номенклатура 	= Номенклатура;
				Движение.Клиент 		= Клиент;
				Движение.Договор 		= Договор;
				Движение.Количество 	= -Количество;
				Движение.Сумма 			= -СуммаТов;
				Движение.Комментарий	= КомментарийСтроки;
				
				Движение = Движения.ТоварыПереданныеНаКомиссию.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопленияРасход;
				Движение.Период 	 	= ДатаВозврата;
				Движение.Номенклатура 	= Номенклатура;
				Движение.СерияНоменклатуры = ТекСтрокаТовара.СерияНоменклатуры;
				Движение.Клиент 		= Клиент;
				Движение.Договор 		= Договор;
				Движение.Количество 	= Количество;
				Движение.Сумма 			= ТекСтрокаТовара.Сумма;
				Движение.ДатаОтчетаПоКомиссии = Дата;
				Движение.Комментарий 	= КомментарийСтроки;
			КонецЦикла;
			
		КонецЕсли;
		
		Движения.ВаловаяПрибыль.Записывать 	= НЕ Отказ;
		Движения.Деньги.Записывать 			= НЕ Отказ;
		Движения.Продажи.Записывать 		= НЕ Отказ;
		Движения.ПродажиСотрудников.Записывать = НЕ Отказ;
		Движения.Расчеты.Записывать 		= НЕ Отказ;
		Движения.РасчетыСОтсрочкой.Записывать = НЕ Отказ;
		Движения.Товары.Записывать 			= НЕ Отказ;
		движения.ТоварыПереданныеНаКомиссию.Записывать = НЕ Отказ;	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодготовкаКПроведению(Отказ) Экспорт
	
	Если Не Отказ ТОгда
		Если ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетДвиженияДенег")
			И не ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетДенегВНесколькихХранилищах")
			И ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетВалют") Тогда
			
			Для Каждого СтрокаДенег Из ДеньгиЗаТовар Цикл
				Если Не СтрокаДенег.Валюта = ПредопределенноеЗначение("Справочник.Валюты.ОсновнаяВалюта") Тогда
					СтрокаДенег.ХранилищеДенег = ОбщийМодульПовтор.ПолучитьАвтоХранилищеДенегПоВалюте(СтрокаДенег.Валюта, СтрокаДенег.ХранилищеДенег);
				КонецЕсли;	
			КонецЦикла;	
			Для Каждого СтрокаДенег Из КомиссионноеВознаграждение Цикл
				Если Не СтрокаДенег.Валюта = ПредопределенноеЗначение("Справочник.Валюты.ОсновнаяВалюта") Тогда
					СтрокаДенег.ХранилищеДенег = ОбщийМодульПовтор.ПолучитьАвтоХранилищеДенегПоВалюте(СтрокаДенег.Валюта, СтрокаДенег.ХранилищеДенег);
				КонецЕсли;	
			КонецЦикла;	
		КонецЕсли;
		
		массивпустыхстрок  = новый массив;
		массивноменклатуры = Новый массив;
		
		для каждого СтрокаТовары из Товары цикл
			
			Если НЕ ЗначениеЗаполнено(СтрокаТовары.Номенклатура) Тогда
				массивпустыхстрок.Добавить(СтрокаТовары);
				
			иначеесли не массивноменклатуры.Найти(СтрокаТовары.Номенклатура) = Неопределено тогда
				
				если ОбщийМодульПовторВТеченииСервера.ВыводитьСообщенияМожно() тогда
					ОбщийМодульСервисСервер.ДобавитьСообщениеПользователю(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("В строке №") + " " + СтрокаТовары.НомерСтроки + " " + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("повторяется материал:") + " " + СтрокаТовары.Номенклатура, , Ссылка);
				КонецЕсли;
				
			иначе
				массивноменклатуры.Добавить(СтрокаТовары.Номенклатура);
				
			КонецЕсли;                                    	
		КонецЦикла;
		
		для каждого СтрокаТовары из массивпустыхстрок цикл
			Товары.Удалить(СтрокаТовары);	
		КонецЦикла;	
		
		массивпустыхстрок  = новый массив;
		массивноменклатуры = Новый массив;
		
		для каждого СтрокаТовары из ВозвратНепроданногоТовара цикл
			
			Если НЕ ЗначениеЗаполнено(СтрокаТовары.Номенклатура) Тогда
				массивпустыхстрок.Добавить(СтрокаТовары);
				
			иначеесли не массивноменклатуры.Найти(СтрокаТовары.Номенклатура) = Неопределено тогда
				
				если ОбщийМодульПовторВТеченииСервера.ВыводитьСообщенияМожно() тогда
					ОбщийМодульСервисСервер.ДобавитьСообщениеПользователю(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("В строке №") + " " + СтрокаТовары.НомерСтроки + " " + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("повторяется работа:") + " " + СтрокаТовары.Номенклатура, , Ссылка);
				КонецЕсли;
				
			иначе
				массивноменклатуры.Добавить(СтрокаТовары.Номенклатура);
				
			КонецЕсли;                                    	
		КонецЦикла;
		
		для каждого СтрокаТовары из массивпустыхстрок цикл
			ВозвратНепроданногоТовара.Удалить(СтрокаТовары);	
		КонецЦикла;	
		
		ОбщийМодульТоварСервер.ПроверитьИСортироватьТаблицуТовары(ЭтотОбъект);
		ОбщийМодульТоварСервер.ПроверитьИСортироватьТаблицуТовары(ЭтотОбъект, , "ВозвратНепроданногоТовара");
		
		ТовараВКоличестве = товары.итог("Количество");
		ТовараНаСумму 	  = товары.Итог("Сумма");
		
		Если НЕ ЗначениеЗаполнено(ДатаВозврата) Тогда
			ДатаВозврата = дата;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Комментарий = "";
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РасходыТовара") Тогда
		
		ВидЦен 	= ДанныеЗаполнения.ВидЦен;
		Договор = ДанныеЗаполнения.Договор;
		Клиент 	= ДанныеЗаполнения.КлиентПоставщик;
		Склад 	= ДанныеЗаполнения.Склад;
		Сотрудник = ДанныеЗаполнения.Сотрудник;
		Для Каждого ТекСтрокаТовары Из ДанныеЗаполнения.Товары Цикл
			НоваяСтрока = Товары.Добавить();
			НоваяСтрока.ЕдиницаИзмерения = ТекСтрокаТовары.ЕдиницаИзмерения;
			НоваяСтрока.Количество 		 = ТекСтрокаТовары.Количество;
			НоваяСтрока.Номенклатура 	 = ТекСтрокаТовары.Номенклатура;
			НоваяСтрока.ОСтроке 			= ТекСтрокаТовары.ОСтроке;
			НоваяСтрока.СерияНоменклатуры 	= ТекСтрокаТовары.СерияНоменклатуры;
			НоваяСтрока.Цена 	= ТекСтрокаТовары.Цена;
			НоваяСтрока.Сумма 	= НоваяСтрока.Цена * НоваяСтрока.Количество;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьДвиженияДляНабора(Номенклатура, КоличествоВх, Сумма, ВестиУчетВаловойПрибыли, Этоклиент, ЗначениеЗаполненоКлиент, ВалютаЦены) //для вложенных наборов
	
	общкво = Номенклатура.Состав.Итог("Количество");
	Если общкво = 0 Тогда
		общкво = 1;
	КонецЕсли;
	
	для каждого СтрокаСоставаНабора из Номенклатура.Состав цикл
		
		СуммаЭлемента = (Сумма / общкво) * СтрокаСоставаНабора.количество;
		НоменклатураСостава = СтрокаСоставаНабора.Номенклатура;
		
		Если ОбщийМодульПовтор.ЭтоНабор(СтрокаСоставаНабора.Номенклатура) Тогда
			обработатьДвижениядлянабора(НоменклатураСостава, КоличествоВх * СтрокаСоставаНабора.количество, СуммаЭлемента, ВестиУчетВаловойПрибыли, Этоклиент, ЗначениеЗаполненоКлиент, ВалютаЦены);
		иначе
			
			Количество = КоличествоВх * ?(ЗначениеЗаполнено(СтрокаСоставаНабора.ЕдиницаИзмерения), СтрокаСоставаНабора.ЕдиницаИзмерения.Количество, 1) * СтрокаСоставаНабора.Количество;
			Если ВалютаЦены = Неопределено Тогда
				СуммаТов = СуммаЭлемента;
			Иначе
				СуммаТов = ОбщийМодульСервер.ПоКурсу(СуммаЭлемента, , ВалютаЦены, Дата);
			КонецЕсли;
			
			ПредопределенныйТовар = ОбщийМодульПовтор.ТоварНеУчитываетсяПоКоличеству(НоменклатураСостава);
			
			Если НЕ ПредопределенныйТовар Тогда
				Движение = Движения.Товары.Добавить();
				Движение.ВидДвижения 	= ВидДвиженияНакопления.Приход;
				Движение.Период 		= ДатаВозврата;
				Движение.Номенклатура 	= НоменклатураСостава;
				Движение.Склад 			= Склад;
				Движение.Количество 	= Количество;				
				Движение.Сумма 			= СуммаТов;
				//Движение.СерияНоменклатуры = //серийный состав набора насильно разворачивается в таблице
				Движение.Комментарий	= Комментарий;
				Движение.СписаниеИлиОприходование = НЕ ЗначениеЗаполненоКлиент;
			КонецЕсли;
			
			Если ВестиУчетВаловойПрибыли Тогда  				
				если Этоклиент тогда					
					
					ЦенаСписания = ОбщийМодульСервер.ПолучитьЦенуСписания(НоменклатураСостава, Количество, ДатаВозврата);
					
					если ЦенаСписания = 0 
						или не (СуммаТов - (ЦенаСписания * Количество)) = 0 тогда
						
						Движение = Движения.ВаловаяПрибыль.Добавить();
						Движение.ВидДвижения 	= ВидДвиженияНакопления.Расход;
						Движение.Период 		= ДатаВозврата;
						Движение.Номенклатура 	= НоменклатураСостава;
						Движение.Комментарий 	= Комментарий;
						Движение.Склад			= Склад;
						
						Если ЦенаСписания = 0 
							или ПредопределенныйТовар
							или Количество = 0 Тогда
							
							Движение.Сумма = СуммаТов;
							Движение.ПроцентНаценки = 100;
							
						Иначе
							Движение.Сумма 	= СуммаТов - (ЦенаСписания * Количество);						
							Цена 			= СуммаТов / Количество;
							//Движение.ПроцентНаценки = (Цена / (ЦенаСписания / 100)) -100;
							Если НЕ ВалютаЦены = Неопределено Тогда
								Цена = ОбщийМодульСервер.ПоКурсу(Цена, , ВалютаЦены, Дата);
							КонецЕсли;
							Движение.ПроцентНаценки = (Цена / (ЦенаСписания / 100)) - 100 ;
							Движение.РентабельностьПродаж = (ЦенаСписания / ?(Цена = 0, 1, Цена)) * 100;
							
						КонецЕсли; 				
					КонецЕсли;
					
				КонецЕсли;	
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры //ОбработатьНабор

