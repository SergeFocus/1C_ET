//sza140605-0124 SZA: 
//sza140125-0334 SZA: 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отказ = ОбщийМодульСервисСервер.ПроверитьОтказДоступа("000690", ЭтаФорма, Отказ, Объект);
	
	Если НЕ Отказ Тогда
		ОбъектСсылка = Объект.Ссылка;
		
		ДопИнфоТипВладельца = ОбщийМодульПовтор.ПолучитьТипВладельца(ОбъектСсылка); 	
		ОбщийМодульСервисСервер.ЗаполнитьДополнительнуюИнформацию(ОбъектСсылка, ДополнительнаяИнформация, ДопИнфоТипВладельца);
		
		УчетПоСериям 		   = ОбщийМодульПовтор.ПолучитьПараметрСеанса("ВестиУчетПоСериямНоменклатуры");
		ИспользоватьСкидки 	   = ОбщийМодульПовтор.ПолучитьПараметрСеанса("ИспользоватьСкидкиПС");	
		ОсновнаяВалюта 		   = ПредопределенноеЗначение("Справочник.Валюты.ОсновнаяВалюта");
		ВестиУчетДвиженияДенег = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетДвиженияДенег");
		ВестиУчетПоСкладу 	   = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетПоСкладам");
		ВестиУчетДенегВНесколькихХранилищах  = ВестиУчетДвиженияДенег И ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетДенегВНесколькихХранилищах");
		ОтключитьПланированиеВДокументеВыполненияРабот = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ОтключитьПланированиеВДокументеВыполненияРабот");
		ДобавитьФункцииПриемаИВыдачиОбъектаРаботДокументуВыполненияРабот = Элементы.ДвижениеОбъектаРабот.Видимость = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ДобавитьФункцииПриемаИВыдачиОбъектаРаботДокументуВыполненияРабот");
		ИспользоватьСложныйМеханизмЦен 		 = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ИспользоватьСложныйМеханизмЦен");
		ИспользоватьПодключаемоеОборудование = ПодключаемоеОборудованиеДСервер.ИспользоватьПодключаемоеОборудование();
		
		Элементы.ПечатьГарантийногоТалона.Видимость = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ПоказыватьКнопкуПечатиГарантийногоТаблонаВРасходномДокументе");
		Элементы.ПечатьЧека.Видимость 				= ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ПечататьЧек");
		Элементы.ГруппаДополнительнаяИнформация.Видимость = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиДополнительнуюИнформациюДляВыполненияРабот");
		
		элементы.ОплатыФормаОплаты.Видимость  = не ВестиУчетДенегВНесколькихХранилищах;
		элементы.РасходыФормаОплаты.Видимость = не ВестиУчетДенегВНесколькихХранилищах;
		
		Если НЕ ЗначениеЗаполнено(ОбъектСсылка) Тогда
			
			Если ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ИспользоватьМеханизмОтсрочкиПлатежа")
				И не ЗначениеЗаполнено(Объект.ДнейОтсрочки) Тогда
				
				Объект.ДнейОтсрочки = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ЧислоДнейОтсрочкиКлиентамПоУмолчанию");
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Объект.ВидЦен) Тогда
				Объект.ВидЦен 		= ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВидЦенРасходованияПоУмолчанию");
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Объект.Клиент) Тогда
				Объект.Клиент 		= ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("КлиентПоУмолчанию");	
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
				Объект.Организация 	= ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ОрганизацияПоУмолчанию");	
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Объект.Валюта) Тогда
				Объект.Валюта		= ОсновнаяВалюта;	
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Объект.ХранилищеДенег) Тогда
				Объект.ХранилищеДенег = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ОсновноеХранилищеДенег");	
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Объект.СпособДоставки) Тогда
				Объект.СпособДоставки = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("СпособДоставкиПоУмолчанию");
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(объект.ХранилищеДенег) Тогда
			ВалютаХранилища = объект.ХранилищеДенег.Валюта;
		Иначе
			ВалютаХранилища = Объект.Валюта;
		КонецЕсли;
		
		Если (ОтключитьПланированиеВДокументеВыполненияРабот
			ИЛИ параметры.БезПлана)
			И НЕ объект.БезПлана Тогда
			
			объект.БезПлана = Истина;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Объект.ДатаПлан) Тогда
			Объект.ДатаПлан = ОбщийМодульСервисСервер.ПользователяТекущаяДата() + 24 * 3600;
		КонецЕсли;
		
		СменаБезПланаНаСервере();
		
		Элементы.БезПлана.Видимость = НЕ ОтключитьПланированиеВДокументеВыполненияРабот;
		
		Элементы.ОсновнаяВалюта1.Видимость = ВестиУчетДвиженияДенег;
		Элементы.ОсновнаяВалюта2.Видимость = ВестиУчетДвиженияДенег;
		
		ОбщийМодульСервисСервер.ОформитьФорматКоличества(Элементы.РаботыКоличество);
		ОбщийМодульСервисСервер.ОформитьФорматКоличества(Элементы.РаботыКоличествоПлан);
		
		Если ОбщийМодульПовтор.ПолучитьПараметрСеанса("НеМожетМенятьЦены") тогда
			если ЗначениеЗаполнено(объект.ВидЦен) Тогда
				элементы.ВидЦен.Доступность = Ложь;	
			КонецЕсли;
			элементы.РаботыЦенаПлан.Доступность = Ложь;
			элементы.РаботыЦена.Доступность 	= Ложь;
		КонецЕсли;
		
		ОбновитьВидимостьНаСервере();	
		
		Если ВестиУчетДвиженияДенег ТОгда
			СтруктураОтказа = ОбщийМодульСервисСервер.ПроверитьОтказДоступа("000892", , , , Истина);
			Если СтруктураОтказа.УровеньДоступа = Перечисления.УровниДоступа.ТолькоЧтение Тогда
				Элементы.Оплаты.ТолькоПросмотр = Истина;
				
			ИначеЕсли СтруктураОтказа.Отказ Тогда
				Элементы.Оплаты.Видимость = Ложь;
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьНаСервере()
	
	ОбъектАктуален = Объект.Актуален;
	Элементы.СметаУтвержденаПриступитьКВыполнению.Видимость = НЕ ОбъектАктуален;
	Элементы.Результат.Видимость = ОбъектАктуален;
	Элементы.РезультатРазница.Видимость = ОбъектАктуален;
	Если НЕ ОбъектАктуален Тогда
		Элементы.ГруппаРаботы.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Выполняемые работы");
	КонецЕсли;
	
	Элементы.Дата.Видимость = ОбъектАктуален;
	Элементы.ПечатьСметы.Видимость = не объект.БезПлана и не ОтключитьПланированиеВДокументеВыполненияРабот;
	
	Элементы.РаботыДата.Видимость = ОбъектАктуален;
	Элементы.РаботыСотрудник.Видимость = ОбъектАктуален;
	СерииНоменклатурыВидны = ОбщийМодульТоварСервер.ПроверитьНаличиеСерийНоменклатуры(Объект.Ссылка, "Товары");
	Элементы.РаботыСерияНоменклатуры.Видимость = ОбъектАктуален И СерииНоменклатурыВидны;
	
	Элементы.РаботыПодтвердитьВыполнениеПланаДляВыделенныхСтрок.Видимость = не объект.БезПлана и не ОтключитьПланированиеВДокументеВыполненияРабот И ОбъектАктуален;
	Элементы.РаботыСумма.Видимость = ОбъектАктуален;
	Элементы.РасходыФакт.Видимость = ОбъектАктуален;
	Элементы.ОплатыДата.Видимость  = ОбъектАктуален;
	Элементы.ОплатыСумма.Видимость = ОбъектАктуален;
	Элементы.ОплатыФакт.Видимость  = ОбъектАктуален;
	Элементы.РаботыКоличество.Видимость = ОбъектАктуален;
	Элементы.РаботыЦена.Видимость 		= ОбъектАктуален;
	Элементы.РаботыВалюта.Видимость 	= ОбъектАктуален;
	Элементы.РаботыКурс.Видимость 		= ОбъектАктуален;
	Элементы.ОплатыВалюта.Видимость 	= ОбъектАктуален;
	Элементы.ОплатыКурс.Видимость 		= ОбъектАктуален;
	Элементы.РасходыВалюта.Видимость 	= ОбъектАктуален;
	Элементы.РасходыКурс.Видимость 		= ОбъектАктуален;
	Элементы.РасходыДата.Видимость 		= ОбъектАктуален;
	Элементы.РасходыСотрудник.Видимость = ОбъектАктуален;
	Элементы.РасходыСумма.Видимость     = ОбъектАктуален;
	Элементы.РаботыИтогСумма.Видимость  = ОбъектАктуален;
	Элементы.ВалютаХранилища.Видимость  = ВестиУчетДенегВНесколькихХранилищах;
	Элементы.РаботыПроцентСкидки.Видимость  = ОбъектАктуален;
	Элементы.РаботыСуммаБезСкидки.Видимость = ОбъектАктуален;
	Элементы.ДвижениеОбъектаРабот.Видимость = ОбъектАктуален И ДобавитьФункцииПриемаИВыдачиОбъектаРаботДокументуВыполненияРабот;
	
КонецПроцедуры

&НаКлиенте
Процедура ЧастныйПокупатель(Команда)
	
	объект.Клиент = ПредопределенноеЗначение("Справочник.Клиенты.ФизическоеЛицо");
	ПриИзмененииКонтрагента();	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 0, Объект.Ссылка, истина);
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОбработатьБлокировку(истина);		
	КонецЕсли;
	
	Если ИспользоватьПодключаемоеОборудование И 
		МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда
		
		ОписаниеОшибки = "" ;
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("СчитывательМагнитныхКарт");
		ПоддерживаемыеТипыВО.Добавить("СканерШтрихКода");
		
		Если Не МенеджерОборудованияКлиент.ПодключитьОборудованиеПоТипу(УникальныйИдентификатор, ПоддерживаемыеТипыВО, ОписаниеОшибки) Тогда
			ТекстСообщения = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке( "При подключении оборудования произошла ошибка") + ": " + ОписаниеОшибки + ".";
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтраницаОсновноеДополнительно)Тогда
		
		попытка
			СтраницыПанели  = Элементы.Страницы.ПодчиненныеЭлементы;
			ТекущаяСтраница = СтраницыПанели.получить(СтраницаОсновноеДополнительно);
			Элементы.Страницы.ТекущаяСтраница = ТекущаяСтраница;
		Исключение
		конецпопытки;				
	КонецЕсли;
	
	ОбновитьТекущийОстаток();	
	ПересчетИтоговРасходовНаСервере();//не наоборот
	ПересчетИтоговОплаты();
	ОбновитьВидимостьСерииОбъекта();	
	ОбновитьДнейХранилось();
	
КонецПроцедуры

Процедура ОбновитьТекущийОстаток()
	//остаток долга контра например
КонецПроцедуры

&НаСервере
Процедура ОбработатьБлокировку(ПриСозданииФормы = ложь)
	
	Если ОбщийМодульСервер.ОбработатьБлокировку(Объект, ЭтаФорма, ПриСозданииФормы) Тогда
		Элементы.РаботыДобавить.Доступность  = Ложь;
		Элементы.ОплатыДобавить.Доступность  = Ложь;
		Элементы.РасходыДобавить.Доступность = Ложь;
		Элементы.ВзятьДатуНачалаРабот.Доступность = Ложь;
		Элементы.СметаУтвержденаПриступитьКВыполнению.Доступность = Ложь;
		Элементы.РаботыПодтвердитьВыполнениеПланаДляВыделенныхСтрок.Доступность = Ложь;
		Элементы.РаботыОбновитьРезультаты.Доступность = Ложь;
	Иначе
		Элементы.РаботыДобавить.Доступность  = Истина;
		Элементы.ОплатыДобавить.Доступность  = Истина;
		Элементы.РасходыДобавить.Доступность = Истина;
		Элементы.ВзятьДатуНачалаРабот.Доступность = Истина;
		Элементы.СметаУтвержденаПриступитьКВыполнению.Доступность = Истина;
		Элементы.РаботыПодтвердитьВыполнениеПланаДляВыделенныхСтрок.Доступность = Истина;
		Элементы.РаботыОбновитьРезультаты.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 1, Объект.Ссылка, Истина);
	
	Если ИспользоватьПодключаемоеОборудование Тогда
		ПоддерживаемыеТипыВО = Новый Массив ();
		ПоддерживаемыеТипыВО.Добавить("СчитывательМагнитныхКарт");
		ПоддерживаемыеТипыВО.Добавить("СканерШтрихКода");
		ПоддерживаемыеТипыВО.Добавить("ДисплейПокупателя");
		МенеджерОборудованияКлиент.ОтключитьОборудованиеПоТипу(УникальныйИдентификатор, ПоддерживаемыеТипыВО);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОрганизацияПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Организация) тогда
		Если ОбщийМодульПовтор.ПолучитьПараметрСеанса("ВестиУчетОтдельныхДоговоровСКонтрагентамиПС") Тогда
			
			Если ЗначениеЗаполнено(Объект.Договор) Тогда
				Если не объект.Договор.Организация = Объект.Организация Тогда		
					объект.Договор = ПредопределенноеЗначение("Справочник.Договора.ПустаяСсылка") ;	
				КонецЕсли;
				
			Иначеесли ЗначениеЗаполнено(Объект.КлиентПоставщик) Тогда
				Объект.Договор = ОбщийМодульСервер.ПодобратьДоговор(Объект.Организация, Объект.Клиент, Объект.Дата, Объект.ВидЦен);
			КонецЕсли;
			
		КонецЕсли;
		
		Если ВестиУчетДенегВНесколькихХранилищах
			И НЕ ЗначениеЗаполнено(Объект.ХранилищеДенег) 
			и ЗначениеЗаполнено(Объект.Организация.ХранилищеДенег) Тогда
			
			Объект.ХранилищеДенег = Объект.Организация.ХранилищеДенег;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорПриИзменении(Элемент)
	ДоговорПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ДоговорПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Договор) Тогда //насильно
		
		если не Объект.Клиент = Объект.Договор.КлиентПоставщик тогда
			Объект.Клиент = Объект.Договор.КлиентПоставщик;             	
			ПриИзмененииКонтрагента();
		КонецЕсли;
		
		если не Объект.Организация 	= Объект.Договор.Организация тогда
			Объект.Организация = Объект.Договор.Организация;         	
		КонецЕсли;		
		
		Если ВестиУчетДенегВНесколькихХранилищах
			И НЕ ЗначениеЗаполнено(Объект.ХранилищеДенег) 
			и ЗначениеЗаполнено(Объект.Договор.ХранилищеДенег) Тогда
			
			Объект.ХранилищеДенег = Объект.Договор.ХранилищеДенег;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбновитьДатуОтсрочки();
	
КонецПроцедуры

&НаКлиенте
Процедура КлиентПриИзменении(Элемент)	
	ПриИзмененииКонтрагента();	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииКонтрагента()
	
	Если  ЗначениеЗаполнено(Объект.Клиент) тогда
		
		Если ОбщийМодульПовтор.ПолучитьПараметрСеанса("ВестиУчетОтдельныхДоговоровСКонтрагентамиПС")
			И (НЕ ЗначениеЗаполнено(Объект.Договор) 
			ИЛИ не объект.Договор.КлиентПоставщик = Объект.Клиент) Тогда
			
			Объект.Договор = ОбщийМодульСервер.ПодобратьДоговор(Объект.Организация, Объект.КлиентПоставщик, Объект.Дата);
		КонецЕсли;		
		
		Если ВестиУчетДенегВНесколькихХранилищах
			И НЕ ЗначениеЗаполнено(Объект.ХранилищеДенег) 
			и ЗначениеЗаполнено(Объект.Клиент.ХранилищеДенег) Тогда
			
			Объект.ХранилищеДенег = Объект.Клиент.ХранилищеДенег;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Объект.Клиент.ВидЦен) Тогда
			Объект.ВидЦен = Объект.Клиент.ВидЦен;
		КонецЕсли;
		
		Если ИспользоватьСкидки Тогда
			Для Каждого СтрокаТовара из Объект.Товары Цикл
				СтрокаТовара.ПроцентСкидки = ОбщийМодульСервер.ПолучитьПроцентСкидкиНаСервере(СтрокаТовара.Номенклатура, СтрокаТовара.Количество, объект.ВидЦен, объект.Клиент);
				
				пересчитатьСтрокуРаботНаСервере(, , СтрокаТовара, НЕ Объект.Актуален);
				ПересчитатьРезультатыНаСервере();
			КонецЦикла;		
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Объект.Клиент.СпособДоставки) Тогда
			Объект.СпособДоставки = Объект.Клиент.СпособДоставки;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбновитьДатуОтсрочки();
	
КонецПроцедуры

&НаКлиенте
Процедура БезПланаПриИзменении(Элемент)
	
	БезПлана = Объект.БезПлана;
	ОтключитьПланированиеВДокументеВыполненияРабот = БезПлана;
	Если БезПлана Тогда
		Объект.Актуален = Истина;
	КонецЕсли;
	
	СменаБезПланаНаСервере();
	ОбновитьВидимостьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура СменаБезПланаНаСервере()
	
	БезПлана = ОтключитьПланированиеВДокументеВыполненияРабот или объект.БезПлана;
	
	Элементы.ДатаПлан.Видимость 		= НЕ БезПлана;
	Элементы.РезультатПлан.Видимость    = НЕ БезПлана;
	Элементы.РезультатРазница.Видимость = НЕ БезПлана;
	Элементы.РасходыДатаПлан.Видимость  = НЕ БезПлана;
	Элементы.РасходыПлан.Видимость 	    = НЕ БезПлана;
	Элементы.РаботыИтогПлан.Видимость   = НЕ БезПлана;
	Элементы.ОплатыПлан1.Видимость   	= НЕ БезПлана;
	Элементы.ОплатыПлан.Видимость 	  	= НЕ БезПлана;
	Элементы.ОплатыДатаПлан.Видимость 	= НЕ БезПлана;
	Элементы.РаботыПлан.Видимость	  	= НЕ БезПлана;
	Элементы.РаботыВалютаПлан.Видимость	= НЕ БезПлана;
	Элементы.РаботыКурсПлан.Видимость	= НЕ БезПлана;
	Элементы.РаботыДатаПлан.Видимость 	= НЕ БезПлана;
	Элементы.РасходыПлан1.Видимость 	= НЕ БезПлана;
	Элементы.РасходыВалютаПлан.Видимость= НЕ БезПлана;
	Элементы.РасходыКурсПлан.Видимость	= НЕ БезПлана;
	Элементы.ОплатыВалютаПлан.Видимость	= НЕ БезПлана;
	Элементы.ОплатыКурсПлан.Видимость	= НЕ БезПлана;
	Элементы.РаботыЦенаПлан.Видимость	= НЕ БезПлана;
	Элементы.РаботыКоличествоПлан.Видимость 	= НЕ БезПлана;
	Элементы.РаботыПроцентСкидкиПлан.Видимость	= НЕ БезПлана;
	Элементы.РаботыСуммаБезСкидкиПлан.Видимость = НЕ БезПлана;
	
	Если БезПлана 
		И не объект.Актуален Тогда		
		
		объект.Актуален = истина;    	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	БезПлана = объект.БезПлана или объект.Актуален;	
	СтрокаТовара = Элементы.Работы.ТекущиеДанные;
	
	Если БезПлана
		И НЕ ЗначениеЗаполнено(СтрокаТовара.Дата) Тогда
		
		СтрокаТовара.Дата = ОбщийМодульСервисСервер.ПользователяТекущаяДата();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтрокаТовара.Склад) Тогда
		СтрокаТовара.Склад = ПолучитьСкладПоУмолчанию();
	КонецЕсли;
	
	Если БезПлана Тогда
		Если Не ЗначениеЗаполнено(СтрокаТовара.Валюта) Тогда
			Если ЗначениеЗаполнено(Объект.ВидЦен) Тогда
				СтрокаТовара.Валюта = ОбщийМодульСервер.получитьЗначениеРеквизита(Объект.ВидЦен, "ВалютаЦены");
			Иначе
				СтрокаТовара.Валюта = ОсновнаяВалюта;
			КонецЕсли;
		КонецЕсли;
		СтрокаТовара.Курс = ОбщийМодульПовтор.ПолучитьТекущийКурс(СтрокаТовара.Валюта, СтрокаТовара.Дата);
	Иначе
		Если Не ЗначениеЗаполнено(СтрокаТовара.ВалютаПлан) Тогда
			Если ЗначениеЗаполнено(Объект.ВидЦен) Тогда
				СтрокаТовара.ВалютаПлан = ОбщийМодульСервер.получитьЗначениеРеквизита(Объект.ВидЦен, "ВалютаЦены");
			Иначе
				СтрокаТовара.ВалютаПлан = ОсновнаяВалюта;
			КонецЕсли;
		КонецЕсли;
		СтрокаТовара.КурсПлан = ОбщийМодульПовтор.ПолучитьТекущийКурс(СтрокаТовара.ВалютаПлан, СтрокаТовара.ДатаПлан);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция   ПолучитьСкладПоУмолчанию()
	
	Склад = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("СкладПоУмолчанию");
	Если НЕ ЗначениеЗаполнено(Склад) Тогда
		Склад = ПредопределенноеЗначение("Справочник.Склады.ОсновнойСклад");
	КонецЕсли;
	
	Возврат Склад;
	
КонецФункции

&НаКлиенте
Процедура РаботыКоличествоПриИзменении(Элемент)
	
	ПересчитатьСтрокуРабот(Ложь, Ложь);
	ПересчитатьРезультаты();
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьСтрокуРабот(ИзСуммы = Ложь, ИзКонечнойСуммы = Истина, СтрокаТовара = Неопределено, План = Ложь)
	
	//	Если СтрокаТовара = Неопределено Тогда
	СтрокаТовара = Элементы.Работы.ТекущиеДанные;
	//	КонецЕсли;
	
	Если План Тогда
		Если СтрокаТовара.КоличествоПлан = 0 Тогда
			СтрокаТовара.КоличествоПлан = 1;
		КонецЕсли;
		Если СтрокаТовара.КурсПлан = 0 Тогда
			Если ЗначениеЗаполнено(СтрокаТовара.ВалютаПлан) Тогда
				СтрокаТовара.КурсПлан = ОбщийМодульПовтор.ПолучитьТекущийКурс(СтрокаТовара.ВалютаПлан, СтрокаТовара.ДатаПлан);
			Иначе
				СтрокаТовара.КурсПлан = 1;
			КонецЕсли;			
		КонецЕсли;
		
		Если ИзКонечнойСуммы Тогда
			СтрокаТовара.СуммаБезСкидкиПлан = СтрокаТовара.План;
			СтрокаТовара.ПроцентСкидкиПлан  = 0;
		КонецЕсли;
		
		Если ИзСуммы Тогда			
			СтрокаТовара.ценаПлан = СтрокаТовара.СуммаБезСкидкиПлан / СтрокаТовара.КоличествоПлан;
			
		Иначе
			СтрокаТовара.СуммаБезСкидкиПлан = СтрокаТовара.КоличествоПлан * СтрокаТовара.ЦенаПлан;
			Если НЕ СтрокаТовара.ПроцентСкидкиПлан = 0 Тогда
				СтрокаТовара.План = СтрокаТовара.СуммаБезСкидкиПлан - ((СтрокаТовара.СуммаБезСкидкиПлан / 100) * СтрокаТовара.ПроцентСкидкиПлан);
				
			Иначе
				СтрокаТовара.План = СтрокаТовара.СуммаБезСкидкиПлан;
			КонецЕсли;
		КонецЕсли;
		
	иначе
		Если СтрокаТовара.Количество = 0 Тогда
			СтрокаТовара.Количество = 1;
		КонецЕсли;
		Если СтрокаТовара.Курс = 0 Тогда
			Если ЗначениеЗаполнено(СтрокаТовара.Валюта) Тогда
				СтрокаТовара.Курс = ОбщийМодульПовтор.ПолучитьТекущийКурс(СтрокаТовара.Валюта, СтрокаТовара.Дата);
			Иначе
				СтрокаТовара.Курс = 1;
			КонецЕсли;			
		КонецЕсли;
		
		Если ИзКонечнойСуммы Тогда
			СтрокаТовара.СуммаБезСкидки = СтрокаТовара.Сумма;
			СтрокаТовара.ПроцентСкидки  = 0;
		КонецЕсли;
		
		Если ИзСуммы Тогда			
			СтрокаТовара.цена = СтрокаТовара.СуммаБезСкидки / СтрокаТовара.Количество;
			
		Иначе
			СтрокаТовара.СуммаБезСкидки = СтрокаТовара.Количество * СтрокаТовара.Цена;
			Если НЕ СтрокаТовара.ПроцентСкидки = 0 Тогда
				СтрокаТовара.Сумма = СтрокаТовара.СуммаБезСкидки - ((СтрокаТовара.СуммаБезСкидки / 100) * СтрокаТовара.ПроцентСкидки);
				
			Иначе
				СтрокаТовара.Сумма = СтрокаТовара.СуммаБезСкидки;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	//	если не СтрокаТовара.Количество = 0
	//		и ЗначениеЗаполнено(СтрокаТовара.Дата) Тогда
	
	СтрокаТовара.ВаловаяПрибыль = ОбщийМодульСервер.РасчитатьОриентировочнуюВаловуюПрибыль(СтрокаТовара.номенклатура, СтрокаТовара.Количество, СтрокаТовара.Дата, СтрокаТовара.Сумма, СтрокаТовара.Валюта, СтрокаТовара.Курс);
	//	Иначе
	СтрокаТовара.ВаловаяПрибыльПлан = ОбщийМодульСервер.РасчитатьОриентировочнуюВаловуюПрибыль(СтрокаТовара.номенклатура, СтрокаТовара.КоличествоПлан, СтрокаТовара.ДатаПлан, СтрокаТовара.План, СтрокаТовара.ВалютаПлан, СтрокаТовара.КурсПлан);
	//	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьСтрокуРаботНаСервере(ИзСуммы = Ложь, ИзКонечнойСуммы = Истина, СтрокаТовара, План = Ложь)
	
	Если План Тогда
		Если СтрокаТовара.КоличествоПлан = 0 Тогда
			СтрокаТовара.КоличествоПлан = 1;
		КонецЕсли;
		Если СтрокаТовара.КурсПлан = 0 Тогда
			Если ЗначениеЗаполнено(СтрокаТовара.ВалютаПлан) Тогда
				СтрокаТовара.КурсПлан = ОбщийМодульПовтор.ПолучитьТекущийКурс(СтрокаТовара.ВалютаПлан, СтрокаТовара.ДатаПлан);
			Иначе
				СтрокаТовара.КурсПлан = 1;
			КонецЕсли;			
		КонецЕсли;
		
		Если ИзКонечнойСуммы Тогда
			СтрокаТовара.СуммаБезСкидкиПлан = СтрокаТовара.План;
			СтрокаТовара.ПроцентСкидкиПлан  = 0;
		КонецЕсли;
		
		Если ИзСуммы Тогда
			
			СтрокаТовара.ценаПлан = СтрокаТовара.СуммаБезСкидкиПлан / СтрокаТовара.КоличествоПлан;
			
		Иначе
			СтрокаТовара.СуммаБезСкидкиПлан = СтрокаТовара.КоличествоПлан * СтрокаТовара.ЦенаПлан;
			Если НЕ СтрокаТовара.ПроцентСкидкиПлан = 0 Тогда
				СтрокаТовара.План = СтрокаТовара.СуммаБезСкидкиПлан - ((СтрокаТовара.СуммаБезСкидкиПлан / 100) * СтрокаТовара.ПроцентСкидкиПлан);
				
			Иначе
				СтрокаТовара.План = СтрокаТовара.СуммаБезСкидкиПлан;
			КонецЕсли;
		КонецЕсли;
		
	иначе
		Если СтрокаТовара.Количество = 0 Тогда
			СтрокаТовара.Количество = 1;
		КонецЕсли;
		Если СтрокаТовара.Курс = 0 Тогда
			Если ЗначениеЗаполнено(СтрокаТовара.Валюта) Тогда
				СтрокаТовара.Курс = ОбщийМодульПовтор.ПолучитьТекущийКурс(СтрокаТовара.Валюта, СтрокаТовара.Дата);
			Иначе
				СтрокаТовара.Курс = 1;
			КонецЕсли;			
		КонецЕсли;
		
		Если ИзКонечнойСуммы Тогда
			СтрокаТовара.СуммаБезСкидки = СтрокаТовара.Сумма;
			СтрокаТовара.ПроцентСкидки  = 0;
		КонецЕсли;
		
		Если ИзСуммы Тогда
			
			СтрокаТовара.цена = СтрокаТовара.СуммаБезСкидки / СтрокаТовара.Количество;
			
		Иначе
			СтрокаТовара.СуммаБезСкидки = СтрокаТовара.Количество * СтрокаТовара.Цена;
			Если НЕ СтрокаТовара.ПроцентСкидки = 0 Тогда
				СтрокаТовара.Сумма = СтрокаТовара.СуммаБезСкидки - ((СтрокаТовара.СуммаБезСкидки / 100) * СтрокаТовара.ПроцентСкидки);
				
			Иначе
				СтрокаТовара.Сумма = СтрокаТовара.СуммаБезСкидки;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	//	если не СтрокаТовара.Количество = 0
	//		и ЗначениеЗаполнено(СтрокаТовара.Дата) Тогда
	
	СтрокаТовара.ВаловаяПрибыль = ОбщийМодульСервер.РасчитатьОриентировочнуюВаловуюПрибыль(СтрокаТовара.номенклатура, СтрокаТовара.Количество, СтрокаТовара.Дата, СтрокаТовара.Сумма, СтрокаТовара.Валюта, СтрокаТовара.Курс);
	//Иначе
	СтрокаТовара.ВаловаяПрибыльПлан = ОбщийМодульСервер.РасчитатьОриентировочнуюВаловуюПрибыль(СтрокаТовара.номенклатура, СтрокаТовара.КоличествоПлан, СтрокаТовара.ДатаПлан, СтрокаТовара.План, СтрокаТовара.ВалютаПлан, СтрокаТовара.КурсПлан);
	//	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьРезультаты() 	
	
	ВаловаяПрибыль = Объект.Товары.Итог("ВаловаяПрибыль");
	ВаловаяПрибыльПлан = Объект.Товары.Итог("ВаловаяПрибыльПлан");
	РезультатПлан  = ВаловаяПрибыльПлан - РасходыПлан; 
	Результат 	   = ?(Объект.Актуален, ВаловаяПрибыль, 0) - РасходыФакт; 
	
	Если НЕ РезультатПлан = Объект.РезультатПлан Тогда
		Объект.РезультатПлан = РезультатПлан; 	
	КонецЕсли;
	Если НЕ Результат = Объект.Результат Тогда
		Объект.Результат = Результат; 	
	КонецЕсли;
	РезультатРазница = Результат - РезультатПлан;
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьРезультатыНаСервере();
	
	ВаловаяПрибыль = Объект.Товары.Итог("ВаловаяПрибыль");
	ВаловаяПрибыльПлан = Объект.Товары.Итог("ВаловаяПрибыльПлан");
	РезультатПлан  = ВаловаяПрибыльПлан - РасходыПлан; 
	Результат 	  = ?(Объект.Актуален, ВаловаяПрибыль, 0) - РасходыФакт; 
	
	Если НЕ РезультатПлан = Объект.РезультатПлан Тогда
		Объект.РезультатПлан = РезультатПлан; 	
		РезультатРазница = Результат - РезультатПлан;
	КонецЕсли;
	Если НЕ Результат = Объект.Результат Тогда
		Объект.Результат = Результат; 	
		РезультатРазница = Результат - РезультатПлан;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыЦенаПриИзменении(Элемент)
	
	ПересчитатьСтрокуРабот(Ложь, Ложь);
	ПересчитатьРезультаты();
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыПроцентСкидкиПриИзменении(Элемент)
	
	ПересчитатьСтрокуРабот(Ложь, Ложь);
	ПересчитатьРезультаты();
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыСуммаБезСкидкиПриИзменении(Элемент)
	
	ПересчитатьСтрокуРабот(Истина, Ложь);
	ПересчитатьРезультаты();
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыПланПриИзменении(Элемент)
	
	ПересчитатьСтрокуРабот(Истина, Ложь);
	ПересчитатьРезультаты();
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыСуммаПриИзменении(Элемент)
	
	ПересчитатьСтрокуРабот(Истина, Истина);
	ПересчитатьРезультаты();
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	БезПлана = объект.БезПлана или объект.Актуален;
	
	СтрокаТовара = Элементы.Оплаты.ТекущиеДанные;
	Если БезПлана
		И НЕ ЗначениеЗаполнено(СтрокаТовара.Дата) Тогда
		
		СтрокаТовара.Дата = ОбщийМодульСервисСервер.ПользователяТекущаяДата();
	КонецЕсли;
	
	Если не ЗначениеЗаполнено(СтрокаТовара.Валюта) 
		и ЗначениеЗаполнено(ВалютаХранилища) Тогда
		
		СтрокаТовара.Валюта = ВалютаХранилища;
		СтрокаТовара.Курс = ОбщийМодульПовтор.ПолучитьТекущийКурс(ВалютаХранилища, ?(Объект.Актуален, СтрокаТовара.Дата, СтрокаТовара.ДатаПлан));
	КонецЕсли;
	
	Если СтрокаТовара.Курс = 0 Тогда
		СтрокаТовара.Курс = 1;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасходыПередНачаломИзменения(Элемент, Отказ)
	
	БезПлана = объект.БезПлана или объект.Актуален;
	
	СтрокаТовара = Элементы.Расходы.ТекущиеДанные;
	Если БезПлана
		И НЕ ЗначениеЗаполнено(СтрокаТовара.Дата) Тогда
		
		СтрокаТовара.Дата = ОбщийМодульСервисСервер.ПользователяТекущаяДата();
	КонецЕсли;
	
	Если не ЗначениеЗаполнено(СтрокаТовара.Валюта) 
		и ЗначениеЗаполнено(ВалютаХранилища) Тогда
		
		СтрокаТовара.Валюта = ВалютаХранилища;
		СтрокаТовара.Курс = ОбщийМодульПовтор.ПолучитьТекущийКурс(ВалютаХранилища, ?(Объект.Актуален, СтрокаТовара.Дата, СтрокаТовара.ДатаПлан));
	КонецЕсли;
	
	Если СтрокаТовара.Курс = 0 Тогда
		СтрокаТовара.Курс = 1;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыНоменклатураПриИзменении(Элемент)
	ПриИзмененииНоменклатуры() ;	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииНоменклатуры(СтрокаТовара = Неопределено) 
	
	Если СтрокаТовара = Неопределено Тогда
		СтрокаТовара = Элементы.Работы.ТекущиеДанные;	
	КонецЕсли;
	
	Номенклатура = СтрокаТовара.Номенклатура;
	Если ЗначениеЗаполнено(Номенклатура) Тогда  		
		
		Если объект.Актуален Тогда
			
			если СтрокаТовара.Количество = 0 тогда
				СтрокаТовара.Количество = ОбщийМодульСервер.ПолучитьКоличествоПоУмолчанию(Номенклатура);
			КонецЕсли;
			
			если ОбщийМодульПовтор.ЭтоНабор(Номенклатура) 
				и ОбщийМодульТоварСервер.ПроверитьСерийностьЭлементовНабора(Номенклатура) тогда
				
				СтрокаТовараСтруктура = ДобавитьСоставНабораВСвязиССерийнымУчетомКомпонент(Номенклатура, СтрокаТовара.Количество);
				
				если не СтрокаТовараСтруктура = неопределено тогда
					Номенклатура = СтрокаТовараСтруктура.Номенклатура;
					СтрокаТовара.Номенклатура = Номенклатура;
					СтрокаТовара.Количество   = СтрокаТовараСтруктура.Количество;
				конецесли;
			конецесли;
			
			Если УчетПоСериям ТОгда
				
				ТоварВедетсяПоСериям = ОбщийМодульПовтор.ТоварВедетсяПоСериям(Номенклатура);
				
				Если ТоварВедетсяПоСериям Тогда
					СтрокаТовара.Количество = 1;
					СерииНоменклатурыВидны 	= Истина;		
					Элементы.РаботыСерияНоменклатуры.Видимость 	= Истина;	
				КонецЕсли;             				
			КонецЕсли;
			
			СтрокаТовара.Цена = ПолучитьЦенуНаСервере(СтрокаТовара.Номенклатура, СтрокаТовара.Количество, СтрокаТовара.Дата, СтрокаТовара.ЕдиницаИзмерения);
			
			если ИспользоватьСкидки тогда
				СтрокаТовара.СуммаБезСкидки = СтрокаТовара.Цена * СтрокаТовара.Количество;
				СтрокаТовара.ПроцентСкидки 	= ПолучитьПроцентСкидкиНаСервере(СтрокаТовара.Номенклатура, СтрокаТовара.Количество);
			иначе
				СтрокаТовара.Сумма = СтрокаТовара.Цена * СтрокаТовара.Количество;
			КонецЕсли;
			
			ПересчитатьСтрокуРабот(Ложь, Ложь);
			
		иначе
			если СтрокаТовара.КоличествоПлан = 0 тогда
				СтрокаТовара.КоличествоПлан = ОбщийМодульСервер.ПолучитьКоличествоПоУмолчанию(Номенклатура);
			КонецЕсли;
			
			если ОбщийМодульПовтор.ЭтоНабор(Номенклатура) 
				и ОбщийМодульТоварСервер.ПроверитьСерийностьЭлементовНабора(Номенклатура) тогда
				
				СтрокаТовараСтруктура = ДобавитьСоставНабораВСвязиССерийнымУчетомКомпонент(Номенклатура, СтрокаТовара.КоличествоПлан);
				
				если не СтрокаТовараСтруктура = неопределено тогда
					Номенклатура = СтрокаТовараСтруктура.Номенклатура;
					СтрокаТовара.Номенклатура = Номенклатура;
					СтрокаТовара.КоличествоПлан = СтрокаТовараСтруктура.КоличествоПлан;
				конецесли;
			конецесли;
			
			Если УчетПоСериям Тогда
				
				ТоварВедетсяПоСериям = ОбщийМодульПовтор.ТоварВедетсяПоСериям(Номенклатура);
				Если ТоварВедетсяПоСериям Тогда
					СтрокаТовара.КоличествоПлан = 1;
				КонецЕсли;             				
			КонецЕсли;
			
			СтрокаТовара.ЦенаПлан = ПолучитьЦенуНаСервере(СтрокаТовара.Номенклатура, СтрокаТовара.КоличествоПлан, СтрокаТовара.ДатаПлан, СтрокаТовара.ЕдиницаИзмерения);
			
			если ИспользоватьСкидки тогда
				СтрокаТовара.СуммаБезСкидкиПлан = СтрокаТовара.ЦенаПлан * СтрокаТовара.КоличествоПлан;
				СтрокаТовара.ПроцентСкидкиПлан 	= ПолучитьПроцентСкидкиНаСервере(СтрокаТовара.Номенклатура, СтрокаТовара.КоличествоПлан);
			иначе
				СтрокаТовара.План = СтрокаТовара.ЦенаПлан * СтрокаТовара.КоличествоПлан;
			КонецЕсли;
			
			ПересчитатьСтрокуРабот(, Ложь, , Истина);		
		КонецЕсли;
		
		ПересчитатьРезультаты();		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция   ПолучитьЦенуНаСервере(Знач Номенклатура, Знач Количество, Знач Дата, Знач ЕдиницаИзмерения)
	
	Возврат ОбщийМодульСервер.ПолучитьЦенуНаСервере(Номенклатура, Объект.ВидЦен, Дата, истина, Количество, Объект.Договор, , , Объект.Ссылка, Истина, ЕдиницаИзмерения);
	
КонецФункции

&НаСервере
Процедура ДобавитьПозициюНоменклатуры(Знач НоменклатураВх, НеСтановитсяНаЭтуСтроку = Ложь, множительКоличества = 1)
	
	СерияНоменклатуры = Неопределено;
	
	Если ТипЗнч(НоменклатураВх) = Тип("СправочникСсылка.Номенклатура") Тогда  		
		Номенклатура = НоменклатураВх;
		Количество 	 = множительКоличества * ОбщийМодульСервер.ПолучитьКоличествоПоУмолчанию(НоменклатураВх);
		Цена 		 = 0;
		ЦенаЕсть 	 = Ложь;
		
	Иначе
		Номенклатура = НоменклатураВх.Номенклатура;
		Количество 	 = НоменклатураВх.Количество * множительКоличества * ОбщийМодульСервер.ПолучитьКоличествоПоУмолчанию(НоменклатураВх.Номенклатура);
		НоменклатураВх.Свойство("СерияНоменклатуры", СерияНоменклатуры);
		Цена 		 = 0;
		ЦенаЕсть 	 = НоменклатураВх.Свойство("Цена", Цена);
		
	КонецЕсли; 
	ТоварВедетсяПоСериям = УчетПоСериям И ОбщийМодульПовтор.ТоварВедетсяПоСериям(Номенклатура);
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Номенклатура", Номенклатура);
	Если ТоварВедетсяПоСериям Тогда
		ПараметрыОтбора.Вставить("СерияНоменклатуры", СерияНоменклатуры);
	КонецЕсли;
	
	СтрокаТовара = Объект.Товары.НайтиСтроки(ПараметрыОтбора);
	если не ДокументЗаблокирован Тогда
		
		Если СтрокаТовара.Количество() = 0 Тогда
			СтрокаТовара = Объект.Товары.Добавить();	
			СтрокаТовара.Номенклатура = Номенклатура;
			
			Если ТоварВедетсяПоСериям 
				И НЕ СерииНоменклатурыВидны Тогда
				
				СерииНоменклатурыВидны = Истина;		
				Элементы.РаботыСерияНоменклатуры.Видимость 	= Истина;
			КонецЕсли;
			
		иначе
			СтрокаТовара = СтрокаТовара[0];
		КонецЕсли;		
		
		Если Объект.Актуален Тогда
			СтрокаТовара.Количество = СтрокаТовара.Количество + Количество;
			если ТоварВедетсяПоСериям тогда
				
				если не серияНоменклатуры = неопределено Тогда				
					СтрокаТовара.Количество = 1;
					СтрокаТовара.серияНоменклатуры = серияНоменклатуры;	
				КонецЕсли;
				
			конецесли;
			
			если ЦенаЕсть тогда
				СтрокаТовара.Цена = Цена;
			Иначе			
				СтрокаТовара.Цена = ПолучитьЦенуНаСервере(СтрокаТовара.Номенклатура, Строкатовара.Количество, СтрокаТовара.Дата, СтрокаТовара.ЕдиницаИзмерения);	
			КонецЕсли;		
			
			Если ИспользоватьСкидки Тогда
				СтрокаТовара.ПроцентСкидки = ОбщийМодульСервер.ПолучитьПроцентСкидкиНаСервере(Номенклатура, строкатовара.Количество, объект.ВидЦен, объект.Клиент);	
			КонецЕсли;
			
			пересчитатьСтрокуРаботНаСервере(, , СтрокаТовара, Ложь);	
		иначе
			СтрокаТовара.КоличествоПлан = СтрокаТовара.КоличествоПлан + Количество;
			если ТоварВедетсяПоСериям тогда
				
				если не серияНоменклатуры = неопределено Тогда				
					СтрокаТовара.КоличествоПлан = 1;
				КонецЕсли;
				
			конецесли;
			
			если ЦенаЕсть тогда
				СтрокаТовара.ЦенаПлан = Цена;
			Иначе			
				СтрокаТовара.ЦенаПлан = ПолучитьЦенуНаСервере(СтрокаТовара.Номенклатура, Строкатовара.КоличествоПлан, СтрокаТовара.ДатаПлан, СтрокаТовара.ЕдиницаИзмерения);	
			КонецЕсли;		
			
			Если ИспользоватьСкидки Тогда
				СтрокаТовара.ПроцентСкидкиПлан = ОбщийМодульСервер.ПолучитьПроцентСкидкиНаСервере(Номенклатура, строкатовара.КоличествоПлан, объект.ВидЦен, объект.Клиент);	
			КонецЕсли;
			
			пересчитатьСтрокуРаботНаСервере(, , СтрокаТовара, Истина);	
		КонецЕсли;
		ПересчитатьРезультатыНаСервере();
		
		
		если не НеСтановитсяНаЭтуСтроку тогда
			Элементы.Работы.ТекущаяСтрока 	= СтрокаТовара.ПолучитьИдентификатор();
			Элементы.Работы.ТекущийЭлемент 	= элементы.РаботыКоличество;
		КонецЕсли;
		
	иначеЕсли не НеСтановитсяНаЭтуСтроку
		И НЕ СтрокаТовара.Количество() = 0 Тогда //Встать на строку
		
		СтрокаТовара = СтрокаТовара[0];
		Элементы.Работы.ТекущаяСтрока 	= СтрокаТовара.ПолучитьИдентификатор();
		Элементы.Работы.ТекущийЭлемент 	= элементы.РаботыКоличество;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
процедура ДобавитьВсеЭлементыСостава(Знач Номенклатура, Состав, Знач Количество)
	
	Для Каждого СтрокаНабора Из Номенклатура.Состав Цикл
		
		если ОбщийМодульПовтор.ЭтоНабор(Номенклатура) тогда
			ДобавитьВсеЭлементыСостава(СтрокаНабора.Номенклатура, Состав, Количество)
			
		иначе
			СтрокаСостава = состав.добавить();
			СтрокаСостава.Номенклатура 		= СтрокаНабора.Номенклатура;
			СтрокаСостава.Количество 		= СтрокаНабора.Количество * Количество;
			СтрокаСостава.СерияНоменклатуры = Неопределено;
			
		конецесли;
	КонецЦикла;
	
конецпроцедуры

&НаСервере
Функция   ПолучитьПроцентСкидкиНаСервере(Знач НоменклатураИлиКлиентПоставщик, Знач Количество = 0)
	
	если ИспользоватьСкидки тогда
		Возврат ОбщийМодульСервер.ПолучитьПроцентСкидкиНаСервере(НоменклатураИлиКлиентПоставщик, Количество, объект.ВидЦен, объект.Клиент);
		
	иначе
		возврат 0;
	КонецЕсли;
	
КонецФункции //ПолучитьПроцентСкидкиНаСервере

&НаСервере
Функция   ДобавитьСоставНабораВСвязиССерийнымУчетомКомпонент(знач Номенклатура, Знач Количество = 1)
	
	Результат = неопределено;
	
	Состав = новый ТаблицаЗначений;
	Состав.Колонки.Добавить("Номенклатура");
	Состав.Колонки.Добавить("Количество");
	Состав.Колонки.Добавить("СерияНоменклатуры");
	
	ДобавитьВсеЭлементыСостава(Номенклатура, Состав, Количество);
	
	сч = 0;
	Для Каждого СтрокаСостава Из Состав Цикл
		
		если сч = 0 тогда
			Результат = Новый Структура;
			Результат.Вставить("Номенклатура", строкасостава.номенклатура);
			Результат.Вставить("Количество", строкасостава.Количество * Количество);
			
		иначе
			ДобавитьПозициюНоменклатуры(строкасостава, Истина, Количество);
		конецесли;
		сч = сч + 1;
	КонецЦикла;
	
	Возврат Результат;
	
конецФункции

&НаКлиенте
Процедура ОплатыПланПриИзменении(Элемент)	
	ПересчетИтоговОплаты();	
КонецПроцедуры

&НаКлиенте
Процедура ОплатыСуммаПриИзменении(Элемент)
	ПересчетИтоговОплаты();
КонецПроцедуры

&НаКлиенте
Процедура ОплатыКурсПриИзменении(Элемент)
	ПересчетИтоговОплаты();
КонецПроцедуры

&НаКлиенте
Процедура ОплатыХранилищеДенегПриИзменении(Элемент)
	
	СтрокаТовара = Элементы.Оплаты.ТекущиеДанные;	
	ХранилищеДенегПриИзмененииНаСервере(СтрокаТовара);
	ПересчетИтоговОплаты();
	
КонецПроцедуры

&НаСервере
Процедура ХранилищеДенегПриИзмененииНаСервере(СтрокаТовара)
	
	Если ЗначениеЗаполнено(СтрокаТовара.ХранилищеДенег) 
		и ЗначениеЗаполнено(СтрокаТовара.ХранилищеДенег.Валюта) Тогда
		
		СтрокаТовара.Валюта = СтрокаТовара.ХранилищеДенег.Валюта;
		СтрокаТовара.Курс = ОбщийМодульПовтор.ПолучитьТекущийКурс(СтрокаТовара.Валюта, ?(Объект.Актуален, СтрокаТовара.Дата, СтрокаТовара.ДатаПлан));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчетИтоговОплаты()
	
	ПересчетИтоговОплатыНаСервере();
	ПересчитатьРезультаты();
	
КонецПроцедуры

&НаСервере
Процедура ПересчетИтоговОплатыНаСервере()
	
	ОплатыПлан = 0;
	ОплатыФакт = 0;
	Для Каждого СтрокаОплаты Из Объект.Оплаты Цикл
		Если СтрокаОплаты.Курс = 0 Тогда
			СтрокаОплаты.Курс = 1;
		КонецЕсли;
		Если СтрокаОплаты.КурсПлан = 0 Тогда
			СтрокаОплаты.КурсПлан = 1;
		КонецЕсли;
		ОплатыФакт = ОплатыФакт + СтрокаОплаты.Сумма * СтрокаОплаты.Курс;
		ОплатыПлан = ОплатыПлан + СтрокаОплаты.План * СтрокаОплаты.КурсПлан;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ХранилищеДенегПриИзменении(Элемент)
	ХранилищеДенегПриИзмененииНаСервере1();
КонецПроцедуры

&НаСервере
Процедура ХранилищеДенегПриИзмененииНаСервере1()
	
	Если ЗначениеЗаполнено(объект.ХранилищеДенег) Тогда
		ВалютаХранилища = объект.ХранилищеДенег.Валюта;
		Объект.Валюта = ВалютаХранилища;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидЦенПриИзменении(Элемент)
	ВидЦенПриИзмененииНаСервере(Ложь);
КонецПроцедуры

&НаСервере
Процедура ВидЦенПриИзмененииНаСервере(НеМенятьЦеныТаблицы = Ложь)
	
	Если ЗначениеЗаполнено(Объект.ВидЦен) Тогда
		
		Объект.Валюта = Объект.ВидЦен.ВалютаЦены;
		
		Если НЕ НеМенятьЦеныТаблицы Тогда
			Для Каждого СтрокаТовара из Объект.Товары Цикл
				
				Если объект.Актуален Тогда
					СтрокаТовара.Валюта = Объект.ВидЦен.ВалютаЦены;
					СтрокаТовара.Курс = ОбщийМодульПовтор.ПолучитьТекущийКурс(СтрокаТовара.Валюта, СтрокаТовара.Дата);
					СтрокаТовара.Цена = ПолучитьЦенуНаСервере(СтрокаТовара.Номенклатура, строкатовара.количество, СтрокаТовара.Дата, СтрокаТовара.ЕдиницаИзмерения);
					
					если ИспользоватьСкидки тогда
						СтрокаТовара.СуммаБезСкидки = СтрокаТовара.Количество * СтрокаТовара.Цена;
						
						если не СтрокаТовара.СуммаБезСкидки = 0 тогда
							СтрокаТовара.Сумма = СтрокаТовара.СуммаБезСкидки - СтрокаТовара.ПроцентСкидки * СтрокаТовара.СуммаБезСкидки / 100;
							
						иначе
							СтрокаТовара.Сумма = 0;
						КонецЕсли;
						
					иначе
						СтрокаТовара.Сумма = СтрокаТовара.Количество * СтрокаТовара.Цена;	
					КонецЕсли;
					
				иначе
					СтрокаТовара.ВалютаПлан = Объект.ВидЦен.ВалютаЦены;
					СтрокаТовара.КурсПлан = ОбщийМодульПовтор.ПолучитьТекущийКурс(СтрокаТовара.ВалютаПлан, СтрокаТовара.ДатаПлан);
					СтрокаТовара.ЦенаПлан = ПолучитьЦенуНаСервере(СтрокаТовара.Номенклатура, строкатовара.количествоПлан, СтрокаТовара.ДатаПлан, СтрокаТовара.ЕдиницаИзмерения);
					
					если ИспользоватьСкидки тогда
						СтрокаТовара.СуммаБезСкидкиПлан = СтрокаТовара.КоличествоПлан * СтрокаТовара.ЦенаПлан;
						
						если не СтрокаТовара.СуммаБезСкидкиПлан = 0 тогда
							СтрокаТовара.план  = СтрокаТовара.СуммаБезСкидкиПлан - СтрокаТовара.ПроцентСкидкиПлан * СтрокаТовара.СуммаБезСкидкиПлан / 100;
							
						иначе
							СтрокаТовара.План  = 0;
						КонецЕсли;
						
					иначе
						СтрокаТовара.План  = СтрокаТовара.КоличествоПлан * СтрокаТовара.ЦенаПлан;	
					КонецЕсли;	
				КонецЕсли;
				
			КонецЦикла;	
		КонецЕсли;  	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасходыХранилищеДенегПриИзменении(Элемент)
	
	СтрокаТовара = Элементы.Расходы.ТекущиеДанные;	
	ХранилищеДенегПриИзмененииНаСервере(СтрокаТовара);
	ПересчетИтоговРасходов();
	
КонецПроцедуры

&НаКлиенте
Процедура РасходыПланПриИзменении(Элемент)
	ПересчетИтоговРасходов();	
КонецПроцедуры

&НаКлиенте
Процедура РасходыСуммаПриИзменении(Элемент)
	ПересчетИтоговРасходов();	
КонецПроцедуры

&НаКлиенте
Процедура РасходыКурсПриИзменении(Элемент)
	ПересчетИтоговРасходов();	
КонецПроцедуры

&НаКлиенте
Процедура ПересчетИтоговРасходов()
	
	ПересчетИтоговРасходовНаСервере();
	ПересчитатьРезультаты();
	
КонецПроцедуры

&НаСервере
Процедура ПересчетИтоговРасходовНаСервере()
	
	РасходыПлан = 0;
	РасходыФакт = 0;
	Для Каждого СтрокаОплаты Из Объект.Расходы Цикл
		Если СтрокаОплаты.Курс = 0 Тогда
			СтрокаОплаты.Курс = 1;	
		КонецЕсли;
		Если СтрокаОплаты.КурсПлан = 0 Тогда
			СтрокаОплаты.КурсПлан = 1;	
		КонецЕсли;
		
		РасходыПлан = РасходыПлан + СтрокаОплаты.План * СтрокаОплаты.КурсПлан;
		РасходыФакт = РасходыФакт + СтрокаОплаты.Сумма * СтрокаОплаты.Курс;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
процедура ПодготовкаКПроведению(Отказ)
	
	Если Не Отказ тогда
		ДокументОбъект = РеквизитФормыВЗначение("Объект"); 
		ДокументОбъект.ПодготовкаКПроведению(Отказ);
		ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если НЕ ОТказ
		И УчетПоСериям Тогда
		
		Отказ = ПроверитьУчетПоСериям(Отказ);
	конецесли;
	
	Если Не Отказ Тогда
		Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение тогда
			
			Если не значениеЗаполнено(объект.Клиент) 
				и Вопрос(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Вы не указали Клиента.") + символы.ПС + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Провести документ?"), РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
				
				Отказ = Истина;
				
			ИначеЕсли объект.Товары.Количество() > 10 Тогда
				Состояние(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Проведение документа.."));	
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	ПодготовкаКПроведению(Отказ);
	
	Если НЕ отказ
		и ДобавитьФункцииПриемаИВыдачиОбъектаРаботДокументуВыполненияРабот 
		и объект.Актуален
		и ЗначениеЗаполнено(Объект.Номенклатура)
		и ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетПоСкладам")
		и (ЗначениеЗаполнено(объект.ДатаВыдачиОбъектаРаботКлиентуОбратно) или ЗначениеЗаполнено(Объект.ДатаПоступленияОбъектаРаботНаНашСклад))
		и НЕ ЗначениеЗаполнено(Объект.СкладДляОбъектаВыполненияРабот) Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Вы не указали Склад хранения объекта работ!");
		Сообщение.Поле  = "СкладДляОбъектаВыполненияРабот";
		Сообщение.Сообщить();
		
		Отказ = Истина;
		
	КонецЕсли;
	
	глПроверятьСообщения = Истина;
	
КонецПроцедуры

&НаСервере
Функция   ПроверитьУчетПоСериям(Отказ)
	
	если не отказ тогда
		
		МассивСерий = Новый Массив;
		Для Каждого СтрокаТовара Из Объект.Товары Цикл
			СерияНоменклатуры = СтрокаТовара.СерияНоменклатуры;
			
			Если ЗначениеЗаполнено(СерияНоменклатуры) Тогда
				Если МассивСерий.Найти(СерияНоменклатуры) = Неопределено Тогда
					МассивСерий.Добавить(СерияНоменклатуры);
					
				Иначе
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("В строке №") + СтрокаТовара.НомерСтроки + " " + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Серия повторяется!");
					Сообщение.Поле 	= "РаботыСерияНоменклатуры";
					Сообщение.УстановитьДанные(Объект);
					Сообщение.Сообщить();
					
					Отказ = Истина;
					
				КонецЕсли;
				
			ИначеЕсли ОбщийМодульПовтор.ТоварВедетсяПоСериямИСерияОбязательна(СтрокаТовара.номенклатура) тогда
				
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("В строке №") + СтрокаТовара.НомерСтроки + " " + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("обязательная Серия НЕ указана!");
				Сообщение.Поле 	= "РаботыСерияНоменклатуры";
				Сообщение.УстановитьДанные(Объект);
				Сообщение.Сообщить();
				
				Отказ = Истина;
				
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат отказ;
	
КонецФункции //ПроверитьУчетПоСериям

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если НЕ Отказ
		И ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Если ЗначениеЗаполнено(объект.Клиент)
			и ОбщийМодульПовтор.ПолучитьПараметрСеанса("ВестиУчетОтдельныхДоговоровСКонтрагентамиПС") Тогда
			
			Если не ЗначениеЗаполнено(Объект.Договор) тогда
				Если ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ДоговорДолженБытьВыбранОбязательноИначеПрограммаВыберетДоговорПоУмолчанию") Тогда
					
					ДатаСКоторойТребуетсяОбязательноеУказаниеДоговора = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ДатаСКоторойТребуетсяОбязательноеУказаниеДоговора");
					
					Если ДатаСКоторойТребуетсяОбязательноеУказаниеДоговора= '00010101000000' 
						или ДатаСКоторойТребуетсяОбязательноеУказаниеДоговора <= Объект.Дата Тогда
						
						Сообщение = Новый СообщениеПользователю;
						Сообщение.Текст = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Вы не указали обязательный Договор с контрагентом!");
						Сообщение.Поле  = "Договор";
						Сообщение.УстановитьДанные(ТекущийОбъект);
						Сообщение.Сообщить();
						
						Отказ = Истина;
						
					КонецЕсли;	
				КонецЕсли;
				
			ИначеЕсли Объект.Договор.ПредусматриваетСтрогиеЦены тогда
				
				Договор = Объект.Договор;
				Дата 	= Объект.Дата;
				ВидЦен 	= Объект.ВидЦен;
				
				Для Каждого СтрокаТовара Из Объект.Товары Цикл
					
					Цена = ОбщийМодульСервер.ПолучитьЦенуНаСервере(Строкатовара.Номенклатура, ВидЦен, Строкатовара.Дата, Ложь, СтрокаТовара.Количество, Договор, Истина, Строкатовара.Цена, Объект.Ссылка, , СтрокаТовара.ЕдиницаИзмерения);
					Если НЕ Строкатовара.Цена = Цена тогда
						
						Отказ = Истина;
						
						Сообщение = Новый СообщениеПользователю;
						Сообщение.Текст = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Цена в строке №") + " " + СтрокаТовара.НомерСтроки + " " + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("не соответствует строгой цене из договора:") + " " + Цена;
						Сообщение.УстановитьДанные(ТекущийОбъект);					
						Сообщение.Сообщить();
						
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;		
		КонецЕсли;	
		
		Если НЕ Отказ Тогда
			Для Каждого СтрокаТовара Из Объект.Товары Цикл
				
				Если СтрокаТовара.процентскидки = 0
					и строкатовара.суммабезскидки = 0 Тогда
					
					строкатовара.суммабезскидки = строкатовара.сумма;
				КонецЕсли;
			КонецЦикла;	
		КонецЕсли;
		
		Если не отказ
			И не ЗначениеЗаполнено(Объект.ХранилищеДенег) Тогда
			
			Объект.ХранилищеДенег = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ОсновноеХранилищеДенег") ;
			Если не ЗначениеЗаполнено(Объект.ХранилищеДенег) Тогда
				Объект.ХранилищеДенег = ПредопределенноеЗначение("Справочник.ХранилищаДенег.ОсновнаяКассаВОсновнойВалюте");
			КонецЕсли;
		КонецЕсли;
		
		Если объект.Актуален
			И не отказ 
			и ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			
			Для Каждого СтрокаТовара Из Объект.Товары Цикл
				
				Если НЕ ЗначениеЗаполнено(СтрокаТовара.Дата) Тогда
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("В строке работ №") + " " + СтрокаТовара.НомерСтроки + " " + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("не указана дата!");
					Сообщение.УстановитьДанные(ТекущийОбъект);					
					Сообщение.Сообщить();
					
					Отказ = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;	
			
			Для Каждого СтрокаТовара Из ТекущийОбъект.Оплаты Цикл
				
				Если НЕ ЗначениеЗаполнено(СтрокаТовара.Дата) Тогда
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("В строке оплат №") + " " + СтрокаТовара.НомерСтроки + " " + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("не указана дата!");
					Сообщение.УстановитьДанные(ТекущийОбъект);					
					Сообщение.Сообщить();
					
					Отказ = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;	
			
			Для Каждого СтрокаТовара Из ТекущийОбъект.Расходы Цикл
				
				Если НЕ ЗначениеЗаполнено(СтрокаТовара.Дата) Тогда
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("В строке расходов №") + " " + СтрокаТовара.НомерСтроки + " " + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("не указана дата!");
					Сообщение.УстановитьДанные(ТекущийОбъект);					
					Сообщение.Сообщить();
					
					Отказ = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;	
			
			Отказ = ПроверитьНаОтказ();
		КонецЕсли;   	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция   ПроверитьНаОтказ()
	
	отказ = ложь;
	
	Ссылка = Объект.Ссылка;
	Дата   = Объект.Дата;
	видцен = Объект.ВидЦен;
	ПоступилоДенег = ОплатыФакт;
	ХранилищеДенег = объект.ХранилищеДенег;
	Договор = Объект.Договор;
	Клиент  = Объект.Клиент;
	ТовараНаСумму 	  = Объект.Товары.Итог("Сумма");
	ТовараВКоличестве = Объект.Товары.Итог("Количество");
	
	ЗначениеЗаполненоКлиентПоставщик = ЗначениеЗаполнено(Клиент);
	Клиент 							 = ЗначениеЗаполненоКлиентПоставщик;
	ВестиУчетДвиженияДенег = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетДвиженияДенег") ;
	
	ВестиУчетЗаказовКлиентов = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетЗаказовКлиентов") ;
	
	Если ОбщийМодульПовтор.ПолучитьПараметрСеанса("ВестиУчетОтдельныхДоговоровСКонтрагентамиПС")
		и ЗначениеЗаполнено(Договор) Тогда
		
		Отказ = ОбщийМодульСервер.ПроверитьОтказПоСуммеИлиКоличествуДляДоговора(Отказ, Договор, Клиент, Дата, ТовараНаСумму, ТовараВКоличестве, Ложь, ПоступилоДенег, Ссылка);
	КонецЕсли;
	
	Если Не Отказ ТОгда
		
		ИспользоватьСложныйМеханизмЦен = ОбщийМодульПовтор.ПолучитьПараметрСеанса("ИспользоватьСложныйМеханизмЦенПС") ;
		ВестиУчетВаловойПрибыли 	   = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетВаловойПрибыли") ;
		
		ВестиУчетПоСкладам = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетПоСкладам");
		
		ПоведениеПрограммыПриРасходеТоваровНижеРекомендованногоМинимальногоОстатка = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ПоведениеПрограммыПриРасходеТоваровНижеРекомендованногоМинимальногоОстатка");
		ПоведениеПрограммыПриРасходеТоваровБезОстатка = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ПоведениеПрограммыПриРасходеТоваровБезОстатка");
		
		ОстаткиСледуетПроверять = (ЗначениеЗаполнено(ПоведениеПрограммыПриРасходеТоваровНижеРекомендованногоМинимальногоОстатка)
		И НЕ ПоведениеПрограммыПриРасходеТоваровНижеРекомендованногоМинимальногоОстатка = Перечисления.ИгнорироватьРазрешитьЗапретить.Игнорировать )
		ИЛИ (ЗначениеЗаполнено(ПоведениеПрограммыПриРасходеТоваровБезОстатка)
		И НЕ ПоведениеПрограммыПриРасходеТоваровБезОстатка = Перечисления.ИгнорироватьРазрешитьЗапретить.Игнорировать) ;
		
		Если ИспользоватьСложныйМеханизмЦен
			и ЗначениеЗаполнено(видцен)
			и ЗначениеЗаполнено(видцен.ВалютаЦены) Тогда
			
			ВалютаЦены = видцен.ВалютаЦены ;
		иначе
			ВалютаЦены = ОбщийМодульПовтор.ЗначениеПредопределенного("Справочники.Валюты.ОсновнаяВалюта") ;
		КонецЕсли;
		
		Для Каждого ТекСтрокаТовары Из Объект.Товары Цикл
			
			Склад = ТекСтрокаТовары.Склад;
			Дата  = ТекСтрокаТовары.Дата;
			Если ВестиУчетПоСкладам
				и НЕ ЗначениеЗаполнено(Склад) Тогда
				
				ОбщийМодульСервисСервер.ДобавитьСообщениеПользователю(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("В работах не везде указан склад!"), , Ссылка);
				
				Отказ = Истина;	
				Прервать;
			КонецЕсли;	
			
			Номенклатура 	   = ТекСтрокаТовары.Номенклатура;		
			СтруктураТаблиц    = ОбщийМодульСервер.ПолучитьТаблицыЦенСписанияИОстатков(, склад, дата, ОстаткиСледуетПроверять, Номенклатура);
			ТаблицаЦенСписания = СтруктураТаблиц.ТаблицаЦенСписания;
			
			если ОстаткиСледуетПроверять тогда
				ГТ = СтруктураТаблиц.ТаблицаОстатков;	
			КонецЕсли;	
			
			СерияНоменклатуры = ТекСтрокаТовары.СерияНоменклатуры;
			Количество 		  = ТекСтрокаТовары.Количество;
			КоличествоОстаток = неопределено;
			
			если ОстаткиСледуетПроверять тогда
				СтрокаОстаток = ГТ.найти(номенклатура, "Номенклатура");
				
				Если не СтрокаОстаток = Неопределено Тогда
					ПредопределенныйТовар = СтрокаОстаток.НеОтслеживатьОстаток или СтрокаОстаток.Предопределенный;
					ЭтоНабор 			  = СтрокаОстаток.ЭтоНабор;
					
				иначе
					ЭтоНабор 			  = ОбщийМодульПовтор.ЭтоНабор(Номенклатура);
					ПредопределенныйТовар = ОбщийМодульПовтор.ТоварНеУчитываетсяПоКоличеству(Номенклатура);		
					
				КонецЕсли;			
				
			иначе 			
				ЭтоНабор 			  = ОбщийМодульПовтор.ЭтоНабор(Номенклатура);
				ПредопределенныйТовар = ОбщийМодульПовтор.ТоварНеУчитываетсяПоКоличеству(Номенклатура);		
				
			КонецЕсли;	
			
			если не ПредопределенныйТовар
				и не этонабор тогда
				
				если ОстаткиСледуетПроверять тогда
					Отказ = ОбщийМодульСервер.ПроверитьОстатокТоваров(ПоведениеПрограммыПриРасходеТоваровНижеРекомендованногоМинимальногоОстатка, ПоведениеПрограммыПриРасходеТоваровБезОстатка, Склад, Номенклатура, Количество, Дата, Истина, КоличествоОстаток, Ссылка);	
				КонецЕсли;
				
			КонецЕсли;
			
			Если Не отказ тогда
				
				сумма 	 = ТекСтрокаТовары.Сумма;
				СуммаТов = сумма;//ОбщийМодульСервер.ПоКурсу(Сумма, , ВалютаЦены, Дата);			
				
				если ЭтоНабор Тогда 										
					отказ = обработатьОТКАЗдвижениядлянабора(Номенклатура, ТекСтрокаТовары.Количество, СуммаТов, ВестиУчетВаловойПрибыли, клиент, ЗначениеЗаполненоКлиентПоставщик, отказ, ПоведениеПрограммыПриРасходеТоваровНижеРекомендованногоМинимальногоОстатка, ПоведениеПрограммыПриРасходеТоваровБезОстатка, ТекСтрокаТовары.Склад);
					
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	//если НЕ ПоступилоДенег = 0 
	//	и ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетДвиженияДенег") тогда
	//	
	//	Если ЗначениеЗаполнено(ХранилищеДенег) Тогда
	//		ФормаОплаты = ХранилищеДенег.ФормаОплаты;
	//	Иначе
	//		ФормаОплаты = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ФормаОплатыДляДенегВДокументахРасходаИПриходаТоваров") ;				
	//	КонецЕсли;
	//	
	//	Отказ = ОбщийМодульСервер.ПроверитьОстатокДенежныхСредств( -ПоступилоДенег, Дата, ФормаОплаты, Валюта, ХранилищеДенег, , Объект.Ссылка);	
	//	
	//КонецЕсли;
	//
	Возврат Отказ;
	
КонецФункции //ПроверитьНаОтказ

&НаСервере
Функция   обработатьОТКАЗдвижениядлянабора(Номенклатура, КоличествоВх, Сумма, ВестиУчетВаловойПрибыли, клиент, ЗначениеЗаполненоКлиентПоставщик, отказ, ПоведениеПрограммыПриРасходеТоваровНижеРекомендованногоМинимальногоОстатка, ПоведениеПрограммыПриРасходеТоваровБезОстатка, Склад) //для вложенных наборов
	
	общкво = Номенклатура.Состав.Итог("Количество");
	Если общкво = 0 Тогда
		общкво = 1;
	КонецЕсли;
	
	для каждого СтрокаСоставаНабора из Номенклатура.Состав цикл
		СуммаЭлемента = (Сумма / общкво) * СтрокаСоставаНабора.количество;
		НоменклатураСостава = СтрокаСоставаНабора.Номенклатура;
		
		Если НоменклатураСостава.ЭтоНабор = ИСТИНА Тогда
			отказ = обработатьОТКАЗдвижениядлянабора(НоменклатураСостава, КоличествоВх * СтрокаСоставаНабора.количество, СуммаЭлемента, ВестиУчетВаловойПрибыли, клиент, ЗначениеЗаполненоКлиентПоставщик, отказ, ПоведениеПрограммыПриРасходеТоваровНижеРекомендованногоМинимальногоОстатка, ПоведениеПрограммыПриРасходеТоваровБезОстатка, склад);
		иначе
			
			количество = СтрокаСоставаНабора.количество * КоличествоВх;
			
			ПредопределенныйТовар = ОбщийМодульПовтор.ТоварНеУчитываетсяПоКоличеству(НоменклатураСостава);
			
			Если НЕ ПредопределенныйТовар Тогда
				Отказ = ОбщийМодульСервер.ПроверитьОстатокТоваров(ПоведениеПрограммыПриРасходеТоваровНижеРекомендованногоМинимальногоОстатка, ПоведениеПрограммыПриРасходеТоваровБезОстатка, Склад, НоменклатураСостава, Количество, Объект.Дата, Истина, , Объект.Ссылка);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;	
	
	Возврат Отказ;
	
КонецФункции

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	ОбновитьВидимостьСерииОбъекта();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидимостьСерииОбъекта()
	
	Если УчетПоСериям
		и ЗначениеЗаполнено(Объект.Номенклатура)
		и ОбщийМодульПовтор.ТоварВедетсяПоСериям(объект.Номенклатура) Тогда
		
		Элементы.СерияНоменклатуры.Видимость = Истина;
	Иначе
		
		Элементы.СерияНоменклатуры.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатыВалютаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Оплаты.ТекущиеДанные;
	ТекущиеДанные.курс = ОбщийМодульПовтор.ПолучитьТекущийКурс(ТекущиеДанные.Валюта, ?(Объект.Актуален, ТекущиеДанные.Дата, ТекущиеДанные.ДатаПлан));
	ПересчетИтоговОплаты();
	
КонецПроцедуры

&НаКлиенте
Процедура РасходыВалютаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Расходы.ТекущиеДанные;
	ТекущиеДанные.курс = ОбщийМодульПовтор.ПолучитьТекущийКурс(ТекущиеДанные.Валюта, ?(Объект.Актуален, ТекущиеДанные.Дата, ТекущиеДанные.ДатаПлан));
	ПересчетИтоговРасходов();	
	
КонецПроцедуры

&НаКлиенте
Процедура СметаУтвержденаПриступитьКВыполнению(Команда)
	
	объект.Актуален = Истина;
	Элементы.СметаУтвержденаПриступитьКВыполнению.Видимость = Ложь;
	ОбновитьВидимостьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьСметы(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Предупреждение(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Документ сначала следует записать."), , ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Перед Печатью сметы"));
	Иначе
		Записать();
		
		ПараметрыФормы = новый Структура;
		ПараметрыФормы.Вставить("Документ", Объект.Ссылка);
		ФормаПечати = получитьформу("ОбщаяФорма.ФормаПечати", ПараметрыФормы);
		
		ПечатьСметыНаСервере(ФормаПечати.Результат, Объект.Ссылка);
		
		ФормаПечати.Результат.ОтображатьСетку 	= Ложь;
		ФормаПечати.Результат.Защита 			= Истина;
		ФормаПечати.Результат.ТолькоПросмотр 	= Истина;
		ФормаПечати.Результат.ОтображатьЗаголовки = Ложь;
		ФормаПечати.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("печать: Смета");
		ФормаПечати.открыть();	
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПечатьСметыНаСервере(ТабДок, ОбъектСсылка)
	
	ОбщийМодульСервер.ДобавитьСобытиеЖурналаНаСервере(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Напечатал Смету "), 2);
	Документы.ВыполненияРабот.Смета(ТабДок, ОбъектСсылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьГарантийногоТалона(Команда)
	
	ПараметрыВыбораЭлементов = Новый Структура("ЭтоВыполнениеРабот", Истина);
	ВыборЭлементовПечатнойФормы = получитьформу("ОбщаяФорма.ВыборЭлементовПечатнойФормы", ПараметрыВыбораЭлементов);
	ПереченьЭлементов = ВыборЭлементовПечатнойФормы.ОткрытьМодально();
	
	Если НЕ ПереченьЭлементов = Неопределено Тогда
		ПараметрыФормы = новый Структура;
		ПараметрыФормы.Вставить("Документ", Объект.Ссылка);		
		ФормаПечати = получитьформу("ОбщаяФорма.ФормаПечати", ПараметрыФормы);
		
		ПараметрыПечати = Новый Структура("ПереченьЭлементов", ПереченьЭлементов);
		ПараметрыПечати.Вставить("ЭтоВыполненияРабот", Истина);
		ПараметрыПечати.Вставить("Ссылка", Объект.Ссылка);
		
		ПечатьГарантийногоТалонаНаСервере(ФормаПечати.Результат, ПараметрыПечати);
		
		ФормаПечати.Результат.ОтображатьСетку = Ложь;
		ФормаПечати.Результат.Защита 		  = Истина;
		ФормаПечати.Результат.ТолькоПросмотр  = Истина;
		ФормаПечати.Результат.ОтображатьЗаголовки = Ложь;
		ФормаПечати.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("печать: Гарантийные обязательства");
		
		ФормаПечати.открыть();	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПечатьГарантийногоТалонаНаСервере(ТабДок, Знач ПараметрыПечати)  
	
	ОбщийМодульСервер.ДобавитьСобытиеЖурналаНаСервере(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Напечатал гарантийные обязательства. "), 2);
	ОбщийМодульТоварСервер.ПечатьГарантийногоТалона(ТабДок, ПараметрыПечати);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	ТекущаяСтраница = Элементы.Страницы.ТекущаяСтраница;
	СтраницыПанели  = Элементы.Страницы.ПодчиненныеЭлементы;
	СтраницаОсновноеДополнительно = СтраницыПанели.Индекс(ТекущаяСтраница);	
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаВыдачиОбъектаРаботКлиентуОбратноПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ДатаВыдачиОбъектаРаботКлиентуОбратно) Тогда
		Если НЕ ЗначениеЗаполнено(объект.ДатаПоступленияОбъектаРаботНаНашСклад) 
			или объект.ДатаПоступленияОбъектаРаботНаНашСклад > Объект.ДатаВыдачиОбъектаРаботКлиентуОбратно Тогда
			
			объект.ДатаПоступленияОбъектаРаботНаНашСклад = Объект.ДатаВыдачиОбъектаРаботКлиентуОбратно - 1
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьДнейХранилось();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПоступленияОбъектаРаботНаНашСкладПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(объект.ДатаПоступленияОбъектаРаботНаНашСклад) Тогда
		Если ЗначениеЗаполнено(объект.ДатаВыдачиОбъектаРаботКлиентуОбратно)
			И объект.ДатаВыдачиОбъектаРаботКлиентуОбратно < Объект.ДатаПоступленияОбъектаРаботНаНашСклад Тогда		
			
			объект.ДатаВыдачиОбъектаРаботКлиентуОбратно = Объект.ДатаПоступленияОбъектаРаботНаНашСклад + 1;	
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Объект.СкладДляОбъектаВыполненияРабот) Тогда
			Объект.СкладДляОбъектаВыполненияРабот = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("СкладДляОбъектовКлиентаПоУмолчанию");
		КонецЕсли;
		
	Иначе
		объект.ДатаВыдачиОбъектаРаботКлиентуОбратно = '00010101000000';
	КонецЕсли;
	
	ОбновитьДнейХранилось();
	
КонецПроцедуры

&НаКлиенте
Процедура ВзятьДатуНачалаРабот(Команда)
	
	объект.ДатаПоступленияОбъектаРаботНаНашСклад = объект.Дата;
	Если ЗначениеЗаполнено(объект.ДатаВыдачиОбъектаРаботКлиентуОбратно)
		И объект.ДатаВыдачиОбъектаРаботКлиентуОбратно < Объект.ДатаПоступленияОбъектаРаботНаНашСклад Тогда		
		
		объект.ДатаВыдачиОбъектаРаботКлиентуОбратно = Объект.ДатаПоступленияОбъектаРаботНаНашСклад + 1;	
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.СкладДляОбъектаВыполненияРабот) Тогда
		Объект.СкладДляОбъектаВыполненияРабот = ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("СкладДляОбъектовКлиентаПоУмолчанию");
	КонецЕсли;
	
	ОбновитьДнейХранилось();
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьНалоговойНакладной(Команда)
	
	ПараметрыВыбораЭлементов = Новый Структура("НалоговаяНакладная", Истина);
	ПараметрыВыбораЭлементов.Вставить("Организация", объект.Организация);
	ПараметрыВыбораЭлементов.Вставить("ЭтоВыполнениеРабот", Истина);
	ВыборЭлементовПечатнойФормы = получитьформу("ОбщаяФорма.ВыборЭлементовПечатнойФормы", ПараметрыВыбораЭлементов);
	ПереченьЭлементов = ВыборЭлементовПечатнойФормы.ОткрытьМодально();
	
	Если НЕ ПереченьЭлементов = Неопределено Тогда
		ПараметрыФормы = новый Структура;
		ПараметрыФормы.Вставить("Документ", Объект.Ссылка);		
		ФормаПечати = получитьформу("ОбщаяФорма.ФормаПечати", ПараметрыФормы);
		
		ПараметрыПечати = Новый Структура("ПереченьЭлементов", ПереченьЭлементов);
		ПараметрыПечати.Вставить("Ссылка", Объект.Ссылка);
		ПараметрыПечати.Вставить("ПечататьОригинал", ПереченьЭлементов.ПечататьОригинал);
		ПараметрыПечати.Вставить("ПечататьКопию",  ПереченьЭлементов.ПечататьКопию);
		ПараметрыПечати.Вставить("ЭтоВыполнениеРабот", Истина);
		
		Если ПереченьЭлементов.ПечататьТОРГ12 Тогда
			ПечатьТорг12(ФормаПечати.Результат, Объект.Ссылка, Истина, , ПереченьЭлементов.ТОРГ12ВДваЭкземпляра);
		КонецЕсли;
		Если ПереченьЭлементов.ПечатьСчета Тогда
			ПечатьРН(ФормаПечати.Результат, Объект.Ссылка, Истина, , ПереченьЭлементов.СчетВДваЭкземпляра);
		КонецЕсли;		
		Если ПереченьЭлементов.ПечататьТакжеРасходнуюНакладную Тогда
			ПечатьРН(ФормаПечати.Результат, Объект.Ссылка, Ложь, ПереченьЭлементов.ПечатьСчета, ПереченьЭлементов.РасходнаяВДваЭкземпляра, ПереченьЭлементов.ЗаголовокРасходнойНакладной);
		КонецЕсли;		
		ФормаПечати.Результат.АвтоМасштаб = Ложь;
		
		Если ПереченьЭлементов.ПечататьОригинал 
			или ПереченьЭлементов.ПечататьКопию Тогда			
			
			ПечатьНалоговойНакладнойНаСервере(ФормаПечати.Результат, ПараметрыПечати);	
		КонецЕсли;
		
		ФормаПечати.Результат.ОтображатьСетку = Ложь;
		ФормаПечати.Результат.Защита 		  = Истина;
		ФормаПечати.Результат.ТолькоПросмотр  = Истина;
		ФормаПечати.Результат.ОтображатьЗаголовки = Ложь;
		ФормаПечати.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("печать: Налоговая накладная");
		
		ФормаПечати.открыть();	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПечатьНалоговойНакладнойНаСервере(ТабДок, Знач ПараметрыПечати) 	
	
	ОбщийМодульСервер.ДобавитьСобытиеЖурналаНаСервере(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Напечатал налоговую накладную."), 2);
	ОбщийМодульТоварСервер.ПечатьНалоговойНакладной(ТабДок, ПараметрыПечати);
	
КонецПроцедуры

&НаСервере
Процедура ПечатьРН(ТабДок, Знач ПараметрКоманды, Знач ЭтоСчет, Знач НоСчетЕсть = Ложь, Знач ВДваЭкземпляра = Ложь, Знач ЗаголовокРасходнойНакладной = "")  
	
	ОбщийМодульСервер.ДобавитьСобытиеЖурналаНаСервере(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Напечатал") + " " + ?(ЭтоСчет, ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Счет"), ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Расход товара")), 2);
	Документы.РасходыТовара.Печать(ТабДок, ПараметрКоманды, ЭтоСчет, НоСчетЕсть, Истина , , , ЗаголовокРасходнойНакладной, ВДваЭкземпляра);
	ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
	
КонецПроцедуры

&НаСервере
Процедура ПечатьТорг12(ТабДок, Знач ПараметрКоманды, Знач ЭтоСчет, Знач НоСчетЕсть = Ложь, Знач ТОРГ12ВДваЭкземпляра = Ложь)  
	
	ОбщийМодульСервер.ДобавитьСобытиеЖурналаНаСервере(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Напечатал ТОРГ 12"), 2);
	Документы.РасходыТовара.Печать(ТабДок, ПараметрКоманды, ЭтоСчет, НоСчетЕсть, Истина, Истина, , , ТОРГ12ВДваЭкземпляра);
	ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыДатаПриИзменении(Элемент)
	ПересчитатьСтрокуРабот(Ложь, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура РаботыКоличествоПланПриИзменении(Элемент)
	ПересчитатьСтрокуРабот(Ложь, Ложь, , Истина);
КонецПроцедуры

&НаКлиенте
Процедура РаботыЦенаПланПриИзменении(Элемент)
	ПересчитатьСтрокуРабот(Ложь, Ложь, , Истина);
КонецПроцедуры

&НаКлиенте
Процедура РаботыПроцентСкидкиПланПриИзменении(Элемент)
	ПересчитатьСтрокуРабот(Ложь, Ложь, , Истина);
КонецПроцедуры

&НаКлиенте
Процедура РаботыСуммаБезСкидкиПланПриИзменении(Элемент)
	ПересчитатьСтрокуРабот(Истина, Истина, , Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОплатыКурсПланПриИзменении(Элемент)
	ПересчетИтоговОплаты();
КонецПроцедуры

&НаКлиенте
Процедура ОплатыВалютаПланПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Оплаты.ТекущиеДанные;
	ТекущиеДанные.курсплан = ОбщийМодульПовтор.ПолучитьТекущийКурс(ТекущиеДанные.ВалютаПлан, ?(Объект.Актуален, ТекущиеДанные.Дата, ТекущиеДанные.ДатаПлан));
	ПересчетИтоговОплаты();
	
КонецПроцедуры

&НаКлиенте
Процедура РасходыВалютаПланПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Расходы.ТекущиеДанные;
	ТекущиеДанные.курсПлан = ОбщийМодульПовтор.ПолучитьТекущийКурс(ТекущиеДанные.ВалютаПлан, ?(Объект.Актуален, ТекущиеДанные.Дата, ТекущиеДанные.ДатаПлан));
	ПересчетИтоговРасходов();	
	
КонецПроцедуры

&НаКлиенте
Процедура РасходыКурсПланПриИзменении(Элемент)
	ПересчетИтоговРасходов();	
КонецПроцедуры

&НаКлиенте
Процедура РаботыНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтрокаТовара = Элементы.Работы.ТекущиеДанные;	
	
	ПараметрыФормы = Новый Структура;
	Если ИспользоватьСложныйМеханизмЦен
		и ЗначениеЗаполнено(Объект.ВидЦен) Тогда
		
		ПараметрыФормы.Вставить("ВидЦен", Объект.ВидЦен);
	КонецЕсли;
	
	Если ВестиУчетПоСкладу
		и ЗначениеЗаполнено(СтрокаТовара.Склад) Тогда
		
		ПараметрыФормы.Вставить("ОтборПоСкладу", СтрокаТовара.Склад);
	КонецЕсли;	
	
	ПараметрыФормы.Вставить("ВызовИзРасходаТовара", истина);
	
	ПараметрыФормы.Вставить("ОтборПоДате", ?(Объект.Актуален, СтрокаТовара.Дата, СтрокаТовара.ДатаПлан));
	ПараметрыФормы.Вставить("ТекущаяСтрока", СтрокаТовара.Номенклатура);
	ФормаВыбора = ПолучитьФорму("Справочник.Номенклатура.ФормаВыбора", ПараметрыФормы);
	
	Номенклатура = ФормаВыбора.ОткрытьМодально();
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		Элементы.Работы.ТекущиеДанные.Номенклатура = Номенклатура;	
		ПриИзмененииНоменклатуры(СтрокаТовара) ;
		элементы.Работы.ЗакончитьРедактированиеСтроки(лОЖЬ);
		Если объект.Актуален Тогда
			Элементы.Работы.ТекущийЭлемент = элементы.РаботыКоличество;
		Иначе
			Элементы.Работы.ТекущийЭлемент = элементы.РаботыКоличествоПлан;
		КонецЕсли;			
		
	иначе
		элементы.Работы.ЗакончитьРедактированиеСтроки(истина);			
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = не РаботаетВыборЧастогоСпроса;
КонецПроцедуры

&НаКлиенте
Процедура РаботыНоменклатураАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	СтандартнаяОбработка = не РаботаетВыборЧастогоСпроса;
КонецПроцедуры

&НаКлиенте
Процедура РаботыНоменклатураОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтрокаТовара = Элементы.Работы.ТекущиеДанные;	
	
	ОбщийМодульКлиент.ПоискОшибкиКодировки("Номенклатура", ДанныеВыбора, Текст, СтрокаТовара.Номенклатура);
	
	Если ЗначениеЗаполнено(ДанныеВыбора) Тогда
		ПриИзмененииНоменклатуры(СтрокаТовара) 	;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьВыполнениеПланаДляВыделенныхСтрок(Команда)
	
	Для Каждого ВыделеннаяСтрокаТаблицы Из Элементы.Работы.ВыделенныеСтроки Цикл
		ВыделеннаяСтрока = Элементы.Работы.ДанныеСтроки(ВыделеннаяСтрокаТаблицы);  
		
		ВыделеннаяСтрока.Дата = ОбщийМодульСервисСервер.ПользователяТекущаяДата();
		ВыделеннаяСтрока.Количество = ВыделеннаяСтрока.КоличествоПлан;
		ВыделеннаяСтрока.Цена = ВыделеннаяСтрока.ЦенаПлан;
		ВыделеннаяСтрока.Сумма = ВыделеннаяСтрока.План;
		ВыделеннаяСтрока.ПроцентСкидки = ВыделеннаяСтрока.ПроцентСкидкиПлан;
		ВыделеннаяСтрока.СуммаБезСкидки = ВыделеннаяСтрока.СуммаБезСкидкиПлан;
		ВыделеннаяСтрока.Валюта = ВыделеннаяСтрока.ВалютаПлан;
		ВыделеннаяСтрока.Курс = ВыделеннаяСтрока.Курс;
	КонецЦикла;
	ПересчитатьРезультаты();
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыВалютаПланПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Работы.ТекущиеДанные;
	ТекущиеДанные.курсплан = ОбщийМодульПовтор.ПолучитьТекущийКурс(ТекущиеДанные.ВалютаПлан, ТекущиеДанные.ДатаПлан);
	ПересчитатьРезультаты();
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыВалютаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Работы.ТекущиеДанные;
	ТекущиеДанные.курс = ОбщийМодульПовтор.ПолучитьТекущийКурс(ТекущиеДанные.Валюта, ТекущиеДанные.Дата);
	ПересчитатьРезультаты();
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыКурсПриИзменении(Элемент)
	ПересчитатьРезультаты();
КонецПроцедуры

&НаКлиенте
Процедура РаботыКурсПланПриИзменении(Элемент)
	ПересчитатьРезультаты();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьРезультаты(Команда)
	
	ОбновитьРезультатыНаСервере();
	ПересчитатьРезультаты();		
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьРезультатыНаСервере()
	
	Для Каждого СтрокаТовара Из Объект.Товары Цикл
		//		если не СтрокаТовара.Количество = 0
		//			и ЗначениеЗаполнено(СтрокаТовара.Дата) Тогда
		
		СтрокаТовара.ВаловаяПрибыль = ОбщийМодульСервер.РасчитатьОриентировочнуюВаловуюПрибыль(СтрокаТовара.номенклатура, СтрокаТовара.Количество, СтрокаТовара.Дата, СтрокаТовара.Сумма, СтрокаТовара.Валюта, СтрокаТовара.Курс);
		//		Иначе
		СтрокаТовара.ВаловаяПрибыльПлан = ОбщийМодульСервер.РасчитатьОриентировочнуюВаловуюПрибыль(СтрокаТовара.номенклатура, СтрокаТовара.КоличествоПлан, СтрокаТовара.ДатаПлан, СтрокаТовара.План, СтрокаТовара.ВалютаПлан, СтрокаТовара.КурсПлан);
		//		КонецЕсли;	
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	если НЕ ПараметрыЗаписи.режимзаписи = РежимЗаписиДокумента.Запись тогда
		ОбработатьБлокировку();                                                	
	КонецЕсли;
	
	Если ДопИнформацияИзменена Тогда
		ОбщийМодульСервисСервер.ЗаписатьДополнительнуюИнформацию(Объект.Ссылка, ДополнительнаяИнформация);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектКлиентаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ОбъектКлиента) Тогда
		ОбъектКлиентаПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбъектКлиентаПриИзмененииНаСервере()
	
	Если НЕ ЗначениеЗаполнено(Объект.Клиент) Тогда
		Объект.Клиент = Объект.ОбъектКлиента.СвязанныйКлиент;
	КонецЕсли;
	Если не ЗначениеЗаполнено(Объект.Номенклатура) Тогда
		Объект.Номенклатура = Объект.ОбъектКлиента.СвязаннаяНоменклатура;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	глЧислоОбъектов = глЧислоОбъектов + 1; глПроверятьСообщения = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Источник = "ПодключаемоеОборудование"
		И ВводДоступен() Тогда
		
		Если ИмяСобытия = "ScanData" Тогда
			
			Если Параметр[ 1 ] = Неопределено Тогда
				ТекКод = Параметр[ 0 ];
			Иначе
				ТекКод = Параметр[ 1 ][ 1 ];
			КонецЕсли;
			
			ОбработатьПолученныйШКНаКлиенте(ТекКод);
			
		ИначеЕсли ИмяСобытия = "TracksData" Тогда
			ПолученКодИзСМК(Параметр);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолученКодИзСМК(Параметр)
	
	Если Параметр[1][3] <> Неопределено Тогда
		МКод = Параметр[1][3][0].ДанныеДорожек[0].ЗначениеПоля;
	Иначе
		МКод = Параметр[0];
	КонецЕсли;
	ПолучитьКлиентаНаСервере(МКод);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьКлиентаНаСервере(МКод)
	
	если не ДокументЗаблокирован Тогда
		
		Клиент = ПодключаемоеОборудованиеДСервер.НайтиКлиентаПоМК(МКод);
		Если Клиент <> Неопределено Тогда
			Объект.Клиент = Клиент;
		Иначе
			ПустаяСсылка = Новый (Тип("СправочникСсылка.Клиенты"));
			Объект.Клиент = ПустаяСсылка;
			ОбщегоНазначения.СообщитьПользователю(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Клиент не найден."));
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция   ОбработатьПолученныйШКНаСервере(ТекКод, Количество = 1)
	
	Результат = Истина;
	
	РезультатОбработки = ОбщийМодульТоварСервер.ПолучитьНоменклатуруПоШтрихКоду(ТекКод, Истина, , Объект.Дата);
	
	Если ЗначениеЗаполнено(РезультатОбработки) Тогда
		
		СерияНоменклатуры = Неопределено;
		
		Если ТипЗнч(РезультатОбработки) = Тип("СправочникСсылка.Номенклатура") Тогда  		
			объект.Номенклатура = РезультатОбработки;
		Иначе
			объект.Номенклатура = РезультатОбработки.Номенклатура;
			РезультатОбработки.Свойство("СерияНоменклатуры", объект.СерияНоменклатуры);
			
			СтрокаДисплеяПокупателя = СокрЛП(объект.Номенклатура);		
		КонецЕсли;
		
	Иначе
		
		ОстановитьПоиск = Ложь;
		Если ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетПродажПоСотрудникам") тогда
			РезультатОбработки = ОбщийМодульСервер.ПолучитьСотрудникаПоШтрихКоду(ТекКод);
			если ЗначениеЗаполнено(РезультатОбработки) Тогда
				объект.Сотрудник = РезультатОбработки;
				ОстановитьПоиск = Истина;
			КонецЕсли;	
		КонецЕсли;
		
		Если НЕ ОстановитьПоиск
			И ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ИспользоватьШтрихКодыДляИдентификацииКонтрагентов") Тогда
			
			РезультатОбработки = ОбщийМодульСервер.ПолучитьКонтрагентаПоШтрихКоду(ТекКод, Истина);
			
			Если ЗначениеЗаполнено(РезультатОбработки) Тогда
				Объект.Клиент = РезультатОбработки;
				ПриИзмененииКонтрагента();				
				ОстановитьПоиск = Истина;
			КонецЕсли;		
		КонецЕсли;
		
		Если НЕ ОстановитьПоиск
			И ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ВестиУчетСобственныхЮридическихЛиц") Тогда
			
			РезультатОбработки = ОбщийМодульСервер.ПолучитьКонтрагентаПоШтрихКоду(ТекКод, , , Истина);
			
			Если ЗначениеЗаполнено(РезультатОбработки) Тогда
				Объект.Организация = РезультатОбработки;
				ОрганизацияПриИзмененииНаСервере();	
				ОстановитьПоиск = Истина;
			КонецЕсли;	
		КонецЕсли;
		
		если не ОстановитьПоиск тогда
			ОбщегоНазначения.СообщитьПользователю(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Товар или сотрудник по Штрих-Коду не найден (") + ТекКод + ").");
			Результат = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьПолученныйШКНаклиенте(ТекКод)	
	ОбработатьПолученныйШКНаСервере(ТекКод);	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЧека(Команда)
	
	Текст = ПечатьТоварногоЧекаНаСервере();
	
	Если ИспользоватьПодключаемоеОборудование
		И НЕ Текст = "" Тогда
		
		ОбщийМодульКлиент.ПечатьЧека(Текст, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция   ПечатьТоварногоЧекаНаСервере()
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат ОбщийМодульСервер.ПечатьТоварногоЧека(Объект.Ссылка, Ложь, Объект.КлиентПоставщик, "Товары", ?(ЗначениеЗаполнено(Объект.ХранилищеДенег), Объект.ХранилищеДенег.ФормаОплаты, ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ФормаОплатыДляДенегВДокументахРасходаИПриходаТоваров")), "Оплаты");
	Иначе
		Возврат "";
	КонецЕсли;		
	
КонецФункции 

&НаКлиенте
Процедура ДополнительнаяИнформацияПриИзменении(Элемент)
	ДопИнформацияИзменена = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДополнительнаяИнформацияВидИнформацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура("ТипВладельца", ДопИнфоТипВладельца);
	ФормаВыбораВидаИнформации = ПолучитьФорму("Справочник.ДополнительныеРеквизиты.ФормаВыбора", ПараметрыФормы, ЭтаФорма);	
	ВидИнформации = ФормаВыбораВидаИнформации.ОткрытьМодально();
	Если ЗначениеЗаполнено(ВидИнформации) Тогда
		Элементы.ДополнительнаяИнформация.ТекущиеДанные.ВидИнформации = ВидИнформации;
		Элементы.ДополнительнаяИнформация.ТекущиеДанные.Информация = ОбщийМодульКлиент.ПолучитьЗначениеПоУмолчаниюПоляДополнительнойИнформации(ВидИнформации);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварОтправленПриИзменении(Элемент)
	
	Если Объект.ТоварОтправлен
		и НЕ ЗначениеЗаполнено(Объект.ДатаОтправки) Тогда
		
		Объект.ДатаОтправки = ОбщийМодульСервисСервер.ПользователяТекущаяДата();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КлиентОповещенПриИзменении(Элемент)
	
	Если Объект.КлиентОповещен
		и НЕ ЗначениеЗаполнено(Объект.ДатаОповещения) Тогда
		
		Объект.ДатаОповещения = ОбщийМодульСервисСервер.ПользователяТекущаяДата();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ОбновитьДатуОтсрочки();
КонецПроцедуры

&НаСервере
Процедура ОбновитьДатуОтсрочки()
	
	Если ОбщийМодульПовтор.получитьЗначениеНастройкиИлиКонстанты("ИспользоватьМеханизмОтсрочкиПлатежа") Тогда
		Если ЗначениеЗаполнено(Объект.Клиент) 
			И не Объект.Клиент.СрокОплатыВДняхПоУмолчанию = 0 Тогда
			
			Объект.ДнейОтсрочки = Объект.Клиент.СрокОплатыВДняхПоУмолчанию;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Объект.Договор) Тогда
			
			Если Объект.Договор.УстанавливаетСрокОплатыВДнях = 0 Тогда
				Элементы.ДнейОтсрочки.Доступность = Истина;
			Иначе
				Объект.ДнейОтсрочки = Объект.Договор.УстанавливаетСрокОплатыВДнях;
				Элементы.ДнейОтсрочки.Доступность = Ложь;
			КонецЕсли;
			
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДнейХранилось()
	
	ДнейХранилось = Окр((Объект.ДатаВыдачиОбъектаРаботКлиентуОбратно - Объект.ДатаПоступленияОбъектаРаботНаНашСклад) / (3600 * 24));
	Элементы.ДнейХранилось.Видимость = ДнейХранилось > 0;
		
КонецПроцедуры
