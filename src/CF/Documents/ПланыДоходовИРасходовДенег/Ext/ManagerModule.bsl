#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// sza140530-1704 :
Процедура НаПечать(ДокументДляПечати, Ссылка) Экспорт
	
	ДокументДляПечати.ИмяПараметровПечати  = "ДокПланДохРасх" + СокрЛП(ИмяКомпьютера());
	ДокументДляПечати.КлючПараметровПечати = ДокументДляПечати.ИмяПараметровПечати;
	
	ДокументДляПечати.Очистить();
	
	СсылкаНаОбъектПечати = Ссылка[0];
	
	РазрядМетаданных = ""; ИмяМетаданных = "";
	ПроизвольнаяПечатнаяФорма = ОбщийМодульТекстСервер.ОпределитьПроизвольнуюПечатнуюФорму(, , СсылкаНаОбъектПечати, РазрядМетаданных, ИмяМетаданных);
	
	Если ЗначениеЗаполнено(ПроизвольнаяПечатнаяФорма) Тогда
		ДокументДляПечати = ОбщийМодульТекстСервер.СформироватьДокументПоПроизвольнойФорме(ПроизвольнаяПечатнаяФорма, , ДокументДляПечати, СсылкаНаОбъектПечати, РазрядМетаданных, ИмяМетаданных);
		
	Иначе		
		Макет = Документы.ПланыДоходовИРасходовДенег.ПолучитьМакет("НаПечать");
		Макет.КодЯзыкаМакета = ОбщийМодульПовтор.ПолучитьТекущийЯзыкДокументов();
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ПланыДоходовИРасходовДенег.Дата,
		|	ПланыДоходовИРасходовДенег.ДатаРедакции,
		|	ПланыДоходовИРасходовДенег.ДатаСоздания,
		|	ПланыДоходовИРасходовДенег.ИтогоДоходов,
		|	ПланыДоходовИРасходовДенег.ИтогоРасходов,
		|	ВЫРАЗИТЬ(ПланыДоходовИРасходовДенег.Комментарий КАК СТРОКА(1000)) КАК Комментарий,
		|	ПланыДоходовИРасходовДенег.МесяцПланирования,
		|	ПланыДоходовИРасходовДенег.Номер,
		|	ПланыДоходовИРасходовДенег.Ответственный,
		|	ПланыДоходовИРасходовДенег.ПланНеАктуален,
		|	ПланыДоходовИРасходовДенег.Разница,
		|	ПланыДоходовИРасходовДенег.ПланРасхода.(
		|		НомерСтроки,
		|		СтатьяДвиженияДенег,
		|		Сумма,
		|		Валюта,
		|		Курс,
		|		ОСтроке
		|	),
		|	ПланыДоходовИРасходовДенег.ПланДохода.(
		|		НомерСтроки,
		|		СтатьяДвиженияДенег,
		|		Сумма,
		|		Валюта,
		|		Курс,
		|		ОСтроке)
		|ИЗ Документ.ПланыДоходовИРасходовДенег КАК ПланыДоходовИРасходовДенег
		|ГДЕ ПланыДоходовИРасходовДенег.Ссылка В (&Ссылка)";
		
		Запрос.Параметры.Вставить("Ссылка", Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		
		ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
		Шапка 					= Макет.ПолучитьОбласть("Шапка");
		ОбластьПланРасходаШапка = Макет.ПолучитьОбласть("ПланРасходаШапка");
		ОбластьПланРасхода 		= Макет.ПолучитьОбласть("ПланРасхода");
		ОбластьПланДоходаШапка 	= Макет.ПолучитьОбласть("ПланДоходаШапка");
		ОбластьПланДохода 		= Макет.ПолучитьОбласть("ПланДохода");
		Подвал 					= Макет.ПолучитьОбласть("Подвал");
		
		ВставлятьРазделительСтраниц = ЛОЖЬ;
		Пока Выборка.Следующий() Цикл
			Если ВставлятьРазделительСтраниц Тогда
				ДокументДляПечати.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьЗаголовок);
			ДокументДляПечати.Вывести(ОбластьЗаголовок);
			
			Шапка.Параметры.Заполнить(Выборка);
			ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(Шапка);
			ДокументДляПечати.Вывести(Шапка, Выборка.Уровень());
			
			ВыборкаПланРасхода = Выборка.ПланРасхода.Выбрать();
			Если НЕ ВыборкаПланРасхода.Количество() = 0 Тогда
				ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьПланРасходаШапка);
				ДокументДляПечати.Вывести(ОбластьПланРасходаШапка);
				
				Пока ВыборкаПланРасхода.Следующий() Цикл
					ОбластьПланРасхода.Параметры.Заполнить(ВыборкаПланРасхода);
					ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьПланРасхода);
					ДокументДляПечати.Вывести(ОбластьПланРасхода, ВыборкаПланРасхода.Уровень());
				КонецЦикла;	
			КонецЕсли;
			
			ВыборкаПланДохода = Выборка.ПланДохода.Выбрать();
			Если НЕ ВыборкаПланДохода.Количество() = 0 Тогда
				ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьПланДоходаШапка);
				ДокументДляПечати.Вывести(ОбластьПланДоходаШапка);
				
				Пока ВыборкаПланДохода.Следующий() Цикл
					ОбластьПланДохода.Параметры.Заполнить(ВыборкаПланДохода);
					ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьПланДохода);
					ДокументДляПечати.Вывести(ОбластьПланДохода, ВыборкаПланДохода.Уровень());
				КонецЦикла;	
			КонецЕсли;
			
			Подвал.Параметры.Заполнить(Выборка);
			ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(Подвал);
			ДокументДляПечати.Вывести(Подвал);
			
			ВставлятьРазделительСтраниц = ИСТИНА;
		КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли