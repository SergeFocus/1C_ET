//sza140420-1717 SZA: 
//sza131005-0239 : 

// Обработчик при создании на сервере.
// 1. Инициализируются возможные типы данных конфигурации для представления параметров.
// 2. Формируется путь к имени формы.
// 3. Автосоздание запроса в табличной части.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)          // ПРИ СОЗДАНИИ НА СЕРВЕРЕ
	
	Отказ = ОбщийМодульСервисСервер.ПроверитьОтказДоступа("000100", ЭтаФорма, Отказ, );	
	
	Если НЕ ОТказ Тогда
		ИмяЗапросаПоУмолчанию 			= НСтр("ru = 'Новый запрос'");
		
		ОбработкаОбъект 							= ОбъектОбработки();
		Объект.ДоступныеТипыДанных					= ОбработкаОбъект.Метаданные().Реквизиты.ДоступныеТипыДанных.Тип;
		Объект.ПутьКФормам 							= ОбработкаОбъект.Метаданные().ПолноеИмя() + ".Форма";
		Объект.ЧередованиеЦветовВрезультатеЗапроса 	= Истина;
		
		Элемент 						= Объект.Запросы.Добавить();
		ИдентификаторТекущегоЗапроса	= Новый УникальныйИдентификатор;
		Элемент.Идентификатор  			= ИдентификаторТекущегоЗапроса;
		Элемент.Имя						= ИмяЗапросаПоУмолчанию;
		
		СписокТипов						= ОбъектОбработки().СформироватьСписокТипов();
		ОбъектОбработки().ФильтрацияСпискаТипов(СписокТипов, "");
		
		ЗаголовокФормы 		= НСтр("ru = 'Консоль запросов (%ИмяЗапросаПоУмолчанию%)'");
		ЗаголовокФормы		= СтрЗаменить(ЗаголовокФормы, "%ИмяЗапросаПоУмолчанию%", ИмяЗапросаПоУмолчанию);
		ЭтаФорма.Заголовок 	= ЗаголовокФормы;
		
		Объект.ТипОбхода	= "Авто";
		
		ВключитьРежимВыбора();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	Если Не Модифицированность Тогда 
		Возврат;
	КонецЕсли;	
	
	ИмяФайла = Объект.ИмяФайла;
	
	Режим = РежимДиалогаВопрос.ДаНетОтмена;
	Текст = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Данные изменены. Сохранить изменения?");
	Ответ = Вопрос(Текст, Режим, 0);
	Если Ответ = КодВозвратаДиалога.Да Тогда	
		Если ДиалогСохраненияФайла(ИмяФайла) тогда
			Модифицированность = Ложь;
			Закрыть();
		Иначе
			Отказ = Истина;
		КонецЕсли;	
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда		
		Модифицированность = Ложь;
		Закрыть();
	ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ПередатьПараметрыНастроек" Тогда 
		ВыгрузитьНастройки(Параметр);
	ИначеЕсли ИмяСобытия = "ПередатьПараметрыНастроекАвтоСохранения" Тогда
		НастройкаАвтосохранения();
	ИначеЕсли ИмяСобытия = "ВыгрузитьЗапросыВРеквизиты" Тогда	
		ВыгрузитьЗапросыВРеквизиты(Параметр);
	ИначеЕсли ИмяСобытия = "ОбновитьФормуКлиент" Тогда 
		ОбновитьФормуКлиент();
	КонецЕсли;	
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ТипВФормеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ТекущийПараметр = Элементы.Параметры.ТекущиеДанные;
	
	ЗаголовокЭлемента = НСтр("ru = 'Выбрать тип'");
	ВыбранныйЭлемент = СписокТипов.ВыбратьЭлемент(ЗаголовокЭлемента);
	Если ВыбранныйЭлемент <> Неопределено Тогда 
		ТекущийТип 					= ВыбранныйЭлемент;
		
		Если ТекущийТип.Значение = "ТаблицаЗначений"
			или ТекущийТип.Значение = "МоментВремени" 
			или ТекущийТип.Значение = "Граница" Тогда 
			
			ТипСтрока						= ТипСтрока(ТекущийТип.Значение);
			ТекущийПараметр.Тип 			= ТипСтрока;
			ТекущийПараметр.ТипВФорме 		= ТекущийТип.Представление;
			ТекущийПараметр.Значение 		= "";
			ТекущийПараметр.ЗначениеВФорме 	= ТекущийТип.Представление;
		Иначе
			ИнициализацияТипаИЗначенияПараметра(ТекущийПараметр, ТекущийТип);
		КонецЕсли;	
		
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
	
	Если Не Копирование Тогда 
		ИндексТекущегоЗапроса = ИндексТекущегоВопроса();
		Если ИндексТекущегоЗапроса = Неопределено тогда
			ТекстСообщения = НСтр("ru = 'Выберите запрос.'");
			ПоказатьСообщениеПользователю(ТекстСообщения, "Объект");
			Возврат;
		КонецЕсли;
		
		ЭлементПараметр 						= Объект.Параметры.Добавить();
		ЭлементПараметр.ИдентификаторЗапроса   	= ИдентификаторТекущегоЗапроса;
		ЭлементПараметр.Имя   					= ПолучитьИмяПараметра();
		ЭлементПараметр.Идентификатор   		= Новый УникальныйИдентификатор;
		
		ОбновитьФормуКлиент();
		
		Эл 					= Элементы.Параметры;
		Эл.ТекущаяСтрока 	= ЭлементПараметр.ПолучитьИдентификатор();
		ТипВФормеНачалоВыбора(Эл, Неопределено, Ложь);
	Иначе
		ЭлементКопирования = Элемент.ТекущиеДанные;
		
		ЭлементПараметр 						= Объект.Параметры.Добавить();
		ЭлементПараметр.Идентификатор 			= Новый УникальныйИдентификатор;
		ЭлементПараметр.ИдентификаторЗапроса 	= ИдентификаторТекущегоЗапроса;
		ЭлементПараметр.Имя						= ЭлементКопирования.Имя;
		ЭлементПараметр.Тип						= ЭлементКопирования.Тип;
		ЭлементПараметр.Значение 				= ЭлементКопирования.Значение;
		ЭлементПараметр.ТипВФорме 				= ЭлементКопирования.ТипВФорме;
		ЭлементПараметр.ЗначениеВФорме 			= ЭлементКопирования.ЗначениеВФорме;
	КонецЕсли;	
	
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТекстЗапросаПриИзменении(Элемент)
	ИндексТекущегоЗапроса = ИндексТекущегоВопроса();
	Если ИндексТекущегоЗапроса = Неопределено тогда
		ТекстСообщения = НСтр("ru = 'Выберите запрос.'");
		ПоказатьСообщениеПользователю(ТекстСообщения, "Объект");
		Возврат;
	КонецЕсли;	
	
	ТЗ					= ТекстЗапроса.ПолучитьТекст();
	ТекущийЗапрос		= Объект.Запросы.Получить(ИндексТекущегоЗапроса - 1);
	
	// Если имя запроса по умолчанию, тогда формируется имя запроса.
	Если ТекущийЗапрос.Имя = ИмяЗапросаПоУмолчанию Тогда 
		ТекущийЗапрос.Имя = ПолучитьИмяЗапроса(ТЗ);
	КонецЕсли;	
	
	ТекущийЗапрос.Текст = ТЗ;
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеВФормеПриИзменении(Элемент)
	ТекущийПараметр = Элементы.Параметры.ТекущиеДанные;
	
	Значение		= ТекущийПараметр.ЗначениеВФорме;
	ТекущийТип		= ТекущийПараметр.Тип;
	Если ТекущийТип <> "ТаблицаЗначений" и ТекущийТип <> "МоментВремени" и ТекущийТип <> "Граница" Тогда 
		ЗначВнутр					= ЗначениеВСтрокуСервер(Значение);
		ТекущийПараметр.Значение	= ЗначВнутр;
		
		Модифицированность 			= Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыПослеУдаления(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеВФормеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ТекущийПараметр = Элементы.Параметры.ТекущиеДанные;
	ТипПараметра	= Элементы.Параметры.ТекущиеДанные.ТипВФорме;
	
	ИдентификаторТекущегоПараметра = ТекущийПараметр.Идентификатор;
	ПередаваемыеЗапросы = ПоместитьЗапросыВСтруктуру();
	
	Если ТипПараметра = "Таблица значений" Тогда 
		Путь = Объект.ПутьКФормам + "." + "ТаблицаЗначений";
	ИначеЕсли ТипПараметра = "Момент времени" Тогда 
		Путь = Объект.ПутьКФормам + "." + "МоментВремени";
	ИначеЕсли ТипПараметра = "Граница" Тогда
		Путь = Объект.ПутьКФормам + "." + "Граница";
	Иначе
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму(Путь, ПередаваемыеЗапросы, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура РезультатЗапросаПриИзменении(Элемент)
	// "Захватыватеся" текущий запрос из списка запросов.
	ИндексТекущегоЗапроса = ИндексТекущегоВопроса();
	Если ИндексТекущегоЗапроса = Неопределено тогда
		ТекстСообщения = НСтр("ru = 'Выберите запрос.'");
		ПоказатьСообщениеПользователю(ТекстСообщения, "Объект");
		Возврат;
	КонецЕсли;	
	
	// Выбор текущего запроса.
	ТекущийЗапрос 					= Объект.Запросы.Получить(ИндексТекущегоЗапроса - 1);
	ТекущийЗапрос.АдресРезультата 	= ПоместитьВоВременноеХранилище(РезультатЗапроса, УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура РезультатЗапросаВыбор(Элемент, Область, СтандартнаяОбработка)
	РасшифровкаЯчейки 	= Область.Расшифровка;
	
	ТипРасшифровки = ТипЗнч(РасшифровкаЯчейки);
	
	Если Объект.ДоступныеТипыДанных.СодержитТип(ТипРасшифровки) и РасшифровкаЯчейки <> Неопределено Тогда 
		СтандартнаяОбработка = Ложь;
		ОткрытьЗначение(РасшифровкаЯчейки);
	Иначе	
		СтандартнаяОбработка = Истина;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура РезультатЗапросаПриИзмененииСодержимогоОбласти(Элемент, Область)
	// "Захватыватеся" текущий запрос из списка запросов.
	ИндексТекущегоЗапроса = ИндексТекущегоВопроса();
	Если ИндексТекущегоЗапроса = Неопределено тогда
		ТекстСообщения = НСтр("ru = 'Выберите запрос.'");
		ПоказатьСообщениеПользователю(ТекстСообщения, "Объект");
		Возврат;
	КонецЕсли;	
	
	// Выбор текущего запроса.
	ТекущийЗапрос 					= Объект.Запросы.Получить(ИндексТекущегоЗапроса - 1);
	ТекущийЗапрос.АдресРезультата 	= ПоместитьВоВременноеХранилище(РезультатЗапроса, УникальныйИдентификатор);
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////
// КОМАНДЫ

&НаКлиенте
Процедура ВыполнитьВыборРезультатаЗапроса(ппКоманда)
	Для Каждого СтрокаЗапроса Из Объект.Запросы Цикл 
		Если СтрокаЗапроса.Идентификатор=ИдентификаторТекущегоЗапроса И Не ПустаяСтрока(СтрокаЗапроса.АдресРезультатовЗапроса) Тогда
			ОповеститьОВыборе(Новый Структура("ДействиеВыбора, ДанныеВыбора", 
			Параметры.ДействиеВыбора, СтрокаЗапроса.АдресРезультатовЗапроса
			));
			Возврат;
		КонецЕсли;	
	КонецЦикла;
	
	ТекстПредупреждения = НСтр("ru = 'Необходимо ввести текст запроса и выполнить его.'");	
	Предупреждение(ТекстПредупреждения);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораЗапроса(Команда)
	ПередаваемыеЗапросы = ПоместитьЗапросыВСтруктуру();
	Путь = Объект.ПутьКФормам + "." + "ВыборЗапроса";
	ОткрытьФорму(Путь, ПередаваемыеЗапросы, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКонструкторЗапроса(Команда)
	#Если ТолстыйКлиентУправляемоеПриложение ИЛИ ТолстыйКлиентОбычноеПриложение Тогда
		ИндексТекущегоЗапроса = ИндексТекущегоВопроса();
		Если ИндексТекущегоЗапроса = Неопределено тогда
			ТекстСообщения = НСтр("ru = 'Выберите запрос.'");
			ПоказатьСообщениеПользователю(ТекстСообщения, "Объект");
			Возврат;
		КонецЕсли;
		
		ТЗ = ТекстЗапроса.ПолучитьТекст();
		
		Если ПустаяСтрока(ТЗ) Тогда 
			КонструкторЗапроса = Новый КонструкторЗапроса();
		Иначе
			КонструкторЗапроса = Новый КонструкторЗапроса(ТЗ);
		КонецЕсли;	
		
		Если КонструкторЗапроса.ОткрытьМодально() тогда
			ТекстЗапроса.УстановитьТекст(КонструкторЗапроса.Текст);
			
			ТЗ					= ТекстЗапроса.ПолучитьТекст();
			
			ТекущийЗапрос		= Объект.Запросы.Получить(ИндексТекущегоЗапроса - 1);
			Если ТекущийЗапрос.Имя = ИмяЗапросаПоУмолчанию тогда 
				ТекущийЗапрос.Имя	= ПолучитьИмяЗапроса(ТЗ);
			КонецЕсли;	
			ТекущийЗапрос.Текст = ТЗ;
			Модифицированность = Истина;
			ОбновитьФормуКлиент();
		КонецЕсли;
	#Иначе
		ТекстСообщения = НСтр("ru = 'Конструктор запроса доступен только в режиме Толстого клиента.'");
		Предупреждение(ТекстСообщения);
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура СчитатьПараметрыИзТекстаЗапроса(Команда)
	ЗаполнитьПараметрыКлиент();	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастроекАвтоСохранения(Команда)
	ПередаваемыеНастройки = ПоместитьНастройкиВСтруктуру();
	Путь = Объект.ПутьКФормам + "." + "Настройки";
	
	ОткрытьФорму(Путь, ПередаваемыеНастройки, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗапрос(Команда)
	ЗапуститьВыполнениеЗапроса(Ложь);	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗапросСВременнымиТаблицами(Команда)
	ЗапуститьВыполнениеЗапроса(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СохранитьЗапросыВФайл(Команда)
	ИмяФайла 		= Объект.ИмяФайла;
	Если ДиалогСохраненияФайла(ИмяФайла) Тогда 
		Объект.ИмяФайла 	= ИмяФайла;
		Модифицированность	= Ложь;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьЗапросыВДругойФайл(Команда)
	ИмяФайла = "";
	Если ДиалогСохраненияФайла(ИмяФайла) Тогда 
		Объект.ИмяФайла 	= ИмяФайла;
		Модифицированность	= Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЗапросыИзФайла(Команда)
	
	Попытка
		ОбработкаЧтенияФайла(Истина);
	Исключение
	КонецПопытки;
	
	КоличествоЗапросов = Объект.Запросы.Количество();
	Если КоличествоЗапросов > 0 Тогда 
		ИдентификаторТекущегоЗапроса 	= Объект.Запросы.Получить(0).Идентификатор;
		Модифицированность				= Ложь;
	Иначе
		Элемент 						= Объект.Запросы.Добавить();
		ИдентификаторТекущегоЗапроса	= Новый УникальныйИдентификатор;
		Элемент.Идентификатор  			= ИдентификаторТекущегоЗапроса;
		Элемент.Имя						= ИмяЗапросаПоУмолчанию;
		Результат						= Новый ТабличныйДокумент;
	КонецЕсли;	
	
	ОбновитьФормуКлиент();
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьВыделенныйТекстЗапроса(Команда)
	ЗапуститьВыполнениеЗапроса(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьТекстЗапросаДляКонфигуратора(Команда)
	ПараметрыПередачи = Новый Структура;
	ПараметрыПередачи.Вставить("ТекстЗапроса", ТекстЗапроса.ПолучитьТекст());
	
	Путь = Объект.ПутьКФормам + "." + "ТекстЗапросаДляКонфигуратора";
	ОткрытьФорму(Путь, ПараметрыПередачи, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВОтдельномОкнеТабличныйДокумент(Команда)
	ПараметрыПередачи = Новый Структура;
	ПараметрыПередачи.Вставить("РезультатЗапроса", РезультатЗапроса);
	
	Путь = Объект.ПутьКФормам + "." + "РезультатЗапроса";
	ОткрытьФорму(Путь, ПараметрыПередачи, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьЗапрос(Команда)
	ИндексТекущегоЗапроса = ИндексТекущегоВопроса();
	Если ИндексТекущегоЗапроса = Неопределено тогда
		ПоказатьСообщениеПользователю(НСтр("ru = 'Выберите запрос.'"), "Объект");
		Возврат;
	КонецЕсли;
	
	ЗапросОснование = Объект.Запросы.Получить(ИндексТекущегоЗапроса - 1);
	
	ИдентификаторНовогоЗапроса 	= Новый УникальныйИдентификатор;
	ИмяНовогоЗапроса			= СформироватьИмяКопииЗапроса(ЗапросОснование.Имя);
	
	НовыйЗапрос 				= Объект.Запросы.Добавить();
	НовыйЗапрос.Идентификатор 	= ИдентификаторНовогоЗапроса;
	НовыйЗапрос.Имя				= ИмяНовогоЗапроса;
	НовыйЗапрос.Текст			= ЗапросОснование.Текст;
	
	// Копирование параметров из запроса основания в новый запрос
	// из запроса, имеющего ИдентификаторТекущегоЗапроса.
	СкопироватьПараметрыИзЗапроса(НовыйЗапрос);
	
	// Изменение занчения ИдентификатораТекущегоЗапроса.
	ИдентификаторТекущегоЗапроса = ИдентификаторНовогоЗапроса;
	
	// Обновление формы.
	ОбновитьФормуКлиент();
	
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОбъектИзРезультата(Команда)
	РасшифровкаЯчейки 	= РезультатЗапроса.ТекущаяОбласть.Расшифровка;
	ТипРасшифровки 		= ТипЗнч(РасшифровкаЯчейки);
	Если Объект.ДоступныеТипыДанных.СодержитТип(ТипРасшифровки) и РасшифровкаЯчейки <> Неопределено Тогда 
		ОткрытьЗначение(РасшифровкаЯчейки);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокИзРезультата(Команда)
	РасшифровкаЯчейки 	= РезультатЗапроса.ТекущаяОбласть.Расшифровка;
	ИмяФормыСписка 		= СформироватьИмяФормыСпискаДляСсылки(РасшифровкаЯчейки);
	Если Не ПустаяСтрока(ИмяФормыСписка) Тогда 
		ОткрытьФорму(ИмяФормыСписка, , Этаформа);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьЯчейки(Команда)
	ОбластьРезультата = РезультатЗапроса.ТекущаяОбласть;
	
	ПерваяСтрока 		= ОбластьРезультата.Верх;
	ПерваяКолонка 		= ОбластьРезультата.Лево;
	ПоследняяСтрока 	= ОбластьРезультата.Низ;
	ПоследняяКолонка 	= ОбластьРезультата.Право;
	
	ЭталоннаяОбласть = РезультатЗапроса.Область(ПерваяСтрока, ПерваяКолонка, ПерваяСтрока, ПерваяКолонка);
	ШрифтЭталоннойОбласти = ЭталоннаяОбласть.Шрифт;
	Если ШрифтЭталоннойОбласти = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	ФлагЖирности = Не ШрифтЭталоннойОбласти.Жирный;
	Если ФлагЖирности = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Для ИндексСтроки = ПерваяСтрока по ПоследняяСтрока Цикл 
		Для ИндексКолонки = ПерваяКолонка по ПоследняяКолонка Цикл 
			ТекущаяОбластьРезультата = РезультатЗапроса.Область(ИндексСтроки, ИндексКолонки, ИндексСтроки, ИндексКолонки);
			ТекущийШрифт = ТекущаяОбластьРезультата.Шрифт;
			Если ТекущийШрифт <> Неопределено Тогда 
				ТекущаяОбластьРезультата.Шрифт = Новый Шрифт(ТекущийШрифт,,, ФлагЖирности);
			КонецЕсли;	
		КонецЦикла;
	КонецЦикла;	
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Функция ОбъектОбработки()
	Возврат РеквизитФормыВЗначение("Объект");
КонецФункции

// Передача табличной части "Запросы", "Параметры" в виде структуры.
//
&НаСервере
Функция ПоместитьЗапросыВСтруктуру()
	АдресХранилища		= ОбъектОбработки().ПоместитьЗапросыВоВременноеХранилище(Объект, ИдентификаторТекущегоЗапроса, ИдентификаторТекущегоПараметра);
	ПараметрАдрес		= Новый Структура;
	ПараметрАдрес.Вставить("АдресХранилища", АдресХранилища);
	Возврат ПараметрАдрес;
КонецФункции

// Получение табличной части "Запросы" в виде структуры.
// Возвращает Идентификатор выбранного запроса, обновляет табличную часть "Запросы".
//
// Параметры:
// 	ПараметрыПередачи - Запросы из передаваемого объекта и Идентификатор текущего запроса.
//
&НаСервере
Функция ВыгрузитьЗапросыВРеквизиты(ПараметрыПередачи) 
	ПолученныеЗапросы 				= ПолучитьИзВременногоХранилища(ПараметрыПередачи.АдресХранилища).Запросы;
	ПолученныеПараметры 			= ПолучитьИзВременногоХранилища(ПараметрыПередачи.АдресХранилища).Параметры;
	Объект.ИмяФайла  				= ПолучитьИзВременногоХранилища(ПараметрыПередачи.АдресХранилища).ИмяФайла;
	ИдентификаторТекущегоЗапроса  	= ПолучитьИзВременногоХранилища(ПараметрыПередачи.АдресХранилища).ИдентификаторТекущегоЗапроса;
	ИдентификаторТекущегоПараметра 	= ПолучитьИзВременногоХранилища(ПараметрыПередачи.АдресХранилища).ИдентификаторТекущегоПараметра;
	Объект.Запросы.Загрузить(ПолученныеЗапросы);
	Объект.Параметры.Загрузить(ПолученныеПараметры);
	
	ВывестиРезультатЗапроса();
КонецФункции

&НаСервере
Процедура ВывестиРезультатЗапроса()
	РезультатЗапроса = Новый ТабличныйДокумент;
	Для каждого СтрЗапросы из Объект.Запросы Цикл 
		Если СтрЗапросы.Идентификатор = ИдентификаторТекущегоЗапроса Тогда 
			ПолучитьРезультатЗапросаИзХранилища(РезультатЗапроса, СтрЗапросы.АдресРезультата);
			
			КоличествоСтрок = СтрЗапросы.КоличествоСтрок;
			ВремяВыполнения = СтрЗапросы.ВремяВыполнения;
			РезультСтрока	= НСтр("ru = 'Результат запроса (количество строк = %КолСтрок%, время выполнения = %ВремяВыполнения% с)'");
			РезультСтрока 	= СтрЗаменить(РезультСтрока, "%КолСтрок%", Строка(КоличествоСтрок));
			РезультСтрока 	= СтрЗаменить(РезультСтрока, "%ВремяВыполнения%", Строка(ВремяВыполнения));
			Элементы.РезультатЗапроса.Заголовок = РезультСтрока;
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры	

// Передача настроек автосохранения.
//
&НаСервере
Функция ПоместитьНастройкиВСтруктуру()
	АдресХранилища		= ОбъектОбработки().ПоместитьНастройкиВоВременноеХранилище(Объект);
	ПараметрАдрес		= Новый Структура;
	ПараметрАдрес.Вставить("АдресХранилища", АдресХранилища);
	Возврат ПараметрАдрес;
КонецФункции	

// Получение настроек в виде структуры.
//
// Параметры:
// 	ПараметрыПередачи - настройки.
//
&НаСервере
Функция ВыгрузитьНастройки(ПараметрыПередачи)
	Объект.ИспользоватьАвтосохранение 						= ПолучитьИзВременногоХранилища(ПараметрыПередачи.АдресХранилища).ИспользоватьАвтосохранение;
	Объект.ПериодАвтосохранения								= ПолучитьИзВременногоХранилища(ПараметрыПередачи.АдресХранилища).ПериодАвтосохранения;
	Объект.ВыводитьВРезультатахЗапросаЗначенияСсылок		= ПолучитьИзВременногоХранилища(ПараметрыПередачи.АдресХранилища).ВыводитьВРезультатахЗапросаЗначенияСсылок;
	Объект.ТипОбхода										= ПолучитьИзВременногоХранилища(ПараметрыПередачи.АдресХранилища).ТипОбхода;
	Объект.ЧередованиеЦветовВрезультатеЗапроса				= ПолучитьИзВременногоХранилища(ПараметрыПередачи.АдресХранилища).ЧередованиеЦветовВрезультатеЗапроса;
КонецФункции	

// Обновление формы.
// Обновление отображения параметров, текста запроса и результата.
//
&НаКлиенте
Процедура ОбновитьФормуКлиент() 
	// Обновление параметров.
	Фильтр 				= Новый Структура;
	Фильтр.Вставить("ИдентификаторЗапроса", ИдентификаторТекущегоЗапроса);
	ФиксированныйФильтр = Новый ФиксированнаяСтруктура(Фильтр);
	Элементы.Параметры.ОтборСтрок = ФиксированныйФильтр;
	
	Для каждого СтрЗапросы из Объект.Запросы Цикл 
		Если СтрЗапросы.Идентификатор = ИдентификаторТекущегоЗапроса тогда
			// Обновление текста запроса.
			ЭтаФорма.ТекстЗапроса.УстановитьТекст(СтрЗапросы.Текст);
			
			// Вывод заголовка формы.
			ЗаголовокФормы = НСтр("ru = 'Консоль запросов (%ИмяЗапроса%)'");
			ЗаголовокФормы = СтрЗаменить(ЗаголовокФормы, "%ИмяЗапроса%", СтрЗапросы.Имя);
			ЭтаФорма.Заголовок = ЗаголовокФормы; 
			
		КонецЕсли;	
	КонецЦикла;	
	
	// Вывод заголовка кнопки "Запросы".
	КоличествоЗапросов = Объект.Запросы.Количество();
	ЗаголовокКнопкиВыбораЗапроса = НСтр("ru = 'Запросы'");
	Если КоличествоЗапросов > 1 Тогда 
		ЗаголовокКнопкиВыбораЗапроса = ЗаголовокКнопкиВыбораЗапроса + " (" + КоличествоЗапросов + ")";
	КонецЕсли;	
	Элементы.ФормаВыбратьЗапрос.Заголовок = ЗаголовокКнопкиВыбораЗапроса;
	
	#Если ВебКлиент Тогда
		ЭтаФорма.ОбновитьОтображениеДанных();	
	#КонецЕсли	
КонецПроцедуры	

&НаКлиенте
Функция ИндексТекущегоВопроса()
	ВозврЗнач = Неопределено;
	Для каждого СтрЗапросы из Объект.Запросы Цикл 
		Если СтрЗапросы.Идентификатор = ИдентификаторТекущегоЗапроса тогда
			ВозврЗнач = СтрЗапросы.НомерСтроки;
		КонецЕсли;	
	КонецЦикла;
	Возврат ВозврЗнач;
КонецФункции

// Возвращает имя запроса по имени первой таблицы.
//
// Параметры:
// 	ТекстЗапроса - текст передаваемого запроса.
//
&НаКлиенте
Функция ПолучитьИмяЗапроса(знач ТекстЗапроса)
	// Если пустой текст запроса тогда возвращается "Запрос".
	Если ПустаяСтрока(ТекстЗапроса) тогда
		ВозврЗнач = ИмяЗапросаПоУмолчанию;
		Возврат ВозврЗнач;
	КонецЕсли;
	
	// Поиск зарезервированного слова "ВЫБРАТЬ".
	Выбрать		= "ВЫБРАТЬ";
	ДлинаВыбрать	= СтрДлина(Выбрать);
	ПозицияВыбрать	= Найти(ВРег(ТекстЗапроса), Выбрать);
	Если ПозицияВыбрать = 0 тогда
		ВозврЗнач = ИмяЗапросаПоУмолчанию;
		Возврат ВозврЗнач;
	КонецЕсли;	
	
	// Срез строки текста запроса без зарезервированного слова "ВЫБРАТЬ".
	ДлинаЗапроса    = СтрДлина(ТекстЗапроса);
	ТекстЗапроса 	= Сред(ТекстЗапроса, ПозицияВыбрать + ДлинаВыбрать);
	
	// Поиск первой "точки", чтобы определить имя таблицы.                                                  
	Точка			= ".";
	ДлинаТочка		= СтрДлина(Точка);
	ПозицияТочка 	= Найти(ВРег(ТекстЗапроса), Точка);
	Если ПозицияТочка = 0 тогда
		ВозврЗнач = ИмяЗапросаПоУмолчанию;
		Возврат ВозврЗнач;
	КонецЕсли;
	
	// Возвращается "Запрос:" и имя первой таблицы.
	ВозврЗнач = СокрЛП(Лев(ТекстЗапроса, ПозицияТочка - ДлинаТочка));
	Если ПустаяСтрока(ВозврЗнач) Тогда 
		ВозврЗнач = ИмяЗапросаПоУмолчанию;
	КонецЕсли;	
	
	Возврат ВозврЗнач;
КонецФункции	

// Считывает параметры из текста запроса.
//
// Параметры:
//	ТекстЗапроса - текст запроса.
//	Удалять - флаг очистки списка параметров для текущего запроса.
//  ИдентификаторЗапроса - Идентификатор текущего запроса.
//
&НаСервере
Процедура СчитатьПараметрыЗапроса(ТекстЗапроса, Удалять, ИдентификаторЗапроса)
	// Считывание параметров из текст запроса в массив структуры.
	ВозврСтруктура = ОбъектОбработки().СчитатьПараметрыЗапроса(ТекстЗапроса, ИдентификаторЗапроса);
	
	// Инициализация параметров.
	ПараметрыВФорме = Объект.Параметры;
	
	//Удаление параметров для текущего запроса.
	Если Удалять Тогда 
		КоличествоСтрок  = ПараметрыВФорме.Количество() - 1;
		Пока КоличествоСтрок >= 0 цикл
			ТекущийПараметр = ПараметрыВФорме.Получить(КоличествоСтрок);
			Если ТекущийПараметр.ИдентификаторЗапроса = ИдентификаторЗапроса Тогда 
				ПараметрыВФорме.Удалить(КоличествоСтрок);
				Модифицированность = Истина;
			КонецЕсли;	
			КоличествоСтрок = КоличествоСтрок - 1;
		КонецЦикла;
	КонецЕсли;	
	
	// Добавление параметров.
	Фильтр = Новый Структура;
	Фильтр.Вставить("ИдентификаторЗапроса", ИдентификаторЗапроса);
	МассивПараметров = ПараметрыВФорме.НайтиСтроки(Фильтр);
	
	Для каждого СтрПараметр из ВозврСтруктура Цикл 
		ЕстьПараметр = Ложь;
		Для каждого Стр из МассивПараметров Цикл
			Если СтрПараметр.Имя = Стр.Имя тогда
				ЕстьПараметр = Истина;
			КонецЕсли;
		КонецЦикла;
		Если Не ЕстьПараметр или не Удалять Тогда 
			ДобавитьПараметрВФорму(ПараметрыВФорме, СтрПараметр);
			Модифицированность = Истина;
		КонецЕсли;	
	КонецЦикла;	
КонецПроцедуры

// Добавляет параметр из структуры в параметр формы.
//
// Параметры:
//	ПараметрыВФорме - таблица значений "Парметры" в форме.
//	ПараметрСтруктуры - текущая строка параметра массива структуры.
//
&НаСервере
Процедура ДобавитьПараметрВФорму(ПараметрыВФорме, ПараметрСтруктуры)
	Значение 	= ПараметрСтруктуры.Значение;
	Тип			= ОбъектОбработки().ИмяТипаИзЗначения(Значение);
	
	// Основные реквизиты.
	Элемент							= ПараметрыВФорме.Добавить();
	Элемент.Идентификатор			= Новый УникальныйИдентификатор;
	Элемент.ИдентификаторЗапроса 	= ПараметрСтруктуры.ИдентификаторЗапроса;
	Элемент.Имя						= ПараметрСтруктуры.Имя;
	Элемент.Тип						= Тип;
	Элемент.Значение				= Значение;
	
	Значение = ЗначениеИзСтрокиВнутр(Значение);
	
	// Форменные реквизиты.
	Элемент.ТипВФорме 				= Строка(ТипЗнч(Значение));
	Элемент.ЗначениеВФорме 			= Значение;
КонецПроцедуры	

// Получить ответ пользователя по удалению параметров запроса.
//
&НаКлиенте
Функция ОтветПользователяНаУдалениеПараметровЗапроса()
	Режим = РежимДиалогаВопрос.ДаНет;
	Текст = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Таблица параметров не пуста. Очистить таблицу?");
	Ответ = Вопрос(Текст, Режим, 0);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;		
КонецФункции	

// Вызов процедуры сохранения запросов в файл.
//
&НаКлиенте
Процедура НастройкаАвтосохранения()
	Если Объект.ИспользоватьАвтосохранение тогда
		// Вызов процедуры сохранения запросов в файл.
		ПериодАвтосохранения 		= Объект.ПериодАвтосохранения * 60;
		Если ПериодАвтосохранения > 0 Тогда 
			ПодключитьОбработчикОжидания("СохранитьЗапросы", ПериодАвтосохранения);
		КонецЕсли;	
	Иначе
		ОтключитьОбработчикОжидания("СохранитьЗапросы");
	КонецЕсли;	
КонецПроцедуры

// Процедура сохранения запросов для автосохранения.
//
&НаКлиенте
Процедура СохранитьЗапросы()
	
	ИмяФайла = Объект.ИмяФайла;
	Если Не ПустаяСтрока(ИмяФайла) Тогда 
		ДвоичныеДанные = СохранитьЗапросыСервер();
		ДвоичныеДанные.Записать(ИмяФайла);
		ПоказатьОповещениеПользователя(НСтр("ru = 'Автосохранение прошло успешно.'"), ИмяФайла);
		Модифицированность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Процедура сохранения запросов(серверная часть).
//
&НаСервере
Функция СохранитьЗапросыСервер()
	
	ДвоичныеДанные = ОбъектОбработки().ЗаписатьЗапросыВФайлXML(Объект);
	Возврат ДвоичныеДанные;
	
КонецФункции

// Запускает выполнение запроса.
//
&НаКлиенте
Процедура ЗапуститьВыполнениеЗапроса(ВыводитьВременныеТаблицы)
	// "Захватыватеся" текущий запрос из списка запросов.
	ИндексТекущегоЗапроса = ИндексТекущегоВопроса();
	Если ИндексТекущегоЗапроса = Неопределено тогда
		ТекстСообщения = НСтр("ru = 'Выберите запрос.'");
		ПоказатьСообщениеПользователю(ТекстСообщения, "Объект");
		Возврат;
	КонецЕсли;	
	
	Если ПустаяСтрока(Элементы.ТекстЗапроса) Тогда 
		ТекстПредупреждения = НСтр("ru = 'Введите текст запроса.'");
		Предупреждение(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	НеОтформатированныйТекст 	= ТекстЗапроса.ПолучитьТекст();
	ОтформатированныйТекст		= СтрЗаменить(НеОтформатированныйТекст, "|", "");
	
	ТекстЗапроса.УстановитьТекст(ОтформатированныйТекст);
	Объект.Запросы.Получить(ИндексТекущегоЗапроса - 1).Текст = ОтформатированныйТекст;
	
	// Определение текста запроса.
	ВыделенныйТекст = Элементы.ТекстЗапроса.ВыделенныйТекст;
	Если Не ПустаяСтрока(ВыделенныйТекст) Тогда 
		Текст = ВыделенныйТекст;
	Иначе
		Текст = Объект.Запросы.Получить(ИндексТекущегоЗапроса - 1).Текст;
	КонецЕсли;	
	
	ВыводитьИдентификатор	= Объект.ВыводитьВРезультатахЗапросаЗначенияСсылок;
	
	// Очистка табличного документа "РезультатЗапроса" в форме.
	РезультатЗапроса 		= Новый ТабличныйДокумент;
	ТекстСообщения			= "";
	
	ОчиститьСообщения();
	
	// Серверная часть для выполнения запроса.
	ВыполнитьЗапросСервер(ИндексТекущегоЗапроса, РезультатЗапроса, ВыводитьВременныеТаблицы, ВыводитьИдентификатор, Текст, ТекстСообщения);
	
	Если Не ПустаяСтрока(ТекстСообщения) Тогда 
		ПоказатьСообщениеПользователю(ТекстСообщения, "Объект");
	КонецЕсли;	
КонецПроцедуры	

// Загружает в результат запроса возвращаемый из Временного хранилища табличный документ.
//
// Параметры:
//	РезультатЗапроса - Результат запроса.
//	АдресРезультата - адрес временного хранилища, хранящийся.
//
&НаСервере
Процедура ВыполнитьЗапросСервер(ИндексТекущегоЗапроса, ТабличныйДокументРезультата, ВыводитьВременныеТаблицы, ВыводитьИдентификатор, Текст, ТекстСообщения)
	// Заполнение параметров.
	ЗаполнитьПараметрыПриВыполненииЗапроса(Текст);
	
	// Обнуление параметры.
	ВремяВыполнения = 0;
	КоличествоСтрок = 0;
	
	// Выбор текущего запроса.
	ТекущийЗапрос = Объект.Запросы.Получить(ИндексТекущегоЗапроса - 1);
	// Выбор параметров по текущему запросу.
	Фильтр = Новый Структура;
	Фильтр.Вставить("ИдентификаторЗапроса", ТекущийЗапрос.Идентификатор);
	МассивПараметров = Объект.Параметры.НайтиСтроки(Фильтр);
	
	МассивМаксШириныЯчеек = Новый Массив;
	МассивМаксШириныЯчеек.Очистить();
	
	ПорядокОбхода 				= Объект.ТипОбхода;
	ИспользованиеЧередования    = Объект.ЧередованиеЦветовВрезультатеЗапроса;
	
	// Выполнение запроса.
	
	// Будет сохранять результат запроса вместе с табличным документом - представлением результата...
	Результат = ОбъектОбработки().ВыполнитьЗапрос(Текст, МассивПараметров, ВыводитьВременныеТаблицы, ВыводитьИдентификатор, ПорядокОбхода, ТабличныйДокументРезультата, ВремяВыполнения, КоличествоСтрок, ТекстСообщения, ИспользованиеЧередования);
	
	// ...только если находимся в режиме выбора
	Если Не Параметры.РежимВыбора Тогда
		Результат = Неопределено;
	КонецЕсли; 
	
	// Заполнение адреса временного хранилища для результата.
	ТекущийЗапрос.АдресРезультата = ПоместитьВоВременноеХранилище(ТабличныйДокументРезультата, УникальныйИдентификатор);
	ТекущийЗапрос.ВремяВыполнения = ВремяВыполнения;
	ТекущийЗапрос.КоличествоСтрок = КоличествоСтрок;
	
	Если Не ПустаяСтрока(ТекущийЗапрос.АдресРезультатовЗапроса) Тогда
		УдалитьИзВременногоХранилища(ТекущийЗапрос.АдресРезультатовЗапроса)
	КонецЕсли;
	Если Результат=Неопределено Тогда
		ТекущийЗапрос.АдресРезультатовЗапроса = "";
	Иначе
		ТекущийЗапрос.АдресРезультатовЗапроса = ПоместитьВоВременноеХранилище(Результат, ЭтаФорма.УникальныйИдентификатор);
	КонецЕсли;		
	
	// Обновление заголовка результата запроса.
	РезультСтрока	= НСтр("ru = 'Результат запроса (количество строк = %КоличествоСтрок%, время выполнения = %ВремяВыполнения% с)'");
	РезультСтрока 	= СтрЗаменить(РезультСтрока, "%КоличествоСтрок%", Строка(КоличествоСтрок));
	РезультСтрока 	= СтрЗаменить(РезультСтрока, "%ВремяВыполнения%", Строка(ВремяВыполнения));
	
	Элементы.РезультатЗапроса.Заголовок = РезультСтрока;
КонецПроцедуры

&НаСервере
Процедура ПолучитьРезультатЗапросаИзХранилища(ТабличныйДокументРезультата, АдресРезультата)
	Если НЕ ПустаяСтрока(АдресРезультата) тогда
		РезультатИзВременногоХранилища 	= ПолучитьИзВременногоХранилища(АдресРезультата);
		ТабличныйДокументРезультата 	= РезультатИзВременногоХранилища;
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Функция ЗначениеВСтрокуСервер(Значение)
	ВозврЗнач = ЗначениеВСтрокуВнутр(Значение);
	Возврат ВозврЗнач;
КонецФункции	

&НаСервере
Функция ЗначениеИзСтрокиСервер(СтрокаВнутр)
	
	ВозврЗнач = ЗначениеИзСтрокиВнутр(СтрокаВнутр);
	Возврат ВозврЗнач;
	
КонецФункции	

// Возвращает строкове представление типа.
// Например, для ссылки справочника возвращает "CatalogRef.ИмяСправочника"
//
&НаСервере
Функция ТипСтрока(Значение)
	
	СписокДобавленныхТипов = новый СписокЗначений;
	ОбъектОбработки().СформироватьСписокТипов(СписокДобавленныхТипов);
	
	ТипСтрока = Строка(Тип(Значение));
	Если Значение = "СписокЗначений" Тогда 
		Возврат "СписокЗначений";
	КонецЕсли;
	
	ТипНайден = Ложь;
	Для Каждого ЭлементСписка из СписокДобавленныхТипов Цикл 
		Если ЭлементСписка.Представление = ТипСтрока Тогда 
			ТипНайден = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ТипНайден Тогда 
		ТипСтрока	= XMLТип(Тип(Значение)).ИмяТипа;
	КонецЕсли;
	
	Возврат ТипСтрока;
	
КонецФункции

// Формирование диалога по сохранению файла запросов.
//
&НаКлиенте
Функция ДиалогСохраненияФайла(ИмяФайла)
	#Если ВебКлиент тогда
		УстановитьРасширениеРаботыСФайлами(); 
		РасширениеПодключено = ПодключитьРасширениеРаботыСФайлами(); 
	#Иначе
		РасширениеПодключено = Истина;
	#КонецЕсли
	Если РасширениеПодключено Тогда 
		Если ПустаяСтрока(ИмяФайла) Тогда 
			Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
			Диалог.Заголовок					= НСтр("ru = 'Выберите файл запросов'");
			Диалог.ПредварительныйПросмотр  	= Ложь;
			Диалог.Фильтр   					= НСтр("ru = 'Файл запросов (*.q1c)|*.q1c'");
			Диалог.Расширение   				= "q1c";
			Диалог.ПроверятьСуществованиеФайла  = Истина;
			Диалог.МножественныйВыбор			= Ложь;
			
			Если Диалог.Выбрать() тогда
				ИмяФайла = Диалог.ПолноеИмяФайла;
			КонецЕсли;
		КонецЕсли;
		
		// Записываются текст и параметры запроса в файл.
		Если Не ПустаяСтрока(ИмяФайла) Тогда 
			ДвоичныеДанные = СохранитьЗапросыСервер();
			ДвоичныеДанные.Записать(ИмяФайла);
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;	
	Иначе 
		ТекстСообщения = НСтр("ru = 'В данном браузере невозможно работать с файлами.'");
		ПоказатьСообщениеПользователю(ТекстСообщения, "Объект");
	КонецЕсли;	
КонецФункции

&НаКлиенте
Процедура ОбработкаЧтенияФайла(Удалять)
	// Выбор файла для загрузки.
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок					= НСтр("ru = 'Выберите файл запросов'");
	Диалог.ПредварительныйПросмотр  	= Ложь;
	Диалог.Фильтр   					= НСтр("ru = 'Файл запросов (*.q1c)|*.q1c'");
	Диалог.Расширение   				= "q1c";
	Диалог.ПроверятьСуществованиеФайла  = Истина;
	Диалог.МножественныйВыбор			= Ложь;
	Если Диалог.Выбрать() тогда
		ИмяФайла 		= Диалог.ПолноеИмяФайла;
	КонецЕсли;
	
	// Чтение данных из файла.
	Если Не ПустаяСтрока(ИмяФайла) Тогда 
		Если Удалять Тогда 
			Объект.Запросы.Очистить();
			Объект.Параметры.Очистить();
		КонецЕсли;
		ДвоичныеДанные = Новый ДвоичныеДанные(ИмяФайла);
		ЗагрузитьЗапросыИзФайла(ДвоичныеДанные);
		Объект.ИмяФайла = ИмяФайла;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьЗапросыИзФайла(ДвоичныеДанные)
	
	ОбъектВнешнейОбработки = ОбъектОбработки().ПрочитатьЗапросыИзФайлаXML(ДвоичныеДанные);
	ЗаполнитьЗапросыИПараметрыИзОбъектаВнешнейОбработки(ОбъектВнешнейОбработки);
	ВывестиРезультатЗапроса();
	
КонецПроцедуры

// Заполняет из объекта внешней обработки запросы и параметры.
//
// Параметры:
//	ОбъектОбработки - объект обработки.
//
&НаСервере
Процедура ЗаполнитьЗапросыИПараметрыИзОбъектаВнешнейОбработки(ОбъектОбработки)
	ЗапросыОб 	= ОбъектОбработки.Запросы;
	ПараметрыОб	= ОбъектОбработки.Параметры;
	
	Объект.Запросы.Очистить();
	Объект.Параметры.Очистить();
	
	// Заполнение запросов и параметров в форме.
	Для каждого ТекЗапрос из ЗапросыОб цикл
		ЭлементЗапроса 						= Объект.Запросы.Добавить();
		ЭлементЗапроса.Идентификатор		= ТекЗапрос.Идентификатор;
		ЭлементЗапроса.Имя					= ТекЗапрос.Имя;
		ЭлементЗапроса.Текст 				= ТекЗапрос.Текст;
	КонецЦикла;	
	
	Для каждого ТекПараметр из ПараметрыОб цикл
		ТипСтрока 	= ТекПараметр.Тип;
		
		Значение	= ТекПараметр.Значение;
		Значение    = ЗначениеИЗСтрокиВнутр(Значение);
		
		Если ТипСтрока = "ТаблицаЗначений" или ТипСтрока = "МоментВремени" или ТипСтрока = "Граница" Тогда
			ЭлементПараметр								= Объект.Параметры.Добавить();
			ЭлементПараметр.ИдентификаторЗапроса		= ТекПараметр.ИдентификаторЗапроса;
			ЭлементПараметр.Идентификатор				= ТекПараметр.Идентификатор;
			ЭлементПараметр.Имя							= ТекПараметр.Имя;
			ЭлементПараметр.Тип		 					= СписокТипов.НайтиПоЗначению(ТипСтрока).Значение;
			ЭлементПараметр.Значение 					= ТекПараметр.Значение;
			ЭлементПараметр.ТипВФорме					= СписокТипов.НайтиПоЗначению(ТипСтрока).Представление;
			ЭлементПараметр.ЗначениеВФорме				= ОбъектОбработки().ФормированиеПредставленияЗначения(Значение);
		Иначе
			Массив 		= новый Массив;
			Массив.Добавить(Тип(ТипСтрока));
			Описание	= Новый ОписаниеТипов(Массив);
			
			ЭлементПараметр								= Объект.Параметры.Добавить();
			ЭлементПараметр.ИдентификаторЗапроса		= ТекПараметр.ИдентификаторЗапроса;
			ЭлементПараметр.Идентификатор				= ТекПараметр.Идентификатор;
			ЭлементПараметр.Имя							= ТекПараметр.Имя;
			ЭлементПараметр.Тип 						= ТипСтрока;
			ЭлементПараметр.ТипВФорме					= Описание;
			ЭлементПараметр.Значение					= ЗначениеВСтрокуВнутр(Значение);
			ЭлементПараметр.ЗначениеВФорме				= Значение;
		КонецЕсли;	
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ИнициализацияТипаИЗначенияПараметра(ТекущийПараметр, ТекущийТип)
	// Тип в табличной части.
	ТипСтрока					= ТипСтрока(ТекущийТип.Значение);
	ТекущийПараметр.Тип 		= ТипСтрока;
	
	// Тип в форме.
	Массив = Новый Массив;
	Массив.Добавить(Тип(ТекущийПараметр.Тип));
	Описание = Новый ОписаниеТипов(Массив);
	
	ТекущийПараметр.ТипВФорме 		= ТекущийТип.Представление;
	
	// Значение.
	Значение						= Описание.ПривестиЗначение(Тип(ТекущийТип.Значение));
	ТекущийПараметр.ЗначениеВФорме	= Значение;   
	
	ЗначениеВнутр					= ЗначениеВСтрокуСервер(Значение);
	ТекущийПараметр.Значение		= ЗначениеВнутр;
КонецПроцедуры	

&НаСервере
Функция ПолучитьИмяПараметра()
	ПараметрыВФорме = Объект.Параметры;
	Флаг = Истина;
	Индекс = 0;
	
	Пока Флаг Цикл
		Имя = "Параметр" + Строка(Формат(Индекс, "ЧН=-"));
		Имя = СтрЗаменить(Имя, "-", "");
		Фильтр = Новый Структура("Имя", Имя);
		
		ОтфильтрованныеСтроки = ПараметрыВФорме.НайтиСтроки(Фильтр);
		Если ОтфильтрованныеСтроки.Количество() = 0 Тогда
			ВозврЗнач 	= Имя;
			Флаг 		= Ложь;
		КонецЕсли;	
		Индекс = Индекс+1;
	КонецЦикла; 
	
	Возврат ВозврЗнач;
КонецФункции	

// Пока зывает сообщение или предупреждение пользователю
//
// Параметры:
//	ТекстСообщения - текст передаваемого сообщения.
//	ПутьКДанным - путь к данным для сообщения.
//
&НаКлиенте
Процедура ПоказатьСообщениеПользователю(ТекстСообщения, ПутьКДанным)
	ОчиститьСообщения();
	Сообщение 				= Новый СообщениеПользователю(); 
	Сообщение.Текст 		= ТекстСообщения;
	Сообщение.ПутьКДанным 	= ПутьКДанным;
	Сообщение.УстановитьДанные(Объект); 
	Сообщение.Сообщить();
КонецПроцедуры	

&НаКлиенте
Процедура ЗаполнитьПараметрыКлиент()
	// "Захватыватеся" текущий запрос из списка запросов.
	ИндексТекущегоЗапроса = ИндексТекущегоВопроса();
	Если ИндексТекущегоЗапроса = Неопределено тогда
		ПоказатьСообщениеПользователю(НСтр("ru = 'Выберите запрос.'"), "Объект");
		Возврат;
	КонецЕсли;
	
	ТекущийЗапрос = Объект.Запросы.Получить(ИндексТекущегоЗапроса - 1);
	
	Если Не ПустаяСтрока(ТекущийЗапрос.Текст) тогда
		Фильтр = Новый Структура;
		Фильтр.Вставить("ИдентификаторЗапроса", ИдентификаторТекущегоЗапроса);
		МассивПараметров = Объект.Параметры.НайтиСтроки(Фильтр);
		
		Если МассивПараметров.Количество() > 0 Тогда 
			Ответ = ОтветПользователяНаУдалениеПараметровЗапроса();
		иначе
			Ответ = Истина;
		КонецЕсли;
		
		Ошибка			 	= "";
		СчитатьПараметрыЗапроса(ТекущийЗапрос.Текст, Ответ, ИдентификаторТекущегоЗапроса);
		
		ОбновитьФормуКлиент();
	Иначе
		Предупреждение(НСтр("ru = 'Текст запроса пустой.'"));
	КонецЕсли;
КонецПроцедуры	

&НаСервере
Функция СформироватьИмяФормыСпискаДляСсылки(Ссылка)
	ИмяФормыСписка = "";
	
	Если Ссылка = Неопределено Тогда
		Возврат ИмяФормыСписка;
	КонецЕсли;	
	
	Попытка
		ИмяФормыСписка = Ссылка.Метаданные().ОсновнаяФормаСписка.ПолноеИмя();
	Исключение
	КонецПопытки;	
	
	Возврат ИмяФормыСписка;
КонецФункции	

&НаСервере
Процедура ЗаполнитьПараметрыПриВыполненииЗапроса(ТекстЗапроса)
	// Считывание параметров из текст запроса в массив структуры.
	ВозврСтруктура = ОбъектОбработки().СчитатьПараметрыЗапроса(ТекстЗапроса, ИдентификаторТекущегоЗапроса);
	
	ФлагВыводаСообщения = Ложь;
	ПараметрыВФорме 	= Объект.Параметры;
	
	Для каждого СчитанныйПараметр из ВозврСтруктура Цикл
		Фильтр = Новый Структура;
		Фильтр.Вставить("ИдентификаторЗапроса", ИдентификаторТекущегоЗапроса);
		МассивПараметров = Объект.Параметры.НайтиСтроки(Фильтр);
		
		НайденПараметр = Ложь;
		Для Индекс = 0 по МассивПараметров.Количество() - 1 Цикл 
			Если НРег(МассивПараметров.Получить(Индекс).Имя) = НРег(СчитанныйПараметр.Имя) Тогда 
				НайденПараметр = Истина;
			КонецЕсли;	
		КонецЦикла;	
		
		Если не НайденПараметр Тогда 
			Если Не ФлагВыводаСообщения Тогда
				Сообщение 				= Новый СообщениеПользователю(); 
				Сообщение.Текст 		= НСтр("ru = 'Найденные параметры были добавлены автоматически.'");
				Сообщение.ПутьКДанным 	= "Объект";
				Сообщение.УстановитьДанные(Объект); 
				Сообщение.Сообщить();
				ФлагВыводаСообщения = Истина;
			КонецЕсли;
			ДобавитьПараметрВФорму(ПараметрыВФорме, СчитанныйПараметр);
		КонецЕсли;	
	КонецЦикла;	
КонецПроцедуры

// Копирует параметры из запроса имеющего идентификатор текущего запроса.
//
// Параметры:
//	ЗапросПолучатель - запрос, к которому привязываются параметры.
//
&НаКлиенте
Процедура СкопироватьПараметрыИзЗапроса(ЗапросПолучатель)
	ПараметрыЗапроса = Объект.Параметры;
	
	МассивПараметров = Новый Массив;
	
	Для каждого ТекущийПараметр из ПараметрыЗапроса Цикл
		Если ТекущийПараметр.ИдентификаторЗапроса <> ИдентификаторТекущегоЗапроса Тогда 
			Продолжить;
		КонецЕсли;
		МассивПараметров.Добавить(ТекущийПараметр);
	КонецЦикла;	
	
	КолПараметров = МассивПараметров.Количество();
	Для Индекс = 0 по КолПараметров - 1 Цикл 		
		ЭлементПараметр 						= Объект.Параметры.Добавить();
		ЭлементПараметр.Идентификатор 			= Новый УникальныйИдентификатор;
		ЭлементПараметр.ИдентификаторЗапроса 	= ЗапросПолучатель.Идентификатор;
		ЭлементПараметр.Имя						= МассивПараметров.Получить(Индекс).Имя;
		ЭлементПараметр.Тип						= МассивПараметров.Получить(Индекс).Тип;
		ЭлементПараметр.Значение 				= МассивПараметров.Получить(Индекс).Значение;
		ЭлементПараметр.ТипВФорме 				= МассивПараметров.Получить(Индекс).ТипВФорме;
		ЭлементПараметр.ЗначениеВФорме 			= МассивПараметров.Получить(Индекс).ЗначениеВФорме;
	КонецЦикла;
КонецПроцедуры	

// Формирует имя копии запроса.
//
// Параметры:
//	Имя - передаваемое имя запроса.
//
&НаКлиенте
Функция СформироватьИмяКопииЗапроса(Имя)
	Флаг 	= Истина;
	Индекс 	= 1;
	
	Пока Флаг Цикл 
		ФормируемоеИмяЗапроса = НСтр("ru = '%ИмяЗапроса% - Копия %НомерКопии%'");
		ФормируемоеИмяЗапроса = СтрЗаменить(ФормируемоеИмяЗапроса, "%ИмяЗапроса%", Имя);
		ФормируемоеИмяЗапроса = СтрЗаменить(ФормируемоеИмяЗапроса, "%НомерКопии%", Индекс);
		
		Фильтр = Новый Структура;
		Фильтр.Вставить("Имя", ФормируемоеИмяЗапроса);
		
		МассивЗапросовПоФильтру = Объект.Запросы.НайтиСтроки(Фильтр);
		
		Если МассивЗапросовПоФильтру.Количество() = 0 Тогда 
			Флаг = Ложь;
		КонецЕсли;	
		
		Индекс 	= Индекс + 1;
	КонецЦикла;	
	
	Возврат ФормируемоеИмяЗапроса;
КонецФункции	

//  Анализирует параметры запуска формы и по необходимости настраивает режим выбора 
&НаСервере
Процедура ВключитьРежимВыбора()
	
	НоваяКнопка = Элементы.ФормаВыполнитьВыборРезультатаЗапроса;
	НоваяКнопка.Видимость   = Параметры.РежимВыбора;
	НоваяКнопка.Доступность = НоваяКнопка.Видимость;
	
	Если Не НоваяКнопка.Видимость Тогда
		Возврат;
	КонецЕсли;		
	
	НоваяКнопка.КнопкаПоУмолчанию = Истина;
	
	ЗакрыватьПриВыборе = Параметры.ЗакрыватьПриВыборе;
	Если Не ПустаяСтрока(Параметры.Заголовок) Тогда
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура ПриОткрытии(Отказ)                                                                    // ПРИ ОТКРЫТИИ
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 0);
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()                                                  // ПРИ ЗАКРЫТИИ
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 1);
КонецПроцедуры
