
&НаСервере
Процедура ДобавитьОМД(ОбъектыМетаданных, ИндексКартинки) 
	
	Для Каждого ОМД Из ОбъектыМетаданных Цикл
		
		Если ОМД.ПолнотекстовыйПоиск = Метаданные.СвойстваОбъектов.ИспользованиеПолнотекстовогоПоиска.Использовать Тогда
			НоваяСтрока = ТаблицаОМД.Добавить();
			НоваяСтрока.ПолныйПуть = ОМД.ПолноеИмя();
			
			Если СписокМетаданных.НайтиПоЗначению(НоваяСтрока.ПолныйПуть) = Неопределено Тогда
				НоваяСтрока.Отметка = ЛОЖЬ;
			Иначе
				НоваяСтрока.Отметка = ИСТИНА;
			КонецЕсли;	
			
			НоваяСтрока.Представление = Строка(ОМД);
			НоваяСтрока.ИндексКартинки = ИндексКартинки;
		КонецЕсли;	
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	СписокМетаданных = Новый СписокЗначений;
	
	Для Каждого ОМД Из ТаблицаОМД Цикл
		Если ОМД.Отметка = ИСТИНА Тогда
			СписокМетаданных.Добавить(ОМД.ПолныйПуть, ОМД.Представление);
		КонецЕсли;	
	КонецЦикла;
	
	Закрыть(СписокМетаданных);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СписокМетаданных = Параметры.СписокМетаданных;
	
	ДобавитьОМД(Метаданные.Справочники, 0);
	ДобавитьОМД(Метаданные.Документы, 1);
	ДобавитьОМД(Метаданные.ПланыОбмена, 2);
	ДобавитьОМД(Метаданные.ПланыВидовХарактеристик, 3);
	ДобавитьОМД(Метаданные.ПланыСчетов, 4);
	ДобавитьОМД(Метаданные.ПланыВидовРасчета, 5);
	ДобавитьОМД(Метаданные.РегистрыРасчета, 6);
	ДобавитьОМД(Метаданные.РегистрыСведений, 7);
	ДобавитьОМД(Метаданные.РегистрыНакопления, 8);
	ДобавитьОМД(Метаданные.РегистрыБухгалтерии, 9);
	ДобавитьОМД(Метаданные.БизнесПроцессы, 10);
	ДобавитьОМД(Метаданные.Задачи, 11);
КонецПроцедуры



