// Признак использования настроек
Перем мИспользоватьНастройки Экспорт;

// Типы объектов, для которых может использоваться обработка.
// По умолчанию для всех.
Перем мТипыОбрабатываемыхОбъектов Экспорт;

Перем мНастройка;

Перем мИмяТипаОбъектов;
Перем мТипНомера;
Перем мДлинаНомера;

// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // 
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Определяет и устанавливает Тип и Длину номера объекта
// 
// Параметры:
//  Нет.
// 
Процедура вОпределитьТипИДлинуНомера()
	мИмяТипаОбъектов = ЭтаФорма.ВладелецФормы.ОбъектПоиска.Тип;
	Если мИмяТипаОбъектов = "Документ" Тогда
		мТипНомера   = Строка(ЭтаФорма.ВладелецФормы.ОбъектПоиска.Объект.ТипНомера);
		мДлинаНомера = ЭтаФорма.ВладелецФормы.ОбъектПоиска.Объект.ДлинаНомера;
	ИначеЕсли мИмяТипаОбъектов = "Справочник" Тогда
		мТипНомера   = Строка(ЭтаФорма.ВладелецФормы.ОбъектПоиска.Объект.ТипКода);
		мДлинаНомера = ЭтаФорма.ВладелецФормы.ОбъектПоиска.Объект.ДлинаКода;
	КонецЕсли; 
КонецПроцедуры // () 

// Выполняет обработку объектов.
// 
// Параметры:
//  Нет.
// 
Функция вВыполнитьОбработку() Экспорт
	вОпределитьТипИДлинуНомера();
	Если (СпособОбработкиПрефиксов = 1) И (НеИзменятьЧисловуюНумерацию) Тогда
		Возврат 0;
	КонецЕсли;
	
	Если (НачальныйНомер = 0) И (Не НеИзменятьЧисловуюНумерацию) Тогда
		Предупреждение(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Измените начальный номер!"));
		Возврат 0;
	КонецЕсли;
	
	Если Не НеИзменятьЧисловуюНумерацию Тогда 
		ЧисловаяЧастьНомера = НачальныйНомер;
	КонецЕсли;

	НеУникальныеНомера = Новый Соответствие;
	МаксимальныйНомер  = Число(вДополнитьСтрокуСимволами("", мДлинаНомера, "9"));
	
	// --------------------------------------------------------------------------------------------------
	
	Для Сч = 0 По НайденныеОбъекты.Количество()-1 Цикл
		
		ОбработкаПрерыванияПользователя();
		
		Строка = НайденныеОбъекты.Получить(Сч);		
		Если Не Строка.Пометка Тогда
			Продолжить;
		КонецЕсли;
	
		Объект = Строка.Объект.ПолучитьОбъект();

		
		// Здесь может быть написан произвольный алгоритм обработки объектов.
	
		Если мТипНомера = "Число" Тогда 
			Если Не НеИзменятьЧисловуюНумерацию Тогда
				Если мИмяТипаОбъектов = "Документ" Тогда
					Объект.Номер = ЧисловаяЧастьНомера;
				Иначе
					Объект.Код = ЧисловаяЧастьНомера;
				КонецЕсли; 
				Попытка
					Объект.Записать();
				Исключение
					Если мИмяТипаОбъектов = "Документ" Тогда
						Объект.Номер = МаксимальныйНомер - Сч;
					Иначе
						Объект.Код = МаксимальныйНомер - Сч;
					КонецЕсли; 
					Объект.Записать();
					НеУникальныеНомера.Вставить(ЧисловаяЧастьНомера, Объект.Ссылка);
				КонецПопытки;		
				ЧисловаяЧастьНомера = ЧисловаяЧастьНомера + 1;
			КонецЕсли;
			Продолжить;
		КонецЕсли;

		
		Если мИмяТипаОбъектов = "Документ" Тогда
			ТекНомер = СокрЛП(Объект.Номер);
		Иначе
			ТекНомер = СокрЛП(Объект.Код);
		КонецЕсли;
		
		Если НеИзменятьЧисловуюНумерацию Тогда
			СтроковаяЧастьНомера = вПолучитьПрефиксЧислоНомера(ТекНомер, ЧисловаяЧастьНомера);
		Иначе
			СтроковаяЧастьНомера = вПолучитьПрефиксЧислоНомера(ТекНомер);
		КонецЕсли;				
		

		Если 	  СпособОбработкиПрефиксов = 1 Тогда
			НовыйНомер = СтроковаяЧастьНомера;
		ИначеЕсли СпособОбработкиПрефиксов = 2 Тогда
			НовыйНомер = СокрЛП(СтрокаПрефикса);
		ИначеЕсли СпособОбработкиПрефиксов = 3 Тогда
			НовыйНомер = СокрЛП(СтрокаПрефикса) + СтроковаяЧастьНомера;
		ИначеЕсли СпособОбработкиПрефиксов = 4 Тогда
			НовыйНомер = СтроковаяЧастьНомера + СокрЛП(СтрокаПрефикса);
		ИначеЕсли СпособОбработкиПрефиксов = 5 Тогда
			НовыйНомер = СтрЗаменить(СтроковаяЧастьНомера, СокрЛП(ЗаменяемаяПодстрока), СокрЛП(СтрокаПрефикса));
		КонецЕсли;
		
		Пока мДлинаНомера - СтрДлина(НовыйНомер) - СтрДлина(Формат(ЧисловаяЧастьНомера,"ЧГ=0")) > 0 Цикл
			НовыйНомер = НовыйНомер + "0";
		КонецЦикла;
		
		НовыйНомер 	 = НовыйНомер + Формат(ЧисловаяЧастьНомера,"ЧГ=0");

		Если мИмяТипаОбъектов = "Документ" Тогда
			Объект.Номер = НовыйНомер;
		Иначе
			Объект.Код = НовыйНомер;
		КонецЕсли; 
		
		Попытка
			Объект.Записать();
		Исключение
			Если мИмяТипаОбъектов = "Документ" Тогда
				Объект.Номер = Формат(МаксимальныйНомер - Сч, "ЧГ=0");
			Иначе
				Объект.Код = Формат(МаксимальныйНомер - Сч, "ЧГ=0");
			КонецЕсли; 
			Объект.Записать();			
			НеУникальныеНомера.Вставить(НовыйНомер, Объект.Ссылка);
		КонецПопытки;		
		
		Если Не НеИзменятьЧисловуюНумерацию Тогда
			ЧисловаяЧастьНомера = ЧисловаяЧастьНомера + 1;
		КонецЕсли;
		
	КонецЦикла;

	Для каждого Зн Из НеУникальныеНомера Цикл
		НовыйНомер   = Зн.Ключ;
		Объект       = Зн.Значение.ПолучитьОбъект();
		Если мИмяТипаОбъектов = "Документ" Тогда
			Объект.Номер = НовыйНомер;
		Иначе
			Объект.Код = НовыйНомер;
		КонецЕсли; 
		Попытка
			Объект.Записать();
		Исключение
			ОбщегоНазначения.СообщитьПользователю(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Повтор номера:") + " " + НовыйНомер + " " + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("за пределами данной выборки!"));
		КонецПопытки;		
	КонецЦикла;

	Возврат Сч;

КонецФункции // вВыполнитьОбработку()

// Сохраняет значения реквизитов формы.
// 
// Параметры:
//  Нет.
// 
Процедура вСохранитьНастройку() Экспорт

	Если ПустаяСтрока(ЭлементыФормы.ТекущаяНастройка) Тогда
		Предупреждение(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Задайте имя новой настройки для сохранения или выберите существующую настройку для перезаписи."));
	КонецЕсли;

    НоваяНастройка = Новый Структура();
	
	Для каждого РеквизитНастройки Из мНастройка Цикл
		Выполнить("НоваяНастройка.Вставить(Строка(РеквизитНастройки.Ключ), " + Строка(РеквизитНастройки.Ключ) + ");");
	КонецЦикла;

	Если      ТекущаяНастройка.Родитель = Неопределено Тогда
		
		НоваяСтрока = ТекущаяНастройка.Строки.Добавить();
		НоваяСтрока.Обработка = ЭлементыФормы.ТекущаяНастройка.Значение;
		ТекущаяНастройка = НоваяСтрока;
		ЭтаФорма.ВладелецФормы.ЭлементыФормы.ДоступныеОбработки.ТекущаяСтрока = НоваяСтрока;
		
	ИначеЕсли НЕ ТекущаяНастройка.Обработка = ЭлементыФормы.ТекущаяНастройка.Значение Тогда
		
		НоваяСтрока           = ТекущаяНастройка.Родитель.Строки.Добавить();
		НоваяСтрока.Обработка = ЭлементыФормы.ТекущаяНастройка.Значение;
		ТекущаяНастройка      = НоваяСтрока;
		ЭтаФорма.ВладелецФормы.ЭлементыФормы.ДоступныеОбработки.ТекущаяСтрока = НоваяСтрока;
		
	КонецЕсли;
	
	ТекущаяНастройка.Настройка = НоваяНастройка;

	ЭтаФорма.Модифицированность = ЛОЖЬ;

КонецПроцедуры // вСохранитьНастройку()

// Восстанавливает сохраненные значения реквизитов формы.
// 
// Параметры:
//  Нет.
// 
Процедура вЗагрузитьНастройку() Экспорт

	Если ТекущаяНастройка.Родитель = Неопределено Тогда
		вУстановитьИмяНастройки(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Новая настройка"));
	Иначе
        Если НЕ ТекущаяНастройка.Настройка = Неопределено Тогда
			мНастройка = ТекущаяНастройка.Настройка;
		КонецЕсли;
	КонецЕсли;

	Для каждого РеквизитНастройки Из мНастройка Цикл
        Значение = мНастройка[РеквизитНастройки.Ключ];
		Выполнить(Строка(РеквизитНастройки.Ключ) + " = Значение;");
	КонецЦикла;
    
	СпособОбработкиПрефиксовПриИзменении("");
	НеИзменятьЧисловуюНумерациюПриИзменении("");

КонецПроцедуры // вЗагрузитьНастройку()

// Устанавливает значение реквизита "ТекущаяНастройка" по имени настройки или произвольно.
// 
// Параметры:
//  ИмяНастройки   - произвольное имя настройки, которое необходимо установить.
// 
Процедура вУстановитьИмяНастройки(ИмяНастройки = "") Экспорт

	Если ПустаяСтрока(ИмяНастройки) Тогда
		Если ТекущаяНастройка = Неопределено Тогда
			ЭлементыФормы.ТекущаяНастройка.Значение = "";
		Иначе
			ЭлементыФормы.ТекущаяНастройка.Значение = ТекущаяНастройка.Обработка;
		КонецЕсли;
	Иначе
		ЭлементыФормы.ТекущаяНастройка.Значение = ИмяНастройки;
	КонецЕсли;

КонецПроцедуры // вУстановитьИмяНастройки()

// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // 
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы.
// 
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	вОпределитьТипИДлинуНомера();
	Если мТипНомера <> "Строка" Тогда
		ЭлементыФормы.Панель1.Видимость = ЛОЖЬ; 
	КонецЕсли;
	
	Если мИспользоватьНастройки Тогда
		вУстановитьИмяНастройки();
		вЗагрузитьНастройку();
	Иначе
		ЭлементыФормы.ТекущаяНастройка.Доступность = ЛОЖЬ;
		ЭлементыФормы.ОсновныеДействияФормы.Кнопки.СохранитьНастройку.Доступность = ЛОЖЬ;
	КонецЕсли;

КонецПроцедуры // ПередОткрытием()

// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // 
// ОБРАБОТЧИКИ СОБЫТИЙ, ВЫЗЫВАЕМЫЕ Из ЭЛЕМЕНТОВ ФОРМЫ

// Обработчик действия "НачалоВыбораИзСписка" реквизита "ТекущаяНастройка".
// 
Процедура ТекущаяНастройкаНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)

	Элемент.СписокВыбора.Очистить();

	Если ТекущаяНастройка.Родитель = Неопределено Тогда
		КоллекцияСтрок = ТекущаяНастройка.Строки;
	Иначе
		КоллекцияСтрок = ТекущаяНастройка.Родитель.Строки;
	КонецЕсли;

	Для каждого Строка Из КоллекцияСтрок Цикл
		Элемент.СписокВыбора.Добавить(Строка, Строка.Обработка);
	КонецЦикла;

КонецПроцедуры // ТекущаяНастройкаНачалоВыбораИзСписка()

// Обработчик действия "ОбработкаВыбора" реквизита "ТекущаяНастройка".
// 
Процедура ТекущаяНастройкаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтандартнаяОбработка = ЛОЖЬ;

	Если НЕ ТекущаяНастройка = ВыбранноеЗначение Тогда

		Если ЭтаФорма.Модифицированность Тогда
			Если Вопрос(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Сохранить текущую настройку?"), РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да) = КодВозвратаДиалога.Да Тогда
				вСохранитьНастройку();
			КонецЕсли;
		КонецЕсли;

		ТекущаяНастройка = ВыбранноеЗначение;
		вУстановитьИмяНастройки();

		вЗагрузитьНастройку();

	КонецЕсли;

КонецПроцедуры // ТекущаяНастройкаОбработкаВыбора()

// Обработчик действия "Выполнить" командной панели "ОсновныеДействияФормы".
// 
Процедура ОсновныеДействияФормыВыполнить(Кнопка)

	ОбработаноОбъектов = вВыполнитьОбработку();

	Предупреждение(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Обработка") + " <" + СокрЛП(ЭтаФорма.Заголовок) + "> " + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("завершена!") + "|" + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Обработано объектов:") + " " + ОбработаноОбъектов + ".");

КонецПроцедуры // ОсновныеДействияФормыВыполнить()

// Обработчик действия "СохранитьНастройку" командной панели "ОсновныеДействияФормы".
// 
Процедура ОсновныеДействияФормыСохранитьНастройку(Кнопка)

	вСохранитьНастройку();

КонецПроцедуры // ОсновныеДействияФормыСохранитьНастройку()

// Обработчик события "ПриИзменении" элемента формы "НеИзменятьЧисловуюНумерацию"
// 
Процедура НеИзменятьЧисловуюНумерациюПриИзменении(Элемент)
	
	Если НеИзменятьЧисловуюНумерацию = 1 Тогда 
		ЭлементыФормы.НачальныйНомер.Доступность = ЛОЖЬ;
	Иначе 
		ЭлементыФормы.НачальныйНомер.Доступность = ИСТИНА;
	КонецЕсли;	    
	
КонецПроцедуры

// Обработчик события "ПриИзменении" элемента формы "СпособОбработкиПрефиксов"
// 
Процедура СпособОбработкиПрефиксовПриИзменении(Элемент)

	Если СпособОбработкиПрефиксов = 1 Тогда 
		ЭлементыФормы.СтрокаПрефикса.Доступность      = ЛОЖЬ;
		ЭлементыФормы.ЗаменяемаяПодстрока.Доступность = ЛОЖЬ;
	ИначеЕсли СпособОбработкиПрефиксов = 5 Тогда 
		ЭлементыФормы.СтрокаПрефикса.Доступность      = ИСТИНА;
		ЭлементыФормы.ЗаменяемаяПодстрока.Доступность = ИСТИНА;
	Иначе
		ЭлементыФормы.СтрокаПрефикса.Доступность      = ИСТИНА;
		ЭлементыФормы.ЗаменяемаяПодстрока.Доступность = ЛОЖЬ;
    КонецЕсли;

КонецПроцедуры

// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // 
// ИНИЦИАЛИЗАЦИЯ МОДУЛЬНЫХ ПЕРЕМЕННЫХ

мИспользоватьНастройки = ИСТИНА;

// Реквизиты настройки и значения по умолчанию.
мНастройка = Новый Структура("НачальныйНомер,НеИзменятьЧисловуюНумерацию,СтрокаПрефикса,ЗаменяемаяПодстрока,СпособОбработкиПрефиксов");

мНастройка.НачальныйНомер              = 1;
мНастройка.НеИзменятьЧисловуюНумерацию = ЛОЖЬ;
мНастройка.СпособОбработкиПрефиксов    = 1;

мТипыОбрабатываемыхОбъектов = "Справочник,Документ";
