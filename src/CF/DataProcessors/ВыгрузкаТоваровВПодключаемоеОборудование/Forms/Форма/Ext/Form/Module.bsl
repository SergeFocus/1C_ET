// sza131005-0234 : 
&НаКлиенте
Процедура ВыгрузитьВыполнить()

	ОчиститьСообщения();
	
	Если ПустаяСтрока(ТипОборудования) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru='Не выбран тип оборудования.'"));
		Возврат;
	КонецЕсли;
	
	ТаблицаЗагрузкиТоваров = Новый Массив;
	
	Если ТипОборудования = "Весы" Тогда
		Для Каждого Товар ИЗ ТаблицаВыгрузки Цикл
			ТаблицаЗагрузкиТоваров.Добавить(Новый Структура("PLU, Код, Номенклатура, НаименованиеПолное, Цена",
			  Товар.PLU, Товар.SKU, Товар.Наименование, Товар.Наименование + " " + Товар.Характеристика, Товар.Цена ));
		КонецЦикла;
		МенеджерОборудованияКлиент.ВыгрузитьДанныеВВесыСПечатьюЭтикеток(УникальныйИдентификатор, ТаблицаЗагрузкиТоваров);
	ИначеЕсли ТипОборудования = "ККМ" Тогда
		Для Каждого Товар ИЗ ТаблицаВыгрузки Цикл
			ТаблицаЗагрузкиТоваров.Добавить(Новый Структура("Код, Артикул, ШтрихКод, Наименование, НаименованиеПолное, ЕдиницаИзмерения, Цена, Остаток",
			  Товар.SKU, Товар.Артикул, Товар.ШтрихКод, Товар.Наименование, Товар.Наименование + " " + Товар.Характеристика, 
			  Товар.ЕдиницаИзмерения, Товар.Цена, 0));
		КонецЦикла;
		МенеджерОборудованияКлиент.ВыгрузитьДанныеВККМOffline(УникальныйИдентификатор, ТаблицаЗагрузкиТоваров);
	ИначеЕсли ТипОборудования = "ТСД" Тогда
		Для Каждого Товар ИЗ ТаблицаВыгрузки Цикл
			ТаблицаЗагрузкиТоваров.Добавить(Новый Структура("Код, ШтрихКод, Номенклатура, ЕдиницаИзмерения, ХарактеристикаНоменклатуры, СерияНоменклатуры, Качество, Цена, Остаток",
			Товар.SKU, Товар.ШтрихКод, Товар.Наименование, Товар.ЕдиницаИзмерения, Товар.Характеристика, Товар.Серия, Товар.Качество, Товар.Цена, 0));
		КонецЦикла;
		МенеджерОборудованияКлиент.ВыгрузитьДанныеВТСД(УникальныйИдентификатор, ТаблицаЗагрузкиТоваров);
	КонецЕсли;
	
КонецПроцедуры

// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // 
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
&НаСервере
Процедура ЗаполнитьБазуТоваров()

	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Номенклатура.Наименование КАК Наименование,
	|	ШтрихКоды.ШтрихКод КАК ШтрихКод,
	|	Номенклатура.Цена КАК Цена,
	|	Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Номенклатура.Характеристика КАК Характеристика,
	|	Номенклатура.Артикул КАК Артикул,
	|	Номенклатура.Серия КАК Серия,
	|	Номенклатура.Качество КАК Качество,
	|	Номенклатура.PLU КАК PLU,
	|	Номенклатура.SKU КАК SKU
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихКоды КАК ШтрихКоды
	|ПО (ШтрихКоды.Номенклатура = Номенклатура.Ссылка)  
	|ГДЕ Номенклатура.ЭтоГруппа = ЛОЖЬ
	|УПОРЯДОЧИТЬ ПО ШтрихКоды.ШтрихКод");

	ТекТаблица = Запрос.Выполнить().Выгрузить();

	ЗначениеВРеквизитФормы(ТекТаблица, "ТаблицаВыгрузки");

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВыполнить()

	ЗаполнитьБазуТоваров();

КонецПроцедуры

&НаКлиенте
Процедура ОчиститьСписокТоваров(Команда)
	
	ОчиститьСообщения();
	
	Если ПустаяСтрока(ТипОборудования) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru='Не выбран тип оборудования.'"));
		Возврат;
	КонецЕсли;

	Если ТипОборудования = "Весы" Тогда
		МенеджерОборудованияКлиент.ОчиститьТоварыВВесахСПечатьюЭтикеток(УникальныйИдентификатор);
	ИначеЕсли ТипОборудования = "ККМ" Тогда
		МенеджерОборудованияКлиент.ОчиститьТоварыВККМOffline(УникальныйИдентификатор);
	ИначеЕсли ТипОборудования = "ТСД" Тогда
		МенеджерОборудованияКлиент.ОчиститьДанныеВТСД(УникальныйИдентификатор);
	КонецЕсли;
	           
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()                                                  // ПРИ ЗАКРЫТИИ
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 1);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)                                                                    // ПРИ ОТКРЫТИИ
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 0);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
			Если ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьСобственныйПереводЭлементовИнтерфейса") Тогда
			ОбщийМодульСервер.ПеревестиРеквизитыФормы(ЭтаФорма);	
		КонецЕсли;
		

КонецПроцедуры



