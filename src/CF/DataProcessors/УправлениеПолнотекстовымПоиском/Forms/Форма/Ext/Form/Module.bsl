// sza150117-0419 
// Процедура обновляет индекс полнотекстового поиска
&НаСервере
Процедура ОбновитьИндекс()
	ПолнотекстовыйПоиск.ОбновитьИндекс();
	ОбновитьСтатусВыполнить();
КонецПроцедуры

// Обработчик команды ОбновитьИндекс
&НаКлиенте
Процедура ОбновитьИндексВыполнить()
	
	Состояние(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Подождите!") 
		+ Символы.ВК 
		+ ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Идет обновление полнотекстового индекса..."));
	ОбновитьИндекс();
	Состояние(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Обновление полнотекстового индекса завершено"));
КонецПроцедуры
        

// Процедура выполняет очистку индекса полнотекстового поиска
&НаСервере
Процедура ОчиститьИндексСервер() Экспорт
	ПолнотекстовыйПоиск.ОчиститьИндекс(); 
	ОбновитьСтатусВыполнить();
КонецПроцедуры	

// Обработчик команды ОчиститьИндекс
&НаКлиенте
Процедура ОчиститьИндексВыполнить()
	ОчиститьИндексСервер();	
КонецПроцедуры

// Процедура обновляет информацию об индексе и управляет доступностью кнопок
&НаСервере
Процедура ОбновитьСтатусВыполнить()
	Элементы.ОбновитьИндекс.Доступность = Ложь;
	Элементы.ОчиститьИндекс.Доступность = Ложь;
	
	РазрешитьПолнотекстовыйПоиск = ПолнотекстовыйПоиск.ПолучитьРежимПолнотекстовогоПоиска() = РежимПолнотекстовогоПоиска.Разрешить;
	ДатаАктуальностиИндекса = '00010101';
	СтатусИндекса = "";
	
	Если ПолнотекстовыйПоиск.ПолучитьРежимПолнотекстовогоПоиска() = РежимПолнотекстовогоПоиска.Разрешить Тогда
		ДатаАктуальностиИндекса = ПолнотекстовыйПоиск.ДатаАктуальности();
		
		// Если ПолнотекстовыйПоиск.ИндексАктуален() Тогда
		// 	СтатусИндекса = Новый ФорматированнаяСтрока(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Обновление индекса не требуется"), , ЦветаСтиля.ЦветТекстаНормальногоСостояния);
		// Иначе                                                 
		// 	СтатусИндекса = Новый ФорматированнаяСтрока(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Требуется обновление индекса"), , ЦветаСтиля.ЦветТекстаОшибочногоСостояния);
		// КонецЕсли;
		// 
		Если ПравоДоступа("Администрирование", Метаданные) Тогда
			Элементы.ОчиститьИндекс.Доступность = Истина;
			Элементы.ОбновитьИндекс.Доступность = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


// Процедура устанавливает режим полнотекстового поиска
&НаСервере
Процедура УстановитьПолнотекстовыйПоискРазрешен(Разрешен) Экспорт
	Если Разрешен Тогда
		ПолнотекстовыйПоиск.УстановитьРежимПолнотекстовогоПоиска(РежимПолнотекстовогоПоиска.Разрешить);
	Иначе
		ПолнотекстовыйПоиск.УстановитьРежимПолнотекстовогоПоиска(РежимПолнотекстовогоПоиска.Запретить);
	КонецЕсли;
	ОбновитьСтатусВыполнить();
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьПолнотекстовыйПоискПриИзменении(Элемент)
	Попытка
		УстановитьПолнотекстовыйПоискРазрешен(РазрешитьПолнотекстовыйПоиск);
	Исключение
		РазрешитьПолнотекстовыйПоиск = НЕ РазрешитьПолнотекстовыйПоиск;
		Инфо = ИнформацияОбОшибке();
		СтрокаИсключения = Инфо.Описание 
		+ "." 
		+ Символы.ВК 
		+ ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Вероятно, запущено регламентное задание обновления индекса. Попробуйте еще раз некоторое время спустя.");
		ВызватьИсключение СтрокаИсключения;
	КонецПопытки;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		Если ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьСобственныйПереводЭлементовИнтерфейса") Тогда
			ОбщийМодульСервер.ПеревестиРеквизитыФормы(ЭтаФорма);	
		КонецЕсли;
		
	ОбновитьСтатусВыполнить();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 0);
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 1);
КонецПроцедуры
