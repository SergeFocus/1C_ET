// sza150117-0231 улучш
// sza140210-0154
// sza140118-2032

&НаСервере
Процедура ЗаполнитьСообщениями()

	СтруктураСообщений = Неопределено;

	Если параметры.ПрочитатьСообщенияСамостоятельно Тогда
		ВыборкаДетальныеЗаписи = РегистрыСведений.СообщенияПользователюОтСистемы.Выбрать(Параметры.глПараметрыСообщенийПользователя);
		СтруктураСообщений 	= Новый Структура;
		МассивСообщений 	= Новый Массив;
		МассивСсылокТ 		= Новый Массив;
		ПользователяТекущаяДата = ОбщийМодульСервисСервер.ПользователяТекущаяДата();
		СохранятьИсториюСообщенийПользователей = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("СохранятьИсториюСообщенийПользователей");

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Если ВыборкаДетальныеЗаписи.Прочитано Тогда
				Продолжить;
			КонецЕсли;

			ВремяДоставить = ВыборкаДетальныеЗаписи.Доставить;

			Если ВремяДоставить > ПользователяТекущаяДата Тогда
				Продолжить;
			КонецЕсли;

			ТекстСообщения = ВыборкаДетальныеЗаписи.Сообщение;
			Отправитель = ВыборкаДетальныеЗаписи.Отправитель;
			Если ЗначениеЗаполнено(Отправитель) Тогда
				ТекстСообщения = ТекстСообщения + " " + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("от") + " " + СокрЛП(Отправитель);

				Если ВыборкаДетальныеЗаписи.ИнформироватьОДоставке Тогда
					ОтветСообщение = РегистрыСведений.СообщенияПользователюОтСистемы.СоздатьМенеджерЗаписи();
					ОтветСообщение.Пользователь = Отправитель;
					ОтветСообщение.Сообщение = "" + СокрЛП(Параметры.глПараметрыСообщенийПользователя.Пользователь) + " " + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("получил Ваше сообщение") + ": " + ВыборкаДетальныеЗаписи.Сообщение + " " + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("в") + " " + СокрЛП(ПользователяТекущаяДата);
					ОтветСообщение.Порядок = ВыборкаДетальныеЗаписи.Порядок;
					ОтветСообщение.ДатаОтправки = ПользователяТекущаяДата;
					ОтветСообщение.Записать(ИСТИНА);
				КонецЕсли;
			КонецЕсли;

			МассивСообщений.Добавить(ТекстСообщения);
			МассивССылокТ.Добавить(ВыборкаДетальныеЗаписи.Ссылка);

			Если НЕ СохранятьИсториюСообщенийПользователей Тогда
				МенеджерЗаписи = ВыборкаДетальныеЗаписи.ПолучитьМенеджерЗаписи();
				МенеджерЗаписи.Удалить();
			Иначе
				МенеджерЗаписи = ВыборкаДетальныеЗаписи.ПолучитьМенеджерЗаписи();
				МенеджерЗаписи.Прочитано = ИСТИНА;
				МенеджерЗаписи.Записать(ИСТИНА);
			КонецЕсли;

		КонецЦикла;

		СтруктураСообщений.Вставить("МассивСообщений", МассивСообщений);
		СтруктураСообщений.Вставить("МассивСсылокТ", МассивСсылокТ);
	Иначе
		СтруктураСообщений = параметры.СтруктураСообщений;
	КонецЕсли;

	Если НЕ СтруктураСообщений = Неопределено Тогда
		СЧ = 0;
		Для Каждого Сообщение Из СтруктураСообщений.МассивСообщений Цикл
			МассивСсылокТ = СтруктураСообщений.МассивСсылокТ;

			Если Сообщение = "ПредложениеВосстановленияВаловойПрибыли" ТОгда
				ПредложениеВосстановленияВаловойПрибыли = ИСТИНА;
				СсылкаДата = МассивСсылокТ[СЧ].Дата;
				Ссылка     = МассивСсылокТ[СЧ];
			Иначе
				СтрокаСообщений = Сообщения.Добавить();
				СтрокаСообщений.Сообщение = Сообщение;
				СтрокаСообщений.Ссылка = МассивСсылокТ[СЧ];
			КонецЕсли;

			СЧ = СЧ + 1;
			ЧислоСообщений = ЧислоСообщений + 1;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура КонстантыДатаАктуальностиПоказателейВаловойПрибылиУстановить(Знач Дата)

	Константы.ДатаАктуальностиПоказателейВаловойПрибыли.Установить(Дата);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаАктивизации(АктивныйОбъект, Источник)

	ЗаполнитьСообщениями();

КонецПроцедуры

&НаКлиенте
Процедура ПослеИзображенияОкнаФормы() // Когда окно уже нарисовано пользователю

	ЭтаФорма.Элементы.ФормаЗакрыть.Видимость = НЕ ЭтаФорма.Окно = Неопределено;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ПодключитьОбработчикОжидания("ПослеИзображенияОкнаФормы", 0.3, ИСТИНА);

	Если ПредложениеВосстановленияВаловойПрибыли Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтаФорма), ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Восстановить актуальность показателей Валовой прибыли для номенклатуры этого документа?"), РежимДиалогаВопрос.ДаНет);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Состояние(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Восстановление показателей Валовой прибыли.."), , ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Ждите.."));
		Если НЕ ОбщийМодульСервер.ВосстановитьАктуальностьПоказателейВаловойПрибылиДокумента(Ссылка, СсылкаДата) Тогда
			КонстантыДатаАктуальностиПоказателейВаловойПрибылиУстановить(СсылкаДата);
		КонецЕсли;
	Иначе
		КонстантыДатаАктуальностиПоказателейВаловойПрибылиУстановить(СсылкаДата);
	КонецЕсли;

	Если ЧислоСообщений = 1 Тогда
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриПовторномОткрытии()

	ЗаполнитьСообщениями();

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

		Если ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьСобственныйПереводЭлементовИнтерфейса") Тогда
			ОбщийМодульСервер.ПеревестиРеквизитыФормы(ЭтаФорма);
		КонецЕсли;

	Если код = 0 Тогда
		код = число(формат(ТекущаяДата(), "ДФ=ччммсс"));
	КонецЕсли;

	ЗаполнитьСообщениями();

	ОбщийМодульСервисСервер.ОбработатьНовуюФорму(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура РеакцияНаПрочиеСобытияФормы(КомандаСобытияФормы) Экспорт // Для реакции на события для запрограммированных элементов
	ОбщийМодульКлиент.РеакцияНаПрочиеСобытияФормы(КомандаСобытияФормы, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура СообщенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	Ссылка = Элементы.Сообщения.ТекущиеДанные.Ссылка;

	Если ЗначениеЗаполнено(Ссылка) Тогда
		ПоказатьЗначение(Неопределено, Элементы.Сообщения.ТекущиеДанные.Ссылка);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СообщенияСообщениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	Ссылка = Элементы.Сообщения.ТекущиеДанные.Ссылка;

	Если ЗначениеЗаполнено(Ссылка) Тогда
		ПоказатьЗначение(Неопределено, Элементы.Сообщения.ТекущиеДанные.Ссылка);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СообщенияСообщениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	Ссылка = Элементы.Сообщения.ТекущиеДанные.Ссылка;

	Если ЗначениеЗаполнено(Ссылка) Тогда
		ПоказатьЗначение(Неопределено, Элементы.Сообщения.ТекущиеДанные.Ссылка);
	КонецЕсли;

КонецПроцедуры
