//sza140420-1345  
//sza131009-1747  
&НаСервереБезКонтекста
Функция   ОбновитьОбщийОстаток(Знач ТекущаяДата)
	
	ОстатокВОсновнойВалюте = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ДеньгиОстатки.Валюта, ДеньгиОстатки.СуммаВВалютеОстаток
	|ИЗ РегистрНакопления.Деньги.Остатки(&ТекущаяДата, ) КАК ДеньгиОстатки";
	
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата);
	
	РезультатЗапроса = Запрос.Выполнить(); 
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		ОстатокВОсновнойВалюте = 0;
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ОстатокВОсновнойВалюте = ОстатокВОсновнойВалюте + ОбщийМодульСервер.ПоКурсу(ВыборкаДетальныеЗаписи.СуммаВВалютеОстаток, , ВыборкаДетальныеЗаписи.валюта);
		КонецЦикла;	
	КонецЕсли;
	
	Возврат ОстатокВОсновнойВалюте;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьПоказательДолга(Команда)
	
	ТекущаяДата = ОбщийМодульКлиент.ПользователяТекущаяДата();
	ОстатокВОсновнойВалюте = ОбновитьОбщийОстаток(ТекущаяДата);	
	Элементы.Остатки.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОстаткиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	форма = ПолучитьФорму("Документ.ДвиженияДенег.ФормаСписка");
	форма.открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()                                                  // ПРИ ЗАКРЫТИИ
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 1);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)                                                                    // ПРИ ОТКРЫТИИ
	
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 0);
	ОстатокВОсновнойВалюте = ОбновитьОбщийОстаток(ТекущаяДата);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)          // ПРИ СОЗДАНИИ НА СЕРВЕРЕ
	
	Отказ = ОбщийМодульСервисСервер.ПроверитьОтказДоступа("000890", ЭтаФорма, Отказ, );
	
	Если НЕ Отказ Тогда	
		УчетВалют = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ВестиУчетВалют");
		ВестиУчетДенегВНесколькихХранилищах = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ВестиУчетДенегВНесколькихХранилищах");
		Элементы.ОстаткиФормаОплаты.Видимость = НЕ ВестиУчетДенегВНесколькихХранилищах;
		
		Элементы.ОстаткиВалюта.Видимость = УчетВалют;
		Элементы.ОстаткиСуммаВВалютеОстаток.Видимость = УчетВалют;
		Если НЕ учетвалют Тогда
			Элементы.ОстатокВОсновнойВалюте.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Всего");
		КонецЕсли;
		ТекущаяДата = ОбщийМодульСервисСервер.ПользователяТекущаяДата();
		
		НаименованиеОсновнойВалюты = Справочники.Валюты.ОсновнаяВалюта.Наименование ;	
	КонецЕсли;
	
КонецПроцедуры



