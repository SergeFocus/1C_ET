
// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // /
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест"
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	Параметры.Свойство("Идентификатор", Идентификатор);
	Заголовок = НСтр("ru='ФР'") + " """ + Строка(Идентификатор) + """";

	ЦветТекста = ЦветаСтиля.ЦветТекстаФормы;
	ЦветОшибки = ЦветаСтиля.ЦветОтрицательногоЧисла;

	СпПорт = Элементы.Порт.СписокВыбора;
	Индекс = Неопределено;
	Для Индекс = 1 По 32 Цикл
		СпПорт.Добавить(Индекс, "COM" + СокрЛП(Индекс));
	КонецЦикла;

	СпСкорость = Элементы.Скорость.СписокВыбора;
	СпСкорость.Добавить(4800,   "4800 бод");
	СпСкорость.Добавить(9600,   "9600 бод");
	СпСкорость.Добавить(14400,  "14400 бод");
	СпСкорость.Добавить(19200,  "19200 бод");
	СпСкорость.Добавить(28800,  "28800 бод");
	СпСкорость.Добавить(38400,  "38400 бод");
	СпСкорость.Добавить(57600,  "57600 бод");
	СпСкорость.Добавить(115200, "115200 бод");

	времПорт                       = Неопределено;
	времСкорость                   = Неопределено;
	времТаймаут                    = Неопределено;
	времСетевойНомер               = Неопределено;
	времПарольУстройства           = Неопределено;
	времПарольПользователя         = Неопределено;
	времПарольАдминистратора       = Неопределено;
	времОтменятьЧекПриПодключении  = Неопределено;
	времНаименованиеОплаты1        = Неопределено;
	времНаименованиеОплаты2        = Неопределено;
	времНомерСекции                = Неопределено;
	времКодСимволаЧастичногоОтреза = Неопределено;
	времЖурналОперацийДрайвера     = Неопределено;
	времМодель                     = Неопределено;

	Параметры.Свойство("Порт"                      , времПорт);
	Параметры.Свойство("Скорость"                  , времСкорость);
	Параметры.Свойство("Таймаут"                   , времТаймаут);
	Параметры.Свойство("СетевойНомер"              , времСетевойНомер);
	Параметры.Свойство("ПарольУстройства"          , времПарольУстройства);
	Параметры.Свойство("ПарольПользователя"        , времПарольПользователя);
	Параметры.Свойство("ПарольАдминистратора"      , времПарольАдминистратора);
	Параметры.Свойство("ОтменятьЧекПриПодключении" , времОтменятьЧекПриПодключении);
	Параметры.Свойство("НаименованиеОплаты1"       , времНаименованиеОплаты1);
	Параметры.Свойство("НаименованиеОплаты2"       , времНаименованиеОплаты2);
	Параметры.Свойство("НомерСекции"               , времНомерСекции);
	Параметры.Свойство("КодСимволаЧастичногоОтреза", времКодСимволаЧастичногоОтреза);
	Параметры.Свойство("ЖурналОперацийДрайвера"    , времЖурналОперацийДрайвера);
	Параметры.Свойство("Модель"                    , времМодель);

	Порт                       = ?(времПорт                       = Неопределено,      1, времПорт);
	Скорость                   = ?(времСкорость                   = Неопределено, 115200, времСкорость);
	Таймаут                    = ?(времТаймаут                    = Неопределено,    150, времТаймаут);
	СетевойНомер               = ?(времСетевойНомер               = Неопределено,      0, времСетевойНомер);
	ПарольУстройства           = ?(времПарольУстройства           = Неопределено,      0, времПарольУстройства);
	ПарольПользователя         = ?(времПарольПользователя         = Неопределено,    "0", времПарольПользователя);
	ПарольАдминистратора       = ?(времПарольАдминистратора       = Неопределено,    "0", времПарольАдминистратора);
	ОтменятьЧекПриПодключении  = ?(времОтменятьЧекПриПодключении  = Неопределено,   ЛОЖЬ, времОтменятьЧекПриПодключении);
	НаименованиеОплаты1        = ?(времНаименованиеОплаты1        = Неопределено,     "", времНаименованиеОплаты1);
	НаименованиеОплаты2        = ?(времНаименованиеОплаты2        = Неопределено,     "", времНаименованиеОплаты2);
	НомерСекции                = ?(времНомерСекции                = Неопределено,      0, времНомерСекции);
	КодСимволаЧастичногоОтреза = ?(времКодСимволаЧастичногоОтреза = Неопределено,     22, времКодСимволаЧастичногоОтреза);
	ЖурналОперацийДрайвера     = ?(времЖурналОперацийДрайвера     = Неопределено,   ЛОЖЬ, времЖурналОперацийДрайвера);
	Модель                     = ?(времМодель                     = Неопределено, Элементы.Модель.СписокВыбора[0], времМодель);

	Элементы.ТестУстройства.Видимость    = (ПараметрыСеанса.РабочееМестоКлиента
	                                        = Идентификатор.РабочееМесто);
	Элементы.УстановитьДрайвер.Видимость = (ПараметрыСеанса.РабочееМестоКлиента
	                                        = Идентификатор.РабочееМесто);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ОбновитьИнформациюОДрайвере();

КонецПроцедуры

// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // /
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

// Процедура представляет обработчик события "Нажатие" кнопки
// "ОК" командной панели "ОсновныеДействияФормы".
// 
// Параметры:
//  Кнопка - <КнопкаКоманднойПанели>
//         - Кнопка, с которой связано данное событие (кнопка "ОК").
// 
&НаКлиенте
Процедура ЗаписатьИЗакрытьВыполнить()

	Параметры.ПараметрыНастройки.Добавить(Порт                      , "Порт");
	Параметры.ПараметрыНастройки.Добавить(Скорость                  , "Скорость");
	Параметры.ПараметрыНастройки.Добавить(Таймаут                   , "Таймаут");
	Параметры.ПараметрыНастройки.Добавить(СетевойНомер              , "СетевойНомер");
	Параметры.ПараметрыНастройки.Добавить(ПарольУстройства          , "ПарольУстройства");
	Параметры.ПараметрыНастройки.Добавить(ПарольПользователя        , "ПарольПользователя");
	Параметры.ПараметрыНастройки.Добавить(ПарольАдминистратора      , "ПарольАдминистратора");
	Параметры.ПараметрыНастройки.Добавить(ОтменятьЧекПриПодключении , "ОтменятьЧекПриПодключении");
	Параметры.ПараметрыНастройки.Добавить(НаименованиеОплаты1       , "НаименованиеОплаты1");
	Параметры.ПараметрыНастройки.Добавить(НаименованиеОплаты2       , "НаименованиеОплаты2");
	Параметры.ПараметрыНастройки.Добавить(НомерСекции               , "НомерСекции");
	Параметры.ПараметрыНастройки.Добавить(КодСимволаЧастичногоОтреза, "КодСимволаЧастичногоОтреза");
	Параметры.ПараметрыНастройки.Добавить(ЖурналОперацийДрайвера    , "ЖурналОперацийДрайвера");
	Параметры.ПараметрыНастройки.Добавить(Модель                    , "Модель");

	ОчиститьСообщения();
	Закрыть(КодВозвратаДиалога.ОК);

КонецПроцедуры

&НаКлиенте
Процедура ТестУстройства(Команда)

	РезультатТеста    = Неопределено;

	ВходныеПараметры  = Неопределено;
	ВыходныеПараметры = Неопределено;

	времПараметрыУстройства = Новый Структура();
	времПараметрыУстройства.Вставить("Порт"                      , Порт);
	времПараметрыУстройства.Вставить("Скорость"                  , Скорость);
	времПараметрыУстройства.Вставить("Таймаут"                   , Таймаут);
	времПараметрыУстройства.Вставить("СетевойНомер"              , СетевойНомер);
	времПараметрыУстройства.Вставить("ПарольУстройства"          , ПарольУстройства);
	времПараметрыУстройства.Вставить("ПарольПользователя"        , ПарольПользователя);
	времПараметрыУстройства.Вставить("ПарольАдминистратора"      , ПарольАдминистратора);
	времПараметрыУстройства.Вставить("ОтменятьЧекПриПодключении" , ОтменятьЧекПриПодключении);
	времПараметрыУстройства.Вставить("НаименованиеОплаты1"       , НаименованиеОплаты1);
	времПараметрыУстройства.Вставить("НаименованиеОплаты2"       , НаименованиеОплаты2);
	времПараметрыУстройства.Вставить("НомерСекции"               , НомерСекции);
	времПараметрыУстройства.Вставить("КодСимволаЧастичногоОтреза", КодСимволаЧастичногоОтреза);
	времПараметрыУстройства.Вставить("ЖурналОперацийДрайвера"    , ЖурналОперацийДрайвера);
	времПараметрыУстройства.Вставить("Модель"                    , Модель);

	Результат = МенеджерОборудованияКлиент.ВыполнитьДополнительнуюКоманду("CheckHealth",
	                                                                      ВходныеПараметры,
	                                                                      ВыходныеПараметры,
	                                                                      Идентификатор,
	                                                                      времПараметрыУстройства);

	ДополнительноеОписание = ?(ТипЗнч(ВыходныеПараметры) = Тип("Массив")
	                           И ВыходныеПараметры.Количество() >= 2,
	                           НСтр("ru = 'Дополнительное описание:'") + " " + ВыходныеПараметры[1],
	                           "");
	Если Результат Тогда
		ТекстСообщения = НСтр("ru = 'Тест успешно выполнен.%ПереводСтроки%%ДополнительноеОписание%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПереводСтроки%", ?(ПустаяСтрока(ДополнительноеОписание),
		                                                                  "",
		                                                                  Символы.ПС));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", ?(ПустаяСтрока(ДополнительноеОписание),
		                                                                           "",
		                                                                           ДополнительноеОписание));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	Иначе
		ТекстСообщения = НСтр("ru = 'Тест не пройден.%ПереводСтроки%%ДополнительноеОписание%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПереводСтроки%", ?(ПустаяСтрока(ДополнительноеОписание),
		                                                                  "",
		                                                                  Символы.ПС));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", ?(ПустаяСтрока(ДополнительноеОписание),
		                                                                           "",
		                                                                           ДополнительноеОписание));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьДрайвер(Команда)

	МенеджерОборудованияКлиент.УстановитьДрайвер(Идентификатор);

	ОбновитьИнформациюОДрайвере();

КонецПроцедуры

// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // /
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ОбновитьИнформациюОДрайвере()

	ВходныеПараметры  = Неопределено;
	ВыходныеПараметры = Неопределено;

	времПараметрыУстройства = Новый Структура();
	времПараметрыУстройства.Вставить("Порт"                      , Порт);
	времПараметрыУстройства.Вставить("Скорость"                  , Скорость);
	времПараметрыУстройства.Вставить("Таймаут"                   , Таймаут);
	времПараметрыУстройства.Вставить("ПарольПользователя"        , ПарольПользователя);
	времПараметрыУстройства.Вставить("ПарольАдминистратора"      , ПарольАдминистратора);
	времПараметрыУстройства.Вставить("ОтменятьЧекПриПодключении" , ОтменятьЧекПриПодключении);
	времПараметрыУстройства.Вставить("НаименованиеОплаты1"       , НаименованиеОплаты1);
	времПараметрыУстройства.Вставить("НаименованиеОплаты2"       , НаименованиеОплаты2);
	времПараметрыУстройства.Вставить("НомерСекции"               , НомерСекции);
	времПараметрыУстройства.Вставить("КодСимволаЧастичногоОтреза", КодСимволаЧастичногоОтреза);
	времПараметрыУстройства.Вставить("ЖурналОперацийДрайвера"    , ЖурналОперацийДрайвера);
	времПараметрыУстройства.Вставить("Модель"                    , Модель);

	Если МенеджерОборудованияКлиент.ВыполнитьДополнительнуюКоманду("ПолучитьВерсиюДрайвера",
	                                                               ВходныеПараметры,
	                                                               ВыходныеПараметры,
	                                                               Идентификатор,
	                                                               времПараметрыУстройства) Тогда
		Драйвер = ВыходныеПараметры[0];
		Версия  = ВыходныеПараметры[1];
	Иначе
		Драйвер = ВыходныеПараметры[2];
		Версия  = НСтр("ru='Не определена'");
	КонецЕсли;

	Элементы.Драйвер.ЦветТекста = ?(Драйвер = НСтр("ru='Не установлен'"), ЦветОшибки, ЦветТекста);
	Элементы.Версия.ЦветТекста  = ?(Версия  = НСтр("ru='Не определена'"), ЦветОшибки, ЦветТекста);

	Элементы.УстановитьДрайвер.Доступность = Не (Драйвер = НСтр("ru='Установлен'"));

КонецПроцедуры
