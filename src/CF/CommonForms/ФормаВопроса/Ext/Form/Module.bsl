// sza140619-1417 список
// sza140619-1417
// sza140604-1537 :

&НаСервере
Функция   ВыполнитьЭтуКомандуНаСервере(Знач ИмяКоманды)

	Результат = Неопределено;
	КодКоманды = СтрЗаменить(ИмяКоманды, "КнопкаКоманды", "");
	ПараметрыОтбора = Новый Структура("Код", КодКоманды);
	СтрокиКоманда = ТаблицаКоманд.НайтиСтроки(ПараметрыОтбора);

	Если НЕ СтрокиКоманда.Количество() = 0 Тогда
		Попытка
			Выполнить(" " + СтрокиКоманда[0].Программа + ";");
		Исключение
			ТекстОписаниеОшибки = ОписаниеОшибки();
			ОбщегоНазначения.СообщитьПользователю(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Произошла ошибка") + ": " + ТекстОписаниеОшибки);
			Результат = ЛОЖЬ;
		КонецПопытки;
	КонецЕсли;

	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ОткрытьКнопкуКоманды(Команда)

	Если НЕ ВремяВопроса = 0 Тогда
		ОтключитьОбработчикОжидания("ЗакрытьЕслиПользовательНеОтвечает");	
		ОтключитьОбработчикОжидания("ОбновитьСчетчикДоЗакрытия");
	КонецЕсли;
	
	Если Команда.Имя = "КнопкаЗакрыть" Тогда
		Закрыть();
	Иначе
		Закрыть(ВыполнитьЭтуКомандуНаСервере(Команда.Имя));
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьСобственныйПереводЭлементовИнтерфейса") Тогда
		ОбщийМодульСервер.ПеревестиРеквизитыФормы(ЭтаФорма);
	КонецЕсли;

	МассивКомандФормы = Новый Массив;
	ЗаголовокВопроса  = Параметры.ЗаголовокВопроса;
	Элементы.ГруппаЗаголовкаВопроса.Видимость = НЕ ПустаяСтрока(ЗаголовокВопроса);
	ПодвалВопроса 	  = Параметры.ПодвалВопроса;
	Элементы.ПодвалВопроса.Видимость = НЕ ПустаяСтрока(ПодвалВопроса);
	ТекстВопроса 	  = Параметры.ТекстВопроса;
	Элементы.ТекстВопроса.Видимость  = НЕ ПустаяСтрока(ТекстВопроса);

	Если НЕ ПустаяСТрока(Параметры.ЗаголовокФормы) Тогда
		ЭтаФорма.Заголовок  = Параметры.ЗаголовокФормы;
	ИначеЕсли НЕ ПустаяСТрока(ЗаголовокВопроса) Тогда
		ЭтаФорма.Заголовок  = ЗаголовокВопроса;
	Иначе
		ЭтаФорма.Заголовок  = ТекстВопроса;
	КонецЕсли;

	Если НЕ Параметры.СтруктураКнопокИПоведения = Неопределено Тогда

		ГруппаКоманд = Элементы.ГруппаКоманд;
		Для Каждого ЭлементКоманда Из Параметры.СтруктураКнопокИПоведения Цикл
			НоваяКоманда = Команды.Добавить("КнопкаКоманды" + ЭлементКоманда[0]);
			НоваяКоманда.Действие  = "ОткрытьКнопкуКоманды";
			НоваяКоманда = Элементы.Добавить("КнопкаКоманды" + ЭлементКоманда[0], тип("КнопкаФормы"), группаКоманд);
			НоваяКоманда.Заголовок  = ЭлементКоманда[1];
			СтрокаКоманда = ТаблицаКоманд.Добавить();
			СтрокаКоманда.Код = ЭлементКоманда[0];

			Если ЭлементКоманда.Количество() > 2 Тогда
				СтрокаКоманда.Программа = ЭлементКоманда[2];
			КонецЕсли;

			Если ЭлементКоманда.Количество() > 3 Тогда
				ВысотаЭлемента = ЭлементКоманда[3];
				Если НЕ ВысотаЭлемента = Неопределено Тогда
					НоваяКоманда.ВысотаЗаголовка = ВысотаЭлемента;
				КонецЕсли;
			КонецЕсли;

			Если ЭлементКоманда[0] = "OK"
				ИЛИ ЭлементКоманда[0] = "ОК" Тогда

				НоваяКоманда.КнопкаПоУмолчанию = ИСТИНА;
			КонецЕсли;

			НоваяКоманда.ИмяКоманды = "КнопкаКоманды" + ЭлементКоманда[0];
			МассивКомандФормы.Добавить(НоваяКоманда);
		КонецЦикла;
	Иначе
		НоваяКоманда = Команды.Добавить("КнопкаЗакрыть");
		НоваяКоманда.Действие  = "ОткрытьКнопкуКоманды";
		НоваяКоманда = Элементы.Добавить("КнопкаЗакрыть", Тип("КнопкаФормы"), ГруппаКоманд);
		НоваяКоманда.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Закрыть");
		НоваяКоманда.ИмяКоманды = "КнопкаЗакрыть";
		НоваяКоманда.КнопкаПоУмолчанию = ИСТИНА;
		МассивКомандФормы.Добавить(НоваяКоманда);
	КонецЕсли;

	Если НЕ Параметры.НомерКнопкиПоУмолчанию = 0 Тогда
		Если НЕ Команды.Количество() < Параметры.НомерКнопкиПоУмолчанию Тогда
			МассивКомандФормы[Параметры.НомерКнопкиПоУмолчанию - 1].КнопкаПоУмолчанию = ИСТИНА;
		КонецЕсли;
	КонецЕсли;

	Если Параметры.ЭтоВыборПериода Тогда
		ВыбранныйПериод.ДатаНачала    = Параметры.ДатаНачала;
		ВыбранныйПериод.ДатаОкончания = Параметры.ДатаОкончания;
	КонецЕсли;

	Элементы.ГруппаВыбораПериода.Видимость  = Параметры.ЭтоВыборПериода;
	Элементы.ИконкаВопроса.Видимость 		= Параметры.ЭтоВопрос И НЕ ПустаяСтрока(ЗаголовокВопроса);
	Элементы.ИконкаПредупреждения.Видимость = Параметры.ЭтоПредупреждение И НЕ ПустаяСтрока(ЗаголовокВопроса);
	Элементы.ИконкаПериода.Видимость 		= Параметры.ЭтоПериод И НЕ ПустаяСтрока(ЗаголовокВопроса);
	Элементы.ИконкаВыборИзСписка.Видимость 	= Параметры.ЭтоВыборИзСписка И НЕ ПустаяСтрока(ЗаголовокВопроса);
	Элементы.СписокДляВыбора.Видимость 		= Параметры.ЭтоВыборИзСписка;
	ВернутьНомерЗначения = Параметры.ВернутьНомерЗначения;

	Если НЕ Параметры.МассивЗначенийСписка = Неопределено Тогда
		СписокДляВыбора.ЗагрузитьЗначения(Параметры.МассивЗначенийСписка);
		Если НЕ Параметры.ТекущееЗначение = 0 Тогда
			Элементы.СписокДляВыбора.ТекущаяСтрока = Параметры.ТекущееЗначение - 1;
		КонецЕсли;

		Элементы.СписокДляВыбора.Ширина = Параметры.ШиринаСписка;
	КонецЕсли;
	
	ВремяВопроса = Параметры.ВремяВопроса - 1;
	ДоЗакрытия = ВремяВопроса - 1;
	
	ЭтоАвтомат = Параметры.ЭтоАвтомат;

	ОбщийМодульСервисСервер.ОбработатьНовуюФорму(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура РеакцияНаПрочиеСобытияФормы(КомандаСобытияФормы) Экспорт // Для реакции на события для запрограммированных элементов
	ОбщийМодульКлиент.РеакцияНаПрочиеСобытияФормы(КомандаСобытияФормы, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура СписокДляВыбораВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Закрыть(?(ВернутьНомерЗначения, ВыбраннаяСтрока, СписокДляВыбора[ВыбраннаяСтрока].Значение));
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("ФормаУжеНарисована", 0.2, ИСТИНА);	
	
	Если НЕ ВремяВопроса = 0 Тогда
		ПодключитьОбработчикОжидания("ЗакрытьЕслиПользовательНеОтвечает", ВремяВопроса, ИСТИНА);
		Элементы.ГруппаДоЗакрытия.Видимость = ИСТИНА;
		ПодключитьОбработчикОжидания("ОбновитьСчетчикДоЗакрытия", 1, ЛОЖЬ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФормаУжеНарисована()

	// восстановить форму по размерам видимых элементов
	ФормаУжеНарисованаНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьЕслиПользовательНеОтвечает()
 	Закрыть("99");
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСчетчикДоЗакрытия()
	
	ДоЗакрытия = ДоЗакрытия - 1;
//	Этаформа.ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаСервере
Процедура ФормаУжеНарисованаНаСервере()
   
КонецПроцедуры
