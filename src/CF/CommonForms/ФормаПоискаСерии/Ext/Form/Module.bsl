// sza150216-2355 
// sza150210-0203

&НаКлиенте
Процедура СерияПриИзменении(Элемент)
	ПоискСерий();
КонецПроцедуры

&НаКлиенте
Процедура КодПродуктаПриИзменении(Элемент)
	ПоискСерий();
КонецПроцедуры

&НаКлиенте
Процедура КомандаПоискСерий(Команда)
	ПоискСерий();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 0);
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 1);
КонецПроцедуры

&НаСервере
Процедура ПоискСерий()
	
	Результат.Очистить();
	ТабДок = Новый ТабличныйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ СерииНоменклатуры.Ссылка,
	|	СерииНоменклатуры.Владелец КАК Номенклатура
	|ИЗ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|ГДЕ (СерииНоменклатуры.Наименование ПОДОБНО &Наименование
	|			ИЛИ СерииНоменклатуры.КодПродукта ПОДОБНО &КодПродукта)";
	
	Запрос.УстановитьПараметр("КодПродукта", ?(ПустаяСтрока(КодПродукта), "25021980", "%" + КодПродукта + "%"));
	Запрос.УстановитьПараметр("Наименование", ?(ПустаяСтрока(Серия), "25021980", "%" + Серия + "%"));
	
	РезультатЗапроса = Запрос.Выполнить();
	МакетПечати  = ПолучитьОбщийМакет("ПрочиеОбластиПечати");
	ОбластьСвязанныйОбъект = МакетПечати.ПолучитьОбласть("СвязанныйОбъект");
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ОбластьСерия = МакетПечати.ПолучитьОбласть("Серия");
		
		ВыборкаПоСериям = РезультатЗапроса.Выбрать();
		Пока ВыборкаПоСериям.Следующий() Цикл
			
			МассивСсылок = Новый Массив;	
			МассивСсылок.Добавить(ВыборкаПоСериям.Ссылка);
			
			ОбластьСерия.Параметры.СерияНоменклатуры = ВыборкаПоСериям.Ссылка;
			ОбластьСерия.Параметры.Номенклатура = ВыборкаПоСериям.Номенклатура;
			ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьСерия);
			ТабДок.Вывести(ОбластьСерия);
			
			ТабСсылок = НайтиПоСсылкам(МассивСсылок);
			Для Каждого СтрокаСвязи из ТабСсылок Цикл
				ОбластьСвязанныйОбъект.Параметры.СвязанныйОбъект = СтрокаСвязи.Данные;
				ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьСвязанныйОбъект);
				ТабДок.Вывести(ОбластьСвязанныйОбъект);
			КонецЦикла;	
		КонецЦикла;
		
	Иначе
		ОбластьСвязанныйОбъект.Параметры.СвязанныйОбъект = "" + ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Серия номенклатуры не найдена!");
		ОбщийМодульСервисСервер.ЗаменитьСвоиЗначенияПараметровПечати(ОбластьСвязанныйОбъект);
		ТабДок.Вывести(ОбластьСвязанныйОбъект);		
	КонецЕсли;
	
	Результат.Вывести(ТабДок);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьСобственныйПереводЭлементовИнтерфейса") Тогда
			ОбщийМодульСервер.ПеревестиРеквизитыФормы(ЭтаФорма);	
		КонецЕсли; 		

КонецПроцедуры
