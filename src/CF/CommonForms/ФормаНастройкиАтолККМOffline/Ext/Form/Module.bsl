                                     
// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // /
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест"
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("Идентификатор", Идентификатор);

	Заголовок = НСтр("ru='Оборудование:'") + Символы.НПП  + Строка(Идентификатор);
	
	времБазаТоваров 	= Неопределено;
	времФайлОтчета 		= Неопределено;
	времФлагВыгрузки 	= Неопределено;
	Параметры.Свойство("БазаТоваров", 	времБазаТоваров);
	Параметры.Свойство("ФайлОтчета", 	времФайлОтчета);
	Параметры.Свойство("ФлагВыгрузки", 	времФлагВыгрузки);
	БазаТоваров 	= ?(времБазаТоваров  = Неопределено, "", времБазаТоваров);
	ФайлОтчета 		= ?(времФайлОтчета 	 = Неопределено, "", времФайлОтчета);
	ФлагВыгрузки 	= ?(времФлагВыгрузки = Неопределено, "", времФлагВыгрузки);
	
	Драйвер = НСтр("ru='Не требуется'");
	Версия  = НСтр("ru='Не определена'");
	
КонецПроцедуры

// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // /
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура БазаТоваровНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВыбратьФайл(РежимДиалогаВыбораФайла.Открытие, БазаТоваров);
	СтандартнаяОбработка = ЛОЖЬ;

КонецПроцедуры

&НаКлиенте
Процедура ФлагВыгрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВыбратьФайл(РежимДиалогаВыбораФайла.Открытие, ФлагВыгрузки);
	СтандартнаяОбработка = ЛОЖЬ;

КонецПроцедуры

&НаКлиенте
Процедура ФайлОтчетаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВыбратьФайл(РежимДиалогаВыбораФайла.Открытие, ФайлОтчета);
	СтандартнаяОбработка = ЛОЖЬ;

КонецПроцедуры

// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // /
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ОчиститьСообщения();
	
	ТекстОшибки = "";
	ТекстОшибкиОбщий = "";

	Если ПустаяСтрока(БазаТоваров) Тогда
		Результат = ЛОЖЬ;
		ТекстОшибкиОбщий = НСтр("ru='Файл базы товаров не указан.'");
	Иначе
		Результат = МенеджерОборудованияКлиент.ПроверитьДоступностьКаталога(БазаТоваров, ТекстОшибкиОбщий);
	КонецЕсли;

	Если ПустаяСтрока(ФайлОтчета) Тогда
		Результат = ЛОЖЬ;
		ТекстОшибкиОбщий = ТекстОшибкиОбщий + ?(ПустаяСтрока(ТекстОшибкиОбщий), "", Символы.ПС); 
		ТекстОшибкиОбщий = ТекстОшибкиОбщий + НСтр("ru='Файл отчета не указан.'") 
	Иначе
		Результат = МенеджерОборудованияКлиент.ПроверитьДоступностьКаталога(ФайлОтчета, ТекстОшибки);
		Если НЕ Результат Тогда
			ТекстОшибкиОбщий = ?(ПустаяСтрока(ТекстОшибкиОбщий), "", ТекстОшибкиОбщий + Символы.ПС) + ТекстОшибки;
		КонецЕсли;
	КонецЕсли;

	Если НЕ ПустаяСтрока(ФлагВыгрузки) Тогда
		Результат = МенеджерОборудованияКлиент.ПроверитьДоступностьКаталога(ФлагВыгрузки, ТекстОшибки);
		Если НЕ Результат Тогда
			ТекстОшибкиОбщий = ?(ПустаяСтрока(ТекстОшибкиОбщий), "", ТекстОшибкиОбщий + Символы.ПС) + ТекстОшибки;
	КонецЕсли;

	КонецЕсли;

	Если ПустаяСтрока(ТекстОшибкиОбщий) Тогда
		Параметры.ПараметрыНастройки.Добавить(БазаТоваров,  "БазаТоваров");
		Параметры.ПараметрыНастройки.Добавить(ФайлОтчета,   "ФайлОтчета");
		Параметры.ПараметрыНастройки.Добавить(ФлагВыгрузки,	"ФлагВыгрузки");
		Закрыть(КодВозвратаДиалога.ОК);
	Иначе
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru= 'При проверке были обнаружены следующие ошибки:'")+ Символы.ПС + ТекстОшибкиОбщий);
	КонецЕсли;
				   
КонецПроцедуры

&НаКлиенте
Процедура ТестУстройства(Команда)
	
	ОчиститьСообщения();
	
	РезультатТеста = Неопределено;

	ВходныеПараметры  = Неопределено;
	ВыходныеПараметры = Неопределено;

	времПараметрыУстройства = Новый Структура();
	времПараметрыУстройства.Вставить("БазаТоваров", БазаТоваров);
	времПараметрыУстройства.Вставить("ФайлОтчета", 	ФайлОтчета);
	времПараметрыУстройства.Вставить("ФлагВыгрузки",ФлагВыгрузки);

	Результат = МенеджерОборудованияКлиент.ВыполнитьДополнительнуюКоманду("ТестУстройства",
	                                                                      ВходныеПараметры,
	                                                                      ВыходныеПараметры,
	                                                                      Идентификатор,
	                                                                      времПараметрыУстройства);

	ДополнительноеОписание = ?(ТипЗнч(ВыходныеПараметры) = Тип("Массив")
	                           И ВыходныеПараметры.Количество() >= 2,
	                           НСтр("ru = 'Дополнительное описание:'") + " " + ВыходныеПараметры[1],
	                           "");
	Если Результат Тогда
		ТекстСообщения = НСтр("ru = 'Тест успешно выполнен.%ПереводСтроки%%ДополнительноеОписание%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПереводСтроки%", ?(ПустаяСтрока(ДополнительноеОписание),
		                                                                  "",
		                                                                  Символы.ПС));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", ?(ПустаяСтрока(ДополнительноеОписание),
		                                                                           "",
		                                                                           ДополнительноеОписание));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	Иначе
		ТекстСообщения = НСтр("ru = 'Тест не пройден.%ПереводСтроки%%ДополнительноеОписание%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПереводСтроки%", ?(ПустаяСтрока(ДополнительноеОписание),
		                                                                  "",
		                                                                  Символы.ПС));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", ?(ПустаяСтрока(ДополнительноеОписание),
		                                                                           "",
		                                                                           ДополнительноеОписание));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;

КонецПроцедуры

// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // /
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ВыбратьФайл(РежимОткрытияФайла, ИмяФайла)

	Диалог = Новый ДиалогВыбораФайла(РежимОткрытияФайла);
	Диалог.МножественныйВыбор = ЛОЖЬ;
	Диалог.ПолноеИмяФайла = ИмяФайла;

	Если Диалог.Выбрать() Тогда
		ИмяФайла = Диалог.ПолноеИмяФайла;
	КонецЕсли;

КонецПроцедуры
