// sza160211-0402
// sza151223-0313
// sza150711-0152

&НаКлиенте
Процедура ВыбранныеПоляДоступны(ЭлементСтруктуры)

	Если Отчет.КомпоновщикНастроек.Настройки.НаличиеВыбораУЭлемента(ЭлементСтруктуры) Тогда
		ЛокальныеВыбранныеПоля = ИСТИНА;
		Элементы.СтраницыПолейВыбора.ТекущаяСтраница = Элементы.НастройкиВыбранныхПолей;
	Иначе

		ЛокальныеВыбранныеПоля = Ложь;
		Элементы.СтраницыПолейВыбора.ТекущаяСтраница = Элементы.ОтключенныеНастройкиВыбранныхПолей;
	КонецЕсли;

	Элементы.ЛокальныеВыбранныеПоля.ТолькоПросмотр = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеПоляНедоступны()

	ЛокальныеВыбранныеПоля = Ложь;
	Элементы.ЛокальныеВыбранныеПоля.ТолькоПросмотр = ИСТИНА;
	Элементы.СтраницыПолейВыбора.ТекущаяСтраница = Элементы.НедоступныеНастройкиВыбранныхПолей;

КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекНастройкиПриАктивизацииПоля(Элемент)

	Перем ВыбраннаяСтраница;

	Если Элементы.КомпоновщикНастроекНастройки.ТекущийЭлемент.Имя = "КомпоновщикНастроекНастройкиНаличиеВыбора" Тогда
		ВыбраннаяСтраница = Элементы.СтраницаПолейВыбора;
	ИначеЕсли Элементы.КомпоновщикНастроекНастройки.ТекущийЭлемент.Имя = "КомпоновщикНастроекНастройкиНаличиеОтбора" Тогда
		ВыбраннаяСтраница = Элементы.СтраницаОтбора;
	ИначеЕсли Элементы.КомпоновщикНастроекНастройки.ТекущийЭлемент.Имя = "КомпоновщикНастроекНастройкиНаличиеПорядка" Тогда
		ВыбраннаяСтраница = Элементы.СтраницаПорядка;
	ИначеЕсли Элементы.КомпоновщикНастроекНастройки.ТекущийЭлемент.Имя = "КомпоновщикНастроекНастройкиНаличиеУсловногоОформления" Тогда
		ВыбраннаяСтраница = Элементы.СтраницаУсловногоОформления;
	ИначеЕсли Элементы.КомпоновщикНастроекНастройки.ТекущийЭлемент.Имя = "КомпоновщикНастроекНастройкиНаличиеПараметровВывода" Тогда
		ВыбраннаяСтраница = Элементы.СтраницаПараметровВывода;
	КонецЕсли;

	Если ВыбраннаяСтраница <> Неопределено Тогда
		Элементы.СтраницыНастроек.ТекущаяСтраница = ВыбраннаяСтраница;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекНастройкиПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("СписокПриАктивизацииСтрокиОповещение", 0.2, ИСТИНА);
КонецПроцедуры

&НаКлиенте
Процедура ЛокальноеУсловноеОформлениеПриИзменении(Элемент)

	Если ЛокальноеУсловноеОформление Тогда
		Элементы.СтраницыУсловногоОформления.ТекущаяСтраница = Элементы.НастройкиУсловногоОформления;
	Иначе
		Элементы.СтраницыУсловногоОформления.ТекущаяСтраница = Элементы.ОтключенныеНастройкиУсловногоОформления;
		ЭлементСтруктуры = Отчет.КомпоновщикНастроек.Настройки.ПолучитьОбъектПоИдентификатору(Элементы.КомпоновщикНастроекНастройки.ТекущаяСтрока);
		Отчет.КомпоновщикНастроек.Настройки.ОчиститьУсловноеОформлениеЭлемента(ЭлементСтруктуры);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЛокальныеВыбранныеПоляПриИзменении(Элемент)

	Если ЛокальныеВыбранныеПоля Тогда
		Элементы.СтраницыПолейВыбора.ТекущаяСтраница = Элементы.НастройкиВыбранныхПолей;
	Иначе
		Элементы.СтраницыПолейВыбора.ТекущаяСтраница = Элементы.ОтключенныеНастройкиВыбранныхПолей;
		ЭлементСтруктуры = Отчет.КомпоновщикНастроек.Настройки.ПолучитьОбъектПоИдентификатору(Элементы.КомпоновщикНастроекНастройки.ТекущаяСтрока);
		Отчет.КомпоновщикНастроек.Настройки.ОчиститьВыборЭлемента(ЭлементСтруктуры);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЛокальныеПараметрыВыводаПриИзменении(Элемент)

	Если ЛокальныеПараметрыВывода Тогда
		Элементы.СтраницыПараметровВывода.ТекущаяСтраница = Элементы.НастройкиПараметровВывода;
	Иначе
		Элементы.СтраницыПараметровВывода.ТекущаяСтраница = Элементы.ОтключенныеНастройкиПараметровВывода;
		ЭлементСтруктуры = Отчет.КомпоновщикНастроек.Настройки.ПолучитьОбъектПоИдентификатору(Элементы.КомпоновщикНастроекНастройки.ТекущаяСтрока);
		Отчет.КомпоновщикНастроек.Настройки.ОчиститьПараметрыВыводаЭлемента(ЭлементСтруктуры);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЛокальныйОтборПриИзменении(Элемент)

	Если ЛокальныйОтбор Тогда
		Элементы.СтраницыОтбора.ТекущаяСтраница = Элементы.НастройкиОтбора;
	Иначе
		Элементы.СтраницыОтбора.ТекущаяСтраница = Элементы.ОтключенныеНастройкиОтбора;
		ЭлементСтруктуры = Отчет.КомпоновщикНастроек.Настройки.ПолучитьОбъектПоИдентификатору(Элементы.КомпоновщикНастроекНастройки.ТекущаяСтрока);
		Отчет.КомпоновщикНастроек.Настройки.ОчиститьОтборЭлемента(ЭлементСтруктуры);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЛокальныйПорядокПриИзменении(Элемент)

	Если ЛокальныйПорядок Тогда
		Элементы.СтраницыПорядка.ТекущаяСтраница = Элементы.НастройкиПорядка;
	Иначе
		Элементы.СтраницыПорядка.ТекущаяСтраница = Элементы.ОтключенныеНастройкиПорядка;
		ЭлементСтруктуры = Отчет.КомпоновщикНастроек.Настройки.ПолучитьОбъектПоИдентификатору(Элементы.КомпоновщикНастроекНастройки.ТекущаяСтрока);
		Отчет.КомпоновщикНастроек.Настройки.ОчиститьПорядокЭлемента(ЭлементСтруктуры);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидимостьЭлементовНастроек()

	Элементы.СтраницаПараметровДанных.Видимость = ПоказатьВсеЭлементыНастроек;
	Элементы.СтраницаПользовательскихПолей.Видимость = ПоказатьВсеЭлементыНастроек;
	Элементы.СтраницаПолейГруппировки.Видимость = ПоказатьВсеЭлементыНастроек;
	Элементы.ФормаПоказатьВсеЭлементыНастроек.Заголовок = ?(ПоказатьВсеЭлементыНастроек, ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Скрыть дополнительные элементы настроек"), ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Показать дополнительные элементы настроек"));
	Элементы.СтраницаУсловногоОформления.Заголовок = ?(ПоказатьВсеЭлементыНастроек, ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Условное оформление"), ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Оформление"));
	Элементы.СтраницаПараметровВывода.Заголовок = ?(ПоказатьВсеЭлементыНастроек, ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Дополнительные настройки"), ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Дополнительно"));
	ОбновитьВидимостьЭлементовНастроекНаСервере();

КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьЭлементовНастроекНаСервере()

	Если ПоказатьВсеЭлементыНастроек Тогда
		ЭтаФорма.Элементы.Переместить(Элементы.КомпоновщикНастроекНастройки, Элементы.ГруппаГоризонта);
	Иначе
		ЭтаФорма.Элементы.Переместить(Элементы.КомпоновщикНастроекНастройки, Элементы.СтраницаГруппировка);
	КонецЕсли;

	Элементы.КомпоновщикНастроекНастройки.РастягиватьПоГоризонтали = ПоказатьВсеЭлементыНастроек;

КонецПроцедуры

&НаКлиенте
Процедура ОтборДоступен(ЭлементСтруктуры)

	Если Отчет.КомпоновщикНастроек.Настройки.НаличиеОтбораУЭлемента(ЭлементСтруктуры) Тогда
		ЛокальныйОтбор = ИСТИНА;
		Элементы.СтраницыОтбора.ТекущаяСтраница = Элементы.НастройкиОтбора;
	Иначе
		ЛокальныйОтбор = Ложь;
		Элементы.СтраницыОтбора.ТекущаяСтраница = Элементы.ОтключенныеНастройкиОтбора;
	КонецЕсли;

	Элементы.ЛокальныйОтбор.ТолькоПросмотр = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ОтборНедоступен()

	ЛокальныйОтбор = Ложь;
	Элементы.ЛокальныйОтбор.ТолькоПросмотр = ИСТИНА;
	Элементы.СтраницыОтбора.ТекущаяСтраница = Элементы.НедоступныеНастройкиОтбора;

КонецПроцедуры

&НаКлиенте
Процедура ПараметрыВыводаДоступны(ЭлементСтруктуры)

	Если Отчет.КомпоновщикНастроек.Настройки.НаличиеПараметровВыводаУЭлемента(ЭлементСтруктуры) Тогда
		ЛокальныеПараметрыВывода = ИСТИНА;
		Элементы.СтраницыПараметровВывода.ТекущаяСтраница = Элементы.НастройкиПараметровВывода;
	Иначе
		ЛокальныеПараметрыВывода = Ложь;
		Элементы.СтраницыПараметровВывода.ТекущаяСтраница = Элементы.ОтключенныеНастройкиПараметровВывода;
	КонецЕсли;

	Элементы.ЛокальныеПараметрыВывода.ТолькоПросмотр = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ПараметрыВыводаНедоступны()

	ЛокальныеПараметрыВывода = Ложь;
	Элементы.ЛокальныеПараметрыВывода.ТолькоПросмотр = ИСТИНА;
	Элементы.СтраницыПараметровВывода.ТекущаяСтраница = Элементы.НедоступныеНастройкиПараметровВывода;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)

	ТекущаяСтраница = Элементы.СтраницыНастроек.ТекущаяСтраница;
	Если НЕ ТекущаяСтраница = Неопределено Тогда
		СтраницыПанели  = Элементы.СтраницыНастроек.ПодчиненныеЭлементы;
		СтраницаПанели  = СтраницыПанели.Индекс(ТекущаяСтраница);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКОтчету(Элемент)

	ЭлементСтруктуры = Отчет.КомпоновщикНастроек.Настройки.ПолучитьОбъектПоИдентификатору(Элементы.КомпоновщикНастроекНастройки.ТекущаяСтрока);
	НастройкиЭлемента =  Отчет.КомпоновщикНастроек.Настройки.НастройкиЭлемента(ЭлементСтруктуры);
	Элементы.КомпоновщикНастроекНастройки.ТекущаяСтрока = Отчет.КомпоновщикНастроек.Настройки.ПолучитьИдентификаторПоОбъекту(НастройкиЭлемента);

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВсеЭлементыНастроек(Команда)

	ПоказатьВсеЭлементыНастроек = НЕ ПоказатьВсеЭлементыНастроек;
	ОбновитьВидимостьЭлементовНастроек();

КонецПроцедуры

&НаКлиенте
Процедура ПоляГруппировкиНедоступны()
	Элементы.СтраницыПолейГруппировки.ТекущаяСтраница = Элементы.НедоступныеНастройкиПолейГруппировки;
КонецПроцедуры

&НаКлиенте
Процедура ПорядокДоступен(ЭлементСтруктуры)

	Если Отчет.КомпоновщикНастроек.Настройки.НаличиеПорядкаУЭлемента(ЭлементСтруктуры) Тогда
		ЛокальныйПорядок = ИСТИНА;
		Элементы.СтраницыПорядка.ТекущаяСтраница = Элементы.НастройкиПорядка;
	Иначе
		ЛокальныйПорядок = Ложь;
		Элементы.СтраницыПорядка.ТекущаяСтраница = Элементы.ОтключенныеНастройкиПорядка;
	КонецЕсли;

	Элементы.ЛокальныйПорядок.ТолькоПросмотр = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ПорядокНедоступен()

	ЛокальныйПорядок = Ложь;
	Элементы.ЛокальныйПорядок.ТолькоПросмотр = ИСТИНА;
	Элементы.СтраницыПорядка.ТекущаяСтраница = Элементы.НедоступныеНастройкиПорядка;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()

	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 1);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 0);

	Если ЗначениеЗаполнено(СтраницаПанели) Тогда
		СтраницыПанели  = Элементы.СтраницыНастроек.ПодчиненныеЭлементы;
		ТекущаяСтраница = СтраницыПанели.получить(СтраницаПанели);
		Если НЕ ТекущаяСтраница = Неопределено Тогда
			Элементы.СтраницыНастроек.ТекущаяСтраница = ТекущаяСтраница;
		КонецЕсли;
	КонецЕсли;

	ОбновитьВидимостьЭлементовНастроек();

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Элементы.НадписьЛокальногоОтбора.Заголовок =
	ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("В текущем элементе дополнительные настройки не установлены.") + Символы.ПС +
	ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Выберите в дереве структуры отчет для редактирования дополнительных настроек отчета в целом.") + Символы.ПС +
	ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Если необходимо, чтобы текущий элемент имел собственные дополнительные настройки, установите флажок, расположенный над этим текстом.");
	Элементы.НадписьНедоступныхПараметровВывода.Заголовок =
	ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("У текущего элемента не могут быть установлены дополнительные настройки.") + Символы.ПС +
	ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Выберите в дереве структуры отчет для редактирования дополнительных настроек отчета в целом.");
	Элементы.НадписьЛокальногоОтбора.Заголовок =
	ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("В текущем элементе отбор не установлен.") + Символы.ПС +
	ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Выберите в дереве структуры отчет для редактирования отбора отчета в целом.") + Символы.ПС +
	ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Если необходимо, чтобы текущий элемент имел собственный отбор, установите флажок, расположенный над этим текстом.");
	Элементы.НадписьНедоступногоОтбора.Заголовок =
	ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("У текущего элемента не может быть установлен отбор.") + Символы.ПС +
	ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Выберите в дереве структуры отчет для редактирования отбора отчета в целом.");
	Элементы.НадписьЛокальногоУсловногоОформления.Заголовок =
	ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("В текущем элементе условное оформление не установлено.") + Символы.ПС +
	ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Выберите в дереве структуры отчет для редактирования условного оформления отчета в целом.") + Символы.ПС +
	ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Если необходимо, чтобы текущий элемент имел собственное условное оформление, установите флажок, расположенный над этим текстом.");
	Элементы.НадписьНедоступныхПолейГруппировки.Заголовок =
	ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Текущий элемент не может содержать поля группировки.") + Символы.ПС +
	ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Выберите в дереве структуры группировку, для которой требуется просмотреть или изменить состав полей группировки.");

	Если ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьСобственныйПереводЭлементовИнтерфейса") Тогда
		ОбщийМодульСервер.ПеревестиРеквизитыФормы(ЭтаФорма);
	КонецЕсли;

	ОбщийМодульСервисСервер.ОбработатьНовуюФорму(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура РеакцияНаПрочиеСобытияФормы(КомандаСобытияФормы) Экспорт // Для реакции на события для запрограммированных элементов
	ОбщийМодульКлиент.РеакцияНаПрочиеСобытияФормы(КомандаСобытияФормы, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтрокиОповещение()

	ЭлементСтруктуры = Отчет.КомпоновщикНастроек.Настройки.ПолучитьОбъектПоИдентификатору(Элементы.КомпоновщикНастроекНастройки.ТекущаяСтрока);
	ТипЭлемента = ТипЗнч(ЭлементСтруктуры);

	Если ТипЭлемента = Неопределено  ИЛИ
		ТипЭлемента = Тип("КоллекцияЭлементовСтруктурыДиаграммыКомпоновкиДанных") ИЛИ
		ТипЭлемента = Тип("КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных") Тогда

		ПоляГруппировкиНедоступны();
		ВыбранныеПоляНедоступны();
		ОтборНедоступен();
		ПорядокНедоступен();
		УсловноеОформлениеНедоступно();
		ПараметрыВыводаНедоступны();
	ИначеЕсли ТипЭлемента = Тип("НастройкиКомпоновкиДанных") ИЛИ
		ТипЭлемента = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда

		ПоляГруппировкиНедоступны();
		ЛокальныеВыбранныеПоля = ИСТИНА;
		Элементы.ЛокальныеВыбранныеПоля.ТолькоПросмотр = ИСТИНА;
		Элементы.СтраницыПолейВыбора.ТекущаяСтраница = Элементы.НастройкиВыбранныхПолей;
		ЛокальныйОтбор = ИСТИНА;
		Элементы.ЛокальныйОтбор.ТолькоПросмотр = ИСТИНА;
		Элементы.СтраницыОтбора.ТекущаяСтраница = Элементы.НастройкиОтбора;
		ЛокальныйПорядок = ИСТИНА;
		Элементы.ЛокальныйПорядок.ТолькоПросмотр = ИСТИНА;
		Элементы.СтраницыПорядка.ТекущаяСтраница = Элементы.НастройкиПорядка;
		ЛокальноеУсловноеОформление = ИСТИНА;
		Элементы.ЛокальноеУсловноеОформление.ТолькоПросмотр = ИСТИНА;
		Элементы.СтраницыУсловногоОформления.ТекущаяСтраница = Элементы.НастройкиУсловногоОформления;
		ЛокальныеПараметрыВывода = ИСТИНА;
		Элементы.ЛокальныеПараметрыВывода.ТолькоПросмотр = ИСТИНА;
		Элементы.СтраницыПараметровВывода.ТекущаяСтраница = Элементы.НастройкиПараметровВывода;
	ИначеЕсли ТипЭлемента = Тип("ГруппировкаКомпоновкиДанных") ИЛИ
		ТипЭлемента = Тип("ГруппировкаТаблицыКомпоновкиДанных") ИЛИ
		ТипЭлемента = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда

		Элементы.СтраницыПолейГруппировки.ТекущаяСтраница = Элементы.НастройкиПолейГруппировки;
		ВыбранныеПоляДоступны(ЭлементСтруктуры);
		ОтборДоступен(ЭлементСтруктуры);
		ПорядокДоступен(ЭлементСтруктуры);
		УсловноеОформлениеДоступно(ЭлементСтруктуры);
		ПараметрыВыводаДоступны(ЭлементСтруктуры);
	ИначеЕсли ТипЭлемента = Тип("ТаблицаКомпоновкиДанных") ИЛИ
		ТипЭлемента = Тип("ДиаграммаКомпоновкиДанных") Тогда

		ПоляГруппировкиНедоступны();
		ВыбранныеПоляДоступны(ЭлементСтруктуры);
		ОтборНедоступен();
		ПорядокНедоступен();
		УсловноеОформлениеДоступно(ЭлементСтруктуры);
		ПараметрыВыводаДоступны(ЭлементСтруктуры);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УсловноеОформлениеДоступно(ЭлементСтруктуры)

	Если Отчет.КомпоновщикНастроек.Настройки.НаличиеУсловногоОформленияУЭлемента(ЭлементСтруктуры) Тогда
		ЛокальноеУсловноеОформление = ИСТИНА;
		Элементы.СтраницыУсловногоОформления.ТекущаяСтраница = Элементы.НастройкиУсловногоОформления;
	Иначе
		ЛокальноеУсловноеОформление = Ложь;
		Элементы.СтраницыУсловногоОформления.ТекущаяСтраница = Элементы.ОтключенныеНастройкиУсловногоОформления;
	КонецЕсли;

	Элементы.ЛокальноеУсловноеОформление.ТолькоПросмотр = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура УсловноеОформлениеНедоступно()

	ЛокальноеУсловноеОформление = Ложь;
	Элементы.ЛокальноеУсловноеОформление.ТолькоПросмотр = ИСТИНА;
	Элементы.СтраницыУсловногоОформления.ТекущаяСтраница = Элементы.НедоступныеНастройкиУсловногоОформления;

КонецПроцедуры
