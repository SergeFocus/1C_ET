// sza140412-0025 :
// sza140411-2239 :

&НаСервере
Процедура ВыгрузитьШтрихКодКоличество(Знач Адрес, Знач ПараметрКоманды)

	ТаблицыДанных = ТипЗнч(ПараметрКоманды) = Тип("ДокументСсылка.ТаблицыДанных");

	Если ТаблицыДанных Тогда
		ИмяТаблицы  = "ТаблицаСДанными";
		ИмяКолонки1 = "ЗначениеПоляТаблицы1";
		ИмяКолонки2 = "ЗначениеПоляТаблицы2";
	Иначе
		ИмяТаблицы = "Товары";
		ИмяКолонки1 = "Номенклатура";
		ИмяКолонки2 = "Количество";
	КонецЕсли;

	ВидДокумента 		 = ПараметрКоманды.МЕТАДАННЫЕ().Имя;
	ЕстьЕдиницаИзмерения = ОбщийМодульСервисСервер.ЕстьЛиРеквизит(ВидДокумента, "ЕдиницаИзмерения", ИмяТаблицы);
	ЕстьСерия 			 = ОбщийМодульСервисСервер.ЕстьЛиРеквизит(ВидДокумента, "СерияНоменклатуры", ИмяТаблицы);
	ЕстьКоличество 		 = ОбщийМодульСервисСервер.ЕстьЛиРеквизит(ВидДокумента, ИмяКолонки2, ИмяТаблицы);
	ТекстДок = Новый ТекстовыйДокумент;

	Для Каждого СтрокаТовара Из ПараметрКоманды[ИмяТаблицы] Цикл
		Если ЕстьСерия Тогда
			СерияНоменклатуры = СтрокаТовара.СерияНоменклатуры;
		Иначе
			СерияНоменклатуры = Неопределено;
		КонецЕсли;

		Если ЕстьЕдиницаИзмерения Тогда
			ЕдиницаИзмерения = СтрокаТовара.ЕдиницаИзмерения;
		Иначе
			ЕдиницаИзмерения = Неопределено;
		КонецЕсли;

		Если ЕстьКоличество Тогда
			Количество = СтрокаТовара[ИмяКолонки2];
		Иначе
			Количество = СтрокаТовара.Цена;
		КонецЕсли;

		Если НЕ ЗначениеЗаполнено(Количество) Тогда
			Количество = 1;
		КонецЕсли;

		Если НЕ ТаблицыДанных
			ИЛИ ТипЗнч(СтрокаТовара[ИмяКолонки1]) = Тип("СправочникСсылка.Номенклатура") Тогда

			ШтрихКод = ОбщийМодульТоварСервер.ПолучитьШтрихКодНоменклатурыСерии(СтрокаТовара[ИмяКолонки1], СерияНоменклатуры, ЕдиницаИзмерения);
		Иначе
			ШтрихКод = СтрокаТовара[ИмяКолонки1];
		КонецЕсли;

		ТекстДок.ДобавитьСтроку(ШтрихКод);
		ТекстДок.ДобавитьСтроку(Формат(Количество, "ЧЦ=15; ЧДЦ=3; ЧРД=.; ЧРГ=; ЧГ=0"));
	КонецЦикла;

	ТекстДок.Записать(Адрес);
	ТекстДок = Неопределено;

КонецПроцедуры // ВыгрузитьШтрихКодКОличество(Адрес)

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	Отказ = ОбщийМодульСервисСервер.ПроверитьОтказДоступа("002700", , , );

	Если НЕ Отказ Тогда
		КвоСтрок = ?(ТипЗнч(ПараметрКоманды) = Тип("ДокументСсылка.ТаблицыДанных"), 1, ПодсистемаИЭ.ПараметрКомандыТоварыКоличество(ПараметрКоманды));

		Если НЕ КвоСтрок = Неопределено
			И КвоСтрок > 0 Тогда

			Адрес = "";
			ДиалогФильтр	 = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Файл текста") + " (*.*)|*.*";
			ДиалогРасширение = "*";
			ДиалогДляВыбораФайла 	 = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
			ДиалогДляВыбораФайла.Заголовок				=	ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Выберите файл для выгрузки") + ": ";
			ДиалогДляВыбораФайла.ПолноеИмяФайла			=	Адрес; // АДРЕС
			ДиалогДляВыбораФайла.Фильтр					=	ДиалогФильтр;
			ДиалогДляВыбораФайла.Расширение				=	ДиалогРасширение;
			ДиалогДляВыбораФайла.МножественныйВыбор		=	ЛОЖЬ;
			ДиалогДляВыбораФайла.ИндексФильтра			=	0;

			Если ДиалогДляВыбораФайла.Выбрать() Тогда
				Адрес = ДиалогДляВыбораФайла.ПолноеИмяФайла;
				Состояние(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Выгружается документ"), , ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Ждите.."));
				ВыгрузитьШтрихКодКоличество(Адрес, ПараметрКоманды);
				ОбщийМодульКлиент.ДобавитьСобытиеЖурнала(ОбщийМодульПовтор.ПолучитьТекстНаЯзыке("Выгрузил таблицу штрихкодов из файла"), 2, ПараметрКоманды);
			КонецЕсли; // когда файл Адрес выбран
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры
