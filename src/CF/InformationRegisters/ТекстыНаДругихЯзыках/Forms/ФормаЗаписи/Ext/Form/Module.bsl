// sza150429-1726
// sza131003-1245 : 

&НаКлиенте
Процедура НаПоследнийЯзык(Команда)
	Запись.Язык = ПоследнийЯзык;
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПереводАвтору(Команда)
	
	ТестОпереводе = "---" + Символы.ПС + Запись.ТекстНаРусском + Символы.ПС +
	Запись.Язык + Символы.ПС +
	Запись.ТекстНаЯзыке + Символы.ПС + 
	Запись.ЭтоЭлементИнтерфейса + Символы.ПС;
	
	СтруктураПисьма = Новый Структура;
	СтруктураПисьма.Вставить("ТемаСообщения", "ЭТ Пользовательский перевод");
	СтруктураПисьма.Вставить("ТекстСообщения", ТестОпереводе);
	СтруктураПисьма.Вставить("Вручную", ИСТИНА);
	
	ОбщийМодульСервисСервер.ПисьмоАвтору(СтруктураПисьма);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Запись.МашинныйПеревод = ЛОЖЬ;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	глМассивФормСПользовательскимПереводом.Очистить();
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ОбновитьПовторноИспользуемыеЗначения();
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()             // ПРИ ЗАКРЫТИИ   	
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 1);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)        // ПРИ ОТКРЫТИИ		
	
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 0);
	Элементы.НаПоследнийЯзык.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке(ПоследнийЯзык);
	Элементы.НаПоследнийЯзык.Видимость = ЗначениеЗаполнено(ПоследнийЯзык);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьСобственныйПереводЭлементовИнтерфейса") Тогда
		ОбщийМодульСервер.ПеревестиРеквизитыФормы(ЭтаФорма);	
	КонецЕсли;
	
	Если НЕ Запись.ЭтоЭлементИнтерфейса
		И Параметры.ЭтоЭлементИнтерфейса Тогда
		
		Запись.ЭтоЭлементИнтерфейса = ИСТИНА;	
	КонецЕсли;
	
	Элементы.ЭтоРезультатМашинногоПеревода.Видимость = Запись.МашинныйПеревод;
	Элементы.ГруппаПереводаИнтерфейса.Видимость  	 = Запись.ЭтоЭлементИнтерфейса;
	
КонецПроцедуры

&НаКлиенте
Процедура ЯзыкПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Запись.Язык) Тогда
		ПоследнийЯзык = Запись.Язык;
		Элементы.НаПоследнийЯзык.Заголовок = ОбщийМодульПовтор.ПолучитьТекстНаЯзыке(ПоследнийЯзык);
		Элементы.НаПоследнийЯзык.Видимость = ИСТИНА;
	КонецЕсли;
	
КонецПроцедуры
