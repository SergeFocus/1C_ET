// sza150714-1751 чужие админу
// sza140509-0122  
// sza131129-2329  
&НаКлиенте
Процедура ГруппыНастроекПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("УстановитьОтборПоИерархииОбработчикОжидания", 0.2, ИСТИНА);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗначениямиОбщихНастроек(Команда)
	
	ЗаполнитьЗначениямиОбщихНастроекНаСервере(ТекущийПользователь);
	ЭтаФорма.ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьЗначениямиОбщихНастроекНаСервере(ТекущийПользователь)
	
	ПредопределеннаяНастройка = ПланыВидовХарактеристик.НастройкиПользователей.Выбрать();
	Пока ПредопределеннаяНастройка.Следующий() Цикл
		
		Если НЕ ПредопределеннаяНастройка.ЭтоГруппа Тогда
			
			НастройкаИмя = планывидовхарактеристик.НастройкиПользователей.ПолучитьИмяПредопределенного(ПредопределеннаяНастройка.Ссылка);
			Настройка 	 = ПланыВидовХарактеристик.НастройкиПользователей[НастройкаИмя];
			
			РСНЗ = регистрысведений.НастройкиПользователей.СоздатьНаборЗаписей();
			
			РСНЗ.Отбор.Пользователь.Установить(ТекущийПользователь);
			РСНЗ.Отбор.Настройка.Установить(Настройка);
			
			Добавлять = ЛОЖЬ;
			РСНЗ.Прочитать();
			Если РСНЗ.Количество() = 0 тогда
				Добавлять = ИСТИНА;
			Иначе
				Если РСНЗ[0].ЗначениеНастройки = Неопределено 
					ИЛИ НЕ ЗначениеЗаполнено(РСНЗ[0].ЗначениеНастройки) Тогда
					
					Добавлять = ИСТИНА;
				КонецЕсли;
			КонецЕсли;
			
			Если Добавлять Тогда	
				РСНЗ.Очистить();
				
				НоваяЗапись = РСНЗ.Добавить();
				НоваяЗапись.Пользователь = ТекущийПользователь;
				НоваяЗапись.Настройка 	 = Настройка;						
				НоваяЗапись.ЗначениеНастройки = ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты(НастройкаИмя, ИСТИНА);
				
				РСНЗ.Записать(ИСТИНА);          	
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	ТекущиеДанные.ДатаУстановки = ТекущаяДата();
	ТекущиеДанные.Пользователь 	= ТекущийПользователь;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	ТекущиеДанные.Пользователь = ТекущийПользователь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидОкна()
	
	Элементы.ГруппаГоризонт.Видимость 		= НЕ ВидОкнаВсеНастройки;
	Элементы.ГруппаВсеНастройки.Видимость 	= ВидОкнаВсеНастройки;
	
Конецпроцедуры

&НаСервере
Процедура ОбновитьПараметрыПользователя()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиПользователей.Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.НастройкиПользователей КАК НастройкиПользователей
	|ГДЕ
	|	НастройкиПользователей.ЭтоГруппа = ЛОЖЬ
	|	И НастройкиПользователей.ПометкаУдаления = ЛОЖЬ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		РСНЗ = регистрысведений.НастройкиПользователей.СоздатьНаборЗаписей();
		
		РСНЗ.Отбор.Пользователь.Установить(ТекущийПользователь);
		РСНЗ.Отбор.Настройка.Установить(ВыборкаДетальныеЗаписи.Ссылка);
		
		РСНЗ.Прочитать();
		Если РСНЗ.Количество() = 0 тогда
			
			НоваяЗапись = РСНЗ.Добавить();
			НоваяЗапись.Пользователь 	= ТекущийПользователь;
			НоваяЗапись.Настройка 		= ВыборкаДетальныеЗаписи.Ссылка;			
			
			РСНЗ.Записать(ИСТИНА);          	
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 1);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбщийМодульКлиент.СобытиеФормы(ЭтаФорма, 0);
	ОбновитьВидОкна();
	Если НЕ ВидОкнаВсеНастройки Тогда
		УстановитьОтборПоИерархииОбработчикОжидания();
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ПослеИзображенияОкнаФормы", 0.3, ИСТИНА);	
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеИзображенияОкнаФормы() // Когда окно уже нарисовано пользователю
	ЭтаФорма.Элементы.ФормаЗакрыть.Видимость = НЕ ЭтаФорма.Окно = Неопределено;	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Отказ Тогда
		Если ОбщийМодульПовтор.ПолучитьЗначениеНастройкиИлиКонстанты("ИспользоватьСобственныйПереводЭлементовИнтерфейса") Тогда
			ОбщийМодульСервер.ПеревестиРеквизитыФормы(ЭтаФорма);	
		КонецЕсли;
		
		ТекущийПользователь = ПараметрыСеанса.ТекущийПользователь;
		Список.Параметры.УстановитьЗначениеПараметра("ТекущийПользователь", ТекущийПользователь);
		СписокВсеНастройки.Параметры.УстановитьЗначениеПараметра("ТекущийПользователь", ТекущийПользователь);
		
		ПолнаяВерсия = ОбщийМодульПовтор.ПолнаяВерсия();
		Если НЕ ПолнаяВерсия Тогда
			
			ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбора.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("НастройкаКод");
			ЭлементОтбора.ВидСравнения 		= ВидСравненияКомпоновкиДанных.НеСодержит;
			ЭлементОтбора.ПравоеЗначение 	= "NF";
			ЭлементОтбора.Использование 	= ИСТИНА;
			Элементы.Список.Обновить();
			
			ЭлементОтбора = СписокВсеНастройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбора.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("НастройкаКод");
			ЭлементОтбора.ВидСравнения 		= ВидСравненияКомпоновкиДанных.НеСодержит;
			ЭлементОтбора.ПравоеЗначение 	= "NF";
			ЭлементОтбора.Использование 	= ИСТИНА;
			Элементы.Список.Обновить();
			
		КонецЕсли;
		
		ОбновитьПараметрыПользователя();
		ОбщийМодульСервер.ОбеспечитьСписокОтборов(Список);	
		ОбщийМодульСервер.ОбеспечитьСписокОтборов(СписокВсеНастройки);
		
		АдминистраторСистемы = ИСТИНА;
		Попытка 
			Выполнить(" АдминистраторСистемы = РольДоступна(""АдминистраторСистемы""); ");
		Исключение 	
		КонецПопытки;
		
		Если АдминистраторСистемы Тогда
			Элементы.Пользователь.ТолькоПросмотр = ЛОЖЬ;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СброситьЛичныеНастройки(Команда)
	
	СброситьЛичныеНастройкиНаСервере();
	ЭтаФорма.ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаСервере
Процедура СброситьЛичныеНастройкиНаСервере()
	
	РСНЗ = регистрысведений.НастройкиПользователей.СоздатьНаборЗаписей();
	РСНЗ.Отбор.Пользователь.Установить(ТекущийПользователь);
	РСНЗ.Прочитать();
	РСНЗ.Очистить();
	РСНЗ.Записать(ИСТИНА);
	
	ОбновитьПовторноИспользуемыеЗначения();
	ОбновитьПараметрыПользователя();
	
КонецПроцедуры

&НаКлиенте
Процедура СменитьВидОкна(Команда)
	
	ВидОкнаВсеНастройки = НЕ ВидОкнаВсеНастройки;
	ОбновитьВидОкна();	
	Если НЕ ВидОкнаВсеНастройки Тогда
		УстановитьОтборПоИерархииОбработчикОжидания();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборПоИерархииОбработчикОжидания()
	
	СменупроизвелиСлева = ИСТИНА;
	ТекущиеДанныеНавигации = Элементы.ГруппыНастроек.ТекущиеДанные;
	
	ЗначениеОтбора = ?(ТекущиеДанныеНавигации <> Неопределено, ТекущиеДанныеНавигации.Ссылка, ПредопределенноеЗначение("ПланВидовХарактеристик.НастройкиПользователей.ПустаяСсылка"));
	ОбщийМодульКлиент.УстановитьЭлементОтбора(
	Список.Отбор,
	"Настройка.Родитель",
	ЗначениеОтбора,
	ВидСравненияКомпоновкиДанных.Равно,
	,
	ИСТИНА
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ПользовательПриИзменении(Элемент)
	ПользовательПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПользовательПриИзмененииНаСервере()
	
	Список.Параметры.УстановитьЗначениеПараметра("ТекущийПользователь", ТекущийПользователь);
	СписокВсеНастройки.Параметры.УстановитьЗначениеПараметра("ТекущийПользователь", ТекущийПользователь);
	ОбновитьПараметрыПользователя();
	ОбщийМодульСервер.ОбеспечитьСписокОтборов(Список);	
	ОбщийМодульСервер.ОбеспечитьСписокОтборов(СписокВсеНастройки);
	
КонецПроцедуры
